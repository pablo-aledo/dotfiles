= Operators

Operators are methods that allow you to connect channels, transform values emitted by a channel, or apply some user-provided rules.

There are seven main groups of operators are described in greater detail within the Nextflow Reference Documentation, linked below:

1. https://www.nextflow.io/docs/latest/operator.html#filtering-operators[Filtering operators]

2. https://www.nextflow.io/docs/latest/operator.html#transforming-operators[Transforming operators]

3. https://www.nextflow.io/docs/latest/operator.html#splitting-operators[Splitting operators]

4. https://www.nextflow.io/docs/latest/operator.html#combining-operators[Combining operators]

5. https://www.nextflow.io/docs/latest/operator.html#forking-operators[Forking operators]

6. https://www.nextflow.io/docs/latest/operator.html#maths-operators[Maths operators]

7. https://www.nextflow.io/docs/latest/operator.html#other-operators[Other operators]


== Basic example

[source,nextflow,linenums]
----
nums = Channel.of(1,2,3,4)         // <1>
square = nums.map { it -> it * it }  // <2>
square.view()                        // <3>
----

image::channel-map.png[]

<1> Creates a queue channel emitting four values
<2> Creates a new channel, transforming each number into its square
<3> Prints the channel content

Operators can also be chained to implement custom behaviors, so the previous snippet can also be written as:

[source,nextflow,linenums]
----
Channel
    .of(1,2,3,4)
    .map { it -> it * it }
    .view()
----

== Basic operators

Here we explore some of the most commonly used operators.

=== view

The `view` operator prints the items emitted by a channel to the console standard output, appending a
_new line_ character to each item. For example:

[source,nextflow,linenums]
----
Channel
    .of('foo', 'bar', 'baz')
    .view()
----

It prints:

[cmd]
----
foo
bar
baz
----

An optional _closure_ parameter can be specified to customize how items are printed. For example:

[source,nextflow,linenums]
----
Channel
    .of('foo', 'bar', 'baz')
    .view { "- $it" }
----

It prints:

[cmd]
----
- foo
- bar
- baz
----

=== map

The `map` operator applies a function of your choosing to every item emitted by a channel and returns the items obtained as a new channel. The function applied is called the _mapping_ function and is expressed with a _closure_ as shown in the example below:

[source,nextflow,linenums]
----
Channel
    .of( 'hello', 'world' )
    .map { it -> it.reverse() }
    .view()
----

A `map` can associate a generic _tuple_ to each element and can contain any data.

[source,nextflow,linenums]
----
Channel
    .of( 'hello', 'world' )
    .map { word -> [word, word.size()] }
    .view { word, len -> "$word contains $len letters" }
----

[discrete]
=== Exercise

Use `fromPath` to create a channel emitting the _fastq_ files matching the pattern `data/ggal/*.fq`,
then use `map` to return a pair containing the file name and the path itself, and finally, use `view` to print the resulting channel.

.Click here for the answer:
[%collapsible]
====
[source,nextflow,linenums]
----
Channel
    .fromPath('data/ggal/*.fq')
    .map { file -> [ file.name, file ] }
    .view { name, file -> "> $name : $file" }
----
====

=== mix

The `mix` operator combines the items emitted by two (or more) channels into a single channel.

[source,nextflow,linenums]
----
c1 = Channel.of( 1,2,3 )
c2 = Channel.of( 'a','b' )
c3 = Channel.of( 'z' )

c1 .mix(c2,c3).view()
----

[cmd]
----
1
2
a
3
b
z
----

IMPORTANT: The items in the resulting channel have the same order as in the respective original channels.
However, there is no guarantee that the element of the second channel are appended after the elements
of the first. Indeed, in the example above, the element `a` has been printed before `3`.

=== flatten

The `flatten` operator transforms a channel in such a way that every _tuple_ is flattened so that each entry is emitted as a sole element by the resulting channel.

[source,nextflow,linenums]
----
foo = [1,2,3]
bar = [4,5,6]

Channel
    .of(foo, bar)
    .flatten()
    .view()
----

The above snippet prints:

[cmd]
----
1
2
3
4
5
6
----

=== collect

The `collect` operator collects all of the items emitted by a channel in a list and returns the object as a sole emission.

[source,nextflow,linenums]
----
Channel
    .of( 1, 2, 3, 4 )
    .collect()
    .view()
----

It prints a single value:

[cmd]
----
[1,2,3,4]
----

TIP: The result of the `collect` operator is a *value* channel.

=== groupTuple

The `groupTuple` operator collects tuples (or lists) of values emitted by the source channel, grouping the elements that share the same key. Finally, it emits a new tuple object for each distinct key collected.

Try the following example:

[source,nextflow,linenums]
----
Channel
    .of( [1,'A'], [1,'B'], [2,'C'], [3, 'B'], [1,'C'], [2, 'A'], [3, 'D'] )
    .groupTuple()
    .view()
----

It shows:

[cmd]
----
[1, [A, B, C]]
[2, [C, A]]
[3, [B, D]]
----

This operator is useful to process a group together with all the elements that share a common property or grouping key.

[discrete]
=== Exercise

Use `fromPath` to create a channel emitting all of the files in the folder `data/meta/`,
then use a `map` to associate the `baseName` prefix to each file. Finally, group all
files that have the same common prefix.

.Click here for the answer:
[%collapsible]
====
[source,nextflow,linenums]
----
Channel.fromPath('data/meta/*')
    .map { file -> tuple(file.baseName, file) }
    .groupTuple()
    .view { baseName, file -> "> $baseName : $file" }
----
====

=== join

The `join` operator creates a channel that joins together the items emitted by two channels with a matching key. The key is defined, by default, as the first element in each item emitted.

[source,nextflow,linenums]
----
left = Channel.of(['X', 1], ['Y', 2], ['Z', 3], ['P', 7])
right = Channel.of(['Z', 6], ['Y', 5], ['X', 4])
left.join(right).view()
----

The resulting channel emits:

[cmd]
----
[Z, 3, 6]
[Y, 2, 5]
[X, 1, 4]
----

TIP: Notice 'P' is missing in the final result.

=== branch

The `branch` operator allows you to forward the items emitted by a source channel to one or more output channels.

The selection criterion is defined by specifying a closure that provides one or more boolean expressions, each of which is identified by a unique label. For the first expression that evaluates to a true value, the item is bound to a named channel as the label identifier.
For example:

[source,nextflow,linenums]
----
Channel
    .of(1,2,3,40,50)
    .branch {
        small: it < 10
        large: it > 10
    }
    .set { result }

result.small.view { "$it is small" }
result.large.view { "$it is large" }
----

NOTE: The `branch` operator returns a multi-channel object (i.e., a variable that holds more than one channel object).

NOTE: In the above example, what would happen to a value of 10? To deal with this, you can also use `>=`.

== More resources

Check the https://www.nextflow.io/docs/latest/operator.html[operators documentation] on Nextflow web site.

