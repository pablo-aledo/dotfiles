= Problem 8
:docinfo: private
:icons: font
:linkcss:
:source-highlighter: coderay
:coderay-linenums-mode: table

.Solution
[source,nextflow,linenums]
----
process '4_rnaseq_gatk_recalibrate' {
  tag "$replicateId"

  input:
    path genome from params.genome <1>
    path index from genome_index_ch <2>
    path dict from genome_dict_ch <3>
    tuple val(replicateId), path(bam), path(bai) from splitted_bam_ch <4>
    tuple path(prepared_variants_file), path(prepared_variants_file_index) from prepared_vcf_ch <5>

  output:
    tuple val(sampleId), path("${replicateId}.final.uniq.bam"), path("${replicateId}.final.uniq.bam.bai") into (final_output_ch, bam_for_ASE_ch) <6>

  script:
  sampleId = replicateId.replaceAll(/[12]$/,'')
  """
  # Indel Realignment and Base Recalibration
  java -jar $GATK -T BaseRecalibrator \
                  --default_platform illumina \
                  -cov ReadGroupCovariate \
                  -cov QualityScoreCovariate \
                  -cov CycleCovariate \
                  -knownSites ${prepared_variants_file} \
                  -cov ContextCovariate \
                  -R ${genome} -I ${bam} \
                  --downsampling_type NONE \
                  -nct ${task.cpus} \
                  -o final.rnaseq.grp

  java -jar $GATK -T PrintReads \
                  -R ${genome} -I ${bam} \
                  -BQSR final.rnaseq.grp \
                  -nct ${task.cpus} \
                  -o final.bam

  # Select only unique alignments, no multimaps
  (samtools view -H final.bam; samtools view final.bam| grep -w 'NH:i:1') \
  |samtools view -Sb -  > ${replicateId}.final.uniq.bam <7>

  # Index BAM files
  samtools index ${replicateId}.final.uniq.bam
  """
}
----
<1> the genome fasta file.
<2> the genome index from the `genome_index_ch` channel created in the process `1A_prepare_genome_samtools`.
<3> the genome dictionary from the `genome_dict_ch` channel created in the process `1B_prepare_genome_picard`.
<4> the set containing the split reads from the `splitted_bam_ch` channel created in the process `3_rnaseq_gatk_splitNcigar`.
<5> the set containing the filtered/recoded VCF file and the tab index (TBI) file from the `prepared_vcf_ch` channel created in the process `1D_prepare_vcf_file`.
<6> the set containing the replicate id, the unique bam file and the unique bam index file which goes into two channels.
<7> line specifying the filename of the output bam file
