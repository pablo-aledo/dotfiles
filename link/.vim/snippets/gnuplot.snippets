snippet 3dcells
	set view 55,40
	#set autoscale
	set border 4095 front linetype 0 linewidth 1.000
	set xyplane 0
	set datafile separator ","
	set xrange [0:1]
	set yrange [0:1]
	set zrange [0:1]
	splot 'positions/cells_%n%_1.csv' with points pt 7 fc rgb "black"  lt -1 ps 0.5, \
	      'positions/cells_%n%_1.csv' with points pt 7 fc rgb "blue"   lt -1 ps 0.25, \
	      'positions/cells_%n%_2.csv' with points pt 7 fc rgb "black"  lt -1 ps 0.5, \
	      'positions/cells_%n%_2.csv' with points pt 7 fc rgb "orange" lt -1 ps 0.25
	set xtics axis
	set format ""
	unset key
	set term pngcairo mono enhanced
	set out 'output/cells_%n%.png'
	replot

snippet cmdterm
	set terminal dumb
	plot "-" with lines
	1
	2
	2
	1
	e

snippet airfoil.dem
	#
	# $Id: airfoil.dem,v 1.10 2006/06/30 02:17:21 sfeam Exp $
	#
	# This demo shows how to use bezier splines to define NACA four
	# series airfoils and complex variables to define Joukowski
	# Airfoils.  It will be expanded after overplotting in implemented
	# to plot Coefficient of Pressure as well.
	#		Alex Woo, Dec. 1992
	#
	# The definitions below follows: "Bezier presentation of airfoils",
	# by Wolfgang Boehm, Computer Aided Geometric Design 4 (1987) pp 17-22.
	#
	#				Gershon Elber, Nov. 1992
	#
	# m = percent camber
	# p = percent chord with maximum camber
	print  "NACA four series airfoils by bezier splines"
	print  "Will add pressure distribution later with Overplotting"
	mm = 0.6
	# NACA6xxx
	thick = 0.09  
	# nine percent  NACAxx09
	pp = 0.4
	# NACAx4xx
	# Combined this implies NACA6409 airfoil
	#
	# Airfoil thickness function.
	#
	set xlabel "NACA6409 -- 9% thick, 40% max camber, 6% camber"
	x0 = 0.0
	y0 = 0.0
	x1 = 0.0
	y1 = 0.18556
	x2 = 0.03571
	y2 = 0.34863
	x3 = 0.10714
	y3 = 0.48919
	x4 = 0.21429 
	y4 = 0.58214
	x5 = 0.35714
	y5 = 0.55724
	x6 = 0.53571
	y6 = 0.44992
	x7 = 0.75000
	y7 = 0.30281
	x8 = 1.00000
	y8 = 0.01050
	#
	# Directly defining the order 8 Bezier basis function for a faster evaluation.
	#
	bez_d4_i0(x) =     (1 - x)**4
	bez_d4_i1(x) = 4 * (1 - x)**3 * x
	bez_d4_i2(x) = 6 * (1 - x)**2 * x**2
	bez_d4_i3(x) = 4 * (1 - x)**1 * x**3
	bez_d4_i4(x) =                  x**4
	
	bez_d8_i0(x) =      (1 - x)**8
	bez_d8_i1(x) =  8 * (1 - x)**7 * x
	bez_d8_i2(x) = 28 * (1 - x)**6 * x**2
	bez_d8_i3(x) = 56 * (1 - x)**5 * x**3
	bez_d8_i4(x) = 70 * (1 - x)**4 * x**4
	bez_d8_i5(x) = 56 * (1 - x)**3 * x**5
	bez_d8_i6(x) = 28 * (1 - x)**2 * x**6
	bez_d8_i7(x) =  8 * (1 - x)    * x**7
	bez_d8_i8(x) =                   x**8
	
	
	m0 = 0.0
	m1 = 0.1
	m2 = 0.1
	m3 = 0.1
	m4 = 0.0
	mean_y(t) = m0 * mm * bez_d4_i0(t) + \
		    m1 * mm * bez_d4_i1(t) + \
		    m2 * mm * bez_d4_i2(t) + \
		    m3 * mm * bez_d4_i3(t) + \
		    m4 * mm * bez_d4_i4(t)
	
	p0 = 0.0
	p1 = pp / 2
	p2 = pp
	p3 = (pp + 1) / 2
	p4 = 1.0
	mean_x(t) = p0 * bez_d4_i0(t) + \
		    p1 * bez_d4_i1(t) + \
		    p2 * bez_d4_i2(t) + \
		    p3 * bez_d4_i3(t) + \
		    p4 * bez_d4_i4(t)
	
	z_x(x) = x0 * bez_d8_i0(x) + x1 * bez_d8_i1(x) + x2 * bez_d8_i2(x) + \
		 x3 * bez_d8_i3(x) + x4 * bez_d8_i4(x) + x5 * bez_d8_i5(x) + \
		 x6 * bez_d8_i6(x) + x7 * bez_d8_i7(x) + x8 * bez_d8_i8(x)
	
	z_y(x, tk) = \
	   y0 * tk * bez_d8_i0(x) + y1 * tk * bez_d8_i1(x) + y2 * tk * bez_d8_i2(x) + \
	   y3 * tk * bez_d8_i3(x) + y4 * tk * bez_d8_i4(x) + y5 * tk * bez_d8_i5(x) + \
	   y6 * tk * bez_d8_i6(x) + y7 * tk * bez_d8_i7(x) + y8 * tk * bez_d8_i8(x)
	
	#
	# Given t value between zero and one, the airfoild curve is defined as
	# 
	#			c(t) = mean(t1(t)) +/- z(t2(t)) n(t1(t)),
	#
	# where n is the unit normal to the mean line. See the above paper for more.
	#
	# Unfortunately, the parametrization of c(t) is not the same for mean(t1)
	# and z(t2). The mean line (and its normal) can assume linear function t1 = t,
	#                                                     -1
	# but the thickness z_y is, in fact, a function of z_x  (t). Since it is
	# not obvious how to compute this inverse function analytically, we instead
	# replace t in c(t) equation above by z_x(t) to get:
	# 
	#			c(z_x(t)) = mean(z_x(t)) +/- z(t) n(z_x(t)),
	#
	# and compute and display this instead. Note we also ignore n(t) and assumes
	# n(t) is constant in the y direction,
	#
	
	airfoil_y1(t, thick) = mean_y(z_x(t)) + z_y(t, thick)
	airfoil_y2(t, thick) = mean_y(z_x(t)) - z_y(t, thick)
	airfoil_y(t) = mean_y(z_x(t))
	airfoil_x(t) = mean_x(z_x(t))
	unset grid
	unset zeroaxis
	set parametric
	set xrange [-0.1:1.1]
	set yrange [-0.1:.7]
	set trange [ 0.0:1.0]
	set title "NACA6409 Airfoil"
	plot airfoil_x(t), airfoil_y(t) title "mean line" w l lt 2, \
	     airfoil_x(t), airfoil_y1(t, thick) title "upper surface" w l lt 1, \
	     airfoil_x(t), airfoil_y2(t, thick) title "lower surface" w l lt 1
	pause -1 "Press Return"
	mm = 0.0
	pp = .5
	thick = .12
	set title "NACA0012 Airfoil"
	set xlabel "12% thick, no camber -- classical test case"
	plot airfoil_x(t), airfoil_y(t) title "mean line" w l lt 2, \
	     airfoil_x(t), airfoil_y1(t, thick) title "upper surface" w l lt 1, \
	     airfoil_x(t), airfoil_y2(t, thick) title "lower surface" w l lt 1
	pause -1 "Press Return"
	set title ""
	set xlab ""
	set key box
	set parametric
	set samples 100
	set isosamples 10
	set style data lines
	set style function lines
	print  "Joukowski Airfoil using Complex Variables" 
	set title "Joukowski Airfoil using Complex Variables" offset 0,0
	set time
	set yrange [-.2 : 1.8]
	set trange [0: 2*pi]
	set xrange [-.6:.6]
	zeta(t) = -eps + (a+eps)*exp(t*{0,1})
	eta(t) = zeta(t) + a*a/zeta(t)
	eps = 0.06
	a =.250
	set xlabel "eps = 0.06 real"
	plot real(eta(t)),imag(eta(t))
	pause -1 "Press Return"
	eps = 0.06*{1,-1}
	set xlabel "eps = 0.06 + i0.06"
	plot real(eta(t)),imag(eta(t))
	pause -1 "Press Return"
	reset

snippet all.dem
	#
	# $Id: all.dem,v 1.79 2017/01/11 04:13:58 sfeam Exp $
	#
	# Executes all demos (OK, MOST demos) in this directory.
	# This serves as a series of unit tests for non-interactive capabilities.
	#
	
	print "******************** file simple.dem ********************"
	load "simple.dem"
	reset
	
	print "******************** file controls.dem ********************"
	load "controls.dem"
	reset
	
	print "******************** file electron.dem ********************"
	load "electron.dem"
	reset
	
	print "******************** file using.dem ********************"
	load "using.dem"
	reset
	
	print "******************** file fillstyle.dem ********************"
	load "fillstyle.dem"
	reset
	
	print "******************** file fillcvrs.dem ********************"
	load "fillcrvs.dem"
	load "fillbetween.dem"
	reset
	
	print "******************** file candlesticks.dem ********************"
	load "candlesticks.dem"
	reset
	
	print "******************** file autoscale.dem ********************"
	load "autoscale.dem"
	reset
	
	print "******************** file smooth_splines.dem ********************"
	load "smooth_splines.dem"
	load "monotonic_spline.dem"
	reset
	
	
	print "******************** file errorbars.dem ********************"
	load "errorbars.dem"
	reset
	
	print "******************** file zerror.dem ********************"
	load "zerror.dem"
	reset
	
	print "******************** file fit.dem ********************"
	set fit quiet
	load "fit.dem"
	reset
	load "fitmulti.dem"
	reset
	
	print "******************** file named_var.dem ********************"
	load "named_var.dem"
	reset
	
	print "******************** file param.dem ********************"
	load "param.dem"
	reset
	
	print "******************** file piecewise.dem ********************"
	load "piecewise.dem"
	reset
	
	print "******************** file polar.dem ********************"
	load "polar.dem"
	reset
	
	print "******************** file poldat.dem ********************"
	load "poldat.dem"
	reset
	
	print "******************** file solar_path.dem ****************"
	load "solar_path.dem"
	reset
	
	print "******************** file ttics.dem ********************"
	load "ttics.dem"
	reset
	
	print "******************** file boxplot.dem ********************"
	load "boxplot.dem"
	reset
	
	print "******************** file jitter.dem ********************"
	load "jitter.dem"
	reset
	
	print "******************** file violinplot.dem ********************"
	load "violinplot.dem"
	reset
	
	print "******************** file multiplt.dem ********************"
	load "multiplt.dem"
	load "layout.dem"
	reset
	
	print "******************** file surface1.dem ********************"
	load "surface1.dem"
	reset
	
	print "******************** file discrete.dem ********************"
	load "discrete.dem"
	reset
	
	print "******************** file hidden.dem ********************"
	load "hidden.dem"
	reset
	
	print "******************** file dgrid3d.dem ********************"
	load "dgrid3d.dem"
	reset
	
	print "******************** file world.dem ********************"
	load "world.dem"
	load "world2.dem"
	reset
	
	print "******************** file prob.dem ********************"
	load "prob.dem"
	reset
	
	print "******************** file prob2.dem ********************"
	load "prob2.dem"
	reset
	
	print "******************** file random.dem ********************"
	load "random.dem"
	reset
	
	print "******************** file rugplot.dem ********************"
	load "rugplot.dem"
	reset
	
	print "******************** file smooth.dem ********************"
	load "smooth.dem"
	reset
	
	print "******************** file spline.dem ********************"
	load "spline.dem"
	reset
	
	print "******************** file binary.dem ********************"
	load "binary.dem"
	reset
	
	print "******************** file steps.dem ********************"
	load "steps.dem"
	reset
	
	print "******************** file scatter.dem ********************"
	load "scatter.dem"
	reset
	
	print "******************** file singulr.dem ********************"
	load "singulr.dem"
	reset
	
	print "******************** file airfoil.dem ********************"
	load "airfoil.dem"
	reset
	
	print "******************** file surface2.dem ********************"
	load "surface2.dem"
	reset
	
	print "******************** file azimuth.dem ********************"
	load "azimuth.dem"
	reset
	
	print "******************** file contours.dem ********************"
	load "contours.dem"
	reset
	
	print "******************** file multimsh.dem ********************"
	load "multimsh.dem"
	reset
	
	print "******************** file bivariat.dem ********************"
	load "bivariat.dem"
	reset
	
	print "******************** Time/Date data ********************"
	load "timedat.dem"
	load "gantt.dem"
	reset
	
	print "******************** file animate.dem ********************"
	load "animate.dem"
	reset
	
	print "********************** file rainbow.dem *********************"
	load "rainbow.dem"
	reset
	
	print "********************** file rgb_variable.dem *********************"
	load "rgb_variable.dem"
	reset
	
	print "********************** file rgba_lines.dem *********************"
	load "rgba_lines.dem"
	reset
	
	print "********************** file varcolor.dem *********************"
	load "varcolor.dem"
	reset
	
	print "********************** file pm3d.dem *********************"
	load "pm3d.dem"
	reset
	
	print "********************** file pm3dcolors.dem *********************"
	load "pm3dcolors.dem"
	reset
	
	print "********************** file complex_trig.dem *********************"
	load "complex_trig.dem"
	reset
	
	print "********************** file heatmaps.dem *********************"
	load "heatmaps.dem"
	reset
	
	print "********************** file matrix_index.dem *********************"
	load "matrix_index.dem"
	reset
	
	print "********************** file matrix_every.dem *********************"
	load "matrix_every.dem"
	reset
	
	print "********************** file pm3dgamma.dem *********************"
	load "pm3dgamma.dem"
	reset
	
	print "********************** file hidden2.dem ***********************"
	load "hidden2.dem"
	reset
	
	print "********************** file textcolor.dem *********************"
	load "textcolor.dem"
	reset
	
	print "********************** file textrotate.dem *********************"
	load "textrotate.dem"
	reset
	
	print "********************** enhanced text *********************"
	if (GPVAL_TERM eq "postscript") {
	    load "enhancedtext.dem"
	} else {
	    load "enhanced_utf8.dem"
	}
	reset
	
	print "********************** file arrowstyle.dem *********************"
	load "arrowstyle.dem"
	reset
	
	print "********************** file dashtypes.dem *********************"
	load "dashtypes.dem"
	reset
	
	print "********************** file vector.dem *********************"
	load "vector.dem"
	reset
	
	print "********************** file tics.dem *********************"
	load "tics.dem"
	reset
	
	print "********************** file break_continue.dem *********************"
	load "break_continue.dem"
	reset
	
	print "********************** file callargs.dem *********************"
	load "callargs.dem"
	reset
	
	print "********************** file volatile.dem *********************"
	load "volatile.dem"
	reset
	
	print "********************** file datastrings.dem *********************"
	load "datastrings.dem"
	reset
	
	print "********************** file textbox.dem *********************"
	load "textbox.dem"
	reset
	
	print "********************** file hypertext.dem *********************"
	load "hypertext.dem"
	reset
	
	print "********************** file rotate_labels.dem *****************"
	load "rotate_labels.dem"
	reset
	
	print "********************** file stats.dem *********************"
	load "stats.dem"
	reset
	
	print "********************** file iterate.dem *********************"
	load "iterate.dem"
	reset
	
	print "********************** file histograms.dem *********************"
	load "histograms.dem"
	load "histograms2.dem"
	reset
	
	print "********************** file array.dem *********************"
	load "array.dem"
	reset
	
	print "**********************    Image formats    *********************"
	load "image.dem"
	load "image2.dem"
	load "rgbalpha.dem"
	load "imageNaN.dem"
	load "barchart_art.dem"
	reset
	
	print "********************** file stringvar.dem *********************"
	load "stringvar.dem"
	reset
	
	print "********************** file running_avg.dem *********************"
	load "running_avg.dem"
	reset
	
	print "********************** file pointsize.dem *********************"
	load "pointsize.dem"
	reset
	
	print "********************** file circles.dem *********************"
	load "circles.dem"
	reset
	
	print "********************** file ellipses_style.dem *********************"
	load "ellipses_style.dem"
	reset
	
	print "********************** file key.dem *********************"
	load "key.dem"
	reset
	
	print "********************** file custom_key.dem *********************"
	load "custom_key.dem"
	load "nokey.dem"
	reset
	
	print "********************** file borders.dem *********************"
	load "borders.dem"
	reset
	
	print "********************** file margins.dem *********************"
	load "margins.dem"
	reset
	
	print "********************** file rectangle.dem *********************"
	load "rectangle.dem"
	reset
	
	print "********************** file clipobject.dem *********************"
	load "clipobject.dem"
	reset
	
	print "********************** file approximate.dem *********************"
	load "approximate.dem"
	reset
	
	print "********************** file parallel.dem *********************"
	load "parallel.dem"
	reset
	
	print "********************** nonlinear axis demos *********************"
	load "nonlinear1.dem"
	load "nonlinear2.dem"
	load "nonlinear3.dem"
	load "nonlinear4.dem"
	load "nonlinear5.dem"
	reset
	
	print "********************** file transparent.dem *********************"
	load "transparent.dem"
	reset
	
	print "********************** file transparent_solids.dem *********************"
	load "transparent_solids.dem"
	
	print "********************** file pm3d_lighting.dem *********************"
	load "pm3d_lighting.dem"
	pause 5
	reset

snippet animate2.dem
	#
	# $Id: animate2.dem,v 1.3 2016/12/04 19:28:22 sfeam Exp $
	#
	# demo for creating GIF animation and illustrating saving images to
	# file one pixel to one pixel
	#
	# A combination of what was animate.dem and world2.dem with the
	# addition of saving the rotated image to an animated GIF.
	# Requires GIF_ANIMATION
	#
	# History:
	#   - 1. 3. 2006 Dan Sebald:  1st version
	#    07 Jan 2006 Ethan Merritt:  Revise to assume terminal type is set by caller
	#
	
	# Caller must set terminal type.
	# Example of intended use:
	# set term gif animate transparent opt delay 10 size 200,200 background rgb 'black'
	
	unset title
	unset key
	unset xtics
	unset ytics
	unset ztics
	set border 0
	set hidden3d nooffset
	set parametric
	set angles degrees
	set samples 64,64
	set isosamples 13,13
	set mapping spherical
	set dummy u,v
	set urange [ -90.0000 : 90.0000 ] noreverse nowriteback
	set vrange [ 0.00000 : 360.000 ] noreverse nowriteback
	set style data line
	
	# Defines for gnuplot.rot script
	limit_iterations=72
	xrot=60
	xrot_delta = 0
	zrot=136
	zrot_delta = 355
	xview(xrot)=xrot
	zview(zrot)=zrot
	set view xview(xrot), zview(zrot), 1.5, 1
	set size square
	
	splot cos(u)*cos(v),cos(u)*sin(v),sin(u) notitle with lines lt 5, \
	      'world.dat' notitle with lines lt 2 lw 3
	
	iteration_count=0
	xrot =(xrot+xrot_delta)%360
	zrot =(zrot+zrot_delta)%360
	
	load "gnuplot.rot"
	
	pause -1 "Hit return to continue"
	
	reset
	
	print "End of animate2 demo..."

snippet animate.dem
	#
	# $Id: animate.dem,v 1.7 2006/01/07 23:21:02 sfeam Exp $
	#
	# Demo animation, tumbling around 'glass.dat'.
	#
	# History:
	#   - 1. 1. 2006 Dan Sebald:  Defined variables for more generic rotate
	#   - ?. ?. ?    Hans-Bernhard Broeker:  Changed from rotating whale to
	#                rotating glass
	#   - ?. ?. ?    ?:  Initial tumbling whale demo
	
	set parametric
	set hidden3d
	unset key
	set style data line
	xrot=60
	xrot_delta = 17
	zrot=0
	zrot_delta = 10
	xview(xrot)=(50.+30.*sin((xrot%180)/180.*pi))
	zview(zrot)=(60.+45.*sin(zrot/180.*pi))
	set view xview(xrot),zview(zrot)
	splot "glass.dat"
	
	limit_iterations=40 # limits number of iterations if nonzero
	
	if (!limit_iterations) print "The following animation will never stop on its own. You have"
	if (!limit_iterations) print "to stop it manually by interrupting gnuplot (e.g., press ^C)"
	print "On some screen terminal drivers for PC screens, you'll have"
	print "to hit a key to get to the next frame"
	
	pause -1 "Press a key to start the rotation..."
	
	iteration_count=0
	load "gnuplot.rot"
	reset

snippet approximate.dem
	#
	# Show use of pseudodata mechanism '+' to use plot styles with more than 
	# one relevant value per x coordinate. In this example we use the style
	# "filledcurves" to show the difference between two analytic functions.
	# This corresponds to the specification of multiple columns in the 
	# 'using' option for input from data files.
	# 
	#
	approx_1(x) = x - x**3/6
	approx_2(x) = x - x**3/6 + x**5/120 
	approx_3(x) = x - x**3/6 + x**5/120 - x**7/5040
	
	label1 = "x - {x^3}/3!"
	label2 = "x - {x^3}/3! + {x^5}/5!"
	label3 = "x - {x^3}/3! + {x^5}/5! - {x^7}/7!"
	
	#
	set termoption enhanced
	save_encoding = GPVAL_ENCODING
	set encoding utf8
	#
	set title "Polynomial approximation of sin(x)"
	set key Left center top reverse
	set xrange [ -3.2 : 3.2 ]
	set xtics ("-π" -pi, "-π/2" -pi/2, 0, "π/2" pi/2, "π" pi)
	set format y "%.1f"
	set samples 500
	set style fill solid 0.4 noborder
	
	plot '+' using 1:(sin($1)):(approx_1($1)) with filledcurve title label1 lt 3, \
	     '+' using 1:(sin($1)):(approx_2($1)) with filledcurve title label2 lt 2, \
	     '+' using 1:(sin($1)):(approx_3($1)) with filledcurve title label3 lt 1, \
	     sin(x) with lines lw 1 lc rgb "black"
	
	pause -1 "Hit return to continue"
	
	set encoding save_encoding
	reset

snippet armillary.dem
	#
	# Show placement of circles in 3D using
	# - sampling
	# - circle objects in axis coordinates
	# - circle objects and labels in polar (cylindrical) coordinates
	#
	save_encoding = GPVAL_ENCODING
	set encoding utf8
	
	unset border
	unset key
	set angles degrees
	set view 64, 302, 1.62, 1.0
	set view azimuth 23.4
	set view  equal xyz
	set style data lines
	set xzeroaxis
	set yzeroaxis
	set zzeroaxis
	set xyplane at 0
	unset tics
	set xrange [ -1.1  : 1.1  ]
	set yrange [ -1.1  : 1.1  ]
	set zrange [ -1.1  : 1.1  ]
	#
	set title "Circles and polygons in 3D" font ",20"
	set arrow from 0,0,-1.2 to 0,0,1.2 lw 2 filled
	#
	# Place circles as objects using Cartesian axis coordinates
	#
	set object 21 circle at 0,0,0  radius 1.0 fs empty border lc "black" lw 3 front
	set object 22 circle at 0,0,1  radius 0.1 fs transparent solid 0.5 fc "slategray"
	set object 23 circle at 0,0,-1 radius 0.1 fs transparent solid 0.5 fc "slategray"
	
	#
	# Pentagonal "base"
	#
	set obj 24 polygon from polar 0, 0.5, -1.2 to polar 72, 0.5, -1.2 to polar 144, 0.5, -1.2 to polar 216, 0.5, -1.2 to polar 288, 0.5, -1.2 to polar 0, 0.5, -1.2
	set obj 24 fillstyle solid border border lc "black" back fc "olive"
	
	
	#
	# Plot symbols for Zodiac constellations in polar (actually cylindrical) coordinates
	# Place a circle around the corresponding glyph
	# Note: Many fonts do not provide glyphs for these symbols
	#
	array Zodiac[13] = ["♐", "♑", "♒", "♓", "♈", "♉", "♊", "♋", "♌", "♍", "♎", "♏", "⛎"] 
	array Longitude[13] = [267., 300., 328., 352., 29., 53., 90., 118., 138., 174., 218., 241., 248.]
	do for [i=1:13] {
	    set label i Zodiac[i] at polar Longitude[i], 1.1, 0 center font ",15"
	    set obj i circle at polar Longitude[i], 1.1, 0 radius char 1.5
	    set obj i fs solid fc rgb hsv2rgb( Longitude[i]/360., 0.5, 1.0)
	}
	
	#
	# plot sampled parametric circles
	#
	splot sample [u=0:360] '+' using (cos(u)):(0):(sin(u)) lw 2, \
	             [u=0:360] '+' using (cos(u)):(sin(u)):(0) lw 2, \
	             [u=0:360] '+' using (0):(cos(u)):(sin(u)) lw 2
	
	pause -1 "Hit <cr> to continue"
	
	set encoding save_encoding
	reset

snippet array.dem
	#
	# Use an array to hold the sum of values in each column
	#
	array Sum[20]
	do for [i=5:13] { 
	  stats 'immigration.dat' skip 2 using (column(i)) nooutput
	  Sum[i] = STATS_sum
	  print sprintf("Sum[%2d] = %d",i,Sum[i])
	}
	#
	# Use the stored sum to normalize values in each colum
	#
	set style fill transparent solid 0.2 border -1
	set style data filledcurve y=0
	set tics scale 0
	set xrange [] noextend
	set xlabel "Decade"
	set title "Use of an array to aid normalization and to plot summed values"
	
	set multiplot
	plot for [i=5:13] 'immigration.dat' using 1:(column(i)/Sum[i]) title columnhead
	
	#
	# Plot the array values themselves in a separate inset
	#
	
	set size 0.4, 0.3
	set origin 0.05, 0.6
	
	unset xtics
	unset ytics
	set border 0
	set xlabel "Total"
	set xrange [4:15]
	set yrange [0:*]
	unset title
	set boxwidth 1.0 absolute
	
	plot Sum using 1:2:($1-4) with boxes lc variable notitle
	
	unset multiplot
	
	pause -1 "<cr> to continue"
	reset
	
	set title "Fit function to values stored in an array"
	set xtics nomirror
	
	array A[100]
	do for [i=1:100] { A[i] = sin(2*pi*i/100.) + 0.1*rand(0) }
	a = b = c = 0.01
	set key title "before fit"
	plot A with points title "Array A", c+cos(a+b*x) with lines
	
	pause -1 "<cr> to fit function to array values"
	
	set fit nolog
	fit c+cos(a+b*x) A using 1:2 via a,b,c
	
	set key title "after fit"
	plot A with points title "Array A", c+cos(a+b*x) with lines
	
	pause -1 "<cr> to continue"
	reset
	
	#
	# Illustrate loading an array from a data file
	# This could be done using a preliminary 'plot' command, perhaps together
	# with 'set table' if you don't actually want generate a plot, but here
	# we use 'stats'. The first run is just to pick up the array size needed.
	# The second run stores one value into each array element.
	#
	set title "Illustrate loading an array from a column in a data file\n".\
	          "Note that first data point in the file is 'line 0'\n".\
	          "but it goes into array element Data[1]"
	
	set yrange [0:20]
	set border 3
	set tics nomirror
	set key left center nobox
	
	FILE = 'orbital_elements.dat'
	
	stats FILE index 2 using 2 prefix "A"
	array Data[A_records]
	
	stats FILE index 2 using (Data[int($0+1)] = $2) prefix "B"
	
	plot FILE index 2 using 2 title "Data from file" with lines lw 2, \
	     Data using 0:2:(sprintf("Data[%d]",$1)) with labels point pt 6 ps 2 center offset 0,1 title "Data[i]"
	
	pause -1 "<cr> to continue"
	reset

snippet arrowstyle.dem
	#
	# $Id: arrowstyle.dem,v 1.5 2014/03/09 20:14:17 markisch Exp $
	#
	#
	set xrange [-1000:1000]
	set yrange [-178:86]
	set tics scale 0
	
	set style line 1 lt 1 lw 2
	set style line 2 lt 3 lw 2
	
	set style arrow 1 head filled size screen 0.025,30,45 ls 1
	set style arrow 2 head nofilled size screen 0.03,15 ls 2
	set style arrow 3 head filled size screen 0.03,15,45 ls 1
	set style arrow 4 head filled size screen 0.03,15 ls 2
	set style arrow 5 heads noborder size screen 0.03,15,135 ls 1
	set style arrow 6 head empty size screen 0.03,15,135 ls 2
	set style arrow 7 nohead ls 1
	set style arrow 8 heads size screen 0.008,90 ls 2
	
	print ' We have defined the following arrowstyles:'
	show style arrow
	
	set for [i=1:8] arrow from -500, (-90 - i * 10) to 500, (-90 - i * 10) as i
	set for [i=1:8] label sprintf('arrowstyle %i:', i) at -520, (-90 - i * 10) right
	
	set samples 50
	set angles rad
	pos(x) = 69 * sin(0.01 * x)
	len(x) = 49 * sin(0.01 * x + pi/2)
	
	do for [i=1:8] {
	    set title sprintf('Top: plot with vectors arrowstyle %i,  Bottom: explicit arrows', i)
	    plot '+' using 1:(pos(x)):(0):(len(x)) notitle with vectors arrowstyle i
	    pause -1 "Hit return to continue"
	}
	#reset
	
	#
	# Show plot with data style vectors
	#
	set title "Plot 'file' with vectors <arrowstyle>"
	set key box opaque
	set xrange [*:*]
	set yrange [*:10]
	set for [i=1:8] style arrow i lc i
	plot '1.dat' using 1:2:(+1):(+1) with vectors lt 4 filled title 'filled', \
	     '2.dat' using 1:2:(+1):(+1) with vectors lt 1 heads title 'double-headed', \
	     '2.dat' using ($1):(2-$2/3):(+1):(+2.5):(int($0)%8 + 1) with vectors as var ti 'arrowstyle variable'
	#
	pause -1 "Hi return to continue"
	#
	reset

snippet autoscale.dem
	#
	# $Id: autoscale.dem,v 1.2 2014/01/17 06:02:21 sfeam Exp $
	# Demo of constraint autoscaling: Autoscaling with limits.
	# 
	
	set multiplot layout 2, 2 title "Autoscaling with constraints (y-axis always unaffected)"
	
	unset key
	# set title offset 0,-0.4
	# set xlabel offset 0,0.4
	
	set ytics 5
	
	set title "unconstrained"
	set ylabel "[*:*]"
	set yrange [*:*]
	set xlabel "[*:*]"
	set xrange [*:*]
	plot "3.dat"
	
	set title "minimum range guaranteed"
	set ylabel "[*<-5:5<*]"
	set yrange [*<-5:5<*]
	set xlabel "[*<-40:30<*]"
	set xrange [*<-40:30<*]
	plot "3.dat"
	
	set title "clip to maximum range"
	set ylabel "[-20<*:*<20]"
	set yrange [-20<*:*<20]
	set xlabel "[-30<*:*<10]"
	set xrange [-30<*:*<10]
	plot "3.dat"
	
	set title "mixed"
	set xlabel "[-30<*<10:10<*<15]"
	set xrange [-30<*<10:10<*<15]
	set ylabel "[*:0<*<10]"
	set yrange [*:0<*<10]
	plot "3.dat"
	
	unset multiplot
	
	pause -1 "Hit return to continue"
	
	
	set multiplot layout 2, 2 title "Autoscaling with constraints (x-axis always unaffected)"
	
	unset key
	set ytics 5
	
	set title "unconstrained"
	set ylabel "[*:*]"
	set yrange [*:*]
	set xlabel "[*:*]" offset 0, 0.4
	set xrange [*:*]
	plot "3.dat"
	
	set title "minimum range guaranteed"
	set ylabel "[*<-15:20<*]"
	set yrange [*<-15:20<*]
	set xlabel "[*<-10:10<*]"
	set xrange [*<-10:10<*]
	plot "3.dat"
	
	set ytics 2
	
	set title "clip to maximum range"
	set ylabel "[-8<*:*<5]"
	set yrange [-8<*:*<5]
	set xlabel "[-40<*:*<30]"
	set xrange [-40<*:*<30]
	plot "3.dat"
	
	set title "mixed"
	set xlabel "[-30<*<10:-10<*<45]"
	set xrange [-30<*<10:-10<*<45]
	set ylabel "[-8<*<-6:7<*<9]"
	set yrange [-8<*<-6:7<*<9]
	plot "3.dat"
	
	unset multiplot
	
	pause -1 "Hit return to continue"
	unset xlabel
	unset ylabel
	
	set multiplot layout 2, 2 title "Autoscaling with constraints"
	
	set ytics 50
	
	set autoscale xy
	set title "autoscale xy"
	plot "silver.dat"
	
	set yrange [15<*<25:*]
	set title "set yrange [15<*<25:*]"
	plot "silver.dat"
	
	set autoscale ymin
	set title "set autoscale ymin"
	plot "silver.dat"
	
	set ytics 20
	set yrange [15<*<25:135<*<225]
	set title "set yrange [15<*<25:135<*<225]"
	plot "silver.dat"
	
	unset multiplot
	
	pause -1 "Hit return to continue"
	reset

snippet azimuth.dem
	set view equal xy
	set zzeroaxis; set xzeroaxis; set yzeroaxis
	set xyplane at 0
	unset border
	unset key
	unset xtics
	unset ytics
	set ztics axis
	
	set arrow 1 from 0,0,0 to 1,0,0 head filled lw 1.5
	set label 1 at 1.2,0,0 "X" center
	set arrow 2 from 0,0,0 to 0,1,0 head filled lw 1.5
	set label 2 at 0,1.2,0 "Y" center
	set arrow 3 from 0,0,0 to 0,0,21 head filled lw 1.5
	set label 3 at 0,0,23 "Z" center
	
	set view 60, 30, 1., 1.75
	
	set multiplot layout 1,3
	
	set view azimuth 0.
	set title 'azimuth 0' offset 0,2
	splot sample [t=0:20] '+' using (cos($1)):(sin($1)):($1) with lines lw 2
	
	set title 'azimuth 10' offset 0,2
	set view azimuth 10.
	replot
	
	set title 'azimuth 60' offset 0,2
	set view azimuth 60.
	replot
	
	unset multiplot
	pause -1 "Hit return to continue"
	reset

snippet barchart_art.dem
	#
	# Demo of using rescaled images to construct a bar chart
	#
	if (!strstrt(GPVAL_COMPILE_OPTIONS,"+GD_PNG")) \
	    print ">>> Skipping demo <<<\n" ; \
	    print "This copy of gnuplot was built without support for loading png images" ;\
	    exit ;
	
	reset
	set title "Building Code Height Limits"
	unset key
	
	set xrange [ -10 : 160 ]
	set yrange [   0 : 200 ]
	set y2range[   0 : 200 ]
	
	set y2tics
	set grid y
	
	set xtics   ("NE" 12.0, "S" 42.0, "Downtown" 72.0, "Suburbs" 127.0)  scale 0.0
	
	plot 'bldg.png' binary filetype=png origin=(60,0) dx=0.5 dy=1.5 with rgbimage, \
	     'bldg.png' binary filetype=png origin=(0,0) dx=0.5 dy=1 with rgbimage, \
	     'bldg.png' binary filetype=png origin=(30,0) dx=0.5 dy=0.7 with rgbimage, \
	     'bldg.png' binary filetype=png origin=(100,0) dx=0.5 dy=0.35 with rgbimage, \
	     'bldg.png' binary filetype=png origin=(125,0) dx=0.5 dy=0.35 with rgbimage
	
	pause -1 "Hit return to continue"
	reset

snippet binary.dem
	#
	# $Id: binary.dem,v 1.5 2003/10/28 05:35:54 sfeam Exp $
	#
	set style data lines
	set hidden3d
	set title "Hidden line removal of explicit binary surfaces"
	set ticslevel 0
	set key box
	
	set xrange [-3:3]
	set yrange [-2:2]
	splot "binary1" binary
	pause -1 "Hit return to continue (1)"
	
	set view 70,45
	set xrange [-3:3]
	set yrange [-3:3]
	splot "binary2" binary
	pause -1 "Hit return to continue (2)"
	
	set title "Notice that sampling rate can change"
	set vi 70,10
	set xrange [-3:6]
	set yrange [-3:6]
	splot "binary3" binary
	pause -1 "Hit return to continue (3)"
	
	reset

snippet bins.dem
	#
	# Demo illustrating the relationship between
	# a binned histogram and a kernel density model of the same data.
	#
	$DATA << EOD
	1	1
	2	1
	8	1
	9	1
	17	1
	17	1
	9	1
	9	1
	5	1
	7	1
	7	1
	8	1
	8	1
	8	1
	10	1
	11	1
	11	1
	12	1
	14	1
	3	1
	3	1
	3	1
	8	7
	15	1
	17	1
	17	1
	18	1
	19	1
	20	1
	EOD
	
	set title "Comparison of a binned histogram and\na kernel density model of the same data"
	
	set style data lines
	set xtics 1 norangelimit nomirror
	set grid y
	set yrange [0:5.5]
	set style fill solid 0.5 noborder
	set jitter spread 0.5
	
	plot $DATA using 1 bins=20 with boxes title '20 bins', \
	        '' using 1:(1) smooth kdensity bandwidth .5 lw 2 title 'smooth kdensity', \
	        '' using 1:(.9) with impulse lc "black" title 'jittered data'
	 
	pause -1 "Hit return to continue"
	reset

snippet bivariat.dem
	#
	# $Id: bivariat.dem,v 1.9 2011/08/01 05:14:23 sfeam Exp $
	#
	# This demo is very slow and requires unusually large stack size.
	# Do not attempt to run this demo under MSDOS.
	#
	
	# the function integral_f(x) approximates the integral of f(x) from 0 to x.
	# integral2_f(x,y) approximates the integral from x to y.
	# define f(x) to be any single variable function
	#
	# the integral is calculated using Simpson's rule as 
	#          ( f(x-delta) + 4*f(x-delta/2) + f(x) )*delta/6
	# repeated x/delta times (from x down to 0)
	#
	delta = 0.2
	#  delta can be set to 0.025 for non-MSDOS machines
	#
	# integral_f(x) takes one variable, the upper limit.  0 is the lower limit.
	# calculate the integral of function f(t) from 0 to x
	# choose a step size no larger than delta such that an integral number of
	# steps will cover the range of integration.
	integral_f(x) = (x>0)?int1a(x,x/ceil(x/delta)):-int1b(x,-x/ceil(-x/delta))
	int1a(x,d) = (x<=d*.1) ? 0 : (int1a(x-d,d)+(f(x-d)+4*f(x-d*.5)+f(x))*d/6.)
	int1b(x,d) = (x>=-d*.1) ? 0 : (int1b(x+d,d)+(f(x+d)+4*f(x+d*.5)+f(x))*d/6.)
	#
	# integral2_f(x,y) takes two variables; x is the lower limit, and y the upper.
	# calculate the integral of function f(t) from x to y
	integral2_f(x,y) = (x<y)?int2(x,y,(y-x)/ceil((y-x)/delta)): \
	                        -int2(y,x,(x-y)/ceil((x-y)/delta))
	int2(x,y,d) = (x>y-d*.5) ? 0 : (int2(x+d,y,d) + (f(x)+4*f(x+d*.5)+f(x+d))*d/6.)
	
	set autoscale
	set title "approximate the integral of functions"
	set samples 50
	set key bottom right
	
	f(x) = exp(-x**2)
	
	plot [-5:5] f(x) title "f(x)=exp(-x**2)", \
	  2/sqrt(pi)*integral_f(x) title "erf(x)=2/sqrt(pi)*integral_f(x)", \
	  erf(x) with points
	
	pause -1 "Hit return to continue"
	
	f(x)=cos(x)
	
	plot [-5:5] f(x) title "f(x)=cos(x)", integral_f(x)
	
	pause -1 "Hit return to continue"
	
	set title "approximate the integral of functions (upper and lower limits)"
	
	f(x)=(x-2)**2-20
	
	plot [-10:10] f(x) title "f(x)=(x-2)**2-20", integral2_f(-5,x)
	
	pause -1 "Hit return to continue"
	
	f(x)=sin(x-1)-.75*sin(2*x-1)+(x**2)/8-5
	
	plot  [-10:10] f(x) title "f(x)=sin(x-1)-0.75*sin(2*x-1)+(x**2)/8-5", integral2_f(x,1)
	
	pause -1 "Hit return to continue"
	
	#
	# This definition computes the ackermann. Do not attempt to compute its
	# values for non integral values. In addition, do not attempt to compute
	# its beyond m = 3, unless you want to wait really long time.
	
	ack(m,n) = (m == 0) ? n + 1 : (n == 0) ? ack(m-1,1) : ack(m-1,ack(m,n-1))
	
	set xrange [0:3]
	set yrange [0:3]
	
	set isosamples 4
	set samples 4
	
	set title "Plot of the ackermann function"
	
	splot ack(x, y)
	
	pause -1 "Hit return to continue"
	
	set xrange [-5:5]
	set yrange [-10:10]
	set isosamples 10
	set samples 100
	set key top right at 4,-3
	set title "Min(x,y) and Max(x,y)"
	
	#
	min(x,y) = (x < y) ? x : y
	max(x,y) = (x > y) ? x : y
	
	plot sin(x), x**2, x**3, max(sin(x), min(x**2, x**3))+0.5
	
	pause -1 "Hit return to continue"
	
	#
	# gcd(x,y) finds the greatest common divisor of x and y,
	#          using Euclid's algorithm
	# as this is defined only for integers, first round to the nearest integer
	gcd(x,y) = gcd1(rnd(max(x,y)),rnd(min(x,y)))
	gcd1(x,y) = (y == 0) ? x : gcd1(y, x - x/y * y)
	rnd(x) = int(x+0.5)
	
	set samples 59
	set xrange [1:59]
	set auto
	set key default
	
	set title "Greatest Common Divisor (for integers only)"
	
	plot gcd(x, 60) with impulses
	pause -1 "Hit return to continue"
	
	#
	# This definition computes the sum of the first 10, 100, 1000 fourier
	# coefficients of a (particular) square wave.
	
	set title "Finite summation of 10, 100, 1000 fourier coefficients"
	
	set samples 500
	set xrange [-10:10]
	set yrange [-0.4:1.2]
	set key bottom right
	
	fourier(k, x) = sin(3./2*k)/k * 2./3*cos(k*x)
	sum10(x)   = 1./2 + sum [k=1:10]   fourier(k, x)
	sum100(x)  = 1./2 + sum [k=1:100]  fourier(k, x)
	sum1000(x) = 1./2 + sum [k=1:1000] fourier(k, x)
	
	plot \
	    sum10(x)   title "1./2 + sum [k=1:10]   sin(3./2*k)/k * 2./3*cos(k*x)", \
	    sum100(x)  title "1./2 + sum [k=1:100]  sin(3./2*k)/k * 2./3*cos(k*x)", \
	    sum1000(x) title "1./2 + sum [k=1:1000] sin(3./2*k)/k * 2./3*cos(k*x)"
	pause -1 "Hit return to continue"
	
	reset
	

snippet bolditalic.dem
	set polar
	unset xtics
	unset ytics
	unset key
	set border 0
	
	set title "{/:Bold Enhanced text style markup}"
	set x2label "Default  {/Times:Bold=15 Bold ^{/:Italic Italic}} Default" offset 0,-2
	set xlabel 'Default  {/Times=18:Italic Italic _{/:Bold Bold} ^{/:Normal Normal}} Default'
	
	plot [t=0:4*pi] t with lines
	pause -1 "Hit return to continue"
	reset

snippet borders.dem
	#
	# $Id: borders.dem,v 1.12 2017/02/23 08:51:46 markisch Exp $
	#
	unset grid
	set xrange [0:10]
	set yrange [0:10]
	set format ""
	#
	set multiplot layout 4, 4 title "Demonstration of different border settings"
	do for [bb = 0:15] {
	    set border bb
	    show border
	    set label 1 sprintf("Border = %.0f",bb)  at 5,5 center
	    plot 1/0 notitle
	}
	unset multiplot
	
	pause -1 "Hit return to continue"
	reset

snippet boxclusters.dem
	#
	# The "histograms" style automates the construction of barcharts with
	# various stacking and clustering options.  But it doesn't offer much
	# choice for coloring.
	#
	# This demo show using the "with boxes" plot style to generate clustered
	# histograms with individual box coloring generated by a user-provided function.
	# This could, for example, depend on data from other columns in the input.
	#
	
	set title "Clustered bar graph with individual colors\nspecified vis plotstyle 'boxes'"
	set title  boxed offset 0,-3 font ",15"
	set style fill solid border lt -1
	set style textbox opaque noborder
	set boxwidth 1.0 abs
	ClusterSize = 15
	unset key
	
	set yrange [0:7]
	set xrange [-2:60]
	set border 0
	category = "Yan Tan Tethera Methera Pimp"
	set xtics scale 0 ()
	set ytics scale 0 nomirror
	set grid y
	set colorb horizontal user origin .05, .05 size .9, .05
	set palette cubehelix
	set bmargin at screen 0.2
	set tmargin at screen 0.9
	
	set for [i=1:4] xtics add (word(category,i) 5+(i-1)*ClusterSize)
	
	xcoord(i) =  i*ClusterSize + column(1)
	color(i)  = rand(0)
	
	plot for [i=0:3] 'candlesticks.dat' \
	     using (xcoord(i)):(column(i+2)):(color(i)) with boxes lc palette z
	
	pause -1 "<cr> to continue"
	reset
snippet boxplot.dem
	#
	# Boxplot demo
	#
	reset
	
	print "*** Boxplot demo ***"
	
	set style fill solid 0.5 border -1
	set style boxplot outliers pointtype 7
	set style data boxplot
	set boxwidth  0.5
	set pointsize 0.5
	
	unset key
	set border 2
	set xtics ("A" 1, "B" 2) scale 0.0
	set xtics nomirror
	set ytics nomirror
	set yrange [0:100]
	
	plot 'silver.dat' using (1):2, '' using (2):(5*$3)
	
	pause -1 'Hit <cr> to continue: Compare sub-datasets'
	
	# Comparing sub-datasets
	
	set xtics auto
	set yrange [*:*]
	set title "Distribution of energy usage of the continents, grouped by type of energy source\n"
	set ylabel "Billion Tons of Oil Equivalent"
	
	plot 'energy_circles.dat' using (1):($8/1.e6):(0):4
	
	pause -1 'Hit <cr> to continue: Assign selected colors to each factor'
	
	set linetype 51 lc "dark-red"
	set linetype 52 lc "dark-red"
	set linetype 53 lc "dark-red"
	set linetype 54 lc "midnight-blue"
	set linetype 55 lc "sea-green"
	set linetype 56 lc "sea-green"
	set linetype 57 lc "sea-green"
	
	set title "Distribution of energy usage of the continents, grouped by type of energy source,\n assign individual colors (linetypes) to the factors taken from column 4\n"
	
	plot 'energy_circles.dat' using (1):($8/1.e6):(0):4 lt 51 lc variable
	
	pause -1 'Hit <cr> to continue: Sort factors alphabetically'
	
	# Sort factors alphabetically
	
	set style boxplot sorted
	
	set title "Distribution of energy usage of the continents, sorted by name of energy source\n"
	
	plot 'energy_circles.dat' using (1):($8/1.e6):(0):4 lc rgb "gold"
	
	pause -1 'Hit <cr> to continue: The same, with iteration and manual filtering'
	
	# The same as above, with manual filtering
	# Note that you have to specify the factors and you have to set the xtics as well.
	# However, you have greater control over the appearance of the plot
	# e.g. the order of the boxplots, their colors, the tic labels
	# The previous form is intended for interactive usage while the latter form is better suited
	# to creating publication-ready graphs.
	# Note: we turn off autoscaling for the last two categories so that outliers do not
	# expand the range on y even though they are not shown.
	
	factors = "Nuclear Coal Gas Oil Hydroelectric Renewable"
	NF = words(factors)
	set xtic ("" 1)
	set for [i=1:NF] xtics add (word(factors,i) i) 
	set style boxplot nooutliers
	
	t(x) = x/1.e6
	filter(col, factor_col, level) = (strcol(factor_col) eq word(factors, level)) ? t(column(col)) : 1/0
	
	set title "Distribution of energy usage explicitly ordered by name of energy source\n"
	
	plot for [i=1:NF-2]  'energy_circles.dat' using (i):(filter(8, 4, i)), \
	     for [i=NF-1:NF] 'energy_circles.dat' using (i):(filter(8, 4, i)) noauto
	
	pause -1 'Hit <cr> to continue: boxplot demo finished'
	
	reset

snippet break_continue.dem
	#
	# Unit test for `break` and `continue` commands
	# Two different entry actions:
	# "load break_continue.dem" executes the main loop
	# "call break_continue.dem foo" executes the inner clause
	#
	if (ARGC == 0) {
	
	do for [i=1:6] {
	    print "start ", i;
	    call 'break_continue.dem' sub1
	    if (i > 4) { print ">>> ERROR <<<" }
	    print "end ", i
	}
	print "done with for loop"
	
	} else {
	
	print "   continue if i == 3, break if i > 4"
	if (i > 4) { break; }
	if (i == 3) { continue; print ">>> ERROR <<<" }
	print "      still in load file"
	exit	# exits only from this level of the call stack
	print ">>> failed to exit <<<"
	}

snippet callargs.dem
	#
	# This script exercises the ARG* variable-passing mechanism
	# used by the "call" command.  Note that the only difference between
	# "load" and "call" is that "load" has no parameters (ARGC = 0).
	#
	if (!exists("ARGC")) {
	    print "This copy of gnuplot does not support the ARG call method"
	    exit
	}
	print "\nEntering ", ARG0, " with ", ARGC, " parameters"
	if (ARGC == 0) {
	    undefine FOO
	    BAZ = 5.67
	    NOTAFUNCTION = "a string"
	    print "Now exercise the call mechanism at line ", GPVAL_LINENO
	    call ARG0 1.23e4 "string constant" FOO BAZ "3 + log(BAZ)" NOTAFUNCTION (1+3+4)
	} else {
	    print "\n\tTest whether this copy of gnuplot also supports deprecated"
	    print "\tcall parameter syntax "."$"."0 "."$"."1 "."$"."2 "."etc: "
	    print "\t\t", exists("$3") ? "yes" : "no"
	    show variable ARG
	    print "ARG1 (numerical constant) came through as ", ARG1
	    print "     @ARG1 = ", @ARG1
	    print "     (ARG1 == @ARG1) is ", (ARG1 == @ARG1) ? "TRUE" : "FALSE"
	    print "ARG2 (string constant) came through as ", ARG2
	    print "     words(ARG2) = ", words(ARG2)
	    print "ARG3 (undefined variable FOO) came through as ", ARG3
	    print "ARG4 (numerical variable BAZ=5.67) came through as ", ARG4
	    print "     @ARG4 = ", @ARG4
	    print "ARG5 (quoted expression) came through as ", ARG5
	    print "     @ARG5 = ", @ARG5
	    print "ARG6 (string variable) came through as ", ARG6
	    print "     words(ARG6) = ", words(ARG6)
	    print "ARG7 (expression) came through as ", ARG7
	}

snippet candlesticks.dem
	#
	# $Id: candlesticks.dem,v 1.5 2008/05/31 05:19:01 sfeam Exp $
	#
	
	reset
	#
	set xrange [0:11]
	set yrange [0:10]
	#
	set title "candlesticks with open boxes (default)"
	plot 'candlesticks.dat' using 1:3:2:6:5 with candlesticks
	#
	pause -1 "Hit return to continue"
	#
	set title "candlesticks with specified boxwidth"
	set boxwidth 0.2
	replot
	#
	pause -1 "Hit return to continue"
	#
	set title "candlesticks with style fill solid"
	set style fill solid
	set boxwidth 0.2
	replot
	#
	pause -1 "Hit return to continue"
	#
	set title "candlesticks showing both states of open/close"
	set style fill empty
	set boxwidth 0.2
	plot 'candlesticks.dat' using 1:(int($0)%3?$3:$5):2:6:(int($0)%3?$5:$3) with candlesticks title "open < close", \
	NaN with boxes lt 1 fs solid 1 title "close < open"
	#
	pause -1 "Hit return to continue"
	#
	set title "box-and-whisker plot adding median value as bar"
	set style fill empty
	plot 'candlesticks.dat' using 1:3:2:6:5 with candlesticks lt 3 lw 2 title 'Quartiles', \
	     ''                 using 1:4:4:4:4 with candlesticks lt -1 lw 2 notitle
	#
	pause -1 "Hit return to continue"
	#
	set title "box-and-whisker with median bar and whiskerbars"
	plot 'candlesticks.dat' using 1:3:2:6:5 with candlesticks lt 3 lw 2 title 'Quartiles' whiskerbars, \
	     ''                 using 1:4:4:4:4 with candlesticks lt -1 lw 2 notitle
	pause -1 "Hit return to continue"
	#
	     
	reset
	

snippet cerf.dem
	#
	# Exercise the special functions in external library libcerf.
	# These are only present if libcerf was found during configuration/build
	# of the current gnuplot executable.
	#
	if (!strstrt(GPVAL_COMPILE_OPTIONS,"+LIBCERF")) {
		print  "This copy of gnuplot was not linked against libcerf"
		exit;
	}
	
	save_encoding = GPVAL_ENCODING
	set encoding utf8
	
	set title "Voigt Profile VP(x,σ,γ)"
	set xrange [-10:10]
	set yrange [0.0:0.3]
	set xtics out nomirror
	set ytics in mirror format "%.2f"
	set mxtics
	set mytics
	set sample 200
	
	plot VP(x,1.53,0.0) title "σ=1.53 γ=0.00", \
	     VP(x,1.30,0.5) title "σ=1.30 γ=0.50", \
	     VP(x,1.00,1.0) title "σ=1.00 γ=1.00", \
	     VP(x,0.00,1.8) title "σ=0.00 γ=1.80"
	
	pause -1 "<cr> to continue"
	
	set yrange [-1:1]
	set title "Faddeeva/Voigt Function"
	set key samplen 0.5
	
	plot real( faddeeva(x) ) lw 2, \
	     imag( faddeeva(x) ) lw 2, \
	     real(cdawson(x)) lt -1 lw 0.5 title "Dawson's Integral"
	
	pause -1 "<cr> to continue"
	
	reset
	unset key
	set view map
	set bmargin at screen 0.1
	set sample 200
	set isosamples 200, 200
	set size ratio 1 1,1
	set cbtics   ("0" -pi, "2π" pi)
	set title "Complex error function cerf( x + iy )" 
	set auto xy
	set urange [ -3 : 3 ]
	set vrange [ -3 : 3 ]
	set tics scale 0
	set cblabel "Phase Angle" 
	set cblabel  offset character -2, 0, 0 font "" textcolor lt -1 rotate by -270
	set cbrange [ -pi : pi ] noreverse nowriteback
	
	set contour
	set cntrparam levels discrete 0.1, 0.2, 0.5,  1, 2, 5,  10, 20, 50,  100, 200, 500
	set cntrlabel onecolor
	
	set palette positive nops_allcF maxcolors 0 gamma 1.5 color model HSV 
	set palette defined ( 0 0 1 1, 1 1 1 1 )
	
	Hue(x,y) = (pi + atan2(-y,-x)) / (2*pi)
	phase(x,y) = hsv2rgb( Hue(x,y), sqrt(x**2+y**2), 1. )
	rp(x,y) = real(f(x,y))
	ip(x,y) = imag(f(x,y))
	color(x,y) = hsv2rgb( Hue( rp(x,y), ip(x,y) ), abs(f(x,y)), 1. )
	f(x,y) = cerf(x+y*{0,1})
	
	splot '++' using 1:2:(color($1,$2)) with pm3d lc rgb variable nocontour, \
	      '++' using 1:2:(abs(cerf($1+$2*{0,1}))) with lines nosurf lt -1
	
	pause -1 "<cr> to continue"
	reset
	set encoding save_encoding

snippet charset.dem
	#
	# Dump all (8-bit) characters by octal code value
	#
	set title "Complete dump of characters for this font and encoding"
	#
	set border 0
	unset xtics
	unset ytics
	#
	set label 11 "001-040:" at graph 0.02, 0.9
	set label 12 "041-100:" at graph 0.02, 0.8
	set label 13 "101-140:" at graph 0.02, 0.7
	set label 14 "141-200:" at graph 0.02, 0.6
	set label 15 "201-240:" at graph 0.02, 0.5
	set label 16 "241-300:" at graph 0.02, 0.4
	set label 17 "301-340:" at graph 0.02, 0.3
	set label 18 "341-376:" at graph 0.02, 0.2
	#
	set label 1 at graph 0.2, 0.9
	set label 2 at graph 0.2, 0.8
	set label 3 at graph 0.2, 0.7
	set label 4 at graph 0.2, 0.6
	set label 5 at graph 0.2, 0.5
	set label 6 at graph 0.2, 0.4
	set label 7 at graph 0.2, 0.3
	set label 8 at graph 0.2, 0.2
	#
	set label 1 "\001\002\003\004\005\006\007\010\011 \013\014\015\016\017\020\021\022\023\024\025\026\027\030\031\032\033\034\035\036\037\040"
	set label 2 "\041\042\043\044\045\046\047\050\051\052\053\054\055\056\057\060\061\062\063\064\065\066\067\070\071\072\073\074\075\076\077\100"
	set label 3 "\101\102\103\104\105\106\107\110\111\112\113\114\115\116\117\120\121\122\123\124\125\126\127\130\131\132\133\134\135\136\137\140"
	set label 4 "\141\142\143\144\145\146\147\150\151\152\153\154\155\156\157\160\161\162\163\164\165\166\167\170\171\172\173\174\175\176\177\200"
	set label 5 "\201\202\203\204\205\206\207\210\211\212\213\214\215\216\217\220\221\222\223\224\225\226\227\230\231\232\233\234\235\236\237\240"
	set label 6 "\241\242\243\244\245\246\247\250\251\252\253\254\255\256\257\260\261\262\263\264\265\266\267\270\271\272\273\274\275\276\277\300"
	set label 7 "\301\302\303\304\305\306\307\310\311\312\313\314\315\316\317\320\321\322\323\324\325\326\327\330\331\332\333\334\335\336\337\340"
	set label 8 "\341\342\343\344\345\346\347\350\351\352\353\354\355\356\357\360\361\362\363\364\365\366\367\370\371\372\373\374\375\376"
	#
	set xrange [-1:1]
	set yrange [-1:1]
	plot -10 notitle
	reset

snippet circles.dem
	#
	# demo for the use of "set object circle" and "plot ... with circles"
	#
	# Ethan A Merritt, Ralf Juengling - 2007,2008
	
	set size ratio -1
	set style fill solid 1.0 border -1
	
	set obj 10 circle   arc [  0 :  20] fc rgb "red" 
	set obj 11 circle   arc [ 20 :  50] fc rgb "orange" 
	set obj 12 circle   arc [ 50 :  90] fc rgb "yellow" 
	set obj 13 circle   arc [ 90 : 120] fc rgb "forest-green" 
	set obj 14 circle   arc [120 : 190] fc rgb "dark-turquoise" 
	set obj 15 circle   arc [190 : 360] fc rgb "dark-magenta" 
	
	set obj 10 circle at screen .18,.32   size screen .10  front
	set obj 11 circle at screen .18,.32   size screen .10  front
	set obj 12 circle at screen .18,.32   size screen .10  front
	set obj 13 circle at screen .1767,.342   size screen .10  front
	set obj 14 circle at screen .18,.32   size screen .10  front
	set obj 15 circle at screen .18,.32   size screen .10  front
	
	set obj 20 rect from graph 0,0 to graph 1,1 behind fc rgb "cyan" fs solid 0.2
	
	# plot world map and correspondent locations as a circle
	set title ""
	set xlabel "Note that overlapping transparent circles produce a darker area"
	unset key
	unset xtics
	unset ytics
	set border
	
	set yrange [-70:*]
	
	plot 'world.dat' with filledcurves lc rgb "light-green" , \
	     'world.cor' using 1:2:(7.*rand(0)) with circles lt 3 \
	                 fs transparent solid 0.5 noborder
	
	pause -1 "Hit return to continue"
	
	# by Peter Juhasz - 2010
	
	reset
	
	set style fill solid 1.0 border -1
	set size ratio -1
	
	# Plot pie charts on top of the map of the world.
	# The pie segments are read from a data file.
	# Each segment has to be specified with a start and end angle.
	
	set title "Sources of energy production, plotted for each continent"
	
	unset key
	unset xtics
	unset ytics
	set border
	
	set yrange [-70:*]
	set palette model HSV func gray*0.75, 0.5, 0.99
	unset colorbox
	r = 0.01
	types = 6
	keyx = -137.
	keyy = -15.
	keyr = 25.
	
	set obj 20 rect from graph 0,0 to graph 1,1 behind fc rgb "cyan" fs solid 0.2
	set angle degree
	
	plot 'world.dat' with filledcurves lc rgb "light-green" notit, \
	     'energy_circles.dat' using 2:1:(sqrt($9)*r):6:($6+$7):5 \
	            with circles lc pal fs solid 1.0 border rgb "gray",\
	     for [i=0:types-1] '' using (keyx):(keyy):(keyr-5):(-(i+1)*360./types):(-(i)*360./types):5 \
	            every ::i::i with circles lc pal fs solid 1.0 border rgb "gray",\
	     for [i=0:types-1] '' using \
	            (keyx+keyr*cos(-(i+0.5)*360./types)):(keyy+keyr*sin(-(i+0.5)*360./types)):4 \
	            every ::i::i with labels
	            
	
	
	pause -1 "Hit return to continue"
	
	
	# by Ralf Juengling - 2008
	
	reset
	set title "Trace of unconstrained optimization with trust-region method"
	unset key
	set size ratio -1
	set xrange [-2.5:1.5]
	set yrange [-1:2.5]
	plot 'optimize.dat' with circles lc rgb "blue" fs transparent solid 0.15 noborder,\
	     'optimize.dat' u 1:2 with linespoints lw 2 lc rgb "black"
	
	pause -1 "Hit return to continue"
	
	
	reset
	set size ratio -1
	set title "Lena's key points"
	unset xtics
	unset ytics
	unset key
	unset border
	set yrange [] reverse
	
	plot 'lena.rgb' binary array=(128,128) dx=4 dy=4 format='%uchar' with rgbimage, \
	     'lena-keypoints.bin' binary format='%double' with circles lc rgb "yellow"
	
	pause -1 "Hit return to continue"
	
	
	reset
	set size ratio -1
	set title "Delaunay triangulation of Hemisphere points, some empty circles in red"
	unset key
	
	plot 'empty-circles.dat' with circles lw 2 lc rgb "red", \
	     'delaunay-edges.dat' with lines lc rgb "forest-green", \
	     'hemisphr.dat' u (100*$1):(100*$2) with points pt 7 lc rgb "black"
	
	pause -1 "Hit return to continue"
	reset

snippet cities.dem
	#
	# Demonstrates how to derive variable font size from a data file column.
	# 
	# If you are viewing this via the HTML canvas terminal, be sure to toggle
	# the font scaling icon so that the fonts change size as you zoom in.
	#
	Scale(size) = 0.25*sqrt(sqrt(column(size)))
	CityName(String,Size) = sprintf("{/=%d %s}", Scale(Size), stringcolumn(String))
	
	set termoption enhanced
	save_encoding = GPVAL_ENCODING
	set encoding utf8
	unset xtics
	unset ytics
	unset key
	set border 0
	set size square
	set datafile separator "\t"
	plot 'cities.dat' using 5:4:($3 < 5000 ? "-" : CityName(1,3)) with labels
	pause -1 "hit return to continue"
	set encoding save_encoding
	reset

snippet clipobject.dem
	#
	# Demo and test for clipping applied to objects.
	# axis coordinates run from [-1:1]
	# graph coordinates from from [0:1]
	# screen coordinates from from [0.2:0.8]
	#
	reset
	
	set title "Default object clipping"
	set label 1 at graph 0.5, 0.9 center
	set label 1 "Outer = screen coords\nMiddle = graph coords\nInner = axis coords"
	
	set xrange[-1:1]
	set yrange[-1:1]
	set macros
	fs="fillstyle solid 1.0 border lc rgb 'black' lw 2"
	
	set lmargin at screen 0.2
	set rmargin at screen 0.8
	set tmargin at screen 0.8
	set bmargin at screen 0.2
	
	set object 1 rectangle from screen 0.1,screen 0.1 to screen 0.4, screen 0.4
	set object 1 fc rgb '#880000' @fs
	
	set object 11 rectangle from first -1.1,first -1.1 to first -0.9, first -0.9
	set object 11 fc rgb 'red' @fs
	set object 11 front
	
	set object 111 rectangle from graph -.1, -.1 to graph .1,.1
	set object 111 fc rgb "#DDAA00' @fs
	
	
	set object 2 circle at screen 0.2, screen 0.8 radius screen 0.1
	set object 2 fc rgb '#008800' @fs
	
	set object 22 circle at first -1, 1 radius first .15
	set object 22 fc rgb 'green' @fs
	set object 22 front
	
	set object 222 circle at graph 0, graph 1 radius graph 0.1
	set object 222 fc rgb '#00DDAA' @fs
	
	
	set object 3 polygon from screen 0.6,screen 0.6 rto 0.4,0 rto -0.2,0.4
	set object 3 fc rgb '#BBBB00' @fs
	
	set object 33 polygon from first 0.8,first 0.8 rto first 0.4,0  rto first -0.2, 0.4
	set object 33 fc rgb 'yellow' @fs
	set object 33 front
	
	set object 333 polygon from graph .8,graph .8 rto graph 0.4,0 rto graph -0.2, 0.4
	set object 333 fc rgb '#AAAA88' @fs
	
	set object 4 ellipse at screen 0.8, screen 0.2 size screen 0.3, 0.25
	set object 4 fc rgb '#4444bb' @fs
	
	set object 44 ellipse at first 1, -1 size first 0.4, first 0.25
	set object 44 fc rgb "cyan" @fs
	set object 44 front
	
	set object 444 ellipse at graph 1,graph 0 size first 0.8, first 0.5
	set object 444 fc rgb '#6666ff' @fs
	
	
	plot 0
	
	pause -1 "Hit return to continue"
	
	set title "Object property 'noclip'"
	
	do for [i=1:4]{ set object i noclip; set object i*11 noclip; set object i*111 noclip; }
	
	plot 0
	
	pause -1 "Hit return to continue"
	
	set title "Clipping in 3D projection ('set view map')"
	do for [i=1:4]{ set object i clip; set object i*11 clip; set object i*111 clip; }
	set view map
	set zrange [.1:100]
	set log z
	splot 0
	
	pause -1 "Hit return to continue"
	
	undefine fs
	reset

snippet colorscheme.dem
	#
	# Demo of color usage for principle plot elements
	#
	
	# Set canvas to a parchment color
	set obj 1 rectangle from screen 0,0 to screen 1,1 behind fc rgb "#e7dfcf"
	
	# Set plot area to light gray
	set obj 2 rectangle from graph 0,0 to graph 1,1 behind fc rgb "#dfdfdf"
	
	# border and tics in blue
	set border linetype 3 linewidth 3
	
	# key box and title in magenta, 
	set key box lt 4 title "Key Box"
	
	# Apply the plot linetype color to the corresponding key entry title
	set key textcolor rgb variable
	set key left
	
	set title "Set new colors for principle plot elements"
	plot sin(x)/x lw 2, sin(x/2)/(x/2) lw 2 lt 4
	
	pause -1 "Hit return to continue"
	reset

snippet colorwheel.dem
	#
	# Set palette to HSV color wheel and define common colors as macros
	# This allows commands like
	#	plot sin(x) with lines @orange
	#
	set palette mode HSV
	set palette defined ( 0 0 1 1, 1 1 1 1 )
	set cbrange [0:1]
	red = "lt pal frac 0"
	orange = "lt pal frac 0.10"
	yellow = "lt pal frac 0.16"
	green = "lt pal frac 0.33"
	cyan = "lt pal frac 0.5"
	blue = "lt pal frac 0.66"
	violet = "lt pal frac 0.79"
	magenta = "lt pal frac 0.83"
	black = "lt -1"
	set macros

snippet complex_trig.dem
	#
	# Set up a color mapping for vectors in the complex plane similar to the one
	# used on Wikipedia for plotting complex trigonometric functions.
	# HSV colors
	#	Hue is the vector angle atan( Real(f) / Imaginary(f) )
	#	Saturation is the vector magnitude (length)
	#	V = 1
	#
	save_encoding = GPVAL_ENCODING
	set encoding utf8
	
	# We don't use the palette for plotting, but defining it allows us to
	# draw a colorbar showing the phase angle color scheme
	set palette model HSV defined ( 0 0 1 1, 1 1 1 1 )
	set cbrange [-pi : pi]
	set cbtics ("0" -pi, "2π" pi)
	set cblabel "Phase Angle" rotate offset -2,0
	
	Hue(x,y) = (pi + atan2(-y,-x)) / (2*pi)
	phase(x,y) = hsv2rgb( Hue(x,y), sqrt(x**2+y**2), 1. )
	
	set xrange [-pi/2. : pi/2.]
	set yrange [-pi/2. : pi/2.]
	set urange [-pi/2. : pi/2.]
	set vrange [-pi/2. : pi/2.]
	set xtics ("-π/2" -pi/2., "-π/4" -pi/4., "0" 0, "π/4" pi/4., "π/2" pi/2.)
	set ytics ("-π/2" -pi/2., "-π/4" -pi/4., "0" 0, "π/4" pi/4., "π/2" pi/2.)
	
	set view map; set size square; unset key
	set isosamples 100,100
	
	set title "Color (Hue) indicates angle\nSaturation indicates amplitude"
	splot '++' using 1:2:(phase($1,$2)) with pm3d lc rgb variable
	
	pause -1
	
	rp(x,y) = real(f(x,y))
	ip(x,y) = imag(f(x,y))
	color(x,y) = hsv2rgb( Hue( rp(x,y), ip(x,y) ), abs(f(x,y)), 1. )
	
	set title "asin( x + iy )"
	f(x,y) = asin(x + y*{0,1})
	splot '++' using 1:2:(color($1,$2)) with pm3d lc rgb variable
	pause -1
	
	set title "acos( x + iy )"
	f(x,y) = acos(x + y*{0,1})
	splot '++' using 1:2:(color($1,$2)) with pm3d lc rgb variable
	pause -1
	
	set title "atan( x + iy )"
	f(x,y) = atan(x + y*{0,1})
	splot '++' using 1:2:(color($1,$2)) with pm3d lc rgb variable
	pause -1
	
	set title "sinh( x + iy )"
	f(x,y) = sinh(x + y*{0,1})
	splot '++' using 1:2:(color($1,$2)) with pm3d lc rgb variable
	pause -1
	
	set title "cosh( x + iy )"
	f(x,y) = cosh(x + y*{0,1})
	splot '++' using 1:2:(color($1,$2)) with pm3d lc rgb variable
	pause -1
	
	set title "tanh( x + iy )"
	f(x,y) = tanh(x + y*{0,1})
	splot '++' using 1:2:(color($1,$2)) with pm3d lc rgb variable
	pause -1
	
	set title "asinh( x + iy )"
	f(x,y) = asinh(x + y*{0,1})
	splot '++' using 1:2:(color($1,$2)) with pm3d lc rgb variable
	pause -1
	
	set title "acosh( x + iy )"
	f(x,y) = acosh(x + y*{0,1})
	splot '++' using 1:2:(color($1,$2)) with pm3d lc rgb variable
	pause -1
	
	set title "atanh( x + iy )"
	f(x,y) = atanh(x + y*{0,1})
	splot '++' using 1:2:(color($1,$2)) with pm3d lc rgb variable
	pause -1
	
	#
	# The final plot is atanh() again, but this time we represent
	# the magnitude as a z-coordinate.  The angle in the complex
	# plane is colored as before.
	#
	
	set view 66, 336, 1.2, 1.2
	set view equal xyz
	set colorbox user origin 0.85, 0.2
	unset ztics
	set zlabel "magnitude" rotate offset 3
	set xlabel "Real" rotate parallel offset 0,-2
	set ylabel "Imaginary" rotate parallel offset 0,-2
	set grid x y
	set xyplane at 0.0
	set border -1
	
	splot '++' using 1:2:(abs(f($1,$2))):(color($1,$2)) with pm3d lc rgb variable
	
	pause -1
	
	#
	set encoding save_encoding
	reset

snippet contours.dem
	#
	# $Id: contours.dem,v 1.18 2017/02/16 23:53:13 sfeam Exp $
	#
	set samples 20
	set isosamples 21
	set xlabel "X axis"
	set ylabel "Y axis"
	set zlabel "Z " offset 1, 0
	set view 60, 30, 0.85, 1.1
	set key at screen 1.0, 0.9
	set style textbox opaque noborder margins 0.5, 0.5
	
	set title "contour plot"
	set contour
	splot x*y
	pause -1 "Hit return to continue (1)"
	
	set cntrparam levels 15
	set title "more contours (15 levels)"
	replot
	pause -1 "Hit return to continue (2)"
	
	set cntrparam levels incr -100,10,100
	set title "contour by increments (every 10, starting at -100)"
	replot
	pause -1 "Hit return to continue (3)"
	
	set cntrparam levels disc -75,-50,0
	set title "discrete set of contours (at -75, -50, 0)"
	replot
	pause -1 "Hit return to continue (4)"
	
	set cntrparam levels auto 10
	set view 60, 30, 1.0, 1.1
	set title "contours on base grid with labels"
	set contour base
	set cntrlabel font ",7"
	splot x**2-y**2 with lines, x**2-y**2 with labels boxed notitle
	pause -1 "Hit return to continue (5)"
	
	set title "contours drawn on surface"
	set contour surface
	splot x**2-y**2 with lines notitle, x**2-y**2 with lines nosurf lw 3
	pause -1 "Hit return to continue (6)"
	
	set title "contours on both base and surface"
	set contour both
	set hidden3d
	splot x**2-y**2 with lines, x**2-y**2 with labels boxed notitle
	pause -1 "Hit return to continue (7)"
	unset hidden3d
	
	set contour base
	set title "2 surfaces"
	set key opaque
	splot x**2*y**3, x**3*y**2
	pause -1 "Hit return to continue (8)"
	
	set title "some more interesting contours"
	unset key
	splot x*y / (x**2 + y**2 + 0.1)
	pause -1 "Hit return to continue (9)"
	
	set zrange [-1.0:1.0]
	splot [x=-3:3] [y=-3:3] sin(x) * cos(y)
	pause -1 "Hit return to continue (10)"
	
	set samples 6
	set isosamples 6
	set cntrparam levels 5
	set title "low resolution (6x6)"
	replot
	pause -1 "Hit return to continue (11)"
	
	set title "low resolution (6x6) using cubic splines"
	set cntrparam cubic
	replot
	pause -1 "Hit return to continue (12)"
	
	set cntrparam bspline
	set title "low resolution (6x6) using bspline approx."
	replot
	pause -1 "Hit return to continue (13)"
	
	set cntrparam order 8
	set title "low resolution (6x6) raise bspline order."
	replot
	pause -1 "Hit return to continue (14)"
	
	set samples 25
	set isosamples 26
	set key default at screen 1.0, 0.9
	set title "contour of Sinc function"
	splot [-5:5.01] [-5:5.01] sin(sqrt(x**2+y**2)) / sqrt(x**2+y**2)
	pause -1 "Hit return to continue (15)"
	
	splot [-12:12.01] [-12:12.01] sin(sqrt(x**2+y**2)) / sqrt(x**2+y**2)
	pause -1 "Hit return to continue (16)"
	
	set cntrparam levels 10
	set auto
	set zrange [-1.0:1.0]
	set style data lines
	set title "contour of data grid plotting"
	set parametric
	splot "glass.dat"
	pause -1 "Hit return to continue (17)"
	
	set xrange [0:15]
	set yrange [0:15]
	set zrange [-1.2:1.2]
	unset parametric
	splot "glass.dat" using 1
	pause -1 "Hit return to continue (18)"
	
	set view map
	unset key
	unset surface
	set title "2D contour projection of previous plot"
	set cntrlabel start 2 font ",7"
	splot "glass.dat" using 1 with lines, \
	      "glass.dat" using 1 with labels boxed
	pause -1 "Hit return to continue (19)"
	
	#
	# The Rosenbrock function [HH Rosenbrock (1960) The Computer Journal 3:175–184]
	#    (1-x)**2 + 100 * (y - x**2)**2
	# is a well-known difficult test case for general purpose minimizers. 
	#
	set auto
	set surface
	unset contour
	set cntrparam levels 5
	set cntrparam linear
	set samples 100
	set logscale z
	set hidden3d
	set isosamples 60
	set ticslevel 0.
	set view 20,340	#HBB: ,1,2
	set xlabel "x"
	set ylabel "y"
	Rosenbrock(x,y) = (1-x)**2 + 100*(y - x**2)**2
	
	set title "Rosenbrock Function"
	splot [-1.5:1.5] [-0.5:1.5] Rosenbrock(x,y)
	pause -1 "Hit Return to Continue (20)"
	
	set contour
	unset surface
	unset ztics
	unset zlabel
	set border 15
	replot
	pause -1 "Hit Return to Continue (21)"
	
	#
	set title "All contours drawn in a single color"
	set cntrlabel onecolor
	set cntrparam bspline
	splot [-1.5:1.5] [-0.5:1.5] Rosenbrock(x,y) lc rgb "#007700"
	pause -1 "Hit Return to Continue (22)"
	
	set title "Sometimes it helps to use multiplot"
	set view map
	set xr [-0.5:1.5]
	set yr [0:1.5]
	unset xlabel
	unset ylabel
	set tics scale 0.0
	set lmargin at screen 0.1
	set rmargin at screen 0.9
	set bmargin at screen 0.1
	set tmargin at screen 0.9
	
	set multiplot
	
	set cntrparam levels discrete 0.1, 1.0, 10.0, 100.0
	set isosamples 500,100
	splot Rosenbrock(x,y) with lines lc rgb "#007700"
	
	set isosamples 50,50
	set cntrlabel start 25 interval -1 font ",7"
	splot Rosenbrock(x,y) with labels boxed
	
	unset multiplot
	pause -1 "Hit Return to Continue (23)"
	
	#
	# Clean up:
	#
	reset

snippet controls.dem
	#
	# $Id: controls.dem,v 1.3 1999/10/17 19:16:58 lhecking Exp $
	#
	# warning:  this demo is SLOW on PCs without math coprocessors!
	#
	# From _Automatic_Control_Systems_, fourth ed., figure 6-14
	# transient response of a second-order system to a unit step input function
	#
	damp(t) = exp(-s*wn*t)/sqrt(1.0-s*s)
	per(t) = sin(wn*sqrt(1.0-s**2)*t - atan(-sqrt(1.0-s**2)/s))
	c(t) = 1-damp(t)*per(t)
	#
	#	wn is natural undamped frequency
	#	s is damping factor
	#
	wn = 1.0
	set xrange [0:13]
	set samples 50
	set dummy t
	set key box
	#
	# plot c(t) for several different damping factors s
	#
	plot s=.1,c(t),s=.3,c(t),s=.5,c(t),s=.7,c(t),s=.9,c(t),s=1.0,c(t),s=1.5,c(t),s=2.0,c(t)
	pause -1 "Hit return to continue"
	reset
	

snippet custom_key.dem
	#
	# Illustrate custom placement of individual plot titles outside the key box
	#
	set style data boxes
	set style fill solid border
	set boxwidth 0.5
	set datafile missing '-'
	set yrange [0:*]
	unset ytics
	unset xtics
	set xrange [-0.5:7.5]
	set offset 0,0,graph .1,0
	
	set label 1 at screen 0.75, screen 0.22 "{/:Bold Custom combined key area}" center
	set key autotitle columnhead nobox title "  "
	set multiplot layout 3,2 columnsfirst \
	              title "{/:Bold Illustrate use of a custom key area}" \
	              margins 0.05, 0.95, 0.05, 0.90 spacing 0.0, 0.0
	
	plot 'immigration.dat' using 0:6   lt 1 title at 0.75, 0.18
	plot 'immigration.dat' using 0:12  lt 2 title at 0.75, 0.15
	plot 'immigration.dat' using 0:13  lt 3 title at 0.75, 0.12
	plot 'immigration.dat' using 0:14  lt 4 title at 0.75, 0.09
	set xtics scale 0 font ",8"
	
	plot 'immigration.dat' using 0:($6+$12+$13+$14):xtic(strcol(1)[6:]) with linespoints \
	                                   lt black pt 7 title "total"
	
	unset multi
	
	if (exists("MANUAL_FIGURES")) exit
	pause -1 "Hit return to continue"
	
	reset

snippet dashcolor.dem
	#
	# Demonstrate explicit choice of both dot/dash pattern (linetype) and color (linecolor).
	#
	set termoption dash
	save_encoding = GPVAL_ENCODING
	set encoding utf8
	
	reset
	set xrange [-0.5:3.5]
	set yrange [-1:1.4]
	set bmargin 7
	unset ytics
	unset xtics
	#
	set title "Independent colors and dot/dash styles"
	unset colorbox
	#
	# reset linetypes to base dash patterns
	#
	set for [i=1:5] linetype i dt i
	
	#
	# define line styles using explicit rgbcolor names
	#
	set style line 1 lt 2 lc rgb "red" lw 3
	set style line 2 lt 2 lc rgb "orange" lw 2
	set style line 3 lt 2 lc rgb "yellow" lw 3
	set style line 4 lt 2 lc rgb "green" lw 2
	
	#
	set label 1 'set style line 1 lt 2 lc rgb "red" lw 3'    at -0.4, -0.25 tc rgb "red"
	set label 2 'set style line 2 lt 2 lc rgb "orange" lw 2' at -0.4, -0.35 tc rgb "orange"
	set label 3 'set style line 3 lt 2 lc rgb "yellow" lw 3' at -0.4, -0.45 tc rgb "yellow"
	set label 4 'set style line 4 lt 2 lc rgb "green" lw 2'  at -0.4, -0.55 tc rgb "green"
	set label 5 'plot ... lt 1 lc 3 ' at -0.4, -0.65 tc lt 3
	set label 6 'plot ... lt 3 lc 3 ' at -0.4, -0.75 tc lt 3
	set label 7 'plot ... lt 5 lc 3 ' at -0.4, -0.85 tc lt 3
	#
	set xlabel "You will only see dashed lines if your current terminal supports it"
	#
	show style line
	#
	# draw some plots
	#
	plot cos(x)     ls 1 title 'ls 1',   \
	     cos(x-.2)  ls 2 title 'ls 2',\
	     cos(x-.4)  ls 3 title 'ls 3',\
	     cos(x-.6)  ls 4 title 'ls 4', \
	     cos(x-.8)  lt 1 lc 3 title 'lt 1 lc 3',  \
	     cos(x-1.)  lt 3 lc 3 title 'lt 3 lc 3',  \
	     cos(x-1.2) lt 5 lc 3 title 'lt 5 lc 3'
	
	#
	pause -1 "Hit return to continue"
	
	unset for [i=1:8] label i
	set title "The pointinterval property is another way to create interrupted lines"
	set xlabel "This technique works best for equally spaced data points"
	set bmargin 6
	set offset .05, .05
	set xrange [-0.5:3.3]
	set style func linespoints
	
	plot cos(x)     lt -1 pi -4 pt 6 title 'pi -4',   \
	     cos(x-.8)  lt -1 pi -3 pt 7 ps 0.2 title 'pi -3 pt 7 ps 0.2',  \
	     cos(x-.2)  lt -1 pi -6 pt 7 title 'pi -6',\
	     cos(x-.4)  lt -1 pi -3 pt 4 title 'pi -3',\
	     cos(x-.6)  lt -1 pi -5 pt 5 title 'pi -5', \
	     cos(x-1.)  with line lt -1 notitle,  \
	     cos(x+.2)  with line lt -1 lw 2 title 'lw 2'
	
	#
	pause -1 "Hit return to continue"
	
	set title "The pointinterval property also works with character point symbols"
	set style data linespoints
	set pointintervalbox 1.5
	unset xlabel
	
	plot '+' using 1:(cos(x-1.))  with line lt -1 lw 1 title 'lw 1',  \
	     '+' using 1:(cos(x))     lt -1 pi -4 pt "C" title 'pi -4',   \
	     '+' using 1:(cos(x-.8))  lt -1 pi -3 pt "D" ps 0.2 title 'pi -3 pt "D" ps 0.2',  \
	     '+' using 1:(cos(x-.2))  lt -1 pi -6 pt "✩" title 'pi -6',\
	     '+' using 1:(cos(x-.4))  lt -1 pi -3 pt "✲" title 'pi -3',\
	     '+' using 1:(cos(x-.6))  lt -1 pi -5 pt "☺" title 'pi -5', \
	     '+' using 1:(cos(x+.2))  with line lt -1 lw 2 title 'lw 2'
	
	pause -1 "Hit return to continue"
	
	set encoding save_encoding
	reset

snippet dashtypes.dem
	#
	# Exercise various dashtype options
	# (Version 5 only)
	#
	set border 0
	unset xtics
	unset ytics
	unset key
	set yrange [0:1]
	
	y = 0.9
	set label 101 at screen 0.1, screen y "Terminal's native dashtypes"
	do for [i=1:10] {
	    y = y - 0.05
	    set arrow i from screen 0.25, screen y to screen 0.8, screen y nohead lw 1.5 lt 2 dt i
	    set label i "dt ".i at screen 0.15, screen y
	}
	y = y - 0.05
	set label 102 at screen 0.1, screen y "Custom dashtypes"
	y = y - 0.05
	    new = "."
	    set label at screen 0.15, screen y 'dt "'.new.'"'
	    set arrow 11 from screen 0.25, screen y to screen 0.8, screen y nohead lw 1.5 lt 3 dt new
	y = y - 0.05
	    new = "-"
	    set label at screen 0.15, screen y 'dt "'.new.'"'
	    set arrow 12 from screen 0.25, screen y to screen 0.8, screen y nohead lw 1.5 lt 3 dt new
	y = y - 0.05
	    new = "._"
	    set label at screen 0.15, screen y 'dt "'.new.'"' noenh
	    set arrow 13 from screen 0.25, screen y to screen 0.8, screen y nohead lw 1.5 lt 3 dt new
	y = y - 0.05
	    new = "..- "
	    set label at screen 0.15, screen y 'dt "'.new.'"'
	    set arrow 14 from screen 0.25, screen y to screen 0.8, screen y nohead lw 1.5 lt 3 dt new
	y = y - 0.05
	    set arrow 15 from screen 0.25, screen y to screen 0.8, screen y nohead lw 1.5 lc "blue'
	    set arrow 15 dt (50,6,2,6)
	    set label at screen 0.10, screen y 'dt (50,6,2,6)'
	
	
	plot 0 with lines lt nodraw
	
	pause -1 "Hit return to continue"
	
	# Now test effect of linewidth
	unset arrow 15
	unset label 15
	
	set for [i=1:14] arrow i lw 1+(i-1)%5
	
	plot 0 with lines lt nodraw
	
	pause -1 "Hit return to continue"
	
	reset

snippet datastrings.dem
	#
	# Demonstrate features of datastrings patch
	# Ethan A Merritt <merritt@u.washington.edu>
	# 08-Mar-2003
	#
	reset
	#
	# Illustrate reading plot labels from column head in datafile
	#
	set title "Auto-labeling plots from text fields in datafile"
	set label 1 "Generate plot labels from first row in each column"
	set label 1 at graph 0.02, 0.85 tc lt 3
	set label 2 "Generate x-axis labels from first column in each row"
	set label 2 at graph 0.02, 0.80 tc lt 3
	set style data linespoints
	set ylabel "mm"
	set yrange [0:200]
	#
	set key autotitle columnhead
	plot 'ctg-y2.dat' using 2:xticlabel(1) index 2, \
	     ''           using 2 index 3 
	#
	pause -1 "<cr> to plot again using x2ticlabels"
	#
	# Illustrate reading xtic axis labels from the datafile
	#
	reset
	set title "Read tic labels from a datafile column\nAn approximation of Hans Olav Eggestad's categoric plot patch\nusing 'using ($0):2:xticlabels(1)' and 'set style fill solid border -1'"
	set boxwidth 0.3
	set style fill solid 1.000000 border -1
	set bmargin 3
	set pointsize 2
	set yrange [0:180]
	set xrange [-0.5:11.5]
	set ylabel "mm"
	set tics scale 0.0
	set grid y
	set xtics
	#
	set key autotitle columnhead
	plot 'ctg-y2.dat' using ($0-0.2):2 index 0 with boxes title "precipitation 1992-2000", \
	     ''           using ($0+0.2):2 index 1 with boxes title "              2000-2001", \
	     ''           using ($0):2 index 2 with linespoints lw 3 title "runoff 1992-2000", \
	     ''           using ($0):2:xticlabels(1) index 3 with linespoints lw 3 title "       2000-2001"
	#
	pause -1 "<cr> to plot again using x2ticlabels"
	#
	set title "Same plot using x2ticlabels also"
	set x2tics
	plot 'ctg-y2.dat' using ($0-0.2):2 index 0 with boxes title "precipitation 1992-2000", \
	     ''           using ($0+0.2):2 index 1 with boxes title "              2000-2001", \
	     ''           using ($0):2 index 2 with linespoints lw 3 title "runoff 1992-2000", \
	     ''           using ($0):2:x2ticlabels(1) index 3 with linespoints lw 3 title "       2000-2001"
	#
	pause -1 "<cr> to plot same data from table format"
	set title "Plot from table format (titles taken from column headers)"
	unset x2tics
	plot 'table.dat' using ($0-0.2):2 title column(2) with boxes, \
	     ''          using ($0+0.2):3 title column(3) with boxes, \
	     ''          using ($0):4     title column(4) with linespoints, \
	     ''          using ($0):5:xtic(1) title column(5) with linespoints
	#
	pause -1 "<cr> to show double use of y values"
	#
	set title "Plot actual y-value as a label"
	plot 'table.dat' using 0:2:xtic(1) title columnhead with boxes, \
	     ''          using 0:2:2 with labels center offset 0,1 notitle
	pause -1 "<cr> to show use of boxed labels"
	#
	# 
	# Not all terminals support boxed labels
	set key box opaque
	set yrange [20:120]
	set grid x
	set style textbox opaque
	set title "Plot using boxed labels"
	plot 'table.dat' using 0:2:xtic(1) title col with lines lw 3, \
	     ''          using 0:2:2 with labels center boxed notitle
	
	#
	pause -1 "<cr> to end demo"
	reset

snippet dgrid3d.dem
	set view 60,75
	unset key
	set hidden3d
	
	# Raw data
	set title "The Valley of the Gnu"
	splot "gnu-valley" u 1:2:3 w linesp
	pause -1 "Hit return to continue"
	
	# Splines
	set title "dgrid3d splines"
	set dgrid3d splines
	splot "gnu-valley" u 1:2:3 w lines
	pause -1 "Hit return to continue"
	
	# QNorm
	set title "dgrid3d 30,30 qnorm 1"
	set dgrid3d 30,30 qnorm 1
	splot "gnu-valley" u 1:2:3 w l
	pause -1 "Hit return to continue"
	
	set title "dgrid3d 30,30 qnorm 2"
	set dgrid3d 30,30 qnorm 2
	splot "gnu-valley" u 1:2:3 w l
	pause -1 "Hit return to continue"
	
	set title "dgrid3d 30,30 qnorm 3"
	set dgrid3d 30,30 qnorm 3
	splot "gnu-valley" u 1:2:3 w l
	pause -1 "Hit return to continue"
	
	set title "dgrid3d 30,30 qnorm 4"
	set dgrid3d 30,30 qnorm 4
	splot "gnu-valley" u 1:2:3 w l
	pause -1 "Hit return to continue"
	
	set title "dgrid3d 30,30 qnorm 5"
	set dgrid3d 30,30 qnorm 5
	splot "gnu-valley" u 1:2:3 w l
	pause -1 "Hit return to continue"
	
	
	# Gauss
	set title "dgrid3d 30,30 gauss 1"
	set dgrid3d 30,30 gauss 1
	splot "gnu-valley" u 1:2:3 w l
	pause -1 "Hit return to continue"
	
	set title "dgrid3d 30,30 gauss .75"
	set dgrid3d 30,30 gauss .75
	splot "gnu-valley" u 1:2:3 w l
	pause -1 "Hit return to continue"
	
	set title "dgrid3d 30,30 gauss .5"
	set dgrid3d 30,30 gauss .5
	splot "gnu-valley" u 1:2:3 w l
	pause -1 "Hit return to continue"
	
	set title "dgrid3d 30,30 gauss .35"
	set dgrid3d 30,30 gauss .35
	splot "gnu-valley" u 1:2:3 w l
	pause -1 "Hit return to continue"
	
	set title "dgrid3d 30,30 gauss .25"
	set dgrid3d 30,30 gauss .25
	splot "gnu-valley" u 1:2:3 w l
	pause -1 "Hit return to continue"
	
	set title "dgrid3d 30,30 gauss .5,.35"
	set dgrid3d 30,30 gauss 0.5,0.35
	splot "gnu-valley" u 1:2:3 w l
	pause -1 "Hit return to continue"
	
	set title "dgrid3d 30,30 gauss .35,.5"
	set dgrid3d 30,30 gauss 0.35,0.5
	splot "gnu-valley" u 1:2:3 w l
	pause -1 "Hit return to continue"
	
	reset

snippet discrete.dem
	#
	# $Id: discrete.dem,v 1.4 2003/10/28 05:35:54 sfeam Exp $
	#
	#
	set contour
	set title "Demo of specifying discrete contour levels  -  default contours"
	splot x*y
	pause -1 "Hit return to continue"
	
	#set discrete levels
	set cntrparam levels discrete 0, 15, 75
	set title "3 discrete contours at 0 15 & 75"
	replot
	pause -1 "Hit return to continue"
	
	#set incremental levels
	set cntrp level incr -20, 5, 9
	set title "9 incremental contours starting at -20, stepping by 5"
	replot
	pause -1 "Hit return to continue"
	
	#restore defaults
	set title ""
	unset contour
	set cntrparam levels auto 5
	

snippet electron.dem
	#
	# $Id: electron.dem,v 1.10 2010/01/01 02:06:35 sfeam Exp $
	#
	# Electronics demo
	#
	# Bipolar Transistor (NPN) Mutual Characteristic
	Ie(Vbe)=Ies*exp(Vbe/kT_q)
	Ic(Vbe)=alpha*Ie(Vbe)+Ico
	alpha = 0.99
	Ies = 4e-14
	Ico = 1e-09
	kT_q = 0.025
	set dummy Vbe
	set grid
	set offsets
	unset log
	unset polar
	set samples 160
	set title "Mutual Characteristic of a Transistor"
	set xlabel "Vbe (base emmitter voltage)"
	set xrange [0 : 0.75]
	set ylabel "Ic (collector current)"
	set yrange [0 : 0.005]
	set key box
	set key at .2,.0045
	set format y "%.4f"
	plot Ic(Vbe)
	set format "%g"
	
	pause -1 "Hit return to continue"
	
	# Junction Field Effect Transistor (JFET) Mutual Characteristic
	# drain current above pinch off
	Ida(Vd)=Ido*(1-Vg/Vp)**2
	# drain current below pinch off
	Idb(Vd)=Ido*(2*Vd*(Vg-Vp)-Vd*Vd)/(Vp*Vp)
	# drain current
	Id(Vd)= (Vd>Vg-Vp) ? Ida(Vd) : Idb(Vd)
	# drain current at zero gate voltage
	Ido = 2.5
	# pinch off voltage
	Vp = -1.25
	# gate voltage
	Vg = 0
	set dummy Vd
	unset grid
	unset key
	#set offsets 0, 1, 0, 0
	set title "JFET Mutual Characteristic"
	set xlabel "Drain voltage Vd (V)"
	set xrange [0 : 4]
	set ylabel "Drain current Id (mA)"
	set yrange [0 : 5]
	set rmargin 12
	set label 1 "-0.5 Vp" at 4.1,0.625
	set label 2 "-0.25 Vp" at 4.1,1.4
	set label 3 "0" at 4.1,2.5
	set label 4 "Vg = 0.5 Vp" at 4.1,3.9
	plot Vg=0.5*Vp,Id(Vd),Vg=0.25*Vp,Id(Vd),Vg=0,Id(Vd),Vg=-0.25*Vp,Id(Vd)
	
	pause -1 "Hit return to continue"
	unset label
	unset rmargin
	
	# show off double axes
	
	# amplitude frequency response
	A(jw) = ({0,1}*jw/({0,1}*jw+p1)) * (1/(1+{0,1}*jw/p2))
	p1 = 10
	p2 = 10000
	set dummy jw
	set grid x y2
	set logscale xy
	set log x2
	unset log y2
	set key default
	set key bottom center box
	set title "Amplitude and Phase Frequency Response"
	set xlabel "jw (radians)"
	set xrange [1.1 : 90000.0]
	set ylabel "magnitude of A(jw)"
	set y2label "Phase of A(jw) (degrees)"
	set ytics nomirror tc lt 1
	set y2tics nomirror tc lt 3
	set xtics mirror
	set tics out
	set autoscale  y
	set autoscale y2
	plot abs(A(jw)) lt 1, 180/pi*arg(A(jw)) axes x1y2 lt 3
	
	pause -1 "Hit return to continue"
	
	# undo what we've done
	reset

snippet ellipse.dem
	#
	# Ethan A Merritt - Feb 2007
	# This demo requires support for 'set object ellipse'
	#
	load "gen-random.inc"
	
	unset key
	set xrange [-8:8]
	set yrange [-8:8]
	set size ratio 1.0
	set zeroaxis
	set border 0
	set xtics axis
	set ytics axis
	set tics scale 0.5
	set format xy ""
	
	set title 'Example of `set object ellipse`'
	
	set object 1 ellipse center 1.5,1.0 size 2.,4. angle 20. front fillstyle empty border -1
	set obj 2 ellipse center 1.5, 1  size 6, 12  angle 20 front fs empty bo 3
	
	A = pi/9.
	plot $random using (1.5 + $1*cos(A)-2.*$2*sin(A)):(1.0 + $1*sin(A)+2.*$2*cos(A)) with dots
	
	pause -1 "Hit return to continue"
	
	set title 'Example of range-limited axes and tics'
	
	set tics out scale 0.5
	set xtics 1.0 border rangelimited nomirror
	set ytics 1.0 border rangelimited nomirror
	set format xy "%.0f"
	set border 3
	
	replot
	
	pause -1 "Hit return to continue"
	reset

snippet ellipses_style.dem
	# Demo to illustrate the ellipses style
	
	set title "Demonstration of the 'ellipses' plotting style\nTwo-column form: x  y  (default size)"
	
	plot 'ellipses.dat' u 1:2 with ellipses
	
	pause -1 'Hit <cr> to continue'
	
	set title "Three-column form: x  y  major_diameter  (minor diameter is the same)" noenh
	
	plot 'ellipses.dat' u 1:2:3 with ellipses
	
	pause -1 'Hit <cr> to continue'
	
	set title "Four-column form: x  y  major_diameter  minor_diameter" noenh
	
	plot 'ellipses.dat' u 1:2:3:4 with ellipses
	
	pause -1 'Hit <cr> to continue'
	
	set title "Five-column form: x  y  major_diameter  minor_diameter angle" noenh
	
	plot 'ellipses.dat' u 1:2:3:4:5 with ellipses
	
	pause -1 'Hit <cr> to continue'
	
	set title "Six-column form: 6th column variable color (lc variable)"
	
	plot 'ellipses.dat' u 1:2:3:4:5:6 with ellipses lc var
	
	pause -1 'Hit <cr> to continue'
	
	set title "Six-column form: 6th column variable color (lc palette)"
	
	plot 'ellipses.dat' u 1:2:3:4:5:6 with ellipses lc pal
	
	pause -1 'Hit <cr> to continue'
	
	set title "Six-column form: 6th column variable color (lc rgb variable)"
	
	plot 'ellipses.dat' u 1:2:3:4:5:7 with ellipses lc rgb var
	
	pause -1 'Hit <cr> to continue'
	
	set title "Scaling of axes: units xy"
	set yr [-15:30]
	set xr [-5:15]
	
	plot 'ellipses.dat' u 1:2:3:4:5 with ellipses units xy
	
	pause -1 'Hit <cr> to continue'
	
	set title "Scaling of axes: units xx"
	
	plot 'ellipses.dat' u 1:2:3:4:5 with ellipses units xx
	
	pause -1 'Hit <cr> to continue'
	
	set title "Scaling of axes: units yy"
	
	plot 'ellipses.dat' u 1:2:3:4:5 with ellipses units yy
	
	pause -1 'Hit <cr> to continue'
	
	set title "Now see all three together"
	
	plot 'ellipses.dat' u 1:2:3:4:5 with ellipses units xy title "units xy",\
	     '' u 1:2:3:4:5 with ellipses units xx title "units xx",\
	     '' u 1:2:3:4:5 with ellipses units yy title "units yy"
	
	pause -1 'Hit <cr> to continue'
	reset

snippet enhancedtext.dem
	#
	# Test page for enhanced text mode
	#
	print "    ----------------------------------------------------"
	print "    This test page for enhanced text mode will only work"
	print "    if enhanced text is selected and supported for the"
	print "    current terminal type.  E.g. 'set term post enh'"
	print "    ----------------------------------------------------"
	#
	set termoption enhanced
	save_encoding = GPVAL_ENCODING
	set encoding iso_8859_1
	#
	set title "Test page for enhanced text mode"
	set xrange [-1:1]
	set yrange [-0.5:1.1]
	set format xy "%.1f"
	set arrow from  0.5, -0.5 to  0.5, 0.0 nohead
	#
	set label 1 at -0.65, 1.0
	set label 1 "These examples require no extra fonts:" tc lt 3
	set label 2 at -0.55, 0.9
	set label 2 "10^{-2}   A_{j,k}   e^x"
	set label 3 at -0.55, 0.8 
	set label 3 'x@^2_k    x@_0^{-3/2}y'
	
	set label 5 at -0.55,  0.7 
	set label 5 "Space-holders:" tc lt 3
	set label 6 at -0.45, 0.6 
	set label 6 "<&{{/=20 B}ig}> <&{x@_0^{-3/2}y}> holds space for"
	set label 7 at -0.45, 0.5 
	set label 7 "<{/=20 B}ig> <x@_0^{-3/2}y>"
	
	set label 8 at -0.9, -0.2
	set label 8 "Overprint\n(v should be centred over d)" tc lt 3
	set label 9 at -0.85, -0.4
	set label 9 " ~{abcdefg}{0.8v}"
	
	
	set label 10 at  -.40, 0.35
	set label 10 "Requires Symbol font:" tc lt 3
	set label 11 at -.30, 0.2
	set label 11 "{/Symbol=18 \362@_{/=9.6 0}^{/=12 ^\245}} {e^{-{/Symbol m}^2/2} d}{/Symbol m=(p/2)^{1/2}}"
	
	set label 21 at 0.5, -.1
	set label 21 "Left  ^{centered} text" left
	set label 22 at 0.5, -.2
	set label 22 "Right ^{centered} text" right
	set label 23 at 0.5, -.3
	set label 23 "Center^{centered} text" center
	#
	set key title " "
	plot sin(x)**2 lt 2 lw 2 title "sin^2(x)"
	#
	pause -1 "Hit <cr> to continue"
	set encoding save_encoding
	reset

snippet enhanced_utf8.dem
	#
	# Test page for enhanced text mode in UTF-8 encoding
	#
	# Suggested PostScript font test
	# set fontpath "/usr/local/share/fonts"
	# set term post color adobeglyphnames fontfile 'l_10646.ttf' font "LucidaSansUnicode,12"
	# set output 'enhanced_utf8.ps'
	#
	set termoption enhanced
	save_encoding = GPVAL_ENCODING
	set encoding utf8
	#
	set title "Demo of enhanced text mode using a single UTF-8 encoded font\nThere is another demo that shows how to use a separate Symbol font"
	set xrange [-1:1]
	set yrange [-0.5:1.1]
	set format xy "%.1f"
	set arrow from  0.5, -0.5 to  0.5, 0.0 nohead
	#
	set label 1 at -0.65, 0.95
	set label 1 "Superscripts and subscripts:" tc lt 3
	
	set label 3 at -0.55, 0.85
	set label 3 'A_{j,k} 10^{-2}  x@^2_k    x@_0^{-3/2}y'
	
	set label 5 at -0.55,  0.7 
	set label 5 "Space-holders:" tc lt 3
	set label 6 at -0.45, 0.6 
	set label 6 "<&{{/=20 B}ig}> <&{x@_0^{-3/2}y}> holds space for"
	set label 7 at -0.45, 0.5 
	set label 7 "<{{/=20 B}ig}> <{x@_0^{-3/2}y}>"
	
	set label 8 at -0.9, -0.2
	set label 8 "Overprint\n(v should be centred over d)" tc lt 3
	set label 9 at -0.85, -0.4
	set label 9 " ~{abcdefg}{0.8v}"
	
	
	set label 10 at  -.40, 0.35
	set label 10 "UTF-8 encoding does not require Symbol font:" tc lt 3
	set label 11 at -.30, 0.2
	set label 11 "{/*1.5 ∫@_{/=9.6 0}^{/=12 ∞}} {e^{-{μ}^2/2} d}{μ=(π/2)^{1/2}}"
	
	set label 21 at 0.5, -.1
	set label 21 "Left  ^{centered} ƒ(αβγδεζ)" left
	set label 22 at 0.5, -.2
	set label 22 "Right ^{centered} ƒ(αβγδεζ)" right
	set label 23 at 0.5, -.3
	set label 23 "Center^{centered} ƒ(αβγδεζ)" center
	
	set label 30 at -.9, 0.0 "{/:Bold Bold} and {/:Italic Italic} markup"
	#
	set key title " "
	plot sin(x)**2 lt 2 lw 2 title "sin^2(x)"
	#
	pause -1 "Hit return to continue"
	set encoding save_encoding
	reset
	

snippet epslatex.dem
	#
	# $Id: epslatex.dem,v 1.3 2012/06/23 07:48:00 markisch Exp $
	#
	# This file demonstrates new features in gnuplot 4.1
	print "\n This file demonstrates how to use the epslatex terminal."
	print " It will generate two files: epslatex-inc.eps and epslatex.tex."
	print " \nIf the file gnuplot.cfg is found by LaTeX it will be loaded"
	print " and can be used for further settings, e.g., here it provides"
	print " the package exscale.sty which enlargens the integral sign.\n"
	pause -1 "Hit return to continue"
	#
	xmin=-270.
	xmax=270.
	ymin=-1.1
	ymax=1.2
	#
	reset
	set terminal push
	set terminal epslatex standalone linewidth 2 rounded \
	  dashlen 4 color '' 12
	set output 'epslatex.tex'
	unset autoscale
	set xr [xmin:xmax]
	set yr [ymin:ymax]
	#
	save_encoding = GPVAL_ENCODING
	set encoding iso_8859_1
	set format x '$%g$'
	set format y '$%.1f$'
	set xtics 90
	set xlabel '$x$ / '
	set ylabel '$y$'
	set arrow from -120,0.8 to -60,0.8 lt 1 lw 2 nohead
	set arrow from -120,0.7 to -60,0.7 lt 2 lw 2 nohead
	set arrow from -120,0.6 to -60,0.6 lt 3 lw 2 nohead
	set arrow from -120,0.5 to -60,0.5 lt 4 lw 2 nohead
	set arrow from -120,0.4 to -60,0.4 lt 5 lw 2 nohead
	set arrow from -120,0.3 to -60,0.3 lt 6 lw 2 nohead
	set arrow from -120,0.2 to -60,0.2 lt 7 lw 2 nohead
	set arrow from -120,0.1 to -60,0.1 lt 8 lw 2 nohead
	set label '$\displaystyle\tan\alpha = \frac{\sin\alpha}{\cos\alpha}$' \
	  at 90,0 centre textcolor lt 3
	set label '$\displaystyle\int x\mathrm{d}x = \frac12 x^2 + C_1$' \
	  at 90,-0.5 centre textcolor lt 4
	plot sin(pi/180.*x) title '$y = \sin x$' w l lt 1 lw 4
	set output
	#
	set terminal epslatex standalone oldstyle linewidth 2 rounded \
	  dashlen 1 color '' 12
	set output 'epslatex-oldstyle.tex'
	set tics scale 2
	replot
	set output
	#
	print "\n Now, either run"
	print "   latex epslatex"
	print "   dvips epslatex"
	print " and have a look at epslatex.ps; or run"
	print "   epstopdf epslatex-inc.eps"
	print "   pdflatex epslatex"
	print " and have a look at epslatex.pdf."
	print " Do the same with epslatex-oldstyle.tex."
	#
	set terminal pop
	set encoding save_encoding
	reset

snippet errorbars.dem
	#
	# $Id: errorbars.dem,v 1.1 2017/01/04 01:00:23 sfeam Exp $
	#
	# Second part of old mgr.dem
	# Show the same plot with various errorbar styles
	#
	print "various styles of errorbar"
	
	set xlabel "Resistance [Ohm]"
	set ylabel "Power [W]"
	
	set title "error represented by xyerrorbars"
	n(x)=1.53**2*x/(5.67+x)**2
	plot [0:50] "battery.dat" t "Power" with xyerrorbars, n(x) t "Theory" w lines
	
	pause -1 "Would you like boxes? (-> return)"
	set title "error represented by boxxyerror"
	plot [0:50] "battery.dat" t "Power" with boxxyerr, n(x) t "Theory" w lines
	
	pause -1 "Only X-Bars? (-> return)"
	set title "error represented by xerrorbars"
	plot [0:50] "battery.dat" u 1:2:3 t "Power" w xerr, n(x) t "Theory" w lines
	
	pause -1 "Only Y-Bars? (-> return)"
	set title "error represented by yerrorbars"
	plot [0:50] "battery.dat" u 1:2:4 t "Power" w yerr, n(x) t "Theory" w lines
	
	pause -1 "Logscaled? (-> return)"
	set title "yerrorbars in log scale"
	set logscale y
	plot [0:50] "battery.dat" u 1:2:4 t "Power" w yerr, n(x) t "Theory" w lines
	
	pause -1 "X as well? (-> return)"
	set title "xyerrorbars in log scale"
	set logscale xy
	plot [1:50] "battery.dat" t "Power" w xyerr, n(x) t "Theory" w lines
	
	pause -1 "If you like bars without tics (-> return)"
	set title "xyerrorbars with no crossbar"
	unset logscale
	set bars small
	plot [0:50] "battery.dat" t "Power" with xyerrorbars, n(x) t "Theory" w lines
	
	pause -1 "X-Bars only (-> return)"
	set title "xerrorbars with no crossbar"
	plot [0:50] "battery.dat" u 1:2:3 t "Power" w xerr, n(x) t "Theory" w lines
	
	pause -1 "Y-Bars only (-> return)"
	set title "yerrorbars with no crossbar"
	plot [0:50] "battery.dat" u 1:2:4 t "Power" w yerr, n(x) t "Theory" w lines
	
	pause -1 "filledcurve shaded error region"
	set title "Error on y represented by filledcurve shaded region"
	set xlabel "Time (sec)" 
	set ylabel "Rate" 
	set grid xtics mxtics ytics mytics
	set log y
	Shadecolor = "#80E0A080"
	#
	plot 'silver.dat' using 1:($2+$3):($2-$3) \
	      with filledcurve fc rgb Shadecolor title "Shaded error region",\
	     '' using 1:2 smooth mcspline lw 2   title "Monotonic spline through data"
	
	pause -1 "Hit return to continue"
	reset

snippet fenceplot.dem
	#
	# Two different methods to construct a fence plot
	#
	# 1) Each fence is a separate parametric surface
	#    surface appearance is controlled by "set hidden3d"
	#
	set title "fence plot constructed with separate parametric surfaces"
	sinc(u,v) = sin(sqrt(u**2+v**2)) / sqrt(u**2+v**2)
	set xrange [-5:5]; set yrange [-5:5]
	set xtics offset 0,-1
	set ytics offset 0,-1
	set zrange [-1:1]
	set format z "%.1f" 
	set arrow 1 from 5,-5,-1.2 to 5,5,-1.2 lt -1
	set label 1 "increasing v" at 6,0,-1
	set arrow 2 from 5,6,-1 to 5,5,-1 lt -1
	set label 2 "u=0" at 5,6.5,-1
	set arrow 3 from 5,6,sinc(5,5) to 5,5,sinc(5,5) lt -1
	set label 3 "u=1" at 5,6.5,sinc(5,5)
	set parametric
	set hidden3d offset 0	# front/back coloring makes no sense for fenceplot #
	set isosamples 2,33
	array U[10]
	u=-5; du=(4.99-(-4.99))/9
	do for [i=1:10] { U[i] = u + i * du }
	
	set view 70, 25
	set urange [0:1]
	set vrange [-4.99:4.99]
	splot for [i=1:10] U[i], v, (u<0.5) ? -1 : sinc(U[i],v) notitle
	
	pause -1 "Hit return to continue"
	unset hidden3d
	
	#
	# 2) Each fence is a curve drawn with zerrorfill
	#    surface appearance is controlled by "set pm3d" and "set style fill"
	#    If depth-cueing is needed, use "set pm3d depthorder"
	#
	set format z "%.1f" 
	unset key
	unset arrow
	unset label
	set view 70,25
	unset xtics
	unset ytics
	set title "fence plot constructed with zerrorfill" 
	set zrange [-1:1]
	set arrow 1 from 5,-.5,-1.2 to 5,.5,-1.2 lt -1
	set label 1 "sampling on Y" at 6,-0.1,-1
	set xlabel "X axis"  rotate parallel 
	set xrange [-5:4]
	set yrange [-0.5:0.5]
	sinc(u,v) = sin(sqrt(u**2+v**2)) / sqrt(u**2+v**2)
	
	set style fill  solid 0.75 noborder
	splot for [x=-4:4][y=-50:50:3] '+' using (x):($1/100.):(-1):(-1):(sinc($1/10., 1.+x)) with zerrorfill
	
	pause -1 "Hit return to continue"
	reset

snippet fillbetween.dem
	#
	# Demonstrate filling the area between two curves
	# and also the application of fillstyle to filled curves.
	# Ethan Merritt  <merritt@u.washington.edu>
	# June 2004
	#
	set title "Fill area between two curves"
	set style data lines
	set xrange [10:*]
	set yrange [0:175]
	plot 'silver.dat' u 1:2:3 "%lf %lf %lf" w filledcu, \
	      '' u 1:2 lt -1 notitle, '' u 1:3 lt -1 notitle
	pause -1 "Hit return to continue"
	set style fill pattern 2
	set title "Fill area between two curves (pattern fill)"
	replot
	pause -1 "Hit return to continue"
	set title "Fill area between two curves (above/below)"
	set style fill solid 1.0 noborder
	set xrange [250:500]
	set auto y
	plot 'silver.dat' u 1:2:($3+$1/50.) w filledcurves above title 'Above', \
	               '' u 1:2:($3+$1/50.) w filledcurves below title 'Below', \
		       '' u 1:2 lt -1 lw 2 title 'curve 1', \
		       '' u 1:($3+$1/50.) lt 3 lw 2 title 'curve 2'
	pause -1 "Hit return to continue"
	reset

snippet fillcrvs.dem
	#
	# $Id: fillcrvs.dem,v 1.6 2007/05/19 20:35:53 sfeam Exp $
	#
	### Demo for 'with filledcurves'
	
	reset
	
	set title
	set key outside
	set title "plot with filledcurve [options]"
	plot [-10:10] [-5:3] \
		1.5+sin(x)/x with filledcurve x2, \
		sin(x)/x with filledcurve, \
		1+sin(x)/x with lines, \
		-1+sin(x)/x with filledcurve y1=-2, \
		-2.5+sin(x)/x with filledcurve xy=-5,-4., \
		-4.3+sin(x)/x with filledcurve x1, \
		(x>3.5 ? x/3-3 : 1/0) with filledcurve y2
	pause -1 'Press Return to continue'
	
	set key on
	set title "Intersection of two parabolas"
	plot x*x with filledcurves, 50-x*x with filledcurves, x*x with line lt 1
	pause -1 'Press Return to continue'
	
	set grid front
	set title "Filled sinus and cosinus curves"
	plot 2+sin(x)**2 with filledcurve x1, cos(x)**2 with filledcurve x1
	pause -1 'Press Return to continue'
	
	set title "The red bat: abs(x) with filledcurve xy=2,5"
	plot abs(x) with filledcurve xy=2,5
	pause -1 'Press Return to continue'
	
	set title "Some sqrt stripes on filled graph background"
	plot [0:10] [-8:6] \
		-8 with filledcurve x2 lt 15, \
		sqrt(x) with filledcurves y1=-0.5, \
		sqrt(10-x)-4.5 with filledcurves y1=-5.5
	pause -1 'Press Return to continue'
	
	reset
	set title "Let's smile with parametric filled curves"
	set size square
	set key off
	unset border
	unset xtics
	unset ytics
	set grid
	set arrow 1 from -0.1,0.26 to 0.18,-0.17 front size 0.1,40 lt 5 lw 4
	set label 1 "gnuplot" at 0,1.2 center front
	set label 2 "gnuplot" at 0.02,-0.6 center front
	set parametric
	set xrange [-1:1]
	set yrange [-1:1.6]
	plot [t=-pi:pi] \
		sin(t),cos(t) with filledcurve xy=0,0 lt 15,	\
		sin(t)/8-0.5,cos(t)/8+0.4 with filledcurve lt 3,	\
		sin(t)/8+0.5,cos(t)/8+0.4 with filledcurve lt 3,	\
		t/5,abs(t/5)-0.8 with filledcurve xy=0.1,-0.5 lt 1, \
		t/3,1.52-abs(t/pi) with filledcurve xy=0,1.8 lt -1
	pause -1 'Press Return to continue'
	
	reset
	
	set title "world.dat plotted with filledcurves"
	set format x ""
	set format y ""
	set grid layerdefault linewidth 0.5 
	set object  1 rect from graph 0, 0 to graph 1, 1 behind fc  rgb "#afffff" fillstyle solid 1.00 border -1
	set xrange [ -180.000 : 180.000 ]
	set yrange [ -70.0000 : 80.0000 ]
	set lmargin  1
	plot 'world.dat' with filledcurve notitle fs solid 1.0 lc rgb 'dark-goldenrod'
	pause -1 'Press Return to continue'
	
	reset

snippet fillstyle.dem
	#
	# $Id: fillstyle.dem,v 1.3 2003/10/17 15:02:21 mikulik Exp $
	#
	# E A Merritt <merritt@u.washington.edu>          24-Sep-2002
	#
	# Demo for revised fillstyle code selected by 
	# ./configure --enable-filledboxes --enable-relative-boxwidth
	#
	reset
	
	set samples 25
	unset xtics
	unset ytics
	set yrange [0:120]
	
	set title "A demonstration of boxes with default properties"
	plot [-10:10] 100/(1.0+x*x) title 'distribution' with boxes
	
	pause -1 "Now draw the boxes with solid fill"
	
	set title "A demonstration of boxes with style fill solid 1.0"
	set style fill solid 1.0
	replot
	
	pause -1 "Now draw the boxes with a black border"
	
	set title "A demonstration of boxes with style fill solid border -1"
	set style fill solid border -1
	replot
	
	pause -1 "Now make the boxes a little less wide"
	
	set title "Filled boxes of reduced width"
	set boxwidth 0.5 
	replot
	
	pause -1 "And now let's try a different fill density"
	
	set title "Filled boxes at 50% fill density"
	set style fill solid 0.25 border
	replot
	
	pause -1 "Now draw the boxes with no border"
	
	set title "A demonstration of boxes with style fill solid 0.25 noborder"
	set style fill solid 0.25 noborder
	replot
	
	pause -1 "Or maybe a pattern fill, instead?"
	
	set title "A demonstration of boxes in mono with style fill pattern"
	set samples 11
	set boxwidth 0.5 
	set style fill pattern border
	plot [-2.5:4.5] 100/(1.0+x*x) title 'pattern 0' with boxes lt -1, \
	                 80/(1.0+x*x) title 'pattern 1' with boxes lt -1, \
	                 40/(1.0+x*x) title 'pattern 2' with boxes lt -1, \
	                 20/(1.0+x*x) title 'pattern 3' with boxes lt -1
	
	pause -1 "Finished this demo"
	
	reset
	

snippet finance.dem
	# 10 May 2005
	# 
	# Though gnuplot is primarily a scientific plotting program, it can do a great
	# job of plotting finance charts as well. The primary challenge is the irregular
	# nature of financial time series. Stocks don't trade every day, so when you set
	# the x-axis to time gaps appear for non-trading days. Investors and traders
	# generally prefer that these gaps be omitted. Another challenge is that finance
	# charts are best presented in semi-log form (log y-axis, linear x-axis),
	# but gnuplot wants to span decades in its log scaling, something that stocks
	# rarely do. These and other challenges are met in finance.dem, a short 
	# demonstration script that proves that gnuplot can really shine in this area.
	# 
	# gnuplot plays a central role in our work. Almost all the graphs in "Bollinger
	# on Bollinger Bands" were plotted by gnuplot, many gnuplot visuals have
	# appeared on CNBC, our in-house analytics use gnuplot for visual display and
	# The Capital Growth Letter relies heavily on gnuplot for its charts.  
	# 
	# Finally, gnuplot is yet another successful demonstration of a powerful idea,
	# open source programming. Thanks to all who made gnuplot possible from the
	# earliest days to the present and to all those who will contribute in the
	# future. (Special thanks to Hans-Bernhard Broeker whose patience helped me to
	# climb the grade and to Ethan Merritt whose recent contributions have been
	# invaluable to our work.)
	# 
	# John Bollinger
	# www.BollingerBands.com 
	
	# a demonstration of gnuplot finance plot styles
	# by John Bollinger, CFA, CMT
	# www.BollingerBands.com
	# BBands@BollingerBands.com
	
	# data and indicators in finance.dat
	# data file layout: 
	# date, open, high, low, close, volume,
	# 50-day moving average volume, Intraday Intensity, 
	# 20-day moving average close, 
	# upper Bollinger Band, lower Bollinger Band 
	
	# last update: 8 May 2005
	
	pause   -1 "Click OK to start the Finance Demo"
	
	reset
	# set label 1 "Demo of plotting financial data" at screen 0.5, screen 0.95 center
	set title "Demo of plotting financial data"
	set yrange [75:105]
	set ytics (105, 100, 95, 90, 85, 80)
	set xrange [50:253]
	set lmargin 9
	set rmargin 2
	plot 'finance.dat' using 0:5 notitle with lines
	
	pause -1 "Turn on the grid"
	
	reset
	set title "Turn on grid"
	set yrange [75:105]
	set ytics (105, 100, 95, 90, 85, 80)
	set xrange [50:253]
	set lmargin 9
	set rmargin 2
	set grid
	plot 'finance.dat' using 0:5 notitle with lines
	
	pause -1 "Semi-log scaling"
	
	reset
	set title "Semi-log scaling"
	set yrange [75:105]
	set ytics (105, 100, 95, 90, 85, 80)
	set xrange [50:253]
	set lmargin 9
	set rmargin 2
	set grid
	set logscale y
	plot 'finance.dat' using 0:5 notitle with lines
	
	pause -1 "Finance bars"
	
	reset
	set title "Finance bars"
	set yrange [75:105]
	set ytics (105, 100, 95, 90, 85, 80)
	set xrange [50:253]
	set lmargin 9
	set rmargin 2
	set grid
	set logscale y
	plot 'finance.dat' using 0:2:3:4:5 notitle with financebars lt 8
	
	pause -1 "Bollinger Bands"
	
	reset
	set title "Bollinger Bands"
	set yrange [75:105]
	set ytics (105, 100, 95, 90, 85, 80)
	set xrange [50:253]
	set lmargin 9
	set rmargin 2
	set grid
	set logscale y
	plot 'finance.dat' using 0:2:3:4:5 notitle with financebars lt 8, \
	'finance.dat' using 0:9 notitle with lines lt 3, \
	'finance.dat' using 0:10 notitle with lines lt 1, \
	'finance.dat' using 0:11 notitle with lines lt 2
	
	
	pause -1 "Overlay an indicator on a seperate scale"
	
	reset
	set title "Overlay an indicator on a separate scale"
	set yrange [75:105]
	set ytics (105, 100, 95, 90, 85, 80)
	set xrange [50:253]
	set lmargin 9
	set rmargin 2
	set grid
	set logscale y
	plot 'finance.dat' using 0:2:3:4:5 notitle with financebars lt 8, \
	'finance.dat' using 0:9 notitle with lines lt 3, \
	'finance.dat' using 0:10 notitle with lines lt 1, \
	'finance.dat' using 0:11 notitle with lines lt 2, \
	'finance.dat' using 0:8 axes x1y2 notitle with lines lt 4
	
	pause -1 "Add some boiler plate"
	
	reset
	set title "Add some boiler plate"
	set label "Courtesy of Bollinger Capital" at graph 0.01, 0.07
	set label "  www.BollingerBands.com" at graph 0.01, 0.03
	set yrange [75:105]
	set ytics (105, 100, 95, 90, 85, 80)
	set xrange [50:253]
	set lmargin 9
	set rmargin 2
	set grid
	set logscale y
	plot 'finance.dat' using 0:2:3:4:5 notitle with financebars lt 8, \
	'finance.dat' using 0:9 notitle with lines lt 3, \
	'finance.dat' using 0:10 notitle with lines lt 1, \
	'finance.dat' using 0:11 notitle with lines lt 2, \
	'finance.dat' using 0:8 axes x1y2 notitle with lines lt 4
	
	pause -1 "Add volume in a seperate clip"
	
	reset
	set label "Courtesy of Bollinger Capital" at graph 0.01, 0.07
	set label "  www.BollingerBands.com" at graph 0.01, 0.03
	set logscale y
	set yrange [75:105]
	set ytics (105, 100, 95, 90, 85, 80)
	set xrange [50:253]
	set grid
	set lmargin 9
	set rmargin 2
	set format x ""
	set xtics (66, 87, 109, 130, 151, 174, 193, 215, 235)
	set multiplot
	set size 1, 0.7
	set origin 0, 0.3
	set bmargin 0
	set title "Add volume in a separate clip"
	plot 'finance.dat' using 0:2:3:4:5 notitle with financebars lt 8, \
	'finance.dat' using 0:9 notitle with lines lt 3, \
	'finance.dat' using 0:10 notitle with lines lt 1, \
	'finance.dat' using 0:11 notitle with lines lt 2, \
	'finance.dat' using 0:8 axes x1y2 notitle with lines lt 4
	unset label 1
	unset label 2
	unset title
	set bmargin
	set format x
	set size 1.0, 0.3
	set origin 0.0, 0.0
	set tmargin 0
	unset logscale y
	set autoscale y
	set format y "%1.0f"
	set ytics 500
	plot 'finance.dat' using 0:($6/10000) notitle with impulses lt 3
	unset multiplot
	
	pause -1 "Add average volume"
	
	reset
	set label "Courtesy of Bollinger Capital" at graph 0.01, 0.07
	set label "  www.BollingerBands.com" at graph 0.01, 0.03
	set logscale y
	set yrange [75:105]
	set ytics (105, 100, 95, 90, 85, 80)
	set xrange [50:253]
	set grid
	set lmargin 9
	set rmargin 2
	set format x ""
	set xtics (66, 87, 109, 130, 151, 174, 193, 215, 235)
	set multiplot
	set title "Add average volume"
	set size 1, 0.7
	set origin 0, 0.3
	set bmargin 0
	plot 'finance.dat' using 0:2:3:4:5 notitle with financebars lt 8, \
	'finance.dat' using 0:9 notitle with lines lt 3, \
	'finance.dat' using 0:10 notitle with lines lt 1, \
	'finance.dat' using 0:11 notitle with lines lt 2, \
	'finance.dat' using 0:8 axes x1y2 notitle with lines lt 4
	unset label 1
	unset label 2
	unset title
	set bmargin
	set format x
	set size 1.0, 0.3
	set origin 0.0, 0.0
	set tmargin 0
	unset logscale y
	set autoscale y
	set format y "%1.0f"
	set ytics 500
	plot 'finance.dat' using 0:($6/10000) notitle with impulses lt 3, \
	'finance.dat' using 0:($7/10000) notitle with lines lt 1
	unset multiplot
	
	pause -1 "Add date labels to the x axis"
	
	reset
	set label "Courtesy of Bollinger Capital" at graph 0.01, 0.07
	set label "  www.BollingerBands.com" at graph 0.01, 0.03
	set logscale y
	set yrange [75:105]
	set ytics (105, 100, 95, 90, 85, 80)
	set xrange [50:253]
	set grid
	set lmargin 9
	set rmargin 2
	set format x ""
	set xtics (66, 87, 109, 130, 151, 174, 193, 215, 235)
	set multiplot
	set title "Add date labels to the x axis"
	set size 1, 0.7
	set origin 0, 0.3
	set bmargin 0
	plot 'finance.dat' using 0:2:3:4:5 notitle with financebars lt 8, \
	'finance.dat' using 0:9 notitle with lines lt 3, \
	'finance.dat' using 0:10 notitle with lines lt 1, \
	'finance.dat' using 0:11 notitle with lines lt 2, \
	'finance.dat' using 0:8 axes x1y2 notitle with lines lt 4
	unset label 1
	unset label 2
	unset title
	set bmargin
	set format x
	set size 1.0, 0.3
	set origin 0.0, 0.0
	set tmargin 0
	unset logscale y
	set autoscale y
	set format y "%1.0f"
	set ytics 500
	set xtics ("6/03" 66, "7/03" 87, "8/03" 109, "9/03" 130, "10/03" 151, "11/03" 174, "12/03" 193, "1/04" 215, "2/04" 235)
	plot 'finance.dat' using 0:($6/10000) notitle with impulses lt 3, \
	'finance.dat' using 0:($7/10000) notitle with lines lt 1
	unset multiplot
	
	pause -1 "Add labels for each clip"
	
	reset
	set label "Courtesy of Bollinger Capital" at graph 0.01, 0.07
	set label "  www.BollingerBands.com" at graph 0.01, 0.03
	set logscale y
	set yrange [75:105]
	set ytics (105, 100, 95, 90, 85, 80)
	set xrange [50:253]
	set grid
	set lmargin 9
	set rmargin 2
	set format x ""
	set xtics (66, 87, 109, 130, 151, 174, 193, 215, 235)
	set multiplot
	set title "Add labels for each clip"
	set size 1, 0.7
	set origin 0, 0.3
	set bmargin 0
	set ylabel "price" offset 1
	plot 'finance.dat' using 0:2:3:4:5 notitle with financebars lt 8, \
	'finance.dat' using 0:9 notitle with lines lt 3, \
	'finance.dat' using 0:10 notitle with lines lt 1, \
	'finance.dat' using 0:11 notitle with lines lt 2, \
	'finance.dat' using 0:8 axes x1y2 notitle with lines lt 4
	unset label 1
	unset label 2
	unset title
	set bmargin
	set format x
	set size 1.0, 0.3
	set origin 0.0, 0.0
	set tmargin 0
	unset logscale y
	set autoscale y
	set format y "%1.0f"
	set ytics 500
	set xtics ("6/03" 66, "7/03" 87, "8/03" 109, "9/03" 130, "10/03" 151, "11/03" 174, "12/03" 193, "1/04" 215, "2/04" 235)
	set ylabel "volume (0000)" offset 1
	plot 'finance.dat' using 0:($6/10000) notitle with impulses lt 3, \
	'finance.dat' using 0:($7/10000) notitle with lines lt 1
	unset multiplot
	
	pause -1 "Add a title"
	
	reset
	set label 1 "Acme Widgets" at graph 0.5, graph 0.9 center front
	set label 2 "Courtesy of Bollinger Capital" at graph 0.01, 0.07
	set label 3 "  www.BollingerBands.com" at graph 0.01, 0.03
	set logscale y
	set yrange [75:105]
	set ytics (105, 100, 95, 90, 85, 80)
	set xrange [50:253]
	set grid
	set lmargin 9
	set rmargin 2
	set format x ""
	set xtics (66, 87, 109, 130, 151, 174, 193, 215, 235)
	set multiplot
	set title "Add a title"
	set size 1, 0.7
	set origin 0, 0.3
	set bmargin 0
	set ylabel "price" offset 1
	plot 'finance.dat' using 0:2:3:4:5 notitle with financebars lt 8, \
	'finance.dat' using 0:9 notitle with lines lt 3, \
	'finance.dat' using 0:10 notitle with lines lt 1, \
	'finance.dat' using 0:11 notitle with lines lt 2, \
	'finance.dat' using 0:8 axes x1y2 notitle with lines lt 4
	unset label 1
	unset label 2
	unset label 3
	unset title
	set bmargin
	set format x
	set size 1.0, 0.3
	set origin 0.0, 0.0
	set tmargin 0
	unset logscale y
	set autoscale y
	set format y "%1.0f"
	set ytics 500
	set xtics ("6/03" 66, "7/03" 87, "8/03" 109, "9/03" 130, "10/03" 151, "11/03" 174, "12/03" 193, "1/04" 215, "2/04" 235)
	set ylabel "volume (0000)" offset 1
	plot 'finance.dat' using 0:($6/10000) notitle with impulses lt 3, \
	'finance.dat' using 0:($7/10000) notitle with lines lt 1
	unset multiplot
	
	pause -1 " Change to Candlesticks"
	
	reset
	set label 1 "Acme Widgets" at graph 0.5, graph 0.9 center front
	set label 2 "Courtesy of Bollinger Capital" at graph 0.01, 0.07
	set label 3 "  www.BollingerBands.com" at graph 0.01, 0.03
	set logscale y
	set yrange [75:105]
	set ytics (105, 100, 95, 90, 85, 80)
	set xrange [50:253]
	set grid
	set lmargin 9
	set rmargin 2
	set format x ""
	set xtics (66, 87, 109, 130, 151, 174, 193, 215, 235)
	set multiplot
	set title "Change to candlesticks"
	set size 1, 0.7
	set origin 0, 0.3
	set bmargin 0
	set ylabel "price" offset 1
	plot 'finance.dat' using 0:2:3:4:5 notitle with candlesticks lt 8, \
	'finance.dat' using 0:9 notitle with lines lt 3, \
	'finance.dat' using 0:10 notitle with lines lt 1, \
	'finance.dat' using 0:11 notitle with lines lt 2, \
	'finance.dat' using 0:8 axes x1y2 notitle with lines lt 4
	unset label 1
	unset label 2
	unset label 3
	unset title
	set bmargin
	set format x
	set size 1.0, 0.3
	set origin 0.0, 0.0
	set tmargin 0
	unset logscale y
	set autoscale y
	set format y "%1.0f"
	set ytics 500
	set xtics ("6/03" 66, "7/03" 87, "8/03" 109, "9/03" 130, "10/03" 151, "11/03" 174, "12/03" 193, "1/04" 215, "2/04" 235)
	set ylabel "volume (0000)" offset 1
	plot 'finance.dat' using 0:($6/10000) notitle with impulses lt 3, \
	'finance.dat' using 0:($7/10000) notitle with lines lt 1
	unset multiplot
	
	pause -1 "Change to Bollinger Boxes"
	
	reset
	set label 1 "Acme Widgets" at graph 0.5, graph 0.9 center front
	set label 2 "Courtesy of Bollinger Capital" at graph 0.01, 0.07
	set label 3 "  www.BollingerBands.com" at graph 0.01, 0.03
	set logscale y
	set yrange [75:105]
	set ytics (105, 100, 95, 90, 85, 80)
	set xrange [50:253]
	set grid
	set lmargin 9
	set rmargin 2
	set format x ""
	set xtics (66, 87, 109, 130, 151, 174, 193, 215, 235)
	set multiplot
	set title "Change to Bollinger Boxes"
	set size 1, 0.7
	set origin 0, 0.3
	set bmargin 0
	set ylabel "price" offset 1
	plot 'finance.dat' using 0:3:3:($2>$5?$2:$5):($2>$5?$2:$5) notitle with candlesticks lt 3, \
	'finance.dat' using 0:($2<$5?$5:1/0):($2<$5?$5:1/0):($2<$5?$2:1/0):($2<$5?$2:1/0) notitle with candlesticks lt 2, \
	'finance.dat' using 0:($2>$5?$2:1/0):($2>$5?$2:1/0):($2>$5?$5:1/0):($2>$5?$5:1/0) notitle with candlesticks lt 1, \
	'finance.dat' using 0:($2<$5?$2:$5):($2<$5?$2:$5):4:4 notitle with candlesticks lt 3, \
	'finance.dat' using 0:9 notitle with lines lt 3, \
	'finance.dat' using 0:10 notitle with lines lt 1, \
	'finance.dat' using 0:11 notitle with lines lt 2, \
	'finance.dat' using 0:8 axes x1y2 notitle with lines lt 4
	unset label 1
	unset label 2
	unset label 3
	unset title
	set bmargin
	set format x
	set size 1.0, 0.3
	set origin 0.0, 0.0
	set tmargin 0
	unset logscale y
	set autoscale y
	set format y "%1.0f"
	set ytics 500
	set xtics ("6/03" 66, "7/03" 87, "8/03" 109, "9/03" 130, "10/03" 151, "11/03" 174, "12/03" 193, "1/04" 215, "2/04" 235)
	set ylabel "volume (0000)" offset 1
	plot 'finance.dat' using 0:($6/10000) notitle with impulses lt 3, \
	'finance.dat' using 0:($7/10000) notitle with lines lt 1
	unset multiplot
	
	pause -1 "all done!"
	reset

snippet fit.dem
	#
	# $Id: fit.dem,v 1.22 2017/04/25 23:40:47 sfeam Exp $
	#
	
	myencoding = GPVAL_ENCODING
	set encoding utf8
	set termoption enhanced
	set style data points
	set dummy x, y
	
	print "Some examples how data fitting using nonlinear least squares fit can be done."
	print ''
	
	print "We fit a straight line to the data -- only as a demo without physical meaning."
	l(x) = y0 + m*x
	# Although gnuplot's internal fit backend can cope with it in this
	# case, initializing variables to zero is a no, no, no!
	#y0 = 0.0
	#m = 0.0
	y0 = 1.1
	m = -0.1
	print "  fit function:", GPFUN_l
	print sprintf("  initial parameters: y0 = %g, m = %g", y0, m)
	print "  fit command: fit l(x) 'lcdemo.dat' via y0, m\n"
	set xlabel "Temperature T (°C)"
	set ylabel "Density (g/cm^3)"
	set key below
	set title 'data set and initial parameters'
	plot 'lcdemo.dat', l(x)
	pause -1 "Now start fitting...  (-> return)"
	
	fit l(x) 'lcdemo.dat' via y0, m
	set title 'unweighted fit'
	plot 'lcdemo.dat', l(x)
	pause -1 "Press enter to proceed with the next example."
	
	# BM: The "errors" given in column 5 of lcdemo.dat are clearly not measurement errors
	#     since the scatter in the data points is _much_ smaller.
	set title 'data with experimental weigths'
	plot 'lcdemo.dat' using 1:2:5 with errorbars
	print ''
	print "Now use the real single-measurement weights from column 5.  (Look at the file"
	print "lcdemo.dat and compare the columns to see the difference.)"
	print "Since these are weights we rescale the resulting parameter errors."
	print "  fit settings: set fit errorscaling"
	print "  fit command : fit l(x) 'lcdemo.dat' using 1:2:5 yerr via y0, m\n"
	pause -1 "Press enter to start the fit."
	
	set fit errorscaling
	set fit covariancevariables
	fit l(x) 'lcdemo.dat' using 1:2:5 yerr via y0, m
	set title 'fit weighted by experimental weights'
	plot 'lcdemo.dat' using 1:2:5 with errorbars, l(x)
	set fit nocovariancevariables
	pause -1 "Press enter to proceed with the next example."
	
	load 'density.fnc'
	print ''
	print "It's time now to try a more realistic model function:\n"
	print "  ", GPFUN_density
	print "  ", GPFUN_curve
	print "  ", GPFUN_lowlin
	print "  ", GPFUN_high
	print "\ndensity(x) is a function which shall fit the whole temperature range using"
	print "a ?: expression. It contains 6 model parameters which will all be varied. Now"
	print "take the start parameters out of the file 'start.par' and plot the function."
	print "  fit command: fit density(x) 'lcdemo.dat' using 1:2:5 yerror via 'start.par'\n"
	load 'start.par'
	set title 'initial parameters for realistic model function'
	plot 'lcdemo.dat' using 1:2:5 with errorbars, density(x)
	pause -1 "Press enter to start the fit."
	
	fit density(x) 'lcdemo.dat' using 1:2:5 yerror via 'start.par'
	set title 'fitted to realistic model function'
	plot 'lcdemo.dat' using 1:2:5 with errorbars, density(x)
	
	# FIXME: Nowadays this fit ends too quickly as to be usefull any more...
	#print ''
	#print "Looks already rather nice?  We will now do the following:  Set the epsilon"
	#print "fit3.dat lower so that we need more iteration steps to convergence."
	#print "During fitting please hit Ctrl-C.  You will be asked:"
	#print "  Stop, Continue, Execute"
	#print "Try all options.  You may define a script using the FIT_SCRIPT environment"
	#print "variable.  An example would be:"
	#print "  FIT_SCRIPT=plot nonsense.dat"
	#print "Normally you don't need to set FIT_SCRIPT since it defaults to 'replot'.  Please"
	#print "note that FIT_SCRIPT cannot be set from inside gnuplot."
	#print "  fit settings: set fit errorscaling limit 1e-10"
	#print "  fit command : fit density(x) 'lcdemo.dat' using 1:2:5 via 'start.par'\n"
	#pause -1 "Press enter to start the fit."
	
	#set fit errorscaling limit 1e-10
	#fit density(x) 'lcdemo.dat' using 1:2:5 yerror via 'start.par'
	#set title 'fit with more iterations'
	#plot 'lcdemo.dat' using 1:2:5 with errorbars, density(x)
	#set fit limit 1e-5
	pause -1 "Press enter to proceed with the next example."
	unset xlabel
	unset ylabel
	
	
	print "\n\nNow a brief demonstration of 3d fitting."
	print "hemisphr.dat contains random points on a hemisphere of radius 1, but we let"
	print "fit figure this out for us.  It takes many iterations, so we limit them to 50."
	print "We also do not want intermediate results here."
	print "  fit settings: set fit results maxiter 50"
	#HBB: made this a lot harder: also fit the center of the sphere
	#h(x,y) = sqrt(r*r - (x-x0)**2 - (y-y0)**2) + z0
	#HBB 970522: distort the function, so it won't fit exactly:
	h(x,y) = sqrt(r*r - (abs(x-x0))**2.2 - (abs(y-y0))**1.8) + z0
	x0 = 0.1
	y0 = 0.2
	z0 = 0.3
	r  = 0.5
	set title 'the scattered points, and the initial parameter'
	splot 'hemisphr.dat' using 1:2:3, h(x,y)
	print "  fit function: " . GPFUN_h
	print "  fit command : fit h(x,y) 'hemisphr.dat' using 1:2:3 via r, x0, y0, z0"
	pause -1 "Press enter to start the fit."
	
	set fit results maxiter 50 errorscaling
	fit h(x,y) 'hemisphr.dat' using 1:2:3 via r, x0, y0, z0
	set title 'the scattered points, fitted curve'
	splot 'hemisphr.dat' using 1:2:3, h(x,y)
	
	print ''
	print "Notice, however, that this would converge much faster when fitted in a more"
	print "appropriate co-ordinate system:"
	print "  fit r 'hemisphr.dat' using 0:($1*$1+$2*$2+$3*$3) via r"
	print "where we are fitting f(x)=r to the radii calculated as the data is read from"
	print "the file.  No x value is required in this case."
	print "(This is left as an excercise for the user)."
	print ''
	
	print "Another possibility is to prescale the variables (set fit prescale),"
	print "which may improve convergence."
	print "  fit settings: set fit maxiter 50 prescale"
	print "  fit command : fit h(x,y) 'hemisphr.dat' using 1:2:3 via r, x0,y0,z0"
	#pause -1 "Press enter to start the fit."
	
	set fit maxiter 50 prescale
	x0 = 0.1
	y0 = 0.2
	z0 = 0.3
	r  = 0.5
	#fit h(x,y) 'hemisphr.dat' using 1:2:3 via r, x0, y0, z0
	#replot
	set fit maxiter 0 noprescale brief
	pause -1 "Press enter to proceed with the next example."
	
	
	print "\n\nNow an example on how to fit multi-branch functions."
	print "The model consists of two branches, the first describing longitudinal sound"
	print "velocity as function of propagation direction (upper data, from dataset 1),"
	print "the second describing transverse sound velocity (lower data, from dataset 0)."
	print ''
	print "The model uses these data in order to fit elastic stiffnesses which occur"
	print "differently in both branches."
	load 'hexa.fnc'
	load 'sound.par'
	set title 'sound data, and model with initial parameters'
	plot 'soundvel.dat', vlong(x), vtrans(x)
	print "  fit function: " . GPFUN_f
	print "                " . GPFUN_vlong
	print "                " . GPFUN_vtrans
	print "y will be the index of the dataset."
	print "  fit command: fit f(x,y) 'soundvel.dat' using 1:-2:2 via 'sound.par'\n"
	pause -1 "Press enter to start the fit."
	
	fit f(x,y) 'soundvel.dat' using 1:-2:2 via 'sound.par'
	
	set title 'pseudo-3d multi-branch fit to velocity data'
	plot 'soundvel.dat', vlong(x), vtrans(x)
	print ''
	print "Look at the file 'hexa.fnc' to see how the branches are realized using the"
	print "data index as input for a pseudo-3d fit."
	print ''
	pause -1 "Press enter to proceed with the next example."
	
	print "Next we only use every fifth data point for fitting by using the 'every'"
	print "keyword. Note the faster fit and its result."
	print "  fit command: fit f(x,y) 'soundvel.dat' every 5 using 1:-2:2 via 'sound.par'\n"
	load 'sound.par'
	plot 'soundvel.dat', vlong(x), vtrans(x)
	pause -1 "Press enter to start the fit."
	
	fit f(x,y) 'soundvel.dat' every 5 using 1:-2:2 via 'sound.par'
	set title 'fitted only every 5th data point'
	plot 'soundvel.dat', vlong(x), vtrans(x)
	print ''
	print "When you compare the results (see 'fit.log') you will note that the error of"
	print "the fitted parameters have become larger, and the quality of the plot is only"
	print "slightly affected."
	pause -1 "Press enter to proceed with the next example."
	
	print ''
	print "By marking some parameters as '# FIXED' in the parameter file, you fit only"
	print "the others (c44 and c13 are fixed here)."
	load 'sound2.par'
	set title 'initial parameters'
	plot 'soundvel.dat', vlong(x), vtrans(x)
	pause -1 "Press enter to start the fit."
	
	fit f(x,y) 'soundvel.dat' using 1:-2:2 via 'sound2.par'
	set title 'fit with c44 and c13 fixed'
	plot 'soundvel.dat', vlong(x), vtrans(x)
	print ''
	print "This has the same effect as specifying only the real free parameters by"
	print "the 'via' syntax:"
	print "  fit f(x) 'soundvel.dat' via c33, c11, phi0\n"
	pause -1 "Press enter to proceed with the next example."
	
	
	print "\n\nHere comes an example of a rather complex function.\n"
	set xlabel "Delta (degrees)"
	set ylabel "Reflectivity"
	load 'reflect.fnc'
	#HBB 970522: Changed initial values to something sensible, i.e. 
	#  something an experienced user of fit would actually use.
	#  The fit limit is also reduced, to ensure a better fit.
	eta = 1.2e-4
	tc  = 1.8e-3
	
	print "First a plot with all parameters set to initial values."
	set title 'data and initial parameters'
	plot 'moli3.dat' w e, R(x)
	print "Now fit the model function to the data."
	print "  fit settings: set fit limit 1e-10"
	print "  fit function: " . GPFUN_R
	print "                " . GPFUN_a
	print "                " . GPFUN_W
	print "  initial parameters:"
	print "                " . sprintf("eta = %g", eta)
	print "                " . sprintf("tc  = %g", tc)
	print "  fit command : fit R(x) 'moli3.dat' u 1:2:3 zerror via eta, tc\n"
	pause -1 "now start fitting...  (-> return)"
	
	set fit limit 1e-10 noerrorscaling
	fit R(x) 'moli3.dat' u 1:2:3 zerror via eta, tc
	set title 'fitted parameters'
	replot
	
	#HBB 970522: added comment on result of last fit.
	print ''
	print "Looking at the plot of the resulting fit curve, you can see that this function"
	print "doesn't really fit this set of data points.  This would normally be a reason to"
	print "to check for measurement problems not yet accounted for, and maybe even re-think"
	print "the theoretic prediction in use."
	print ''
	pause -1 "Press enter to proceed with the next example."
	
	
	set xlabel 'x'
	set ylabel 'y'
	set zlabel 'z'
	set ticslevel .2
	set zrange [-3:3]
	print ''
	print 'Next we show a fit with three independent variables.'
	print 'The file fit3.dat has four columns, with values of the three independent'
	print 'variables x, y and t, and the''resulting value z.  The data lines are in four'
	print 'sections, with t being constant within each section.  The sections are separated'
	print 'by two blank lines, so we can select sections with "index" modifiers.  Here are'
	print 'the data in the first section, where t = -3.'
	print ''
	print 'We will fit the function a0/(1 + a1*x**2 + a2*y**2) to these data.  Since at'
	print 'this point we have two independent variables, our "using" spec has four entries,'
	print 'representing x:y:z:s (where s is the estimated error in the z value).'
	
	a0=1; a1=.1; a2=.1
	f1(x,y)=a0/(1+a1*x**2+a2*y**2)
	print "  fit function: ", GPFUN_f1
	print "  fit command : fit f1(x,y) 'fit3.dat' index 0 using 1:2:4 via a0,a1,a2\n"
	set title "data and initial parameters"
	splot f1(x,y), 'fit3.dat' in 0 u 1:2:4
	pause -1 "Press enter to start the fit."
	
	set fit errorscaling
	fit f1(x,y) 'fit3.dat' index 0 using 1:2:4 via a0,a1,a2
	set title "fit to data with t = -3"
	splot f1(x,y), 'fit3.dat' in 0 u 1:2:4
	pause -1 "Press enter to proceed with the next example."
	
	print ''
	print "Here is the last set of data where t = 3."
	print "We fit the same function to this set."
	print "  fit function: ", GPFUN_f1
	print "  fit command : fit f1(x,y) 'fit3.dat' index 3 using 1:2:4 via a0,a1,a2\n"
	set title "fit to data with t = +3, initial parameters"
	splot f1(x,y), 'fit3.dat' in 3 u 1:2:4
	pause -1 "Press enter to start the fit."
	
	fit f1(x,y) 'fit3.dat' in 3 u 1:2:4 via a0,a1,a2
	set title "fit to data with t = +3"
	splot f1(x,y), 'fit3.dat' in 3 u 1:2:4
	pause -1 "Press enter to proceed with the next example."
	
	print ''
	print "We also have data for several intermediate values of t.  We will fit the"
	print "function f(x,y,t)=a0*t/(1+a1*x**2+a2*y**2) to all the data."
	f(x,y,t)=a0*t/(1+a1*x**2+a2*y**2)
	print "  fit function: ", GPFUN_f
	print "  fit command : fit f(x,y,t) 'fit3.dat' u 1:2:3:4 via a0,a1,a2\n"
	set title "data for all indices t, initial parameters"
	splot f(x,y,1), 'fit3.dat' u 1:2:4
	pause -1 "Press enter to start the fit."
	
	fit f(x,y,t) 'fit3.dat' u 1:2:3:4 via a0,a1,a2
	print ''
	print 'Here are all the data together.'
	set title "fit to all data"
	splot f(x,y,3),  'fit3.dat' in 3 u 1:2:4, \
	      f(x,y,1),  'fit3.dat' in 2 u 1:2:4, \
	      f(x,y,-1), 'fit3.dat' in 1 u 1:2:4, \
	      f(x,y,-3), 'fit3.dat' in 0 u 1:2:4
	
	print ''
	print 'You can use ranges to rename variables and/or limit the data included in the'
	print 'fit.  The first range corresponds to the first "using" entry, etc. For example,'
	print 'we could have gotten the same fit like this:'
	print '   fit [lon=*:*][lat=*:*][time=*:*]        \'
	print '     a0*time/(1 + a1*lon**2 + a2*lat**2)   \'
	print '     "fit3.dat" u 1:2:3:4 via a0,a1,a2'
	pause -1 "Press enter to proceed with the next example."
	
	reset
	print "\n"
	print "The fit command can handle errors in the independent variable, too."
	print "The problem shown here is Pearson's data with York's weights.\n"
	# Pearson's data and York's weights
	$PearsonYork << EOD
	#i x_i  W_xi  y_i  W_yi
	 1 0.0 1000.0 5.9   1.0
	 2 0.9 1000.0 5.4   1.8
	 3 1.8  500.0 4.4   4.0
	 4 2.6  800.0 4.6   8.0
	 5 3.3  200.0 3.5  20.0
	 6 4.4   80.0 3.7  20.0
	 7 5.2   60.0 2.8  70.0
	 8 6.1   20.0 2.8  70.0
	 9 6.5    1.8 2.4 100.0
	10 7.4    1.0 1.5 500.0
	EOD
	
	print "First draw the data with uncertainties and the initial function."
	set xlabel "x"
	set ylabel "y"
	set key right
	set yrange [0:8]
	set xrange [-1:9]
	
	# fit settings
	set fit limit 1.0e-8 start_lambda 1 prescale noerrorscaling errorvariables
	
	# fit function and initial values
	f(x)  = a1 + a2*x
	a1 = 5.
	a2 = -0.5
	fy(x) = a1y + a2y*x
	a1y = 5.
	a2y = -0.5
	
	set title "Pearson's data and York's weights\noriginal data and the initial function"
	plot \
	  $PearsonYork using 2:4:(sqrt(1./$3)):(sqrt(1./$5)) with xyerrorbars lt -1 title 'data', \
	  f(x) lw 2 lt 1 title 'initial function'
	  
	msg = "Press enter to fit the data using no error values"
	print "\n". msg . "\n"
	pause -1 msg
	
	# do the fit
	fit f(x) $PearsonYork using 2:4 via a1, a2
	
	set title "Pearson's data and York's weights\nfunction fit with no error terms"
	plot \
	  $PearsonYork using 2:4:(sqrt(1./$3)):(sqrt(1./$5)) lt -1 with xyerrorbars title 'data', \
	  f(x) lw 2 lt 1 title 'fit using no error terms'
	
	msg = "Press enter to fit the data using only the uncertainties of the y-values."
	print "\n". msg . "\n"
	pause -1 msg
	
	# do the fit
	fit fy(x) $PearsonYork using 2:4:(sqrt(1./$5)) yerrors via a1y, a2y
	
	set title "Pearson's data and York's weights\nfunction fit with yerror keyword"
	plot \
	  $PearsonYork using 2:4:(sqrt(1./$3)):(sqrt(1./$5)) lt -1 with xyerrorbars title 'data', \
	  fy(x) lw 2 lt 2 title 'fit using only dy'
	
	msg = "Press enter to fit the data using the uncertainties of the x and y values."
	print "\n". msg . "\n"
	pause -1 msg
	
	
	# fit function and initial values
	fxy(x) = a1xy + a2xy*x
	a1xy = 5
	a2xy = -0.5
	#a1xy = 5.48
	#a2xy = -0.481
	
	# do the fit
	fit fxy(x) $PearsonYork using 2:4:(sqrt(1./$3)):(sqrt(1./$5)) xyerrors via a1xy, a2xy
	
	set title "Pearson's data and York's weights\nfunction fit with xyerror keyword"
	plot \
	  $PearsonYork using 2:4:(sqrt(1./$3)):(sqrt(1./$5)) with xyerrorbars lt -1 title 'data', \
	  f(x) lw 2 lt 1 title 'fit with no errors', \
	  fy(x) lw 2 lt 2 title 'fit using only dy', \
	  fxy(x) lw 2 lt 3 title 'fit using dx and dy'
	
	print "\n\nSummary of the fit results:"
	print         "                        a1           a2           a1_err       a2_err"
	print         "------------------------------------------------------------------------"
	print sprintf("initial values          %.3e  %.2e", 5, -0.5)
	print sprintf("our result              %.3e  %.2e   %.3e  %.2e", a1xy, a2xy, a1xy_err, a2xy_err)
	print sprintf("ROOT Minuit             %.3e  %.2e   %.3e  %.2e", 5.47990e+000, -4.80531e-001, 2.92561e-001, 5.76089e-002)
	print         "------------------------------------------------------------------------"
	
	
	print ''
	print "You can have a look at all previous fit results by looking into the file"
	print "'fit.log' (or whatever you defined the environment variable 'FIT_LOG' to)."
	print "Remember that this file will always be appended to, so remove it from time"
	print "to time."
	
	print ''
	pause -1 "Done with fitting demo  (-> return)"
	reset
	set encoding myencoding
	# release datablock
	undefine $PearsonYork

snippet fitmulti.dem
	# fitmulti.dem
	#
	# Use up to 5D fitting to determine coefficients in linear combinations
	# of arbitrarily chosen, but linearly independent basis functions.
	# The fit should reproduce these coefficients with high precision.
	# Hanno Hoffstadt	August 2007
	#
	# Increase to 6 dimensions to test revised fit code.
	# Replace a recursive call statement with a while <expr> { ... } loop.
	# Use "set dummy x, y, x2, x3" to specify dummy parameter names used
	# by the fit functions.
	# Ethan A Merritt	June 2013
	#
	
	
	# the basis functions
	f1(x) = sin(x)
	f2(x) = exp(-x)
	f3(x) = sqrt(x)
	f4(x) = cos(x)
	f5(x) = x*x
	f6(x) = sin(x+3.)/(x+3.)
	
	# the coefficients for linear combinations
	k1=   2.5
	k2=  -1.8
	k3=  70.0
	k4=  -3.2
	k5=   0.4
	k6=  -0.25
	
	# generate data table containing functions f1(x) to f6(x),
	# and linear combinations k1*f1(x) to k1*f1(x)+...+k6*f6(x)
	# in increments of dx, up to a value xmax
	#
	set print $fitmulti
	#
	x=0
	dx=0.5
	xmax=10
	while (x <= xmax) {
	
	    lc1 = k1 * f1(x)
	    lc2 = lc1 + k2 * f2(x)
	    lc3 = lc2 + k3 * f3(x)
	    lc4 = lc3 + k4 * f4(x)
	    lc5 = lc4 + k5 * f5(x)
	    lc6 = lc5 + k6 * f6(x)
	
	    # columns 1-5,11 contain the "tabulated basis functions"
	    # columns 6-10,12 contain linear combinations with given coefficients
	    # and increasing number of components
	
	    print f1(x), f2(x), f3(x), f4(x), f5(x), lc1, lc2, lc3, lc4, lc5, f6(x), lc6
	
	    x = x + dx
	
	}  # End of while loop
	
	unset print
	
	# fitting models
	m1(f1) = c1*f1
	m2(f1,f2) = c1*f1 + c2*f2
	m3(f1,f2,f3) = c1*f1 + c2*f2 + c3*f3
	m4(f1,f2,f3,f4) = c1*f1 + c2*f2 + c3*f3 + c4*f4
	m5(f1,f2,f3,f4,f5) = c1*f1 + c2*f2 + c3*f3 + c4*f4 + c5*f5
	m6(f1,f2,f3,f4,f5,f6) = c1*f1 + c2*f2 + c3*f3 + c4*f4 + c5*f5 + c6*f6
	
	# 1D fit:
	# basis function in column 1,
	# combination with one component in column 6
	#
	# since column 6 contains k1 * f1(x), i.e. k1 times column 1,
	# the fit is expected to find c1 == k1.
	#
	# for all fits, the assumed error is 1, and is given last in the using spec.
	
	c1=1
	fit m1(x) $fitmulti u 1:6:(1) via c1
	print "---------------------------------------------------------"
	print "1D fit: expected ", k1, " c1 = ", c1
	print "---------------------------------------------------------"
	pause -1 "Hit return to try for a 2D fit"
	
	# 2D fit:
	# basis functions in columns 1 and 2,
	# combination with two components in column 7
	#
	# do not forget to reset the c values so that the fit needs to search anew.
	# Check the functioning of preset axis range limit on y
	
	c1=1
	c2=1
	set yrange [0:.99]
	fit m2(x,y) $fitmulti u 1:2:7:(1) via c1,c2
	print "---------------------------------------------------------"
	print "2D fit: expected ", k1, " c1 = ", c1
	print "2D fit: expected ", k2, " c2 = ", c2
	print "---------------------------------------------------------"
	set yrange [*:*]
	pause -1 "Hit return to try for a 3D fit"
	
	# 3D fit:
	# basis functions in columns 1, 2, 3,
	# combination with three components in column 8
	
	c1=1
	c2=1
	c3=1
	set dummy x, y, x2, x3, x4, x5
	fit m3(x,y,x2) $fitmulti u 1:2:3:8:(1) via c1,c2,c3
	print "---------------------------------------------------------"
	print "3D fit: expected ", k1, " c1 = ", c1
	print "3D fit: expected ", k2, " c2 = ", c2
	print "3D fit: expected ", k3, " c3 = ", c3
	print "---------------------------------------------------------"
	pause -1 "Hit return to try for a 4D fit"
	
	# 4D fit:
	# basis functions in columns 1, 2, 3, 4,
	# combination with four components in column 9
	
	c1=1
	c2=1
	c3=1
	c4=1
	fit m4(x,y,x2,x3) $fitmulti u 1:2:3:4:9:(1) via c1,c2,c3,c4
	print "---------------------------------------------------------"
	print "4D fit: expected ", k1, " c1 = ", c1
	print "4D fit: expected ", k2, " c2 = ", c2
	print "4D fit: expected ", k3, " c3 = ", c3
	print "4D fit: expected ", k4, " c4 = ", c4
	print "---------------------------------------------------------"
	pause -1 "Hit return to try for a 5D fit"
	
	# 5D fit:
	# basis functions in columns 1, 2, 3, 4, 5,
	# combination with five components in column 10
	
	c1=1
	c2=1
	c3=1
	c4=1
	c5=1
	fit m5(x,y,x2,x3,x4) $fitmulti u 1:2:3:4:5:10:(1) via c1,c2,c3,c4,c5
	print "---------------------------------------------------------"
	print "5D fit: expected ", k1, " c1 = ", c1
	print "5D fit: expected ", k2, " c2 = ", c2
	print "5D fit: expected ", k3, " c3 = ", c3
	print "5D fit: expected ", k4, " c4 = ", c4
	print "5D fit: expected ", k5, " c5 = ", c5
	print "---------------------------------------------------------"
	pause -1 "Hit return to try for a 6D fit"
	
	print "This 6D fit will fail in 4.6 but 4.7 can handle more parameters"
	# 6D fit:
	# basis functions in columns 1, 2, 3, 4, 5, 11
	# combination with five components in column 12
	# test inline range spec
	
	c1=1
	c2=1
	c3=1
	c4=1
	c5=1
	c6=1
	fit [][][][][][-0.2:*] m6(x,y,x2,x3,x4,x5) $fitmulti u 1:2:3:4:5:11:12:(1) via c1,c2,c3,c4,c5,c6
	print "---------------------------------------------------------"
	print "6D fit: expected ", k1, " c1 = ", c1
	print "6D fit: expected ", k2, " c2 = ", c2
	print "6D fit: expected ", k3, " c3 = ", c3
	print "6D fit: expected ", k4, " c4 = ", c4
	print "6D fit: expected ", k5, " c5 = ", c5
	print "6D fit: expected ", k6, " c6 = ", c6
	print "FIT_NDF = ",FIT_NDF," after range filters (expected 14)"
	print "---------------------------------------------------------"
	pause -1 "Hit return to end multidimension fit demo"
	reset

snippet fontfile.dem
	#
	# $Id: fontfile.dem,v 1.5 2012/06/23 07:48:00 markisch Exp $
	#
	# This file demonstrates new features in gnuplot 3.8i
	# -1- loading fontfiles for postscript output
	print "\n This file demonstrates how to use postscript type 1 font"
	print " files with gnuplot."
	print "\n Please note that this demo runs only on postscript terminal,"
	print " because fontfile inclusion is only supported by this terminal."
	print "\n In addition, this demo needs two font files which are not"
	print " included in the gnuplot distribution."
	print " On systems with ghostscript they should be found automatically."
	print " If not, they can be downloaded from the net:"
	print " http://sourceforge.net/projects/gs-fonts/"
	print " Please put the files \"p052003l.pfb\" and \"p052023l.pfb\""
	print " somewhere they are found, e.g. into the working directory."
	pause -1 "Hit return to continue"
	#
	      xmin=-270.
	      xmax=270.
	      ymin=-1.1
	      ymax=1.1
	#
	reset
	set terminal push
	set terminal postscript enhanced linewidth 2 'Times-Roman' 22 \
	    fontfile 'p052003l.pfb' fontfile 'p052023l.pfb'
	set output 'fontfile.ps'
	unset autoscale
	set xr [xmin:xmax]
	set yr [ymin:ymax]
	#
	save_encoding = GPVAL_ENCODING
	set encoding iso_8859_1
	set format y '%.1f'
	set label 'Times-Roman: {/Times-Roman abcdefghABCDEFGH }' at graph 0.05,0.6 left
	set label 'URWPalladioL-Roma: {/URWPalladioL-Roma abcdefghABCDEFGH }' at graph 0.05,0.5 left
	set label 'URWPalladioL-Ital: {/URWPalladioL-Ital abcdefghABCDEFGH }' at graph 0.05,0.4 left
	set xlabel '{/URWPalladioL-Ital x} / '
	set ylabel '{/URWPalladioL-Ital y}'
	plot sin(pi/180.*x) title '{/URWPalladioL-Ital y} = sin {/URWPalladioL-Ital x}'
	print "\n Now have a look at the file fontfile.ps"
	set output
	set terminal pop
	set encoding save_encoding
	reset

snippet fontfile_latex.dem
	#
	# $Id: fontfile_latex.dem,v 1.4 2012/06/23 07:48:00 markisch Exp $
	#
	# This file demonstrates new features in gnuplot 3.8i
	# -1- loading fontfiles for postscript output
	print "\n This file demonstrates how to use postscript type 1 font"
	print " files with gnuplot."
	print "\n Please note that this demo runs only on postscript terminal,"
	print " because fontfile inclusion is only supported by this terminal."
	print "\n In addition, this demo needs two font files which are not"
	print " included in the gnuplot distribution."
	print " On unix systems with a teTeX installation at least \"cmmi10.pfb\" should be"
	print " found automatically."
	print " If not, they can be downloaded from the net:"
	print " ftp://ftp.dante.de/tex-archive/fonts/ps-type1/cm-super/pfb/sfrm1000.pfb"
	print " ftp://ftp.dante.de/tex-archive/fonts/cm/ps-type1/bluesky/pfb/cmmi10.pfb"
	print " Please put the files \"sfrm1000.pfb\" and \"cmmi10.pfb\""
	print " somewhere they are found, e.g. into the working directory."
	pause -1 "Hit return to continue"
	#
	      xmin=-270.
	      xmax=270.
	      ymin=-1.1
	      ymax=1.1
	#
	reset
	set terminal push
	set terminal postscript enhanced linewidth 2 'SFRM1000' 22 \
	    fontfile 'sfrm1000.pfb' fontfile 'cmmi10.pfb'
	set output 'fontfile_latex.ps'
	unset autoscale
	set xr [xmin:xmax]
	set yr [ymin:ymax]
	#
	save_encoding = GPVAL_ENCODING
	set encoding iso_8859_1
	set format y '%.1f'
	set label 'Times-Roman: {/Times-Roman abcdefghABCDEFGH }' at graph 0.05,0.6 left
	set label 'SFRM1000: {/SFRM1000 abcdefghABCDEFGH }' at graph 0.05,0.5 left
	set label 'CMMI10: {/CMMI10 abcdefghABCDEFGH} {/CMMI10 \013\014\015\016\042}' at graph 0.05,0.4 left
	set xlabel '{/CMMI10 \013} / '
	set ylabel '{/CMMI10 y}'
	plot sin(pi/180.*x) title '{/CMMI10 y} = sin {/CMMI10 \013}'
	print "\n Now have a look at the file fontfile_latex.ps"
	print "\n If you have installed TeX, you can generate a table with the"
	print " encoding of CMMI10 by typing \"tex testfont\""
	print " answering \"cmmi10\","
	print " and then typing \"\\table\\bye\"."
	print " Or have a look at the file \"ps_fontfile_doc.ps\" in the directory"
	print " docs/psdoc of the Gnuplot source distribution
	print " If the text \"SFRM1000\" looks like Helvetica you may have an old version"
	print " of the cm-super fonts. Then you will have to use lowercase font names or"
	print " download a new version."
	set output
	set terminal pop
	set encoding save_encoding
	reset

snippet gantt.dem
	#
	# Very simple Gantt Chart
	# Demonstrate using timecolumn(N,format) to plot time data from 
	# multiple columns
	#
	$DATA << EOD
	#Task start      end
	A     2012-11-01 2012-12-31
	B     2013-01-01 2013-03-14
	C     2013-03-15 2014-04-30
	D     2013-05-01 2013-06-30
	E     2013-07-01 2013-08-31
	F1    2013-09-01 2013-10-31
	F2    2013-09-01 2014-01-17
	F3    2013-09-01 2014-01-30
	F4    2013-09-01 2014-03-31
	G1    2013-11-01 2013-11-27
	G2    2013-11-01 2014-01-17
	L     2013-11-28 2013-12-19
	M     2013-11-28 2014-01-17
	N     2013-12-04 2014-03-02
	O     2013-12-20 2014-01-17
	P     2013-12-20 2014-02-16
	Q     2014-01-05 2014-01-13
	R     2014-01-18 2014-01-30
	S     2014-01-31 2014-03-31
	T     2014-03-01 2014-04-28
	EOD
	
	set xdata time
	timeformat = "%Y-%m-%d"
	set format x "%b\n'%y"
	
	set yrange [-1:]
	OneMonth = strptime("%m","2")
	set xtics OneMonth nomirror
	set xtics scale 2, 0.5
	set mxtics 4
	set ytics nomirror
	set grid x y
	unset key
	set title "{/=15 Simple Gantt Chart}\n\n{/:Bold Task start and end times in columns 2 and 3}"
	set border 3
	
	T(N) = timecolumn(N,timeformat)
	
	set style arrow 1 filled size screen 0.02, 15 fixed lt 3 lw 1.5
	
	plot $DATA using (T(2)) : ($0) : (T(3)-T(2)) : (0.0) : yticlabel(1) with vector as 1, \
	     $DATA using (T(2)) : ($0) : 1 with labels right offset -2
	
	pause -1 "Hit return to check backwards compatibility with v4 syntax"
	
	set timefmt timeformat
	plot $DATA using (timecolumn(2)) : ($0) : (T(3)-T(2)) : (0.0) : yticlabel(1) with vector as 1, \
	     $DATA using 2 : ($0) : 1 with labels right offset -2
	
	pause -1 "Hit return to continue"
	reset

snippet heatmaps.dem
	#
	# Various ways to create a 2D heat map from ascii data
	#
	
	set title "Heat Map generated from a file containing Z values only"
	unset key
	set tic scale 0
	
	# Color runs from white to green
	set palette rgbformula -7,2,-7
	set cbrange [0:5]
	set cblabel "Score"
	unset cbtics
	
	set xrange [-0.5:4.5]
	set yrange [-0.5:4.5]
	
	$map1 << EOD
	5 4 3 1 0
	2 2 0 0 1
	0 0 0 1 0
	0 0 0 2 3
	0 1 2 4 3
	EOD
	
	set view map
	splot '$map1' matrix with image
	
	pause -1 "Hit return to continue"
	
	set title "Heat Map generated by 'plot' from a stream of XYZ values"\
	         ."\nNB: Rows must be separated by blank lines!"
	
	$map2 << EOD
	0 0 5
	0 1 4
	0 2 3
	0 3 1
	0 4 0
	
	1 0 2
	1 1 2
	1 2 0
	1 3 0
	1 4 1
	
	2 0 0
	2 1 0
	2 2 0
	2 3 1
	2 4 0
	
	3 0 0
	3 1 0
	3 2 0
	3 3 2
	3 4 3
	
	4 0 0
	4 1 1
	4 2 2
	4 3 4
	4 4 3
	EOD 
	
	plot '$map2' using 2:1:3 with image
	
	pause -1 "Hit return to continue"
	
	set title "Heat map with non-zero pixel values written as labels"
	plot $map1 matrix using 1:2:3 with image, \
	     $map1 matrix using 1:2:($3 == 0 ? "" : sprintf("%g",$3) ) with labels
	
	pause -1 "Hit return to continue"
	
	set title "Heat map from csv data with column and row labels"
	
	$map3 << EOD
	,Apple,Bacon,Cream,Donut,Eclair
	Row 1, 5, 4, 3, 1, 0
	Row 2, 2, 2, 0, 0, 1
	Row 3, 0, 0, 0, 1, 0
	Row 4, 0, 0, 0, 2, 3
	Row 5, 0, 1, 2, 4, 3
	EOD
	
	set datafile separator comma
	plot '$map3' matrix rowheaders columnheaders using 1:2:3 with image
	set datafile separator
	
	pause -1
	
	# Some output modes (SVG, HTML5) allow smoothing of adjacent pixels,
	# which may not be desired. This is not normally an issue with PNG or PDF.
	# The `pixels` option forces pixel-by-pixel drawing with no smoothing.
	
	unset colorbox
	set format xy ""
	set multiplot layout 1,2 title "Compare 'image' and 'image pixels' modes" \
	    margin screen 0.05, 0.95, 0.10, 0.85 spacing screen 0.05
	set title "plot with image"
	plot $map1 matrix using 1:2:3 with image
	set title "plot with image pixels"
	plot $map1 matrix using 1:2:3 with image pixels
	unset multiplot
	
	pause -1 "Hit return to continue"
	
	reset
	
	#
	# Demonstrate use of 4th data column to color a 3D surface.
	# Also demonstrate use of the pseudodata special file '++'.
	# This plot is nice for exploring the effect of the 'l' and 'L' hotkeys.
	#
	set view 49, 28, 1, 1.48
	set urange [ 5 : 35 ] noreverse nowriteback
	set vrange [ 5 : 35 ] noreverse nowriteback
	# set zrange [ 1.0 : 3.0 ] noreverse nowriteback
	set ticslevel 0
	set format cb "%4.1f"
	set colorbox user size .03, .6 noborder
	set cbtics scale 0
	
	set samples 25, 25
	set isosamples 50, 50
	
	set title "4D data (3D Heat Map)"\
	          ."\nIndependent value color-mapped onto 3D surface"  offset 0,1
	set xlabel "x" offset 3, 0, 0 
	set ylabel "y" offset -5, 0, 0
	set zlabel "z" offset 2, 0, 0 
	set pm3d implicit at s
	
	Z(x,y) = 100. * (sinc(x,y) + 1.5)
	sinc(x,y) = sin(sqrt((x-20.)**2+(y-20.)**2))/sqrt((x-20.)**2+(y-20.)**2)
	color(x,y) = 10. * (1.1 + sin((x-20.)/5.)*cos((y-20.)/10.))
	
	splot '++' using 1:2:(Z($1,$2)):(color($1,$2)) with pm3d title "4 data columns x/y/z/color"
	
	pause -1 "Hit return to continue"
	
	set title "4D data (3D Heat Map)"\
	          ."\nZ is contoured. Independent value is color-mapped"  offset 0,1
	
	set view map
	set ylabel norotate offset -1,0
	
	set contour base
	splot '++' using 1:2:(Z($1,$2)):(color($1,$2)) with pm3d title "4 data columns x/y/z/color"
	
	pause -1 "Hit return to continue"
	
	# Release datablocks used in this demo
	undefine $map1 $map2 $map3
	reset

snippet hidden2.dem
	#
	# PM3D surfaces are not fully included in the calculation of hidden line removal.
	# Older versions of gnuplot allowed you to approximate the correct occlusion
	# by drawing the surface twice, once "with pm3d" to produce the surface you
	# want to show, and once "with lines lt -2" to include the same surface in
	# hidden3d calculations but drawn with invisible lines.
	# Current gnuplot does the approximation for you so the extra copy of the 
	# surface is no longer necessary.
	#
	set isosamples 25,25
	set xyplane at 0
	unset key
	
	set palette rgbformulae 31,-11,32
	set style fill solid 0.5
	set cbrange [-1:1]
	
	set title "Mixing pm3d surfaces with hidden-line plots"
	
	f(x,y) = sin(-sqrt((x+5)**2+(y-7)**2)*0.5)
	
	set hidden3d front
	splot f(x,y) with pm3d, x*x-y*y with lines lc rgb "black"
	
	pause -1 "Hit return to continue"
	reset
	
	#
	# Image surfaces are similiarly overlooked in the calculation of hidden line removal.
	# I.e. this demo doesn't actually work yet.  It's here for testing.
	#
	set isosamples 25,25
	set xyplane at 0
	unset key
	
	set cbrange [0:255]
	set palette cubehelix
	
	set title "Mixing image surface with hidden-line plots"
	
	set hidden3d front
	splot 'blutux.rgb' binary array=(128,128) flip=y format='%uchar%uchar%uchar' origin=(-10,-10,0) dx=0.156 dy=0.156 using ($1+$2+$3)/3 with image, \
	x*x-y*y with lines lc rgb "black"
	
	pause -1 "Hit return to continue"
	reset
	#
	# Another example of pm3d hidden surface removal,
	# this time using pm3d depth-ordering
	#
	set multiplot title "Interlocking Tori"
	set title "PM3D surface\nno depth sorting"
	
	set parametric
	set urange [-pi:pi]
	set vrange [-pi:pi]
	set isosamples 50,20
	
	set origin -0.02,0.0
	set size 0.55, 0.9
	
	unset key
	unset xtics
	unset ytics
	unset ztics
	set border 0
	set view 60, 30, 1.5, 0.9
	unset colorbox
	
	set pm3d scansbackward
	splot cos(u)+.5*cos(u)*cos(v),sin(u)+.5*sin(u)*cos(v),.5*sin(v) with pm3d, \
	      1+cos(u)+.5*cos(u)*cos(v),.5*sin(v),sin(u)+.5*sin(u)*cos(v) with pm3d
	
	set title "PM3D surface\ndepth sorting"
	
	set origin 0.40,0.0
	set size 0.55, 0.9
	set colorbox vertical user origin 0.9, 0.15 size 0.02, 0.50
	set format cb "%.1f"
	
	set pm3d depthorder
	splot cos(u)+.5*cos(u)*cos(v),sin(u)+.5*sin(u)*cos(v),.5*sin(v) with pm3d, \
	      1+cos(u)+.5*cos(u)*cos(v),.5*sin(v),sin(u)+.5*sin(u)*cos(v) with pm3d
	
	unset multiplot
	
	pause -1 "Hit return to continue"
	
	reset

snippet hidden.dem
	#
	# $Id: hidden.dem,v 1.7 2003/10/28 05:35:54 sfeam Exp $
	#
	#
	set samples 20
	set isosamples 20
	set hidden3d
	set title "Hidden line removal of explicit surfaces"
	
	
	set xrange [-3:3]
	set yrange [-2:2]
	splot 1 / (x*x + y*y + 1)
	pause -1 "Hit return to continue (1)"
	
	set xrange [-1:1]
	set yrange [-1:1]
	splot x*y / (x**2 + y**2 + 0.1)
	pause -1 "Hit return to continue (2)"
	
	set view 70,45
	unset contour
	set xrange [-3:3]
	set yrange [-3:3]
	splot sin(x*x + y*y) / (x*x + y*y)
	pause -1 "Hit return to continue (3)"
	
	set view 60,30
	set xrange [-3:3]
	set yrange [-3:3]
	set zrange [-1:1]
	set ztics -1,0.5,1
	set grid z
	set border 4095
	splot sin(x) * cos(y)
	pause -1 "Hit return to continue (4)"
	
	unset grid
	set ztics autofreq
	set border 31
	set view 75,230
	set contour
	replot
	pause -1 "Hit return to continue (5)"
	
	set view 80,30,1,1
	set style data lines
	# autoranging loses the verticals
	set xrange [0:15]
	set yrange [0:15]
	splot "glass.dat" using 1
	pause -1 "Hit return to continue (6)"
	
	set view 50
	set grid
	replot
	pause -1 "Hit return to continue (7)"
	
	reset
	

snippet histograms2.dem
	#
	# Example of using histogram modes
	#
	reset
	set title "US immigration from Europe by decade"
	set datafile missing "-"
	set xtics nomirror rotate by -45
	set key noenhanced
	#
	# 'newhistogram' keyword to plot
	#
	set title "Default Histogram Colouring"
	set key under
	set key invert autotitle columnhead
	set style data histogram
	set style histogram clustered gap 1 title offset 2,-2 font ",11" boxed
	set style fill solid noborder
	set boxwidth 0.95
	unset xtics
	set xtics rotate by -45
	set xlabel "Immigration from different regions" offset 0, 0
	set ytics
	set grid y
	set auto y
	set bmargin 12
	#
	plot \
	newhistogram "Northern Europe", \
	'immigration.dat' using 6:xtic(1), '' u 13, '' u 14, \
	newhistogram "Southern Europe", \
	'' u 9:xtic(1), '' u 17 , '' u 22 , \
	newhistogram "British Isles", \
	'' u 10:xtic(1) , '' u 21 
	#
	pause -1 "Same plot using explicit histogram start colors"
	set title "Explicit start color in 'newhistogram' command"
	plot \
	newhistogram "Northern Europe" lt 4, \
	'immigration.dat' using 6:xtic(1), '' u 13, '' u 14, \
	newhistogram "Southern Europe" lt 4, \
	'' u 9:xtic(1), '' u 17, '' u 22, \
	newhistogram "British Isles" lt 4, \
	'' u 10:xtic(1), '' u 21
	#
	#
	pause -1 "Same plot using explicit histogram start pattern"
	set title "Explicit start pattern in 'newhistogram' command"
	set style fill pattern 1 border -1
	plot \
	newhistogram "Northern Europe" fs pattern 1, \
	'immigration.dat' using 6:xtic(1), '' u 13, '' u 14, \
	newhistogram "Southern Europe" fs pattern 1, \
	'' u 9:xtic(1), '' u 17, '' u 22, \
	newhistogram "British Isles" fs pattern 1, \
	'' u 10:xtic(1), '' u 21
	#
	#
	pause -1 "Same plot with both explicit color and pattern"
	set title "Explicit start pattern and linetype"
	set style fill pattern 1 border -1
	plot \
	newhistogram "Northern Europe" lt 2 fs pattern 1, \
	'immigration.dat' using 6:xtic(1), '' u 13, '' u 14, \
	newhistogram "Southern Europe" lt 2 fs pattern 1, \
	'' u 9:xtic(1), '' u 17, '' u 22, \
	newhistogram "British Isles" lt 2 fs pattern 1, \
	'' u 10:xtic(1), '' u 21
	#
	#
	pause -1 "Hit return to continue"
	reset

snippet histograms.dem
	#
	# Example of using histogram modes
	#
	reset
	set title "US immigration from Europe by decade"
	set datafile missing "-"
	set xtics nomirror rotate by -45
	set key noenhanced
	#
	# First plot using linespoints
	set style data linespoints
	plot 'immigration.dat' using 2:xtic(1) title columnheader(2), \
	for [i=3:22] '' using i title columnheader(i)
	#
	pause -1 "<cr> to plot the same data as a histogram"
	#
	set title "US immigration from Northern Europe\nPlot selected data columns as histogram of clustered boxes"
	set auto x
	set yrange [0:300000]
	set style data histogram
	set style histogram cluster gap 1
	set style fill solid border -1
	set boxwidth 0.9
	set xtic rotate by -45 scale 0
	#set bmargin 10 
	plot 'immigration.dat' using 6:xtic(1) ti col, '' u 12 ti col, '' u 13 ti col, '' u 14 ti col
	#
	pause -1 "<cr> to change the gap between clusters"
	#
	set title "US immigration from Northern Europe\n(same plot with larger gap between clusters)"
	set style histogram gap 5
	replot
	#
	pause -1 "<cr> to plot the same dataset as stacked histogram"
	#
	#
	# Stacked histograms
	#
	set title "US immigration from Europe by decade\nPlot as stacked histogram"
	set key invert reverse Left outside
	set key autotitle columnheader
	set yrange [0:7e6]
	set auto x
	unset xtics
	set xtics nomirror rotate by -45 scale 0
	set style data histogram
	set style histogram rowstacked
	set style fill solid border -1
	set boxwidth 0.75
	#
	plot 'immigration.dat' using 2:xtic(1), for [i=3:22] '' using i
	#
	pause -1 "<cr> to rescale each stack to % of total"
	#
	# Stacked histograms by percent
	#
	set title "US immigration from Europe by decade\nFraction of total plotted as stacked histogram"
	set key invert reverse Left outside
	set yrange [0:100]
	set ylabel "% of total"
	unset ytics
	set grid y
	set border 3
	set style data histograms
	set style histogram rowstacked
	set style fill solid border -1
	set boxwidth 0.75
	#
	plot 'immigration.dat' using (100.*$2/$24):xtic(1) t column(2), \
	     for [i=3:23] '' using (100.*column(i)/column(24)) title column(i) 
	#
	pause -1 "Now try histograms stacked by columns"
	#
	# Columnstacks
	#	xtic labels should be picked up from column heads ('title column')
	#	key titles should be picked up from row heads ('key(1)')
	#
	set title "Immigration from Northern Europe\n(columstacked histogram)"
	set style histogram columnstacked
	set key noinvert box
	set yrange [0:*]
	set ylabel "Immigration by decade"
	set xlabel "Country of Origin"
	set tics scale 0.0
	set ytics
	unset xtics
	set xtics norotate nomirror
	plot 'immigration.dat' using 6 ti col, '' using 12 ti col, \
	     '' using 13 ti col, '' using 14:key(1) ti col
	#
	pause -1 "Next we do several sets of parallel histograms"
	#
	# 'newhistogram' keyword to plot
	#
	set title "Immigration from different regions\n(give each histogram a separate title)"
	set key under nobox
	set style histogram clustered gap 1 title offset 2,0.25
	set style fill solid noborder
	set boxwidth 0.95
	unset xtics
	set xtics nomirror rotate by -45 scale 0 font ",8"
	set xlabel "(note: histogram titles have specified offset relative to X-axis label)" offset 0,-2
	set ytics font ",8"
	set grid y
	set auto y
	plot \
	newhistogram "Northern Europe", \
	'immigration.dat' using "Sweden":xtic(1) t col, '' u "Denmark" t col, '' u "Norway" t col, \
	newhistogram "Southern Europe", \
	'' u "Greece":xtic(1) t col, '' u "Romania" t col, '' u "Yugoslavia" t col, \
	newhistogram "British Isles", \
	'' u "Ireland":xtic(1) t col, '' u "United_Kingdom" t col
	#
	pause -1 "Same plot using rowstacked histogram"
	#
	set style histogram rows
	set boxwidth 0.8
	set yrange [0:900000]
	set xlabel "(Same plot using rowstacked rather than clustered histogram)"
	replot
	#
	pause -1 "<cr> to finish histogram demo"
	reset

snippet hypertext.dem
	#
	# Demonstrates how to attach hypertext to points so that
	# the text is displayed on mouse-over.
	# Not much to see here unless you are using the wxt, svg, qt,
	# or HTML5 canvas terminal.
	# 
	set title 'Hypertext is shown when the mouse is over a point'
	
	Scale(size) = 0.08*sqrt(sqrt(column(size)))
	City(String,Size) = sprintf("%s\npop: %d", stringcolumn(String), column(Size))
	
	set termoption enhanced
	save_encoding = GPVAL_ENCODING
	set encoding utf8
	unset xtics
	unset ytics
	unset key
	set border 0
	set size square
	set datafile separator "\t"
	
	plot 'cities.dat' using 5:4:(City(1,3)):(Scale(3)) \
	     with labels hypertext point pt 7 ps var lc rgb "#ffee99", \
	     'cities.dat' using 5:4:(Scale(3)) \
	     with points pt 6 ps var lc rgb "black" lw 0.1
	
	pause -1 "hit return to continue"
	set encoding save_encoding
	reset

snippet image2.dem
	# demo for plotting images using pixels and binary 2d data
	#
	# This demo can be used for terminals supporting image display.
	# Currently supported are:  X11, Aqua, postscript, png, pdf, svg,
	# (e)pslatex, pstex, emf, wxt.
	
	# Prepared by Dan Sebald
	# History:
	#	- Nov   2006 EAM: split into two files
	#	- 9.23. 2003 ds: redone in response to discussion list feedback
	#	- 5. 3. 2003 ds: 1st version
	
	# Repeat of the 'using.dem' demo, but with binary 2d data.  'using.bin' is
	# in the format int32, int8, int16/100, int8, int16/100, int16/100.
	#
	print ""
	print "Not only can the 2d binary data mode be used for image data."
	print "Here is an example that repeats the \`using.dem\` demo with the"
	print "same data, but stored in binary format of differing sizes.  It"
	print "uses different format specifiers within the \'format\' string."
	print "There are machine dependent and machine independent specifiers,"
	print "display by the command \'show datafile binary datasizes\':"
	print ""
	show datafile binary datasizes
	print ""
	set title "Convex     November 1-7 1989    Circadian"
	set key left box
	set xrange[-1:24]
	plot 'using.bin' binary format='%*int32%int8%*int16%int8%*int16%*int16' using 1:2 title "Logged in" with impulses,\
	     'using.bin' binary format='%*int32%int8%*int16%int8%*int16%*int16' using 1:2 title "Logged in" with points
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "Again, a different format specification for \`using\` can be"
	print "used to select different \"columns\" within the file."
	print ""
	set xrange [1:8]
	set title "Convex     November 1-7 1989"
	set key below
	set label "(Weekend)" at 5,25 center
	plot 'using.bin' binary format='%*int32%*int8%int16%int8%*int16%*int16' using ($1/100):2 title "Logged in" with impulses,\
	     'using.bin' binary format='%*int32%*int8%int16%*int8%int16%*int16' using ($1/100):($2/100) t "Load average" with points,\
	     'using.bin' binary format='%*int32%*int8%int16%*int8%*int16%int16' using ($1/100):($2/100) t "%CPU used" with lines
	unset label
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "Here is another example, one repeating the \`scatter.dem\`"
	print "demo.  With binary data we cannot have blank lines to"
	print "indicate a break in data, as is done with ASCII files."
	print "Instead, we can specify the record lenths in the command."
	print "In this case, the data file contains the (x,y,z) coordinate"
	print "information, hence implicit derivation of that information"
	print "is not desired.  Instead, the record lengths can be specified"
	print "using the keyword \'record\', which behaves the same as"
	print "\'array\' but does not generate coordinates.  The command is"
	print "displayed on the graph."
	print ""
	set title "2d binary data example where record length is part of command"
	splot 'scatter2.bin' binary endian=little record=30:30:29:26 using 1:2:3
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "For binary data, the byte endian format of the file and of the"
	print "compiler often require attention.  Therefore, the key word"
	print "\'endian\' is provided for setting or interchanging byte"
	print "order.  The allowable types are \'little\', \'big\', and"
	print "depending upon how your version of Gnuplot was compiled,"
	print "\'middle\' (or \'pdp\') for those still living in the medieval"
	print "age of computers.  These types refer to the file's endian."
	print "Gnuplot arranges bytes according to this endian and what it"
	print "determines to be the compiler's endian."
	print ""
	print "There are also the special types \'default\' and \'swap\' (or"
	print "\'swab\') for those who don't know the file type but realize"
	print "their data looks incorrect and want to change the byte read"
	print "order."
	print ""
	print "Here is an example showing the \`scatter.dem\` data plotted"
	print "with correct and incorrect byte order.  The file is known"
	print "to be little endian, so the upper left plot is correct"
	print "appearance and the upper right plot is incorrect appearance."
	print "The lower two plots are default and swapped bytes.  If the"
	print "plots within the columns match, your compiler uses little"
	print "endian.  If diagonal plots match then your compiler uses"
	print "big endian.  If neither of the bottom plots matches the"
	print "upper plots, Tux says you're living in the past."
	print ""
	set size 1.0, 1.0
	set origin 0.0, 0.0
	set multiplot
	set format "%.2g"
	unset ztics
	set size 0.5,0.48
	set origin 0.0,0.47
	unset key
	set label 1 "If plots in columns match, your compiler is little endian" at 100,1200,3.0 center
	set title 'Little endian' offset 0,-0.5
	splot 'scatter2.bin' binary record=30:30:29:26 endian=little using 1:2:3
	unset label 1
	set size 0.5,0.48
	set origin 0.5,0.47
	set title 'Big endian'
	splot 'scatter2.bin' binary record=30:30:29:26 endian=big using 1:2:3
	set size 0.5,0.48
	set origin 0.0,0.0
	set title 'Default'
	splot 'scatter2.bin' binary record=30:30:29:26 endian=default using 1:2:3
	set size 0.5,0.48
	set origin 0.5,0.0
	set title 'Swapped'
	splot 'scatter2.bin' binary record=30:30:29:26 endian=swap using 1:2:3
	unset multiplot
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "This close up of a 2x2 image illustrates how pixels surround the"
	print "sampling grid points.  This behavior is slightly different than"
	print "that for pm3d where the four grid points would be used to create"
	print "a single polygon element using an average, or similar mathematical"
	print "combination, of the four values at those points."
	print ""
	set title "Close up of pixels having grid points (0,0), (0,2), (2,0) and (2,2)"
	set xrange [-1.5:3.5]
	set yrange [-1.5:3.5]
	# As of version 5.2, RGB color components are always in the range [0:255]
	unset key
	plot '-' with rgbimage, '-' with points pt 7 ps 2 lt -1
	0 0   0   0 127
	0 2   0 127   0
	2 0   0 127   0
	2 2   0   0 127
	e
	0 0
	0 2
	2 0
	2 2
	e
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "Lower dimensional data may be extended to higher dimensional plots"
	print "via a number of simple, logical rules.  The first step Gnuplot does"
	print "is sets the components for higher than the natural dimension of the"
	print "input data to zero.  For example, a two dimensional image can be"
	print "displayed in the three dimensional plot as shown.  Without"
	print "translation, the image lies in the x/y-plane."
	print ""
	set title "Simple extension of a two dimensional image into three dimensions"
	set xrange [-10:137]
	set yrange [-10:157]
	set cbrange [0:255]
	splot 'blutux.rgb' binary array=(128,128) flip=y format='%uchar%uchar%uchar' using ($1+$2+$3)/3 with image
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "The key words \'rotate\' and \'center\' still apply in \'splot\' with"
	print "rules similar to their use in \'plot\'.  However, the center must be"
	print "specified as a three element tuple."
	print ""
	unset colorbox
	set title "Orientation operations from \'plot\' also apply to to \'splot\'"
	splot 'blutux.rgb' binary array=(128,128) flipy rotate=90d center = (63.5,63.5,50) format='%uchar%uchar%uchar' using ($1+$2+$3) with image
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "To have full degrees of freedom in orienting the image, an additional"
	print "key word, \'perpendicular\', can translate the x/y-plane of the 2d"
	print "data so that it lies orthogonal to a vector given as a three element"
	print "tuple.  The default vector is, of course, (0,0,1).  The vector need"
	print "not be of unit length, as this example illustrates.  Viewing this"
	print "plot with the mouse active can help visualize the image's orientation"
	print "by panning the axes."
	print ""
	set title "The key word \'perpendicular\' applies only to \'splot\'"
	set cbrange [0:255]
	set style line 1 lt -1 lw 1
	set style line 2 lt -1 lw 1
	set style arrow 1 nohead ls 1
	set style arrow 2 head filled size screen 0.025,30,45 ls 2
	set arrow from -10,0,0 to 10,0,0 as 1
	set arrow from 0,-10,0 to 0,10,0 as 1
	set arrow from 0,0,-10 to 0,0,10 as 1
	set arrow from 0,0,0 to 30,30,30 as 2
	splot 'blutux.rgb' binary array=(128,128) flipy rot=1.0pi center = (63.5,63.5,50) perp=(1,1,1) format='%uchar%uchar%uchar' using ($1+$2+$3)/3 with image
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "These concepts of extending lower dimensional data also apply"
	print "to temporal-like signals.  For example, a uniformly sampled"
	print "sinusoid, sin(1.75*pi*x), in a binary file having no data for"
	print "the independent variable can be displayed along any direction"
	print "for both \'plot\'..."
	print ""
	set size 1.0, 1.0
	set origin 0.0, 0.0
	set multiplot
	set size 0.5,0.48
	set origin 0.0,0.47
	unset key
	set xrange [0:2]
	set yrange [-1:1]
	set label 1 "Temporal data having one generated coordinate" at 2.25,1.5 center
	set title 'Along the x-axis' offset 0,-0.5
	plot 'sine.bin' binary endian=little array=201 dx=0.01 origin=(0,0) format='%f' using 1 with lines
	unset label 1
	set size 0.5,0.48
	set origin 0.5,0.47
	set xrange [-1:1]
	set yrange [0:2]
	set title 'Along the y-axis'
	plot 'sine.bin' binary endian=little array=201 dx=0.01 origin=(0,0) rotate=0.5pi format='%f' with lines
	set size 0.5,0.48
	set origin 0.25,0.0
	set xrange [-2.2:0.7]
	set yrange [-2.2:0.7]
	set title 'Along a 225 degree projection'
	plot 'sine.bin' binary endian=little array=201 dx=0.01 rotate=225d origin=(0,0) format='%f' using 1 with lines
	unset multiplot
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "...and \'splot\'.  Here is the \'scatter.dem\' example again,"
	print "but this simulates the case of the redundant x coordinates not"
	print "being in the binary file.  The first \"column\" of the binary"
	print "file is ignored and reconstructed by orienting the various"
	print "data records."
	print ""
	set title "2d binary data example where x coordinate is ignored then generated"
	set xrange [20:100]
	set yrange [0:800]
	set zrange [0.2:1.8]
	splot 'scatter2.bin' binary endian=little record=30:30:29:26 origin=(25,0,0):(50,0,0):(75,0,0):(100,0,0) format='%f%f' using (0):2:3
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "Some binary data files have headers, which may be skipped via"
	print "the \'skip\' key word.  Here is the \'scatter.dem\' example"
	print "again, but this time the first and third traces are skipped."
	print "The first trace is 30 samples of three floats so takes up 360"
	print "bytes of space.  Similarly, the third trace takes up 348 bytes."
	print ""
	set title "The key word \'skip\' used to ignore some data"
	set xrange [20:100]
	set yrange [0:800]
	set zrange [0.2:1.8]
	splot 'scatter2.bin' binary endian=little record=30:26 skip=360:348 origin=(50,0,0):(100,0,0) format='%f%f' using (0):2:3
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "Generating uniformly spaced coordinates is valid for polar"
	print "plots as well.  This is useful for data acquired by machines"
	print "sampling in a circular fashion.  Here the sinusoidal data"
	print "of the previous 2D plot put on a polar plot.  Note the"
	print "pseudonyms \'dt\' meaning sample period along the angular,"
	print "or theta, direction.  In Gnuplot, cylindrical coordinate"
	print "notation is (t,r,z).  [Different from common math convention"
	print "(r,t,z).]"
	print ""
	set size ratio 1
	set title "Uniform sampling in the polar coordinate system"
	set polar
	set grid polar
	set xrange [-1.3:1.3]
	set yrange [-1.3:1.3]
	# The sinusoid has period T=8/7.  Also, dx=0.01.  So solving 8/7 dt = 2/3 pi dx, we get dt = 0.018326.
	plot 'sine.bin' binary endian=little array=201 dt=0.018326 origin=(0,0) format='%f' using 1 with lines
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "Binary data stored in matrix format (i.e., gnuplot binary)"
	print "may also be translated with similar syntax.  However, the"
	print "binary keywords \`format\`, \`array\` and \`record\` do not apply"
	print "because gnuplot binary has the requirements of float data"
	print "and grid information as part of the file.  Here is an"
	print "example of a single matrix binary file input four times,"
	print "each translated to a different location with different"
	print "orientation."
	print ""
	set style data lines
	set hidden3d
	set ticslevel 0
	set key box
	set title "Matrix binary data (gnuplot binary) translated"
	set vi 70,10
	set xrange [-3:15]
	set yrange [-3:15]
	set zrange [-0.2:1.2]
	splot "binary3" binary center=(1.5,1.5,0), \
	  "binary3" binary center=(10.5,1.5,0) rotate=0.5pi u 1:2:3, \
	  "binary3" binary center=(10.5,10.5,0) rotate=1.0pi u 1:2:3, \
	  "binary3" binary center=(1.5,10.5,0) rotate=1.5pi u 1:2:3
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "As with ASCII data, decimation in various directions can"
	print "be achieved via the \`every\` keyword.  (Note that no down-"
	print "sampling filter is applied such that you risk aliasing data"
	print "with the \`every\` keyword."
	print ""
	print "Here is a series of plots with increasing decimation."
	print ""
	set style data lines
	set hidden3d
	set ticslevel 0
	set key box
	set title "Non-decimated matrix data file"
	set view 70,45
	set xrange [-3:3]
	set yrange [-3:3]
	splot "binary2" binary
	pause -1 "Hit return to continue"
	set title "Decimate by two in first dimension"
	splot "binary2" binary every 2
	pause -1 "Hit return to continue"
	set title "Decimate by three in second dimension"
	splot "binary2" binary every :3
	pause -1 "Hit return to continue"
	set title "Decimate by four in both dimensions"
	splot "binary2" binary every 4:4
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "Decimation works on general binary data files as well.  Here is the"
	print "image file with increasing decimation."
	print ""
	set title "Decimation works on general binary data files as well.\nLet Tux have his fun..."
	set xrange [28:98]
	set yrange [55:125]
	set label 1 "\"Can I do that?\"" at 64,116 center
	plot 'blutux.rgb' binary array=(128,128) flipy format='%uchar' every 1:1:43:15:83:65 with rgbimage
	pause -1 "Hit return to continue"
	set title "... Sure, go ahead."
	set xrange [-10:130]
	set yrange [-8:157]
	set label 1 "\"Picasso's \'Penguin on Beach\'\"" at 64,139 center
	plot 'blutux.rgb' binary array=(128,128) flipy format='%uchar' every 8:8 with rgbimage
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "Gnuplot understands a few common binary formats.  Internally"
	print "a function is linked with various extensions.  When the"
	print "extension is specified at the command line or recognized via"
	print "a special file type called \'auto\', Gnuplot will call the"
	print "function that sets up the necessary binary information.  The"
	print "known extensions are displayed using the \'show filetype\'"
	print "command.  E.g.,"
	print ""
	show datafile binary filetypes
	print "Here's an example where an EDF file is recognized when Gnuplot"
	print "is in \'auto\' mode.  Details are pulled from the header of"
	print "file itself and not specified at the command line.  The command"
	print "line can still be used to over-ride in-file attributes."
	print ""
	set title "Automatically recognizing file type and extracting file information"
	plot 'demo.edf' binary filetype=auto with image
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "The \'flip\', \'rotate\' and \'perpendicular\' qualifiers"
	print "should provide adequate freedom to orient data as desired."
	print "However, there is an additional key words \'scan\' which may"
	print "offer a more direct and intuitive manner of orienting data"
	print "depending upon the user's application and perspective."
	print ""
	print "\'scan\' is a 2 or 3 letter string representing how Gnuplot"
	print "should derive (x,y), (x,y,z), (t,r) or (t,r,z) from the"
	print "the datafile's scan order.  The first letter pertains to the"
	print "fastest rate or point-by-point increment.  The second letter"
	print "pertains to the medium rate or line-by-line increment.  If"
	print "there is a third letter, it pertains to the slowest rate or"
	print "plane-by-plane increment.  The default or inherent scan order"
	print "is \'scan=xyz\'."
	print ""
	print "The pseudonym \'transpose\' is equivalent to \'scan=yx\' when"
	print "generating 2D coordinates and \'scan=yxz\' when generating"
	print "3D coordinates."
	print ""
	print "There is a subtle difference between the behavior of \'scan\'"
	print "when dimension info is taken from the file itself as opposed"
	print "to entered at the command line.  When information is gathered"
	print "from the file, internal scanning is unaltered so that issuing"
	print "the \'scan\' command may cause the number of samples along"
	print "the various dimensions to change.  However, when the qualifier"
	print "\'array\' is entered at the command line, the array dimensions"
	print "adjust so that \'array=XxYxZ\' is always the number of samples"
	print "along the Cartesian x, y and z directions, respectively."
	print ""
	set size 1.0, 1.0
	set origin 0.0, 0.0
	set multiplot
	set size 0.5,0.5
	set origin 0.0,0.5
	unset key
	set xrange [-5:635]
	set yrange [-5:635]
	set tics out
	set title 'Details read from file' offset 0,-0.5
	plot 'demo.edf' binary filetype=auto with image
	set size 0.5,0.5
	set origin 0.5,0.5
	set title 'Transpose of file-read axes parameters'
	plot 'demo.edf' binary filetype=auto transpose with image
	set size 0.5,0.5
	set origin 0.0,0.0
	set title 'Details specified at command line'
	set label 1 "array=(32,64) dx=10 dy=5 scan=xy" at 315,500 center
	plot 'demo.edf' binary array=(32,64) dx=10 dy=5 scan=xy skip=1024 format="%*float%float" using 1 with image
	set size 0.5,0.5
	set origin 0.5,0.0
	set title 'Transpose of command line axes parameters'
	set label 1 "array=(64,32) dx=5 dy=10 scan=yx" at 315,500 center
	plot 'demo.edf' binary array=(64,32) dx=5 dy=10 scan=yx skip=1024 format="%*float%float" using 1 with image
	unset label 1
	unset multiplot
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "It is possible to enter binary data at the command line.  Of"
	print "course, the limitation to this approach is that keyboards will"
	print "allow entering only a limited subset of the possible character"
	print "values necessary to represent general binary data.  For this"
	print "reason, the primary application for binary data at the command"
	print "line is using Gnuplot through a pipe.  For example, if a pipe"
	print "is established with a C program, the function \'fputs()\' can"
	print "send ASCII strings containing the Gnuplot commands while the"
	print "function \'fwrite()\' can send binary data."
	print ""
	print "Furthermore, there can be no special ending character such as"
	print "in the case of ASCII data entry where \'e\' represents the end"
	print "of data for the special file \'-\'.  It is important to note"
	print "that when \'binary\' is specified, Gnuplot will continue"
	print "reading until reaching the number of elements specified via"
	print "the \'array\' or \'record\' command."
	print ""
	print "Here is an example of binary data at the command line where"
	print "keyboard input has been side stepped by copying 48 bytes from"
	print "a pre-existing binary file into this demo file."
	print ""
	set title "Binary data specified at the command line, intended for use through pipe"
	set xrange [-1.5:3.5]
	set yrange [-1.5:3.5]
	# The binary values are in the range [0.0:1.0] but
	# as of version 5.2, RGB color components are always in the range [0:255]
	plot '-' binary endian=little array=(2,2) dx=2 format="%float" using ($1*255.):($2*255.):($3*255.) with rgbimage,\
	     '-' binary endian=little record=4 format="%char" using 1:2 with points pt 7 ps 2 lt -1
	           ?       ?           ?               ?    
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "ASCII data files have a matrix variant.  Unlike matrix binary,"
	print "ASCII binary may have multiple matrices per file, separted"
	print "by a blank line.  The keyword \`index\` can select the desired"
	print "matrix to plot."
	print ""
	set style data lines
	set hidden3d
	set ticslevel 0
	set key box
	set view 70,45
	set multiplot
	set size 0.5,0.5
	set origin 0.0,0.5
	unset key
	set tics out
	set title "'asciimat.dat' matrix index 0"
	splot 'asciimat.dat' matrix index 0
	set size 0.5,0.5
	set origin 0.5,0.5
	set title "'asciimat.dat' matrix index 1"
	splot 'asciimat.dat' matrix index 1
	set size 0.5,0.5
	set origin 0.0,0.0
	set title "'asciimat.dat' matrix index 2"
	splot 'asciimat.dat' matrix index 2
	set size 0.5,0.5
	set origin 0.5,0.0
	set title "'asciimat.dat' matrix index 2 using 1:(2*$2):($3*$3)"
	splot 'asciimat.dat' matrix index 2 using 1:(2*$2):($3*$3)
	unset multiplot
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "Images maintain orientation with respect to axis direction."
	print "All plots show the same exact plot, but with various states"
	print "of reversed axes.  The upper left plot has reversed x axis,"
	print "the upper right plot has conventional axes, the lower left"
	print "plot has both reversed x and y axes, and the lower right"
	print "plot has reversed y axis."
	print ""
	set size 1.0, 1.0
	set origin 0.0, 0.0
	set multiplot
	set size 0.5,0.48
	unset key
	set origin 0.0,0.47
	set xrange [127.5:-0.5]
	set x2range [127.5:-0.5]
	set yrange [-0.5:127.5]
	set y2range [-0.5:127.5]
	set xtics out mirror
	unset ytics
	set y2tics out mirror
	set title '"Eccentric coordinate systems"' offset 0,-0.5
	set label 1 "Images reverse according to axis orientation" at -30,162 center
	plot 'blutux.rgb' binary array=(128,128) flip=y format='%uchar' using 1:2:3 with rgbimage
	unset label 1
	set origin 0.5,0.47
	set xrange [-0.5:127.5]
	set x2range [-0.5:127.5]
	unset y2tics
	set ytics out mirror
	set title '"Cartesian plane!"'
	replot
	set origin 0.0,0.04
	unset xtics
	set xrange [127.5:-0.5]
	set x2range [127.5:-0.5]
	set x2tics out mirror
	unset ytics
	set yrange [127.5:-0.5]
	set y2range [127.5:-0.5]
	set y2tics out mirror
	unset title
	set title ' ' offset 0,-0.5
	set label 1 '"Nato, ergo sum."' at 64,140 center rotate by 180
	replot
	set origin 0.5,0.04
	set xrange [-0.5:127.5]
	set x2range [-0.5:127.5]
	unset y2tics
	set ytics out mirror
	set title ' ' offset 0,-0.5
	set label 1 '"Physics is phun."' at 64,140 center rotate by 180
	replot
	unset multiplot
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "Tux says \"bye-bye\"."
	print ""
	set xrange [-0.5:137.5]
	set yrange [-0.5:137.5]
	unset border
	unset xtics
	unset ytics
	unset ztics
	unset key
	unset colorbox
	set tmargin 0
	set bmargin 0
	set rmargin 0
	set lmargin 0
	set view 60, 45, 1, 1
	set label 1 "Tux in a reflective mood" at 140,0,40 center
	splot 'blutux.rgb' binary array=(128,128) flip=y rot=90d origin=(0,0,0) perp = (1,0,0) format='%uchar' with rgbimage, \
	      'blutux.rgb' binary array=(128,128) flipy rot=90d origin=(+137,+137,0) perp=(0,1,0) format='%uchar' with rgbimage
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "End of image demo..."

snippet image.dem
	# demo for plotting images using pixels and binary 2d data
	#
	# This demo can be used for terminals supporting image display.
	# Currently supported are:  X11, Aqua, postscript, png, pdf, svg,
	# (e)pslatex, pstex, emf, wxt.
	
	# Prepared by Dan Sebald
	# History:
	#	- Nov   2006 EAM: split into two files
	#	- 9.23. 2003 ds: redone in response to discussion list feedback
	#	- 5. 3. 2003 ds: 1st version
	
	print ""
	print "The plotting styles \`image\` and \`rgbimage\` are intended for plotting"
	print "images described in a data file either in the convential ASCII format or"
	print "in a binary format described by the qualifiers \`binary\` and \`using\`."
	print "All pixels have an (x,y) or (x,y,z) coordinate.  These values can be"
	print "included in the data file or implicitly determined with the sampling"
	print "\'array\' key word and sampling periods \'dx\' and \'dy\'.  The key words"
	print "\'rotate\' and, for 3d plots, \'perpendicular\' control orientation."
	print "\nThe data for this image was stored as RGB triples, one byte per channel,"
	print "without (x,y) coordinate information.  This yields a most compact file."
	print "The plotting command is displayed on the graph."
	print ""
	reset
	set title "Larry Ewing's GIMP penguin on vacation basking in\nthe balmy waters off the coast of Murmansk"
	set xrange [-10:137]
	set yrange [-10:157]
	set label "\"I flew here... by plane.  Why?  For the halibut.\"" at 64,135 center
	plot 'blutux.rgb' binary array=(128,128) flipy format='%uchar' with rgbimage
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "Images are typically stored in a file with the first datum being the"
	print "top, left pixel.  Without the ability to translate coordinates, the"
	print "the result would be an upside down image."
	print ""
	print "The key word \'array\' means an implied sample array is applied"
	print "to generate the locations of file data using the sampling periods"
	print "dx, dy and dz.  The x-dimension is always the contiguous points in"
	print "a binary file.  The y-dimension is the line number which is incremented"
	print "upon the x-dimension reaching the line length.  The z-dimension is"
	print "the plane number which is incremented upon the y-dimension reaching"
	print "the number of lines per plane."
	print ""
	print "To alter the location of the binary data when displayed via the \'plot\'"
	print "command, use the key word \'rotate\' along with changing the sign of dx, dy"
	print "and dz."
	print ""
	set title "Translations of position variables via 'using'"
	set xrange [-10:275]
	set yrange [-10:300]
	unset key
	set label "\"Time for a dip...\"" at 132,285 center
	plot 'blutux.rgb' binary array=(128,128) flipy center=( 64,201 ) format='%uchar' with rgbimage, \
	     'blutux.rgb' binary array=(128,128) flipy rotation=-90d center=(201,201) format='%uchar' with rgbimage, \
	     'blutux.rgb' binary array=(128,128) flip=y rotate=3.1415 center=(201,64) format='%uchar' with rgbimage, \
	     'blutux.rgb' binary array=(128,128) flip=y rot=0.5pi center=(64,64) format='%uchar' using 1:2:3 with rgbimage
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "There is the ability to plot both color images and palette based"
	print "images.  This is controlled by the styles \`image\`, which derives"
	print "color information from the current palette, and \`rgbimage\`,"
	print "which requires three components representing the red, blue and"
	print "green primary color scheme."
	print ""
	print "By the way, if you have a mouse active, click the right button"
	print "inside the image to isolate a portion of the image and see what"
	print "happens."
	print ""
	unset key
	set xrange [-10:137]
	set yrange [-10:157]
	set label 1 "\"I am the penguin, GOO GOO GOO JOOB.\"" at 63,140 center
	set title "Palette mode 'image' used to produce psychedelic bird"
	unset colorbox
	plot 'blutux.rgb' binary array=(128,128) flipy format='%uchar%uchar%uchar' using ($1+$2+$3) with image
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "Naturally, as with 3d color surfaces, the palette may be changed."
	print "This is an example of gray scale."
	print ""
	print "Also, notice in the plot command the key word \'flipy\'.  This"
	print "means to change the direction of the sample along the y dimension"
	print "and is useful for the situation where images or other data are"
	print "stored in some direction other than that of the Cartesion system."
	print "Alone, \'flipD\' means do flipping in the D (x y or z) direction"
	print "for all records.  Individual records can be controlled using the"
	print "syntax \'flipD=#,...,#\', where # is \'0\' or \'1\'."
	print ""
	unset key
	set xrange [-10:137]
	set yrange [-10:157]
	set tics out
	set label 1 "\"This picture was taken by my friend Ansel Adams.\"" at 63,140 center
	set title "The palette can be changed from color to gray scale"
	set palette gray
	unset colorbox
	plot 'blutux.rgb' binary array=(128,128) flipy format='%uchar%uchar%uchar' using ($1+$2+$3)/3 with image
	pause -1 "Hit return to continue"
	unset label 1
	
	print ""
	print "Also, similar to 3d color surface plots, a color box showing the"
	print "palette mapping scheme can be added to the plot.  The default"
	print "location is the right edge of the plot.  The location can be set"
	print "manually using \`set colorbox\` and \`set margin\`."
	print ""
	print "As a prelude to the next graph, resize the plot window to judge"
	print "the refresh speed of the image drawing routine.  Notice that when"
	print "the window is smaller, the image refresh is faster.  There is more"
	print "decimation in the data of the original image and less data to plot."
	print "Furthermore, the window continues to refresh at a reasonable rate"
	print "even when the input image size becomes large (e.g., 1024 x 1024)"
	print "because the number of pixels on the screen remains about the same"
	print "while much of the hi resolution data is decimated."
	print ""
	unset key
	set pm3d map
	set xrange [10:117]
	set yrange [10:117]
	set tics out
	set colorbox
	set cbrange [0:255]
	set title "As with 3d color surfaces, a color box may be added to the plot"
	splot 'blutux.rgb' binary array=(128,128) flipy format='%uchar%uchar%uchar' using ($1+$2+$3)/3 with image
	pause -1 "Hit return to continue"
	
	print ""
	print "The \'rotation\' key word works not only with angles of integer"
	print "multiples of 90 degrees but also arbitrary rotations.  When"
	print "constructing an image, Gnuplot verifies that pixel locations"
	print "form a valid grid.  Pixel widths are based upon the grid"
	print "spacing.  If the image orientation is aligned with the view"
	print "axes, Gnuplot uses an efficient image driver routine.  Otherwise,"
	print "individual pixels are drawn using polygon shapes."
	print ""
	print "Resize this window and compare the plot's refresh rate to that of"
	print "the previous and next plot.  Notice how in this example if the"
	print "window is small the image refresh does not speed up.  Unlike the"
	print "image routine where image data is decimated, all color rectangles"
	print "must be redrawn no matter the size of the output image."
	print ""
	print "Also notice how the center of the image matches the tuple specified"
	print "with the key word \'center\' in the plot command.  Before doing the"
	print "rotation, the image's natural center is subtracted, and after doing"
	print "the rotation, the specified center is added."
	print ""
	set key
	set xrange [-10:138]
	set yrange [-10:138]
	set tics out
	set title "Polygons used to draw pixels for rotated images\nNotice the slower refresh rate than for the next plot"
	unset colorbox
	plot 'blutux.rgb' binary array=(128,128) dx=0.70711 dy=0.70711 flipy rotation=45d center=(63.5,63.5) format='%uchar' using ($1+$2+$3)/3 with image
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "The image of this plot is rotated 90 degrees and can utilize the"
	print "efficient image functions of terminal drivers.  The plot refresh"
	print "is faster than the previous plot."
	print ""
	print "Furthermore, the image location in this case is specified via the"
	print "\'origin\' key word.  As such, the rotation is done about the origin"
	print "as opposed to the center of the image.  Notice the difference in"
	print "axis ranges."
	print ""
	unset key
	set xrange [-138:10]
	set yrange [-10:138]
	set tics out
	set title "Terminal image routine used to draw plot rotated about origin\nNotice the faster refresh rate than for the previous plot"
	unset colorbox
	plot 'blutux.rgb' binary array=(128,128) dx=1 flip=y rotation=0.5pi origin=(0,0) format='%uchar' using ($1+$2+$3)/3 with image
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "Algebraic manipulation of the input variables can select various"
	print "components of the image.  Here are three examples where two"
	print "channels--or analogous to the ASCII file, data \"columns\"--are ignored"
	print "This is done by using \`*\` in the format to indicate that a variable"
	print "of a certain size should be discarded.  For example, to select the"
	print "green channel, \`%*uchar%uchar%*uchar\` is one alternative."
	print ""
	set size 1.0, 1.0
	set origin 0.0, 0.0
	set multiplot
	set size 0.5,0.48
	set origin 0.0,0.47
	unset key
	set xrange [-0.5:127.5]
	set yrange [-0.5:127.5]
	set tics out
	set label 1 "Selection of the input channels via \`using\`" at 140,160 center
	set title '"I do impersonations..."' offset 0,-0.5
	plot 'blutux.rgb' binary array=(128,128) flip=y format='%uchar' using 1:2:3 with rgbimage
	unset label 1
	set size 0.5,0.48
	set origin 0.5,0.47
	set title '"A cardinal."'
	plot 'blutux.rgb' binary array=(128,128) flip=y format='%uchar%*uchar%*uchar' using 1:(0):(0) with rgbimage
	set size 0.5,0.48
	set origin 0.0,0.0
	set title '"A parrot."'
	plot 'blutux.rgb' binary array=(128,128) flipy format='%*uchar%uchar%*uchar' using (0):1:(0) with rgbimage
	set size 0.5,0.48
	set origin 0.5,0.0
	set title '"A bluebird."'
	plot 'blutux.rgb' binary array=(128,128) flipy format='%*uchar%*uchar%uchar' using (0):(0):1 with rgbimage
	unset multiplot
	pause -1 "Hit return to continue"
	reset
	
	print ""
	print "The range of valid RGB color component values is [0:255]"
	print "This is a CHANGE in gnuplot version 5.2.
	print "To adjust the color balance you can filter the individual values"
	print "through a scaling function.  Here we multiply by a constant c,"
	print " c > 1 to brighten, c < 1 to dim."
	print ""
	set size 1.0, 1.0
	set origin 0.0, 0.0
	set multiplot
	set size 0.5,0.48
	set origin 0.0,0.47
	unset key
	set xrange [-0.5:127.5]
	set yrange [-0.5:127.5]
	set tics out
	set label 1 "Adjust color balance in the using spec" at 140,160 center
	set title 'Lake Mendota, "or Wonk-sheck-ho-mik-la!"' offset 0,-0.5
	set cbrange [*:*]
	plot 'blutux.rgb' binary array=(128,128) flipy format='%uchar' using 1:2:3 with rgbimage
	unset label 1
	set size 0.5,0.48
	set origin 0.5,0.47
	set title '"Lucky I brought sunscreen."'
	brighten(x) = x * 1.25
	plot 'blutux.rgb' binary array=(128,128) flip=y format='%uchar' using (brighten($1)):(brighten($2)):(brighten($3)) with rgbimage
	set size 0.5,0.48
	set origin 0.0,0.0
	set title 'Sunset on the Terrace'
	dim(x) = x * 255./400.
	plot 'blutux.rgb' binary array=(128,128) flip=y format='%uchar' using ($1):(dim($2)):(dim($3)) with rgbimage
	set size 0.5,0.48
	set origin 0.5,0.0
	set title 'Sultry evening'
	#set cbrange [0:400]
	plot 'blutux.rgb' binary array=(128,128) flip=y format='%uchar' using ($1 * 255./400.):($2 * 255./400.):($3 * 255./400.) with rgbimage, \
	'-' w points pt 7 ps 6 lt -2, \
	 '-' w points pt 7 ps 0.65 lt -2
	110 100
	e
	10 95
	38 120
	82 102
	26 82
	93 108
	41 99
	123 84
	e
	unset multiplot
	pause -1 "Hit return to continue"
	reset

snippet imageNaN.dem
	#
	# Explore the effect of image data pixels that are
	# missing/NaN/Inf/garbage/...
	# Test 2D and 3D versions of the generic and
	# terminal-optimized image code.
	# The four tests should all come out the same.
	#
	
	set title "Treatment of missing/undefined/NaN/Inf data"
	unset key
	set tic scale 0
	set border 3 front
	
	set cbrange [-2:7]
	set cblabel "Score"
	set cbtics 0,1,5
	
	set xrange [-0.5:4.5]
	set yrange [-0.5:5.5]
	
	set datafile missing "?"
	set ytics ("-Inf" 5, "Inf" 4, "NaN" 3, "Junk" 2, "?" 1, "0" 0)
	unset xtics
	
	# Define the test data as a named data block
	$matrixdata << EOD
	0    5 4 3  0
	?    2 2 0  1
	Junk 1 2 3  5
	NaN  0 0 3  0
	Inf  3 2 0  3
	-Inf 0 1 2  3
	EOD
	
	set xlabel "First column contains various odd values"
	
	set view map
	plot $matrixdata matrix with image 
	
	pause -1 "Hit return to continue"
	#
	#
	set title "Same thing in 'pixels' mode (2D)"
	
	plot $matrixdata matrix with image pixels
	
	pause -1 "Hit return to continue"
	
	set title "Same thing passing data value through 'using 1:2:($3)'"
	
	plot $matrixdata matrix using 1:2:($3) with image pixels
	
	pause -1 "Hit return to continue"
	
	set title "Same thing in 3D mode"
	
	splot $matrixdata matrix with image
	
	pause -1 "Hit return to continue"
	
	set title "Same thing in 'pixels' mode (3D)"
	
	splot $matrixdata matrix with image pixels
	
	pause -1 "Hit return to continue"
	
	set title "3D image with pixel value in 4th column"
	
	splot $matrixdata matrix using 1:2:(0):3 with image
	
	pause -1 "Hit return to continue"
	
	reset
	
	$DATA << EOD
	0	0	0
	0	1	-1
	0	2	-4
	0	3	-9
	
	1	0	1
	1	1	0
	1	2	-3
	1	3	-8
	
	2	0	4
	2	1	3
	2	2	0
	2	3	-5
	
	3	0	9
	3	1	8
	3	2	NaN
	3	3	9
	
	EOD
	
	unset xtics
	unset ytics
	unset key
	
	set title "image from non-matrix data"
	set label 1 at 3,2 "NaN\nshould appear as\nbackground" front center
	plot $DATA with image 
	pause -1 "Hit return to continue"
	
	set title "negative values mapped to log-scale colorbar"
	set label 2 at 0,2.5 "Negative values become NaN\nwith a log-scale color mapping"
	set cbrange [0.1:10]
	set log cb
	plot $DATA with image, $DATA using 1:2:(sprintf("%g",$3)) with labels tc rgb "green"
	
	pause -1 "Hit return to continue"
	
	reset

snippet iterate.dem
	set title "Iteration within plot command"
	set xrange [0:3]
	set label 1 "plot for [n=2:10] sin(x*n)/n" at graph .95, graph .92 right
	plot for [n=2:10] sin(x*n)/n notitle lw (13-n)/2
	pause -1 "Hit return to continue"
	reset
	
	set title "Iteration over all available data in a file"
	set view 38., 341.
	unset xtics
	unset ytics
	unset ztics
	set border 0
	set lmargin at screen 0.09
	set rmargin at screen 0.90
	set key outside below samplen 0.6
	set key title "splot for [scan=1:*] 'whale.dat' index scan"
	set key maxrows 6
	splot for [i=1:*] "whale.dat" index i title sprintf("scan %d",i) with lines
	pause -1 "Hit return to continue"
	reset
	
	
	# Dynamic reevaluation of iteration limits
	# inner loop refers to the control variable of an outer loop
	#
	set key opaque box height 1 title "Dynamic iteration limits"
	set title "plot for [i=1:4] for [k=i:i] for [j=1:k] 10*k + cos(j*x)"
	plot for [i=1:4] for [k=i:i] for [j=1:k] 10*k + cos(j*x) lw j \
	     title sprintf("k=%d  j=%d", i, j)
	pause -1 "Hit return to continue"
	
	# More iteration tests (no graph output)
	#
	print "\ndynamic reevaluation of numeric iteration limits"
	array J[3] = [ 1, 4, 3 ]
	print "\nJ =",J
	Output = ""
	print "do for [i=1:3] for [j=J[i]:3] { save(i,j) }"
	do for [i=1:3] for [j=J[i]:3] { Output = Output . sprintf("%d-%d ",i,j) }
	print Output
	
	array J[3] = [ 4, 1, 3 ]
	print "\nJ =",J
	Output = ""
	print "do for [i=1:3] for [j=J[i]:3] { save(i,j) }"
	do for [i=1:3] for [j=J[i]:3] { Output = Output . sprintf("%d-%d ",i,j) }
	print Output
	
	print ""
	Output = ""
	print "do for [i=1:4] for [k=i:i] for [j=1:k] { save(i,j) }"
	do for [i=1:4] for [k=i:i] for [j=1:k] { Output = Output . sprintf("%d-%d ",i,j) }
	print Output
	
	print "\ndynamic reevaluation of iteration string"
	
	array A[3] = [ "a b", "", "d" ]
	print "A = ", A
	
	print 'do for [ i = 1:|A| ] { do for [ j in A[i]] { print "".i.": ".j }}'
	Output = ""
	do for [ i = 1:|A| ] { do for [ j in A[i]] { Output = Output."  ".i.": ".j }}
	print Output
	
	print 'do for [ i = 1:|A| ] for [ j in A[i]] { print "".i.": ".j }'
	Output = ""
	do for [ i = 1:|A| ] for [ j in A[i]] { Output = Output."  ".i.": ".j }
	print Output
	
	reset

snippet jitter.dem
	#
	# jitter.dem
	#
	
	# Generate a reusable set of N random data points.
	nsamp = 160
	set print $random
	do for [i=1:nsamp] {
	    print sprintf("%8.5g %8.5g", invnorm(rand(0)), 100.*invnorm(rand(0)))
	}
	unset print
	#
	
	set border 2
	set xrange [-1.5:1.4]
	set xtics ("A" -1, "B" 0, "C" 1)
	set xtics nomirror scale 0
	set ytics nomirror rangelimited
	
	set multiplot layout 1,2
	unset jitter
	set title "no jitter"
	set style data point
	plot $random using (int($1)%20):2 notitle  pt 6 ps 1.5 lc "gray"
	set jitter 
	set title "jitter"
	plot $random using (int($1)%20):2 notitle  pt 6 ps 1.5 lc "gray"
	unset multiplot
	
	pause -1 'Hit <cr> to continue'
	
	set linetype  9 lc "#80bb33bb"
	set linetype 10 lc "#8033bbbb"
	set linetype 11 lc "#80bbaa44"
	
	set multiplot layout 1,3 title "vertical overlap criterion"
	set style data point
	set jitter overlap 0.5
	set title "jitter overlap 0.5"
	plot $random using (int($1)%20):2:(10+int($1)) notitle  pt 7 ps 1 lc var
	set jitter overlap 1
	set title "jitter overlap 1.0"
	plot $random using (int($1)%20):2:(10+int($1)) notitle  pt 7 ps 1 lc var
	set jitter overlap 1.5
	set title "jitter overlap 1.5"
	plot $random using (int($1)%20):2:(10+int($1)) notitle  pt 7 ps 1 lc var
	unset multiplot
	
	pause -1 'Hit <cr> to continue'
	
	set multiplot layout 1,3 title "spread parameter scales the horizontal jitter"
	set style data point
	set jitter overlap 1 spread 0.4
	set title "jitter spread 0.4"
	plot $random using (int($1)%20):2:(10+int($1)) notitle  pt 7 ps 1 lc var
	set jitter overlap 1 spread 0.7
	set title "jitter spread 0.7"
	plot $random using (int($1)%20):2:(10+int($1)) notitle  pt 7 ps 1 lc var
	set jitter overlap 1 spread 1.0
	set title "jitter spread 1.0"
	plot $random using (int($1)%20):2:(10+int($1)) notitle  pt 7 ps 1 lc var
	unset multiplot
	
	pause -1 'Hit <cr> to continue'
	
	set multiplot layout 1,3 title "Plot appearance is also affected by point size"
	set style data point
	set jitter overlap 0.4 spread 1
	set title "pointsize 0.5"
	plot $random using (int($1)%20):2:(10+int($1)) notitle  pt 7 ps 0.5 lc var
	set title "pointsize 1.0"
	plot $random using (int($1)%20):2:(10+int($1)) notitle  pt 7 ps 1 lc var
	set title "pointsize 2.0"
	plot $random using (int($1)%20):2:(10+int($1)) notitle  pt 7 ps 2 lc var
	unset multiplot
	
	pause -1 'Hit <cr> to continue'
	
	set multiplot layout 1,2 title "Jitter style options"
	set jitter over 0.5 swarm
	set title "swarm (default)"
	plot $random using (int($1)%20):2:(10+int($1)) notitle  pt 7 ps 1 lc var
	set jitter over 0.5 square
	set title "square"
	plot $random using (int($1)%20):2:(10+int($1)) notitle  pt 7 ps 1 lc var
	unset multiplot
	
	pause -1 'Hit <cr> to continue'
	
	set multiplot layout 1,3 title "Jitter style options"
	set jitter
	set title "no wrap"
	plot $random using (int($1)%20):2:(10+int($1)) notitle  pt 7 ps 1 lc var
	set jitter wrap 5
	set title "wrap 5"
	plot $random using (int($1)%20):2:(10+int($1)) notitle  pt 7 ps 1 lc var
	set jitter wrap 3
	set title "wrap 3"
	plot $random using (int($1)%20):2:(10+int($1)) notitle  pt 7 ps 1 lc var
	unset multiplot
	
	pause -1 'Hit <cr> to continue'
	reset
	

snippet kdensity2d.dem
	reset
	load "gen-random.inc"
	
	set title "How to plot a kernel density estimate for this 2D dataset?"
	plot $random using 1:2
	
	pause -1 "Press Return to continue - the plot may take some time to appear"
	
	set view map
	set palette defined (0 'white', 1 'red')
	set dgrid3d 50,50 gauss kdensity
	set title "set dgrid3d 50,50 gauss kdensity"
	
	splot $random using 1:2:(1) with pm3d
	
	pause -1 "Press Return to continue - the plot may take some time to appear"
	
	set dgrid3d 50,50 gauss kdensity 0.1
	set title "set dgrid3d 50,50 gauss kdensity 0.1"
	
	replot
	
	pause -1 "Hit return to continue"
	reset

snippet key.dem
	# Illustration of key positioning commands and concept.
	#
	# Dan Sebald, 7 nov 2004
	# EAM Dec 2012 - exercise opaque/fonts/superscript spacing/etc
	#
	set termopt enhanced
	set xtics font ",6"
	set ytics font ",6"
	
	set key default
	set key box
	set key samplen .2
	
	set key title "Title^{up}" font ",6"
	set obj 1 rectangle from graph 0,0 to graph 1,1 behind fs solid fc rgb "grey"
	set key opaque
	
	set multiplot layout 3,3
	set key ins vert
	set key left top
	set title "Key (ins vert left top)"
	plot x,-x
	set key center top
	set title "Key (ins vert center top)"
	replot
	set key right
	set title "Key (ins vert right top)"
	replot
	set key center left
	set title "Key (ins vert center left)"
	replot
	set key cent
	set title "Key (inside vertical center)"
	replot
	set key right
	set title "Key (ins vert cent right)"
	replot
	set key bottom left
	set title "Key (ins vert bot left)"
	replot
	set key bot center
	set title "Key (ins vert bot center)"
	replot
	set key right
	set title "Key (ins vert bot right)"
	replot
	unset multiplot
	pause -1 "Hit return to continue"
	
	set multiplot layout 3,3
	set key horiz
	set key left top
	set title "Key (ins horiz left top)"
	replot
	set key center top
	set title "Key (ins horiz center top)"
	replot
	set key right
	set title "Key (ins horiz right top)"
	replot
	set key center left
	set title "Key (ins horiz center left)"
	replot
	set key cent
	set title "Key (inside horizontal center)"
	replot
	set key right
	set title "Key (ins horiz cent right)"
	replot
	set key bottom left
	set title "Key (ins horiz bot left)"
	replot
	set key bot center
	set title "Key (ins horiz bot center)"
	replot
	set key right
	set title "Key (ins horiz bot right)"
	replot
	unset multiplot
	pause -1 "Hit return to continue"
	
	set multiplot layout 3,3
	set key out vert
	set key left top
	set title "Key (out vert left top)"
	replot
	set key center top
	set title "Key (out vert center top)"
	replot
	set key right
	set title "Key (out vert right top)"
	replot
	set key center left
	set title "Key (out vert center left)"
	replot
	set key cent
	set title "Key (outside vertical center)"
	replot
	set key right
	set title "Key (out vert cent right)"
	replot
	set key bottom left
	set title "Key (out vert bot left)"
	replot
	set key bot center
	set title "Key (out vert bot center)"
	replot
	set key right
	set title "Key (out vert bot right)"
	replot
	unset multiplot
	pause -1 "Hit return to continue"
	
	set multiplot layout 3,3
	set key horiz
	set key left top
	set title "Key (out horiz left top)"
	replot
	set key center top
	set title "Key (out horiz center top)"
	replot
	set key right
	set title "Key (out horiz right top)"
	replot
	set key center left
	set title "Key (out horiz center left)"
	replot
	set key cent
	set title "Key (outside horizontal center)"
	replot
	set key right
	set title "Key (out horiz cent right)"
	replot
	set key bottom left
	set title "Key (out horiz bot left)"
	replot
	set key bot center
	set title "Key (out horiz bot center)"
	replot
	set key right
	set title "Key (out horiz bot right)"
	replot
	unset multiplot
	pause -1 "Hit return to continue"
	
	set multiplot layout 3,3
	set key vert
	set arrow 1 from -3,0 to 3,0 nohead lt -1
	set arrow 2 from 0,-3 to 0,3 nohead lt -1
	set key at 0,0 left top
	set title "Key (<manual> vert left top)"
	replot
	set key at 0,0 center top
	set title "Key (<manual> vert center top)"
	replot
	set key at 0,0 right
	set title "Key (<manual> vert right top)"
	replot
	set key at 0,0 center left
	set title "Key (<manual> vert center left)"
	replot
	set key at 0,0 cent
	set title "Key (<manual> vertical center)"
	replot
	set key at 0,0 right
	set title "Key (<manual> vert cent right)"
	replot
	set key at 0,0 bottom left
	set title "Key (<manual> vert bot left)"
	replot
	set key at 0,0 bot center
	set title "Key (<manual> vert bot center)"
	replot
	set key at 0,0 right
	set title "Key (<manual> vert bot right)"
	replot
	unset multiplot
	pause -1 "Hit return to continue"
	
	set multiplot layout 3,3
	set key horiz
	set arrow 1 from -3,0 to 3,0 nohead lt -1
	set arrow 2 from 0,-3 to 0,3 nohead lt -1
	set key at 0,0 left top
	set title "Key (<manual> horiz left top)"
	set xrange [-5:15]
	replot
	set key at 0,0 center top
	set title "Key (<manual> horiz center top)"
	set xrange [-10:10]
	replot
	set key at 0,0 right
	set title "Key (<manual> horiz right top)"
	set xrange [-15:5]
	replot
	set key at 0,0 center left
	set title "Key (<manual> horiz center left)"
	set xrange [-5:15]
	replot
	set key at 0,0 cent
	set title "Key (<manual> horizontal center)"
	set xrange [-10:10]
	replot
	set key at 0,0 right
	set title "Key (<manual> horiz cent right)"
	set xrange [-15:5]
	replot
	set key at 0,0 bottom left
	set title "Key (<manual> horiz bot left)"
	set xrange [-5:15]
	replot
	set key at 0,0 bot center
	set title "Key (<manual> horiz bot center)"
	set xrange [-10:10]
	replot
	set key at 0,0 right
	set title "Key (<manual> horiz bot right)"
	set xrange [-15:5]
	replot
	unset multiplot
	pause -1 "Hit return to continue"
	
	reset

snippet layout.dem
	#
	# Gnuplot version 5.0 demo of multiplot auto-layout capability
	#
	#
	set multiplot layout 3, 1 title "Multiplot layout 3, 1" font ",14"
	set tmargin 2
	set title "Plot 1"
	unset key
	plot sin(x)/x
	#
	set title "Plot 2"
	unset key
	plot 'silver.dat' using 1:2 ti 'silver.dat'
	#
	set style histogram columns
	set style fill solid
	set key autotitle column
	set boxwidth 0.8
	set format y "    "
	set tics scale 0
	set title "Plot 3"
	plot 'immigration.dat' using 2 with histograms, \
	     '' using 7  with histograms , \
	     '' using 8  with histograms , \
	     '' using 11 with histograms 
	#
	unset multiplot
	#
	#
	#
	pause -1 "<cr> to continue"
	reset
	set multiplot layout 1,3 title "Multiplot layout 1, 3" font ",14"
	set xtics rotate
	set bmargin 5
	#
	set title "Plot 1"
	unset key
	plot sin(x)/x
	#
	set title "Plot 2"
	unset key
	plot 'silver.dat' using 1:2 ti 'silver.dat'
	#
	set title "Plot 3"
	set style histogram columns
	set style fill solid
	set key autotitle column
	set boxwidth 0.8
	set format y "    "
	set tics scale 0
	plot 'immigration.dat' using 2 with histograms , \
	     '' using  7 with histograms , \
	     '' using  8 with histograms , \
	     '' using 11 with histograms 
	#
	unset multiplot
	#
	#
	#
	#
	pause -1 "<cr> to continue"
	reset
	set multiplot layout 1,3 title "Same plot with a multi-line title\nshowing adjustment of plot area\n to accommodate it" font ",14"
	set xtics rotate
	set bmargin 5
	#
	set title "Plot 1"
	unset key
	plot sin(x)/x
	#
	set title "Plot 2"
	unset key
	plot 'silver.dat' using 1:2 ti 'silver.dat'
	#
	set title "Plot 3"
	set style histogram columns
	set style fill solid
	set key autotitle column
	set boxwidth 0.8
	set format y "    "
	set tics scale 0
	plot 'immigration.dat' using 2 with histograms , \
	     '' using  7 with histograms , \
	     '' using  8 with histograms , \
	     '' using 11 with histograms 
	#
	unset multiplot
	pause -1 "<cr> to continue"
	#
	#          Stacked Plot Demo
	#
	# Set top and bottom margins to 0 so that there is no space between plots.
	# Fix left and right margins to make sure that the alignment is perfect.
	# Turn off xtics for all plots except the bottom one.
	# In order to leave room for axis and tic labels underneath, we ask for
	# a 4-plot layout but only use the top 3 slots.
	#
	set tmargin 0
	set bmargin 0
	set lmargin 3
	set rmargin 3
	unset xtics
	unset ytics
	
	set multiplot layout 4,1 title "Auto-layout of stacked plots\n" font ",12"
	
	set key autotitle column nobox samplen 1 noenhanced
	unset title
	set style data boxes
	set yrange [0 : 800000]
	
	plot 'immigration.dat' using 3 lt 1
	plot 'immigration.dat' using 8 lt 3
	set xtics nomirror
	set tics scale 0 font ",8"
	set xlabel "Immigration to U.S. by Decade"
	plot 'immigration.dat' using 21:xtic(1) lt 4
	
	unset multiplot
	pause -1 "<cr> to continue"
	#
	#          Stacked Plot Demo Continued
	#
	# Another option is to expand the (N-1)th plot to fill 
	# additional space
	#
	unset xlabel
	unset xtics
	unset ytics
	
	set multiplot layout 6,1 title "Expanding one of the plots to use additional space\n" font ",12"
	
	set key autotitle column nobox samplen 1 noenhanced
	unset title
	set style data boxes
	set yrange [0 : 800000]
	
	plot 'immigration.dat' using 3 lt 1
	plot 'immigration.dat' using 8 lt 3
	plot 'immigration.dat' using 21 lt 4
	set xtics nomirror
	set tics scale 0 font ",8"
	set xlabel "Immigration to U.S. by Decade"
	
	# Override automatic calculation of lower plot boundary
	set bmargin at screen 0.1
	set yrange [0:*]
	plot 'immigration.dat' using ($3+$8+$21):xtic(1) lt 9 title "Total"
	
	unset multiplot
	pause -1 "<cr> to continue"
	
	reset
	
	#
	# Set overall margins for the combined set of plots and size them
	# to generate a requested inter-plot spacing
	#
	if (!exists("MP_LEFT"))   MP_LEFT = .1
	if (!exists("MP_RIGHT"))  MP_RIGHT = .95
	if (!exists("MP_BOTTOM")) MP_BOTTOM = .1
	if (!exists("MP_TOP"))    MP_TOP = .9
	if (!exists("MP_GAP"))    MP_GAP = 0.05
	
	set multiplot layout 2,2 columnsfirst title "{/:Bold=15 Multiplot with explicit page margins}" \
	              margins screen MP_LEFT, MP_RIGHT, MP_BOTTOM, MP_TOP spacing screen MP_GAP
	
	set format y "%.1f"
	set key box opaque
	set ylabel 'ylabel'
	set xrange [-2*pi:2*pi]
	
	plot sin(x) lt 1
	set xlabel 'xlabel'
	plot cos(x) lt 2
	
	unset ylabel
	unset ytics
	
	unset xlabel
	plot sin(2*x) lt 3
	set xlabel 'xlabel'
	plot cos(2*x) lt 4
	unset multiplot
	
	pause -1 "<cr> to continue"
	
	reset

snippet lines_arrows.dem
	#
	# Combine dot/dash, linecolor, and arrowstyle demos into a single file.
	# Demonstrate explicit choice of both dot/dash pattern (linetype) 
	# and color (linecolor).
	#
	set termoption dash
	
	reset
	set xrange [-0.5:3.5]
	set yrange [-1:1.4]
	set bmargin 7
	unset ytics
	unset xtics
	#
	set title "Independent colors and dot/dash styles"
	unset colorbox
	#
	# reset linetypes to base dash patterns
	#
	set for [i=1:5] linetype i dt i
	
	#
	# define line styles using explicit rgbcolor names
	#
	set style line 1 lt 2 lc rgb "red" lw 3
	set style line 2 lt 2 lc rgb "orange" lw 2
	set style line 3 lt 2 lc rgb "yellow" lw 3
	set style line 4 lt 2 lc rgb "green" lw 2
	
	#
	set label 1 'set style line 1 lt 2 lc rgb "red" lw 3'    at -0.4, -0.25 tc rgb "red"
	set label 2 'set style line 2 lt 2 lc rgb "orange" lw 2' at -0.4, -0.35 tc rgb "orange"
	set label 3 'set style line 3 lt 2 lc rgb "yellow" lw 3' at -0.4, -0.45 tc rgb "yellow"
	set label 4 'set style line 4 lt 2 lc rgb "green" lw 2'  at -0.4, -0.55 tc rgb "green"
	set label 5 'plot ... lt 1 lc 3 ' at -0.4, -0.65 tc lt 3
	set label 6 'plot ... lt 3 lc 3 ' at -0.4, -0.75 tc lt 3
	set label 7 'plot ... lt 5 lc 3 ' at -0.4, -0.85 tc lt 3
	#
	set xlabel "You will only see dashed lines if your current terminal setting permits it"
	#
	show style line
	#
	# draw some plots
	#
	plot cos(x)     ls 1 title 'ls 1',   \
	     cos(x-.2)  ls 2 title 'ls 2',\
	     cos(x-.4)  ls 3 title 'ls 3',\
	     cos(x-.6)  ls 4 title 'ls 4', \
	     cos(x-.8)  lt 1 lc 3 title 'lt 1 lc 3',  \
	     cos(x-1.)  lt 3 lc 3 title 'lt 3 lc 3',  \
	     cos(x-1.2) lt 5 lc 3 title 'lt 5 lc 3'
	
	#
	pause -1 "Hit return to continue"
	
	unset for [i=1:8] label i
	set title "The pointinterval property is another way to create interrupted lines"
	set xlabel "This technique works best for equally spaced data points"
	set bmargin 6
	set offset .05, .05
	set xrange [-0.5:3.3]
	set style func linespoints
	
	plot cos(x)     lt -1 pi -4 pt 6 title 'pi -4',   \
	     cos(x-.8)  lt -1 pi -3 pt 7 ps 0.2 title 'pi -3 pt 7 ps 0.2',  \
	     cos(x-.2)  lt -1 pi -6 pt 7 title 'pi -6',\
	     cos(x-.4)  lt -1 pi -3 pt 4 title 'pi -3',\
	     cos(x-.6)  lt -1 pi -5 pt 5 title 'pi -5', \
	     cos(x-1.)  with line lt -1 notitle,  \
	     cos(x+.2)  with line lt -1 lw 2 title 'lw 2'
	
	#
	pause -1 "Hit return to continue"
	
	set title "The pointinterval property also works with character point symbols"
	set style data linespoints
	set pointintervalbox 1.5
	unset xlabel
	myencoding = GPVAL_ENCODING
	set encoding utf8
	
	plot '+' using 1:(cos(x-1.))  with line lt -1 lw 1 title 'lw 1',  \
	     '+' using 1:(cos(x))     lt -1 pi -4 pt "C" title 'pi -4',   \
	     '+' using 1:(cos(x-.8))  lt -1 pi -3 pt "D" title 'pi -3 pt "D"',  \
	     '+' using 1:(cos(x-.2))  lt -1 pi -6 pt "✠" tc rgb "blue" title 'pi -6 tc rgb "blue"',\
	     '+' using 1:(cos(x-.4))  lt -1 pi -3 pt "✲" title 'pi -3',\
	     '+' using 1:(cos(x-.6))  lt -1 pi -5 pt "☺" title 'pi -5', \
	     '+' using 1:(cos(x+.2))  with line lt -1 lw 2 title 'lw 2'
	
	pause -1 "Hit return to continue"
	
	set encoding myencoding
	reset
	
	set xrange [-1000:1000]
	set yrange [-178:86]
	set tics scale 0
	
	set style line 1 lt 1 lw 2
	set style line 2 lt 1 lc rgb "skyblue" lw 2
	
	set style arrow 1 head filled size screen 0.025,30,45 ls 1
	set style arrow 2 head nofilled size screen 0.03,15 ls 2
	set style arrow 3 head filled size screen 0.03,15,45 ls 1
	set style arrow 4 head filled size screen 0.03,15 ls 2
	set style arrow 5 heads filled size screen 0.03,15,135 ls 1
	set style arrow 6 head empty size screen 0.03,15,135 ls 2
	set style arrow 7 nohead ls 1
	set style arrow 8 heads size screen 0.008,90 ls 2
	
	print ' We have defined the following arrowstyles:'
	show style arrow
	
	set arrow from -500,-100 rto 1000, 0  as 1
	set arrow from -500,-110 rto 1000, 0  as 2
	set arrow from -500,-120 rto 1000, 0  as 3
	set arrow from -500,-130 rto 1000, 0  as 4
	set arrow from -500,-140 rto 1000, 0  as 5
	set arrow from -500,-150 rto 1000, 0  as 6
	set arrow from -500,-160 rto 1000, 0  as 7
	set arrow from -500,-170 rto 1000, 0  as 8
	
	set label 'arrowstyle 1:' at -520,-100 right
	set label 'arrowstyle 2:' at -520,-110 right
	set label 'arrowstyle 3:' at -520,-120 right
	set label 'arrowstyle 4:' at -520,-130 right
	set label 'arrowstyle 5:' at -520,-140 right
	set label 'arrowstyle 6:' at -520,-150 right
	set label 'arrowstyle 7:' at -520,-160 right
	set label 'arrowstyle 8:' at -520,-170 right
	
	set title 'Top: plot with vectors arrowstyle 1, Bottom: explicit arrows'
	plot 'arrowstyle.dat' using 1:2:(0):3 notitle with vectors arrowstyle 3
	pause -1 "Hit return to continue"
	reset

snippet linkedaxes.dem
	#
	# Demo for non-linear linked axes. 
	# In this example x2 is proportional to 1/x.
	# X axis is X-ray energy in eV; x2 axis is wavelength in Angstroms.
	#
	# The scattering data are pulled from the web using the GET
	# command, which is provided by the perl-libwww module.
	# You could replace it with wget or some other download tool, or you
	# could manually download the data first and then plot from the local copy.
	#
	# For more information on the plot itself see
	#	http://www.bmsc.washington.edu/scatter
	#
	# Ethan A Merritt - August 2012
	#
	set encoding utf8
	set key outside Left
	set bmargin 5
	set tmargin 6
	set style data lines
	set tics in
	set ticslevel 0.5
	set xlabel  "X-ray energy in eV"
	set format y  '%5.1fe'
	set title " Anomalous scattering factors "
	set xrange  [9000:14400]
	set offset 0,0,1.0,0
	set xtics nomirror
	set link x via 12398./x inverse 12398./x
	
	set x2label  "X-ray wavelength in Å"
	set x2tics 0.1  format "%.1f Å" nomirror
	
	Brdata = "< GET http://www.bmsc.washington.edu/scatter/data/Br.dat"
	Tadata = "< GET http://www.bmsc.washington.edu/scatter/data/Ta.dat"
	
	plot Brdata volatile using 1:3 title 'Br f"'  lt 1 lw 3, \
	'' volatile using 1:2 title "Br f'"  lt 1 lw 1, \
	Tadata volatile using 1:3 title 'Ta f"' lt 2 lw 3, \
	'' volatile using 1:2 title "Ta f'"  lt 2 lw 1
	
	pause -1 "Hit return to continue"
	reset

snippet macros.dem
	#
	# Exercise use of macro substitution
	#
	set macros
	
	title = "Test of command line macro substitution"
	style1 = "points"
	style2 = "lines lw 2"
	plot1 = "'1.dat' using 1:2 with @style1"
	plot2 = "'1.dat' using 1:($2+1) with @style2"
	plot3 = '@plot2'
	plot4 = '@plot3'
	plot5 = '@plot4'
	plot6 = '@plot5'
	#
	set title title
	plot @plot1 title 'plot1', \
	     @plot2 title 'plot2'
	show var
	#
	pause -1 "<cr> to test limit on recursion depth"
	#
	print "testing depth 3"
	plot @plot1 title 'plot1', @plot3 lt 3 title 'Recursion depth 3'
	pause 1
	print "testing depth 4"
	plot @plot1 title 'plot1', @plot4 lt 4 title 'Recursion depth 4'
	pause 1
	print "testing depth 5"
	plot @plot1 title 'plot1', @plot5 lt 5 title 'Recursion depth 5'
	pause 1
	#
	pause -1
	

snippet margins.dem
	#
	# Demonstrate use of 'set {btlr}margin at screen <value>'
	# to pin plot borders to a specific place on the canvas.
	# This allows positioning multiple plots on the page so that their
	# corners line up perfectly.
	#
	# Aug 2006 - Ethan A Merritt
	#
	
	set multiplot title "Demo of placing multiple plots (2D and 3D)\nwith explicit alignment of plot borders"
	
	#
	# First plot  (large)
	#
	set lmargin at screen 0.20
	set rmargin at screen 0.85
	set bmargin at screen 0.25
	set tmargin at screen 0.85
	
	set pm3d
	set palette rgbformulae 7, 5, 15
	set view map
	set samples 50, 50
	set isosamples 50, 50
	unset surface
	set xrange [ -15.00 : 15.00 ]
	set yrange [ -15.00 : 15.00 ]
	set zrange [ -0.250 : 1.000 ]
	
	unset xtics
	unset ytics
	
	unset key
	
	splot sin(sqrt(x**2+y**2))/sqrt(x**2+y**2)
	
	unset pm3d
	unset key
	
	#
	# second plot  (tall and narrow; at left of main plot)
	#
	set lmargin at screen 0.10
	set rmargin at screen 0.20
	
	set ytics
	
	set parametric
	set dummy u,v
	set view map
	
	f(h) = sin(sqrt(h**2))/sqrt(h**2)
	
	set urange [ -15.00 : 15.00 ]
	set vrange [ -15.00 : 15.00 ]
	set xrange [*:*]
	set surface
	
	splot f(u), u, 0 with lines lc rgb "green"
	
	unset parametric
	
	#
	# third plot  (short and wide; at bottom of main plot)
	#
	set lmargin at screen 0.20
	set rmargin at screen 0.85
	set bmargin at screen 0.10
	set tmargin at screen 0.25
	
	set xrange [ -15.00 : 15.00 ]
	set yrange [ * : * ]
	set xtics
	unset ytics
	
	y = 0
	plot sin(sqrt(x**2+y**2))/sqrt(x**2+y**2)
	
	unset multiplot
	
	pause -1 "Hit return to continue"
	reset

snippet matrix_every.dem
	#
	# Exercise use of 'every' subsampling with image plots
	# This was a regression in 5.0.3 
	#
	
	$Matrix << EOM
	3 3 3 3 3 3 3
	3 4 4 4 4 4 3
	3 4 5 5 5 4 3
	3 4 5 6 5 4 3
	3 4 5 5 5 4 3
	3 4 4 4 4 4 3
	3 3 3 3 3 3 3
	EOM
	
	unset key
	set ticscale 0
	set border 0
	set xrange [-0.5:6.5]
	set yrange [-0.5:6.5]
	set grid lt -1
	set cbrange [3:6]
	unset colorbox
	
	set multiplot layout 2,2
	
	set title "Full 7x7 matrix"
	plot $Matrix matrix with image
	set title "Subsample columns by every ::2::4"
	plot $Matrix every ::2::4 matrix with image
	set title "Subsample rows by every :::2::4"
	plot $Matrix every :::2::4 matrix with image
	set title "Sample alternate columns by every 2"
	set xtics 2
	set xrange [-1:7]
	plot $Matrix every 2 matrix with image
	
	unset multiplot
	
	pause -1 "<cr> to continue"
	
	reset

snippet matrix_index.dem
	$MATRICES << EOM
	# A garbage line at the beginning that is not an index
	
	# set1
	0	0.314	0.628
	2	3.06	1.51
	8	1.974	1.03
	
	
	# set2
	0	0.314	0.628
	20	3.06	2.01
	80	1.974	1.03
	
	
	# set3
	0	0.314	0.628
	2	1.06	3.01
	8	2.974	4.03
	EOM
	
	set palette cubehelix
	unset colorbox
	set cbrange [1:3.6]
	set key box opaque samplen 0
	set xrange [] noextend
	set yrange [] noextend
	
	set multiplot layout 2,2 margins char 3,3,2,4 title "{/:Bold Data file contains labeled ascii matrices}
	
	set title "Y range should be the same"
	plot '$MATRICES' nonuniform matrix i "set3" w image title "index 'set3'"
	set title "colors should be the same"
	plot '$MATRICES' nonuniform matrix i "set2" w image title "index 'set2'"
	unset title
	plot '$MATRICES' nonuniform matrix i 0 w image title "index 0"
	plot '$MATRICES' nonuniform matrix i 1 w image title "index 1"
	
	unset multiplot
	
	pause -1  "Hit return to continue"
	
	reset

snippet mgr.dem
	#
	# $Id: mgr.dem,v 1.12 2017/02/23 08:51:47 markisch Exp $
	#
	print "Watch some  cubic splines"
	set samples 50
	set xlabel "Angle (deg)"
	set ylabel "Amplitude"
	set key box
	set title "Bragg reflection -- Peak only"
	plot "big_peak.dat" title "Rate" with errorbars, \
	                 "" smooth csplines t "Rate"
	pause -1 "You would draw smaller bars? (-> return)"
	set bars small
	replot
	set bars large
	#
	pause -1 "An approx-spline demo (-> return)"
	set samples 300
	set xlabel "Time (sec)"
	set ylabel "Rate"
	set title "Ag 108 decay data"
	plot "silver.dat" t "experimental" w errorb, \
	               ""  smooth csplines t "cubic smooth"
	# error is column 3; weight larger errors less
	# start with rel error = 1/($3/$2)
	pause -1 "Now apply a smoothing spline, weighted by 1/rel error (-> return)"
	S=1
	plot "silver.dat" t "experimental" w errorb,\
	               "" u 1:2:(S*$2/$3) smooth acsplines t "acspline Y/Z"
	pause -1 "Make it smoother by changing the smoothing weights (-> return)"
	plot "silver.dat" t "rate" w errorb,\
	               "" u 1:2:($2/($3*1.e1)) sm acs t "acspline Y/(Z*1.e1)",\
	               "" u 1:2:($2/($3*1.e3)) sm acs t "         Y/(Z*1.e3)",\
	               "" u 1:2:($2/($3*1.e5)) sm acs t "         Y/(Z*1.e5)"
	pause -1 "Accentuate the relative changes with a log-scale (-> return)"
	set logscale y
	set grid x y mx my
	replot
	pause -1 "Next - shaded area for error range (-> return)"
	#
	Shadecolor = "#80E0A080"
	plot 'silver.dat' using 1:($2+$3):($2-$3) with filledcurve fc rgb Shadecolor \
	     title "Shaded error region", \
	     '' using 1:2 smooth mcspline lw 2 \
	     title "Monotonic spline through data"
	pause -1 "Now approximate the data with a bezier curve between the endpoints (-> return)"
	#
	unset logscale y
	plot "silver.dat" t "experimental" w errorb,\
	               "" smooth sbezier t "bezier"
	pause -1 "You would rather use log-scales ? (-> return)"
	set logscale y
	plot "silver.dat" t "rate" w errorb, \
	               "" smooth sbezier t "bezier"
	#
	pause -1 "Errorbar demo (-> return)"
	set samples 100
	unset logscale
	unset grid
	set xlabel "Resistance [Ohm]"
	set ylabel "Power [W]"
	set title "UM1-Cell Power"
	n(x)=1.53**2*x/(5.67+x)**2
	plot [0:50] "battery.dat" t "Power" with xyerrorbars, n(x) t "Theory" w lines
	pause -1 "Would you like boxes? (-> return)"
	plot [0:50] "battery.dat" t "Power" with boxxy, n(x) t "Theory" w lines
	pause -1 "Only X-Bars? (-> return)"
	plot [0:50] "battery.dat" u 1:2:3 t "Power" w xerr, n(x) t "Theory" w lines
	pause -1 "Only Y-Bars? (-> return)"
	plot [0:50] "battery.dat" u 1:2:4 t "Power" w yerr, n(x) t "Theory" w lines
	pause -1 "Logscaled? (-> return)"
	set logscale y
	plot [0:50] "battery.dat" u 1:2:4 t "Power" w yerr, n(x) t "Theory" w lines
	pause -1 "X as well? (-> return)"
	set logscale xy
	plot [1:50] "battery.dat" t "Power" w xyerr, n(x) t "Theory" w lines
	pause -1 "If you like bars without tics (-> return)"
	unset logscale
	set bars small
	plot [0:50] "battery.dat" t "Power" with xyerrorbars, n(x) t "Theory" w lines
	pause -1 "X-Bars only (-> return)"
	plot [0:50] "battery.dat" u 1:2:3 t "Power" w xerr, n(x) t "Theory" w lines
	pause -1 "Y-Bars only (-> return)"
	plot [0:50] "battery.dat" u 1:2:4 t "Power" w yerr, n(x) t "Theory" w lines
	pause -1 "Hit return to continue"
	reset

snippet molecule.dem
	set title "GM1 pentasaccharide ball-and-stick representation"
	
	set hidden3d
	set border 0
	unset xtics
	unset ytics
	unset ztics
	set view equal xyz
	
	splot 'GM1_sugar.pdb' using 6:7:8 with points lt 3 pt 7 ps 3 notitle, \
	      'GM1_bonds.r3d' using 1:2:3:($5-$1):($6-$2):($7-$3) with vectors nohead lw 3 lt 1 \
	      title "GM1 pentasaccharide"
	
	pause -1 "Hit return to continue"
	set title "Hidden3d mode with a mixture of surfaces, points, and lines"
	replot 18 notitle
	
	pause -1 "Hit return to continue"
	reset
	

snippet monotonic_spline.dem
	set title "Monotonic cubic splines"
	
	set style data lines
	set xrange [ 233. : 388. ]
	set yrange [ 1.75 : 32.0 ]
	
	plot 'silver.dat' using 1:2 with points pt 7 ps 2 lc "grey" title 'raw data', \
	               '' using 1:2 smooth csplines lt 1 title 'smooth cspline', \
	               '' using 1:2 smooth mcs lt 3 title 'smooth mcs'
	
	pause -1 "Hit <cr> to continue"
	
	set title "Monotonic cubic splines (log-scale data)"
	set log y
	set ytics (1,2,3,4,5,10,20)
	refresh
	
	pause -1 "Hit <cr> to continue"
	reset

snippet mouselab_1.dem
	#
	# $Id: mouselab_1.dem,v 1.1 2006/07/10 19:49:54 sfeam Exp $
	#
	# Demonstrate screen interaction using mouse variables
	#
	#     MOUSE_X MOUSE_Y MOUSE_KEY MOUSE_CHAR
	#
	# On entry, LID is the identifier for the previous label
	#
	LID = LID + 1
	set label 2 sprintf(">>> READY FOR LABEL %d <<<",LID-100) 
	set label 2 at graph .02, graph .65 tc lt (LID-100)
	replot
	
	#
	# Get mouse position and first character of label
	#
	pause mouse key
	LABEL = MOUSE_CHAR
	LABEL_X = MOUSE_X
	LABEL_Y = MOUSE_Y
	set label LID LABEL at LABEL_X, LABEL_Y
	replot
	#
	# Call routine that catches keystrokes one by one and
	# updates the label
	#
	load "mouselab_2.dem"
	#
	# Print out final label details
	#
	show label LID
	
	#
	# Loop until we see an <esc>
	#
	if (MOUSE_KEY != 27) reread

snippet mouselab_2.dem
	#
	# $Id: mouselab_2.dem,v 1.1 2006/07/10 19:49:54 sfeam Exp $
	#
	# Wait for keystroke in plot window
	#
	pause mouse key
	#
	# Add new printable character to label
	#
	if (31 < MOUSE_KEY && MOUSE_KEY < 127) \
	    LABEL = LABEL.MOUSE_CHAR
	#
	# Add newline to label if input character was <cr>
	#
	if (MOUSE_KEY == 13) \
	    LABEL = LABEL."\n"
	#
	# Delete previous character from label on <backspace> or <delete>
	#
	if (MOUSE_KEY == 8 || MOUSE_KEY == 127) \
	    LABEL = LABEL[1:strlen(LABEL)-1]
	#
	# Allow arrow keys to tweak label position
	# The GP_Up GP_Right GP_Down GP_Left (1008-1011) are defined in mousecmn.h
	#
	if (MOUSE_KEY == 1009) LABEL_Y = LABEL_Y + (GPVAL_Y_MAX-GPVAL_Y_MIN)/100.
	if (MOUSE_KEY == 1010) LABEL_X = LABEL_X + (GPVAL_X_MAX-GPVAL_X_MIN)/100.
	if (MOUSE_KEY == 1011) LABEL_Y = LABEL_Y - (GPVAL_Y_MAX-GPVAL_Y_MIN)/100.
	if (MOUSE_KEY == 1008) LABEL_X = LABEL_X - (GPVAL_X_MAX-GPVAL_X_MIN)/100.
	#
	# Update label
	#
	set label LID LABEL at LABEL_X, LABEL_Y
	replot
	#
	# DEBUG feedback 
	#       print "<",MOUSE_KEY,">"
	#
	# Return to caller on <esc> or <tab>, otherwise loop
	#
	if (MOUSE_KEY != 27 && MOUSE_CHAR ne "	") reread

snippet mouselabels.dem
	#
	# $Id: mouselabels.dem,v 1.2 2006/07/10 21:06:11 sfeam Exp $
	#
	# Demonstrate screen interaction using mouse variables
	#
	#     MOUSE_X MOUSE_Y MOUSE_KEY MOUSE_CHAR
	#
	
	set termoption enhanced
	
	set title "Demo interactive placement of labels using mouse feedback"
	set label 1 at graph 0.02, graph 0.9
	set label 1 "Position mouse at desired start of label and start typing\nEnhanced text syntax may be used\nArrow keys will reposition label as you go\n   <del> or <bs> allows editing\n   <tab> to terminate this label\n   <esc> to terminate demo"
	
	set border 0
	unset xtics
	unset ytics
	set key box
	
	plot sin(13*besj0(x))/x
	
	#
	# Initialize label identifier
	#
	LID = 100
	
	#
	# Loop over interactive placement of new labels
	#
	load "mouselab_1.dem"
	show label
	
	#
	# All done with demo
	#
	unset label 1
	set label 2 "DONE"
	replot
	pause -1 "Hit <cr> to continue"
	reset

snippet mousevariables.dem
	#
	# $Id: mousevariables.dem,v 1.8 2006/07/10 19:49:54 sfeam Exp $
	#
	# Demonstrate use of user variables 
	# MOUSE_BUTTON MOUSE_X MOUSE_Y MOUSE_X2 MOUSE_Y2
	# and built-in function exists("<varname>")
	#
	reset
	set title "Scripted zoom using exported MOUSE variables"
	set samples 200
	set label 3 at graph 0.1, graph 0.9 tc lt 3
	set label 3 "Click on one corner of desired zoom region"
	plot sin(13*besj0(x))/x
	#
	pause mouse "Click on one corner of desired zoom region"
	#
	if (exists("MOUSE_BUTTON")) \
	    print "Mouse button ", MOUSE_BUTTON, " at ", MOUSE_X, " ", MOUSE_Y ;\
	    set label 1 sprintf("   Mouse button %d\n   clicked here!", MOUSE_BUTTON) ;\
	    set label 1 at MOUSE_X, MOUSE_Y ;\
	    set label 1 point pt 6 ps 6 ;\
	    replot ;\
	else \
	    print "No mouse click?" ;\
	    exit ;
	#
	BOT_X = MOUSE_X
	BOT_Y = MOUSE_Y
	set label 3 "Click on diagonal corner of desired zoom region"
	replot
	#
	pause mouse "Click on diagonal corner of desired zoom region"
	#
	if (exists("MOUSE_BUTTON")) \
	    print "Mouse button ", MOUSE_BUTTON, " at ", MOUSE_X, " ", MOUSE_Y ;\
	    set label 2 sprintf("   Mouse button %d\n   clicked here!", MOUSE_BUTTON) ;\
	    set label 2 at MOUSE_X, MOUSE_Y ;\
	    set label 2 point pt 6 ps 6 ;\
	    replot ;\
	else \
	    print "No mouse click?" ;\
	    exit ;
	#
	#
	TOP_X = MOUSE_X
	TOP_Y = MOUSE_Y
	#
	set label 3 "Plot will zoom in 3 seconds"
	replot
	pause 3
	#
	set xrange [ BOT_X : TOP_X ]
	set yrange [ BOT_Y : TOP_Y ]
	set grid
	#
	unset label 1
	unset label 2
	unset label 3
	set label 4 sprintf("zoom x range %.3f to %.3f",BOT_X,TOP_X)
	set label 5 sprintf("zoom y range %.3f to %.3f",TOP_X,TOP_Y)
	set label 4 at graph 0.1, graph 0.95 tc lt 3
	set label 5 at graph 0.1, graph 0.90 tc lt 3
	replot
	#
	print "Zoomed to xrange ",BOT_X," ",TOP_X
	print "Zoomed to yrange ",BOT_Y," ",TOP_Y
	#
	pause -1 "Type <cr> to continue"
	
	reset

snippet multiaxis.dem
	#
	# $Id: multiaxis.dem,v 1.1 2007/06/09 22:10:45 sfeam Exp $
	#
	
	# Use the 3rd plot of the electronics demo to show off
	# the use of multiple x and y axes in the same plot.
	# 
	A(jw) = ({0,1}*jw/({0,1}*jw+p1)) * (1/(1+{0,1}*jw/p2))
	p1 = 10
	p2 = 10000
	set dummy jw
	set grid x y2
	set key center top title " "
	set logscale xy
	set log x2
	unset log y2
	set title "Transistor Amplitude and Phase Frequency Response"
	set xlabel "jw (radians)"
	set xrange [1.1 : 90000.0]
	set x2range [1.1 : 90000.0]
	set ylabel "magnitude of A(jw)"
	set y2label "Phase of A(jw) (degrees)"
	set ytics nomirror
	set y2tics
	set tics out
	set autoscale  y
	set autoscale y2
	plot abs(A(jw)) axes x1y1, 180./pi*arg(A(jw)) axes x2y2
	
	pause -1 "Hit return to continue"
	
	# undo what we've done
	reset

snippet multimsh.dem
	#
	# $Id: multimsh.dem,v 1.5 2003/10/28 05:35:54 sfeam Exp $
	#
	set parametric
	set hidden3d
	unset key
	set xrange [0:8]
	set yrange [-4:4]
	set zrange [-2:2]
	set style data line
	set title "Demo of multiple mesh per file capability - Digitized Blue Whale"
	splot "whale.dat"
	pause -1 "Press Return"
	set title "Demo of multiple mesh per file capability - Digitized Blue Whale"
	set xlabel "Mesh or Network 0"
	splot "whale.dat" index 0
	pause -1 "Press Return"
	
	set autoscale z
	set xlabel "Mesh or Network 6"
	set autoscale z
	splot "whale.dat" index 6 using 3:2:1 
	pause -1 "Press Return"
	
	set zrange [-2:2]
	set xlabel "Mesh or Network 12"
	splot "whale.dat" index 12 with points
	pause -1 "Press Return"
	
	set xlabel "Mesh or Network 13"
	splot "whale.dat" i 13
	pause -1 "Press Return - A Loop over Indices could be done with reread"
	
	#  #file "loop"
	#  maxmsh = 16
	#  ind = (ind + 1)%maxmsh
	#  splot "whale.dat" i ind
	#  reread "loop"
	
	set xlabel "Mesh or Network 4"
	splot "whale.dat" i 4
	pause -1 "Press Return"
	
	set xlabel "Mesh or Network 5"
	splot "whale.dat" i 5
	pause -1 "Press Return"
	
	reset
	

snippet multipalette.dem
	#
	# Test case of multiplot containing more than one palette
	# (for debugging)
	#
	set pm3d map
	set multiplot layout 2,1 \
	    title "Test ability to display multiple palettes within a single multiplot"
	set title '10 gray levels'
	set palette gray
	set palette maxcolors 10
	splot x*x
	set title '2 colors'
	set palette color
	set palette maxcolors 2
	splot x
	unset multiplot
	pause -1 "Hit return to continue"
	
	reset

snippet multiplt.dem
	#   GNUPLOT v3.6 beta multiplot script file
	#
	# Second Order System Characteristics
	#
	#       D**2 + 2*zeta*wn*D + (wn**2)y = (wn**2)*x
	#
	#   x           input variable
	#   y           output variable
	#   w           frequency ratio (w/wn)
	#   wn          natural frequency
	#   wd          damped natural frequency
	#   zeta        damping ratio 
	#   mag(w)      amplitude response
	#   phi(w)      phase response
	#   wdwn        damped natural frequency ratio
	#   wnt         normalized time
	#
	#   Plots:
	#   Frequency domain    magnitude response
	#                           phase response
	#
	#   Time domain         unit step response
	#                       unit impulse response
	#
	#
	# Created by: W. D. Kirby email: wdkirby@ix.netcom.com 
	# Date: 1/18/96
	# Released to the public domain with no warranty of any kind
	#
	reset
	set style function lines
	set size 1.0, 1.0
	set origin 0.0, 0.0
	set multiplot
	set size 0.5,0.5
	set origin 0.0,0.5
	set grid
	unset key
	set angles radians
	set samples 250
	#  Plot Magnitude Response
	set title "Second Order System Transfer Function - Magnitude"
	mag(w) = -10*log10( (1-w**2)**2 + 4*(zeta*w)**2)
	set dummy w
	set logscale x
	set xlabel "Frequency (w/wn)"
	set ylabel "Magnitude (dB)" offset 1,0
	set label 1 "Damping =.1,.2,.3,.4,.5,.707,1.0,2.0" at .14,17
	set xrange [.1:10]
	set yrange [-40:20]
	plot \
	  zeta=.1,mag(w), \
	  zeta=.2,mag(w), \
	  zeta=.3,mag(w), \
	  zeta=.4,mag(w), \
	  zeta=.5,mag(w), \
	  zeta=.707,mag(w), \
	  zeta=1.0,mag(w), \
	  zeta=2.0,mag(w),-6
	#  Plot Phase Response
	set size 0.5,0.5
	set origin 0.0,0.0
	set title "Second Order System Transfer Function - Phase"
	set label 1 ""
	set ylabel "Phase (deg)" offset 1,0
	set ytics -180, 30, 0 
	set yrange [-180:0]
	tmp(w) = (-180/pi)*atan( 2*zeta*w/(1-w**2) )
	# Fix for atan function wrap problem
	tmp1(w)= w<1?tmp(w):(tmp(w)-180)
	phi(w)=zeta==1?(-2*(180/pi)*atan(w)):tmp1(w)
	plot \
	  zeta=.1,phi(w), \
	  zeta=.2,phi(w), \
	  zeta=.3,phi(w), \
	  zeta=.4,phi(w), \
	  zeta=.5,phi(w), \
	  zeta=.707,phi(w), \
	  zeta=1,phi(w), \
	  zeta=2.0,phi(w), \
	  -90
	#  Plot Step Response
	set size 0.5,0.5
	set origin 0.5,0.5
	set dummy wnt
	unset logscale x
	set title "Second Order System - Unit Step Response"
	set ylabel "Amplitude y(wnt)" offset 1,0 
	set xlabel "Normalized Time (wnt)"
	set xrange [0:20]
	set xtics 0,5,20
	set yrange [0:2.0]
	set ytics 0, .5, 2.0
	set mytics 5
	set mxtics 10
	wdwn(zeta)=sqrt(1-zeta**2)
	shift(zeta) = atan(wdwn(zeta)/zeta)
	alpha(zeta)=zeta>1?sqrt(zeta**2-1.0):0
	tau1(zeta)=1/(zeta-alpha(zeta))
	tau2(zeta)=1/(zeta+alpha(zeta))
	c1(zeta)=(zeta + alpha(zeta))/(2*alpha(zeta))
	c2(zeta)=c1(zeta)-1
	y1(wnt)=zeta==1?1 - exp(-wnt)*(wnt + 1):0
	y2(wnt)=zeta<1?(1 - (exp(-zeta*wnt)/wdwn(zeta))*sin(wdwn(zeta)*wnt + shift(zeta))):y1(wnt)
	y(wnt)=zeta>1?1-c1(zeta)*exp(-wnt/tau1(zeta))+c2(zeta)*exp(-wnt/tau2(zeta)):y2(wnt)
	plot \
	  zeta=.1,y(wnt), \
	  zeta=.2,y(wnt), \
	  zeta=.3,y(wnt), \
	  zeta=.4,y(wnt), \
	  zeta=.5,y(wnt), \
	  zeta=.707,y(wnt), \
	  zeta=1,y(wnt), \
	  zeta=2,y(wnt)
	#
	#  Plot Impulse Response
	set origin .5,0.
	set title "Second Order System - Unit Impulse Response"
	y(wnt)=exp(-zeta*wnt) * sin(wdwn(zeta)*wnt) / wdwn(zeta)
	set yrange [-1. :1.]
	set ytics -1,.5,1.
	plot \
	  zeta=.1,y(wnt), \
	  zeta=.2,y(wnt), \
	  zeta=.3,y(wnt), \
	  zeta=.4,y(wnt), \
	  zeta=.5,y(wnt), \
	  zeta=.707,y(wnt), \
	  zeta=1,y(wnt), \
	  zeta=2,y(wnt)
	unset multiplot
	pause -1 "Hit return to continue"
	#
	#  Clean up: reset parameter defaults
	#
	reset

snippet named_var.dem
	#
	# Plot a function of a named variable
	# i.e. f(gamma) rather than f(x)
	#
	set title "Plot a function of a named variable"
	set xlabel "gamma"
	
	plot [gamma=0.6:1.2] \
	     [0:20] \
	     fdivm=1.2, \
	     omega = 0.8, \
	     lambda=0.051, \
	     f(gamma)=fdivm/sqrt((omega**2 - gamma**2)**2 + 4 * lambda**2 * gamma**2), \
	     f(gamma) title "f(gamma)" with lines linewidth 6
	
	pause -1 "Hit return to continue"
	
	reset

snippet nokey.dem
	set title "Position plot titles at the end of the corresponding curve\nrather than in a separate key"
	set auto fix
	unset key
	set rmargin at screen 0.8
	set datafile sep '\t'
	set xtics nomirror scale 0
	set ytics nomirror
	set border 2 lw 2
	set style data lines
	plot for [place in "Germany Greece Denmark France"] 'immigration.dat' \
	         using 1:(column(place)) lw 2 title columnhead at end
	
	pause -1 "Hit return to continue"
	
	reset

snippet nonlinear1.dem
	# This example shows how a nonlinear axis definition can be used to 
	# create a "broken axis". X coordinates 0-100 are at the left,
	# X coordinates 500-1000 are at the right, there is a small gap between them.
	# So long as no data points with (100 < x < 500) are plotted, this works as expected.
	#
	# f(x) maps x axis (discontinuous) to shadow axis coords (continuous linear range [0:1000])
	# g(x) maps shadow axis coords to x axis readout
	# 
	  set title "A 'broken' x axis can be defined using 'set nonlinear x'"
	
	# Define the broken-axis mapping
	  axis_gap = 25.
	  f(x) = (x <= 100) ? x : (x < 500) ? NaN : (x - 400 + axis_gap)
	  g(x) = (x <= 100) ? x : (x < 100 + axis_gap) ? NaN : (x + 400 - axis_gap)
	  set xrange [15:600] noextend
	  set nonlinear x via f(x) inverse g(x)
	
	  set xtics 50.
	  set xtics rotate by -90 nomirror
	  set ytics nomirror
	  set border 3
	  unset key
	
	# Creation of the broken axis marks (this should be automated)
	  set arrow 500 from 100, graph 0 to 500, graph 0 nohead lt 500 lw 2 lc bgnd front
	  set arrow 501 from 100, graph 0 length graph  .01 angle 75 nohead lw 2 front
	  set arrow 502 from 100, graph 0 length graph -.01 angle 75 nohead lw 2 front
	  set arrow 503 from 500, graph 0 length graph  .01 angle 75 nohead lw 2 front
	  set arrow 504 from 500, graph 0 length graph -.01 angle 75 nohead lw 2 front
	
	  plot 'silver.dat' with yerrorbars lw 2, '' with lines
	#
	  pause -1 "<cr> to continue"
	  reset

snippet nonlinear2.dem
	# This example shows how a nonlinear axis definition can be used to 
	# set up log-scale axes.  This meachanism is an alternative to the 
	# logscale implementation provided by previous versions of gnuplot.
	# E.g.
	#	set nonlinear x via log10(x) inverse 10**x
	#	set xtics log
	# rather than the original command "set logscale x"
	#
	# This plot is the same as plot #2 in electrom.dem showing log-scaled axes.
	#
	set title "Log-scaled axes defined using 'set nonlinear'"
	set label 1 "Same as plot #2 in 'electron.dem'\nexcept that the axes are set up using\n'set nonlinear' rather than 'set logscale'" at graph 0.5, 0.85, 0 center norotate back nopoint
	
	set dummy jw, y
	set grid xtics ytics
	set key inside center bottom vertical Right noreverse enhanced autotitle box 
	set samples 160, 160
	set style data lines
	set xtics border out scale 1,0.5 nomirror
	set mxtics 10.000000
	set ytics border out scale 1,0.5 nomirror
	set ytics  norangelimit 0.1 textcolor rgb "dark-violet" 
	set y2tics border out scale 1,0.5 nomirror
	set y2tics  norangelimit autofreq  textcolor rgb "#56b4e9" 
	set xlabel "jw (radians)" 
	set ylabel "magnitude of A(jw)" 
	set y2label "Phase of A(jw) (degrees)" 
	#
	set xrange [ 1.1 : 90000.0 ] noextend
	set yrange [ 0.1 : 1.0 ]
	#
	set nonlinear x via log10(x) inverse 10**x 
	set nonlinear y via log10(y) inverse 10**y 
	set xtics log
	#
	A(jw) = ({0,1}*jw/({0,1}*jw+p1)) * (1/(1+{0,1}*jw/p2))
	p1 = 10
	p2 = 10000
	#
	plot abs(A(jw)) lt 1, 180/pi*arg(A(jw)) axes x1y2 lt 3
	
	pause -1 "<cr> to continue"
	reset

snippet nonlinear3.dem
	# This example shows how a nonlinear axis definition can be used to 
	# set up "probability axis" scaling on both the x and y axes.
	# This is also known as "probit" scaling.
	# We compare 3 treatments of an image file (Tux).
	# They differ in the sigma value defining the normal distribution curve
	# used for scaling.
	# The 4th plot shows the original, unscaled image using linear axes.
	#
	unset border
	set title offset 0,-1
	
	set multiplot layout 2,2 title "Probability axes:  Scale image pixels by distance from center treated as a Z-score"
	
	set title "Sigma = 22 pixels"
	sigma = 22.
	ticinterval = 2
	load 'probably_tux.dem'
	
	set title "Sigma = 30 pixels"
	sigma = 30.
	ticinterval = 1
	load 'probably_tux.dem'
	
	set title "Sigma = 60 pixels"
	sigma = 60.
	load 'probably_tux.dem'
	
	set title "Linear Scale"
	set label 1 "Tux"
	set xlabel "   "
	set ylabel "   "
	unset nonlinear x
	unset nonlinear y
	unset xtics
	unset ytics
	replot
	
	unset multiplot
	
	pause -1 "<cr> to continue"
	reset

snippet nonlinear4.dem
	#
	# Nonlinear color axis
	# Confirm equivalence of "set log cb" and 
	# "set nonlinear cb via log10(z) inv 10**z"
	#
	
	set style function pm3d
	set palette cubehelix negative
	unset key
	set title offset 0, -3
	set view 65, 34, 1.00627, 1.41667
	unset border
	unset xtics; unset ytics
	set cbtics add (450) offset -0.5,0
	unset mcbtics
	
	set isosamples 20, 20
	set xyplane at 0
	set xrange [  -2.00 : 6.00 ]
	set yrange [  -5.00 : 5.00 ]
	set zrange [  -50.0 : 450. ]
	set cbrange [ 0.100 : 450. ]
	
	set multiplot layout 1,3
	
	# Linear colors
	#
	set title "Linear cb axis"
	set ztics
	set border 16
	splot 5 + 100.*sin(sqrt(x**2+y**2))/(x**2+y**2)
	
	# Log-scale colors
	#
	set title "set log cb"
	set logscale cb 10
	unset ztics
	unset border
	replot
	
	# Nonlinear color axis (log scale)
	#
	set title "set nonlinear cb\nvia log10(z) inv 10**z"
	unset log cb
	set nonlinear cb via log10(z) inverse 10**z
	set cbtics log
	replot
	
	unset multiplot
	
	pause -1 "<cr> to continue"
	reset

snippet nonlinear5.dem
	#
	# Use of "set nonlinear" to produce a log-scale z axis and contours.
	#
	set border 15 front lt black
	set key at screen 1, 0.9 right top vertical Right
	set style textbox opaque margins  0.5,  0.5 noborder
	set view 22, 341, 1, 1.1
	set isosamples 60, 60
	unset surface 
	set contour base
	set cntrlabel  format '%8.3g' font ',7' start 2 interval 20
	set hidden3d back offset 1 trianglepattern 3 undefined 1 altdiagonal bentover
	set cntrparam order 8
	set style data lines
	set xyplane relative 0
	set nomztics
	unset ztics
	set title "Rosenbrock Function" 
	set xlabel "x" 
	set ylabel "y" 
	Rosenbrock(x,y) = (1-x)**2 + 100*(y - x**2)**2
	u = 0.0
	x = 0.0
	GPFUN_Rosenbrock = "Rosenbrock(x,y) = (1-x)**2 + 100*(y - x**2)**2"
	
	# Achieves same effect as
	#    set log z
	set nonlinear z via log10(z) inverse 10**z 
	
	splot [-1.5:1.5] [-0.5:1.5] Rosenbrock(x,y)
	
	pause -1 "<cr> to continue"
	reset

snippet orbits.dem
	# Ellipse demo - 2D Solar System viewer
	
	reset
	
	set zeroaxis
	set xtics axis
	set ytics axis
	set size ratio 1
	set xrange [-40:40]
	set yrange [-40:40]
	unset border 
	set key right out
	set title "Orbits of selected Solar System objects"
	set angles degrees
	set datafile separator "\t"
	if (strstrt(GNUTERM,"wxt")) \
	    set label 1 "Use Ctrl+mousewheel to zoom!" at graph 0.5,0.05 center 
	
	fn = 'orbital_elements.dat'
	
	# functions to calculate the parameters of the ellipses from orbital elements
	# the actual 3D orbits are projected onto the ecliptic plane
	f1(w,W,i) = cos(w)*cos(W)-sin(w)*sin(W)*cos(i)
	f2(w,W,i) = cos(w)*sin(W)+sin(w)*cos(W)*cos(i)
	
	angle(w,W,i) = atan2(f2(w,W,i), f1(w,W,i))
	# center coordinates
	cx(a,e,i,w,W) = -a*e*f1(w,W,i)
	cy(a,e,i,w,W) = -a*e*f2(w,W,i)
	# axes
	fa(a,e,i,w,W) = 2*a*sqrt(1-sin(w)**2*sin(i)**2)
	fb(a,e,i,w,W) = 2*a*sqrt(1-e**2)*sqrt(1-cos(w)**2*sin(i)**2)
	afromq(q,e) = q/(1-e)
	
	# the ellipse style needs five columns:
	# center x, center y, major axis, minor axis, orientation 
	
	plot fn using (cx($2,$3,$4,$5,$6)):\
	              (cy($2,$3,$4,$5,$6)):\
	              (fa($2,$3,$4,$5,$6)):\
	              (fb($2,$3,$4,$5,$6)):\
	              (angle($5,$6,$4)) index 0 with ellipses lw 3 title "Planets",\
	     fn using (cx($2,$3,$4,$5,$6)):\
	              (cy($2,$3,$4,$5,$6)):\
	              (fa($2,$3,$4,$5,$6)):\
	              (fb($2,$3,$4,$5,$6)):\
	              (angle($5,$6,$4)) index 1 every ::0::20 with ellipses lw 1 title "Minor planets",\
	     fn using (cx($2,$3,$4,$5,$6)):\
	              (cy($2,$3,$4,$5,$6)):\
	              (fa($2,$3,$4,$5,$6)):\
	              (fb($2,$3,$4,$5,$6)):\
	              (angle($5,$6,$4)) index 2 every ::0::10 with ellipses lw 1 title "Comets",\
	     fn using (cx($2,$3,$4,$5,$6)):\
	              (cy($2,$3,$4,$5,$6)):\
	              (fa($2,$3,$4,$5,$6)):\
	              (fb($2,$3,$4,$5,$6)):\
	              (angle($5,$6,$4)) index 3 every ::0::10 with ellipses lw 1 title "Distant objects"
	              
	pause -1
	reset

snippet parallel.dem
	#
	# Parallel axis plot
	# shows plot layout but would better use some real data set
	#
	set title "Parallel Axis Plot" font ",15"
	
	set border 0
	unset key
	set xrange [] noextend
	unset ytics
	
	# Use x-axis tic positions to label the axes
	set xtics 1 format "axis %g" scale 0,0
	
	# Turn on axis tics for the parallel axes
	set for [i=1:5] paxis i tics
	
	# Use the range commands to create an "interesting" plot.
	# For suitable input data this is the sort of result you
	# might get without axis range manipulation.
	
	set paxis 2 range  [0:30]
	set paxis 4 range  [-10:50]
	set paxis 5 range  [50:*] reverse
	set paxis 5 tics left offset 4
	
	plot 'silver.dat' using 2:3:1:($3/2):2:(int($0/25)) with parallel lt 1 lc variable
	
	pause -1 "Hit return to continue"
	
	reset
	
	set title "Parallel Axis Plot" font ",15"
	
	save_encoding = GPVAL_ENCODING
	set encoding utf8
	
	set border 0
	unset ytics
	set xtics ("X" 1, "Y" 2, "Z" 3, "B" 4) nomirror 
	set xrange [] noextend
	unset key
	
	set paxis 1 tics format '%.0f Å' offset -1
	set paxis 4 tics left offset 4 format '%.0f Å^2'
	set paxis 4 range [0:40]
	
	plot 'GM1_sugar.pdb' using 6:7:8:10:5 with parallel lc var lw 2
	
	pause -1 "Hit return to continue"
	
	set encoding save_encoding
	reset

snippet param.dem
	#
	# $Id: param.dem,v 1.6 2014/01/11 05:35:46 sfeam Exp $
	#
	# Show some of the new parametric capabilities.
	#
	set parametric
	set dummy t
	set autoscale
	set samples 160
	set title ""
	set key box
	set key below
	plot t,sin(t)/t title "t,sin(t)/t or sin(x)/x"
	pause -1 "Hit return to continue"
	
	plot sin(t)/t,t
	pause -1 "Hit return to continue"
	
	plot sin(t),cos(t)
	pause -1 "Hit return to continue"
	
	set xrange [-3:3]
	set yrange [-3:3]
	set title "Parametric Conic Sections"
	plot -t,t,cos(t),cos(2*t),2*cos(t),sin(t),-cosh(t),sinh(t)
	set title ""
	pause -1 "Hit return to continue"
	
	set xrange [-5:5]
	set yrange [-5:5]
	plot tan(t),t,t,tan(t)
	pause -1 "Hit return to continue"
	
	set trange [0.00001:3]
	plot t,log(t),-t,log(t),sin(t),t**2,-sin(t),t**2
	pause -1 "Hit return to continue"
	
	set autoscale x
	set yrange [-1.5:1.5]
	set trange [0.0001:10*pi]
	plot sin(t)/t,cos(t)/t
	pause -1 "Hit return to continue"
	
	#
	# Decouple range of parametric axes u/v from that of display axes x/y/z
	#
	reset
	set label 1 "Decouple range of parametric axes u/v\nfrom that of display axes x/y/z"
	set label 1 at screen 0.1, 0.9
	unset colorbox
	set palette cubehelix cycle 3
	set view equal xyz
	set view 120, 300
	set xyplane 0
	set pm3d depthorder
	set border 4095
	set tics scale 0
	set format ""
	set angles radians
	xx(u, v) = cos(v) * cos(u)
	yy(u, v) = cos(v) * sin(u)
	zz(u, v) = sin(v)
	color(u, v) = sin(2*u)+sin(2*v)
	#
	set parametric
	set isosamples 121, 61
	set samples 121, 61
	set urange [-pi:pi] 
	set vrange [-pi/2:pi/2]
	set xrange [-1:1] 
	set yrange [-1:1]
	set zrange [-1:1]
	splot "++" using (xx($1,$2)):(yy($1,$2)):(zz($1,$2)):(color($1,$2)) with pm3d notitle
	pause -1
	
	# undo what we've done above
	reset
	

snippet piecewise.dem
	#
	# Demo of plotting piecewise functions
	#
	unset border
	set key center top reverse Left
	set xzeroaxis
	set yzeroaxis
	set xtics axis out scale 1,8
	set xtics add  (1.00000 1, 6.28319 1)
	set ytics axis
	set title "Piecewise function sampling" font ",15"
	set xrange [ -2 : 10 ] noreverse nowriteback
	
	plot sample [*:1] x, [1:2.*pi] cos(x), [2.*pi:10] (x-8)**2
	
	pause -1 "Hit <cr> to continue"
	reset
	#
	# This example taken from 
	# http://amca01.wordpress.com/2012/05/29/a-piecewise-polynomial-approximation-to-the-normal-cdf/
	# Original approximation from John Hoyt (1962), The American Statistician 22:25-26.
	#
	set termopt enhanced
	set termopt dash
	save_encoding = GPVAL_ENCODING; set encoding utf8
	
	set title "Piecewise approximation to the\nNormal Cumulative Distribution Function"
	
	set format "%.1f"
	set key left Left reverse invert
	set style data lines
	set xtics nomirror
	set xrange [ 0   : 4 ]
	set yrange [ 0.5 : 1.1 ]
	set style line 1 lt 0 lw 3
	set style line 2 lt 0 lw 3 lc rgb "red"
	
	part1(x) = 0.5 + (9.*x-x**3)/ 24.
	part2(x) = 1.0 + (x-3.0)**3 / 48.
	
	part1 = "part1: for x < 1  norm(x) ≈  ½ + (9x-x^3) / 24"
	part2 = "part2: for x > 1  norm(x) ≈   1 +  (x-3)^3 / 48"
	
	set label 1 at 1.0, 0.62
	set label 1 "plot norm(x),  [0:1] part1(x),  [1:4] part2(x)"
	set arrow 1 from 1.,.7 to 1.,.9 nohead
	
	plot norm(x) lt -1, \
	     [1:4] part2(x) ls 2 title part2, \
	     [0:1] part1(x) ls 1 title part1
	
	pause -1 "Hit <cr> to continue"
	
	set termopt solid
	reset
	
	set border 16
	unset xtics
	unset ytics
	set xyplane at 0
	set style data lines
	set key center at screen 0.5, screen 0.15
	set title font ",15"
	set title "Piecewise function of one parameter in 3D"
	
	splot [-2:2][-2:2] sample \
	   [h=1:5]   '+' using (cos(h)):(sin(h)):(h) lw 2, \
	   [h=5:10]  '+' using (cos(h)):(sin(h)):(h) lw 4, \
	   [h=10:15] '+' using (cos(h)):(sin(h)):(h) lw 2
	
	pause -1 "Hit <cr> to continue"
	
	set encoding save_encoding
	reset

snippet pm3dcolors.dem
	#
	# $Id: pm3dcolors.dem,v 1.4 2010/10/31 19:14:02 mikulik Exp $
	#
	# Test of color modes for pm3d palettes.
	
	#
	# Multiplot with some of the recommended rgbformulae palettes
	#
	set pm3d map
	set multiplot layout 3,3 \
	    title "Palettes according to 'help palette rgbformulae'"
	g(x)=x
	set xrange [0:1]; set xtics 0.2 scale 1.5 nomirror; set mxtics 2
	# set palette maxcolors 128
	unset colorbox; unset key; set tics out; unset ytics
	set palette rgb 7,5,15; set title "traditional pm3d\n(black-blue-red-yellow)"; splot g(x)
	set palette rgb 3,11,6; set title "green-red-violet"; splot g(x)
	set palette rgb 23,28,3; set title "ocean (green-blue-white)\ntry also other permutations"; splot g(x)
	set palette rgb 21,22,23; set title "hot (black-red-yellow-white)"; splot g(x)
	set palette rgb 30,31,32; set title "color printable on gray\n(black-blue-violet-yellow-white)"; splot g(x)
	set palette rgb 33,13,10; set title "rainbow (blue-green-yellow-red)"; splot g(x)
	set palette rgb 34,35,36; set title "AFM hot (black-red-yellow-white)"; splot g(x)
	set palette model HSV
	set palette rgb 3,2,2; set title "HSV model\n(red-yellow-green-cyan-blue-magenta-red)"; splot g(x)
	set pal gray; set title "gray palette"; splot g(x)
	unset multiplot
	reset
	pause -1 "Hit return to continue"
	
	
	#
	#   Gradient Palettes
	#
	set pm3d map
	set palette color
	
	f(x)=(x+10)/20
	set cbrange [f(-10):f(10)] # [0:1]
	set xrange [-10:10]
	set yrange [*:*]
	set xtics 2
	set cbtics 0.1
	set format cb "%3.1f"
	unset ztics
	unset ytics
	set samples 101
	set isosamples 2
	unset key
	
	set palette model RGB
	
	set palette defined 
	set title "set palette defined"
	splot f(x)
	pause -1 "Hit return to continue"
	
	set palette defined (0 0 0 0, 1 0 0 1, 3 0 1 0, 4 1 0 0, 6 1 1 1)
	set title 'set palette defined (0 0 0 0, 1 0 0 1, 3 0 1 0, 4 1 0 0, 6 1 1 1)'
	splot f(x)
	pause -1 "Hit return to continue"
	
	
	set palette defined ( 0 "green", 1 "blue", 2 "red", 3 "orange" ) 
	set title 'set palette defined ( 0 "green", 1 "blue", 2 "red", 3 "orange" )'
	splot f(x)
	pause -1 "Hit return to continue"
	
	
	set palette defined ( 20 "#101010", 30 "#ff0000", 40 "#00ff00", 50 "#e0e0e0" ) 
	set title 'set palette defined ( 20 "#101010", 30 "#ff0000", 40 "#00ff00", 50 "#e0e0e0" )'
	splot f(x)
	pause -1 "Hit return to continue"
	
	
	set palette defined ( 0 0 0 0, 1 1 1 1 )
	set title 'set palette defined ( 0 0 0 0, 1 1 1 1 )'
	splot f(x)
	pause -1 "Hit return to continue"
	
	
	set palette model HSV defined ( 0 0 1 1, 1 1 1 1 )
	set title 'set palette model HSV defined ( 0 0 1 1, 1 1 1 1 )'
	splot f(x)
	pause -1 "Hit return to continue"
	
	
	set palette model RGB
	set palette model RGB defined (0 "green", 1 "dark-green", 1 "yellow", 2 "dark-yellow", 2 "red", 3 "dark-red" )
	set title "set palette model RGB defined\n(0 'green', 1 'dark-green', 1 'yellow', 2 'dark-yellow', 2 'red', 3 'dark-red' )"
	splot f(x)
	pause -1 "Hit return to continue"
	
	
	set palette model RGB
	
	
	set title 'set palette file "-" (file with 3 columns)'
	set palette file "-"
	0 0 0
	0 0 1
	0 1 0
	1 0 0
	1 1 1
	e
	splot f(x)
	pause -1 "Hit return to continue"
	
	
	set title 'set palette file "-" (file with 4 columns)'
	set palette file "-"
	10 0 0 0
	20 0 0 1
	40 0 1 0
	60 1 0 0
	70 1 1 1
	e
	splot f(x)
	pause -1 "Hit return to continue"
	
	
	set title 'set palette file "-" using 1:2:($1+$2)/2'
	set palette file "-" using ($0):1:2:(($1+$2)/2)
	0 0
	0 1
	1 0
	1 1
	e
	splot f(x)
	pause -1 "Hit return to continue"
	
	
	
	#
	#    Color Models
	#
	set palette color
	set pm3d map
	set xrange [-10:10]
	unset ztics
	unset ytics
	set samples 101
	set isosamples 2
	set xtics 2
	
	set palette rgbformulae 7,5,15
	
	set palette model RGB rgbformulae 7,5,15
	set title "set palette model RGB rgbformulae 7,5,15"
	splot f(x)
	pause -1 "Hit return to continue"
	
	
	set palette model HSV rgbformulae 7,5,15
	set title "set palette model HSV rgbformulae 7,5,15"
	splot f(x)
	pause -1 "Hit return to continue"
	
	
	set palette model XYZ rgbformulae 7,5,15
	set title "set palette model XYZ rgbformulae 7,5,15"
	splot f(x)
	pause -1 "Hit return to continue"
	
	
	set palette model CMY rgbformulae 7,5,15
	set title "set palette model CMY rgbformulae 7,5,15"
	splot f(x)
	pause -1 "Hit return to continue"
	
	
	set palette model YIQ rgbformulae 7,5,15
	set title "set palette model YIQ rgbformulae 7,5,15"
	splot f(x)
	pause -1 "Hit return to continue"
	
	
	set palette model HSV rgbformulae 3,2,2
	set title "set palette model HSV rrgbformulae 3,2,2"
	splot f(x)
	pause -1 "Hit return to continue"
	
	
	
	#
	#   User Defined Functions
	#
	set palette color
	set pm3d map
	set cbrange [f(-10):f(10)]
	set xrange [-10:10]
	set yrange [0:1]
	unset ztics
	unset ytics
	set samples 101
	set isosamples 2
	set xtics 2
	
	set palette model RGB
	
	set palette functions gray, gray, gray
	set title "set palette functions gray, gray, gray"
	splot f(x)
	pause -1 "Hit return to continue"
	
	
	set palette functions sqrt(gray), gray**3, sin(gray*2*pi) 
	set title 'set palette functions sqrt(gray), gray**3, sin(gray*2*pi)  <--> 7,5,15'
	splot f(x)
	pause -1 "Hit return to continue"
	
	
	set palette rgbformulae 7,5,15
	set title 'set palette rgbformulae 7,5,15'
	splot f(x)
	pause -1 "Hit return to continue"
	
	
	set palette model XYZ functions gray**0.35, gray**0.5, gray**0.8
	set title 'set palette model XYZ functions model XYZ  gray**0.35, gray**0.5, gray**0.8'
	splot f(x)
	pause -1 "Hit return to continue"
	
	
	theta(x) = x<0 ? 0 : 1
	r(x) = 4*x*(1-theta(x-0.25))
	g(x) = 0.5*theta(x-0.25)*(1-theta(x-0.5))
	b(x) = x
	set palette model RGB functions r(gray),g(gray),b(gray)
	set title "set palette model RGB functions\n4*x*(1-theta(x-0.25)), 0.5*theta(x-0.25)*(1-theta(x-0.5)), x"
	splot f(x)
	pause -1 "Hit return to continue"
	
	reset
	

snippet pm3d.dem
	#
	# $Id: pm3d.dem,v 1.22 2012/10/04 20:19:07 sfeam Exp $
	#
	# demo for pm3d splotting
	#
	# This demo can be directly used if your default terminal supports pm3d, 
	# like OS/2 Presentation Manager, X11, Linux VGA, Windows.
	# Otherwise you can use the output to postscript, for example:
	#	set term push; set term postscript color enhanced
	#	set out 'pm3d_demo.ps'
	#	load 'pm3d.dem'
	#	set out; set term pop
	
	# Prepared by Petr Mikulik
	# History:
	#	- 12. 3. 2003 pm: added demo for corners2color
	#	-  9. 3. 2002 pm: added demo for ftriangles
	#	- 31. 1. 2002 pm: removed pm3d/demo and stand-alone pm3d.dem in demo/
	#	- 29. 1. 2002 joze: added demo for the 4th column
	# 	- 18. 3. 2001 pm: updated for cb-axis
	# 	- 18.11. 2000 pm: updated for 'set colorbox'
	# 	- 03. 3. 2000 joze: updated to show new pm3d features
	#	- 15. 6. 1999 pm: update for `set pm3d`+`set palette`
	# 	- 29. 4. 1999 pm: the 1st version, directory pm3d/demo
	
	
	# print "WARNING:  This demo works only on terminals supporting pm3d, i.e. on "
	# print "--------  OS/2 Presentation Manager, X11, Linux VGA, Windows. Please use"
	# print "          output to a postscript file otherwise."
	
	set xlabel "x"
	set ylabel "y"
	set key top
	set border 4095
	set xrange [-15:15]
	set yrange [-15:15]
	set zrange [-0.25:1]
	set samples 25
	set isosamples 20
	
	set title "pm3d demo. Radial sinc function. Default options."
	set pm3d; set palette
	#show pm3d
	#show palette
	splot sin(sqrt(x**2+y**2))/sqrt(x**2+y**2)
	pause -1 "Hit return to continue"
	
	set title "pm3d at s (surface) / ticslevel 0"
	set ticslevel 0
	set pm3d at s
	set cblabel "colour gradient"
	replot
	pause -1 "Hit return to continue"
	
	set title "pm3d at b (bottom)"
	set pm3d at b
	replot
	pause -1 "Hit return to continue"
	
	set title "unset surface; set pm3d at st (surface and top)"
	unset surface
	set pm3d at st
	replot
	pause -1 "Hit return to continue"
	
	unset cblabel
	
	set title "set pm3d at bstbst (funny combination, only for screen or postscript)"
	set view 50,50
	set pm3d at bstbst
	replot
	pause -1 "Hit return to continue"
	
	set title "gray map"
	set pm3d map
	set palette gray
	set samples 100; set isosamples 100
	replot
	pause -1 "Hit return to continue"
	
	set title "gray map, negative"
	set pm3d map
	set palette gray negative
	replot
	pause -1 "Hit return to continue"
	
	set title "colour map, using default rgbformulae 7,5,15 ... traditional pm3d (black-blue-red-yellow)"
	set palette color positive
	set pm3d map
	set samples 50; set isosamples 50
	replot
	pause -1 "Hit return to continue"
	
	set title "colour, rgbformulae 3,11,6 ... green-red-violet"
	set palette rgbformulae 3,11,6
	replot
	pause -1 "Hit return to continue"
	
	set title "colour, rgbformulae 23,28,3  ... ocean (green-blue-white); OK are also all other permutations"
	set palette rgbformulae 23,28,3
	replot
	pause -1 "Hit return to continue"
	
	set title "colour, rgbformulae 30,31,32 ... color printable on gray (black-blue-violet-yellow-white)"
	set palette rgbformulae 30,31,32
	replot
	pause -1 "Hit return to continue"
	
	set title "cubehelix color scheme with monotonic intensity\nD A Green (2011) http://arxiv.org/abs/1108.5083" offset 0,1
	set palette cubehelix
	replot
	pause -1 "Hit return to continue"
	
	test palette
	pause -1 "Hit return to continue"
	
	set title "rgbformulae 31,-11,32: negative formula number=inverted color"
	set palette rgbformulae 31,-11,32
	replot
	pause -1 "Hit return to continue"
	set yrange [*:*] noreverse
	
	
	reset
	
	set title "set pm3d scansforward: wrong, because back overwrites front"
	set pm3d
	set palette
	set view 130,10
	set samples 50; set isosamples 50
	set border 4095
	unset surface
	set pm3d at s scansforward
	splot sin(sqrt(x**2+y**2))/sqrt(x**2+y**2)
	pause -1 "Hit return to continue"
	
	set title "set pm3d scansbackward: correctly looking surface"
	set xlabel
	set border 4095
	set pm3d scansbackward
	replot
	pause -1 "Hit return to continue"
	
	
	set title "set hidden3d"
	set samples 30; set isosamples 30
	set hidden3d
	set pm3d
	set surface
	set view 50,220
	set xrange [-2:2]
	set yrange [-2:2]
	splot log(x*x*y*y)
	pause -1 "Hit return to continue"
	unset hidden3d
	
	# draw the surface using pm3d's hidden3d with line type 100
	unset hidden
	unset surface
	
	set title "set pm3d hidden3d <linetype>: pm3d's much faster hidden3d variant"
	set samples 30; set isosamples 30
	set pm3d
	set style line 100 lt 5 lw 0.5
	set pm3d hidden3d 100
	set view 50,220
	set xrange [-2:2]
	set yrange [-2:2]
	splot log(x*x*y*y)
	pause -1 "Hit return to continue"
	set pm3d nohidden3d
	
	set title "bad: surface and top are too close together"
	set xrange [-1:1]
	set yrange [-1:1]
	unset hidd
	set zrange [-15:4]
	set ticslevel 0
	set pm3d at st
	splot log(x*x*y*y)
	pause -1 "Hit return to continue"
	
	set title "solution: use independent 'set zrange' and 'set cbrange'"
	unset surf
	set cbrange [-15:4]
	set zrange [-15:60]
	splot log(x*x*y*y)
	pause -1 "Press Enter; I will continue by 'set autoscale cb' and much more..."
	set autoscale cb
	
	set title "color box is on by default at a certain position"
	set samples 20; set isosamples 20
	set autoscale
	set key on
	set pm3d
	set pm3d at s
	set view 60,30
	splot y
	pause -1 "Hit return to continue"
	
	set title "color box is on again, now with horizontal gradient"
	set size 0.6,0.6; set origin 0.2,0.25
	set colorbox horizontal
	set colorbox user origin 0.1,0.15 size 0.8,0.1
	set mcbtics 2
	set grid cb
	set cblabel "see cblabel, grid cb, mcbtics, ..."
	replot 
	pause -1 "Hit return to continue"
	
	unset cblabel; unset mcbtics; set grid nocb; set colorbox default
	
	set title "color box is switched off"
	unset colorbox
	replot 
	pause -1 "Hit return to continue"
	
	set title 'using now "set grid back; unset colorbox"'
	unset colorbox
	set pm3d
	set border 895
	set grid back
	replot 
	pause -1 "Hit return to continue"
	
	set size 1,1; set origin 0,0
	set grid layerdefault
	set xlabel "X LABEL"
	set ylabel "Y LABEL"
	
	set sample 11; set isosamples 11
	set pm3d map
	set palette
	set colorbox
	set lmargin 0
	
	
	set pm3d flush begin
	set title "Datafile with different nb of points in scans; pm3d flush begin"
	set pm3d flush begin noftriangles scansforward
	splot 'triangle.dat'
	#show pm3d
	pause -1 "Hit return to continue"
	
	set title "Datafile with different nb of points in scans; pm3d flush center"
	set pm3d flush center scansforward
	replot
	pause -1 "Hit return to continue"
	
	set title "Datafile with different nb of points in scans; pm3d flush end"
	set pm3d flush end scansforward
	replot
	pause -1 "Hit return to continue"
	
	set grid front
	
	set pm3d flush begin
	set title "Data with different nb of points in scans; pm3d ftriangles flush begin"
	set pm3d flush begin ftriangles scansforward
	splot 'triangle.dat'
	#show pm3d
	pause -1 "Hit return to continue"
	
	set title "Data with different nb of points in scans; pm3d ftriangles flush center"
	set pm3d flush center ftriangles scansforward
	replot
	pause -1 "Hit return to continue"
	
	set title "Data with different nb of points in scans; pm3d ftriangles flush end"
	set pm3d flush end ftriangles scansforward
	replot
	pause -1 "Hit return to continue"
	
	set title "Using interpolation with datafile; pm3d map interpolate 2,1"
	set pm3d flush begin noftriangles scansforward interpolate 2,1
	replot
	pause -1 "Hit return to continue"
	
	set title "Using interpolation with datafile; pm3d map ftriangles interpolate 10,1"
	set pm3d flush begin ftriangles scansforward interpolate 10,1
	replot
	pause -1 "Hit return to continue"
	
	set title "Using interpolation with datafile; pm3d at s ftriangles interpolate 10,1"
	set pm3d flush begin ftriangles scansforward at s interpolate 10,1
	set view 20,20
	replot
	pause -1 "Hit return to continue"
	
	reset
	
	set title "only for enhanced terminals: 'set format cb ...'"
	set xlabel "X"
	set ylabel "Y"
	set sample 31; set isosamples 31
	set xrange [-185:185]
	set yrange [-185:185]
	set format cb "%.01t*10^{%T}"
	unset surface
	set border 4095
	set ticslevel 0
	set pm3d at s; set palette gray
	set cblabel "the colour gradient"
	splot abs(x)**3+abs(y)**3
	pause -1 "Hit return to continue"
	
	unset cblabel
	
	set pal color
	set xrange [*:*]; set yrange [*:*]
	set title "function 'x+y' using all colors available, 'set pal maxcolors 0'"
	set pal maxcolors 0
	splot x+y
	pause -1 "Hit return to continue"
	
	set xrange [*:*]; set yrange [*:*]
	set title "function 'x+y' using only 5 colors, 'set pal maxcolors 5'"
	set pal maxcolors 5
	splot x+y
	pause -1 "Hit return to continue"
	
	set pal maxcolors 0
	unset pm3d
	set surface
	set format z
	set palette
	set title "color lines: 'splot sin(y)/(y) with lines palette'"
	set ticslevel 0
	set border 4095
	set xrange [-10:10]; set yrange [-10:10]
	set hidden3d
	set isosamples 40,40; set samples 40,40
	splot sin(y)/(y) w lines palette
	pause -1 "Hit return to continue"
	
	reset
	
	set title "Demo for clipping of 2 rectangles comes now. The xrange is [0:2]..."
	set pm3d; set palette
	set pm3d map
	
	set xrange [0:2]
	splot 'clip14in.dat'
	pause -1 "Hit return to continue"
	
	set xrange [0:1.5]
	set yrange [*:*]
	set title "...and now xrange is [0:1.5] and 'set pm3d clip1in'"
	set pm3d clip1in
	replot
	pause -1 "Hit return to continue"
	
	set title "...now xrange is [0:1.5] and 'set pm3d clip4in'"
	set pm3d clip4in
	replot
	pause -1 "Hit return to continue"
	
	
	reset
	
	sinc(u,v) = sin(sqrt(u**2+v**2)) /sqrt(u**2+v**2)
	
	set sam 40
	set iso 40
	
	set title "pm3d explicit mode --- coloring according to the 4th parameter of 'using'"
	set ticslevel 0
	
	set pm3d at s explicit
	
	#	'binary2' binary u 1:2:(column(3)+column(-1)/20.0):3 w pm3d, 
	
	splot \
		'binary2' binary u 1:2:3:($2+($1+$2)/10) w pm3d, \
		1+sinc(x*4, y*4) w l
	pause -1 "Hit return to continue"
	
	set title "coloring according to the 3rd 'using' parameter (left) and to the z-value (bottom)"
	set border 895
	splot \
	    'binary2' binary u 2:(12 - column(3) * 10):1 notitle w lines, \
	    'binary2' binary u (-12 + column(3) * 10):2:1:(-12 + column(3) * 10) notitle w pm3d, \
	    'binary2' binary u 1:2:(column(3) * 10 - 12) notitle w pm3d
	
	pause -1 "Hit return to continue"
	
	reset
	
	print "Plot by pm3d algorithm draws quadrangles filled with color calculated from"
	print "the z- or color-value of the surrounding 4 corners. The following demo shows"
	print "different color spots for a plot with very small number of quadrangles (here"
	print "rectangular pixels). Note that the default option is 'mean'."
	
	set pm3d map
	set iso 4
	set samples 4
	set xrange [0:3]
	set yrange [0:3]
	set cbrange [0:9]
	f(x,y)=2*x+y
	set key off
	unset xtics
	unset ytics
	unset colorbox
	set tmargin 0
	set bmargin 0
	
	# set tics scale 2
	# set colorbox horizontal user origin 0.1,0.025 size .8,0.02
	
	# Needed by terminals limited on colors:
	#set palette maxcolors 26
	
	# Note: no more space in the following multiplot to show demo for
	#set pm3d corners2color median
	#set pm3d corners2color geomean
	
	set multiplot title "set pm3d corners2color mode"
	# Let us do multiplot grid 3x3:
	dx = 1.0/3; dy = 1.0/3;
	set size dx,dy
	
	set origin 0*dx,0*dy
	set title "c1"
	set pm3d corners2color c1
	splot f(x,y)
	
	set origin 2*dx,0*dy
	set title "c2"
	set pm3d corners2color c2
	replot
	
	set origin 0*dx,2*dy
	set title "c3"
	set pm3d corners2color c3
	replot
	
	set origin 2*dx,2*dy
	set title "c4"
	set pm3d corners2color c4
	replot
	
	set origin 1*dx,2*dy
	set title "mean"
	set pm3d corners2color mean
	replot
	
	set origin 2*dx,1*dy
	set title "geomean"
	set pm3d corners2color geomean
	replot
	
	set origin 1*dx,0*dy
	set title "median"
	set pm3d corners2color median
	replot
	
	set origin 0*dx,1*dy
	set title "harmean"
	set pm3d corners2color harmean
	replot
	
	set origin 1*dx,1*dy
	set title "Original grid points"
	unset pm3d
	set border 0
	unset xtics
	unset ytics
	unset colorbox
	#splot x*2+y with points ps 3 pt 5 palette
	splot f(x,y) with linesp ps 3 pt 5 palette
	
	unset multiplot
	pause -1 "Hit return to continue"
	reset
	
	print "End of pm3d demo."

snippet pm3dgamma.dem
	#
	# $Id: pm3dgamma.dem,v 1.2 2003/10/17 15:02:21 mikulik Exp $
	#
	# Test of gamma correction for gray palettes
	
	set pm3d; set palette
	set palette gray
	set pm3d map
	set cbrange [-10:10]
	set xrange [-10:10]
	set yrange [*:*]
	unset ztics
	unset ytics
	set samples 101
	set isosamples 2
	set xtics 2
	
	set palette gamma 0.75
	set title "gamma = 0.75"
	splot x
	pause -1 "Hit return to continue"
	
	
	set palette gamma 1.0
	set title "gamma = 1.0"
	splot x
	pause -1 "Hit return to continue"
	
	
	set palette gamma 1.25 
	set title "gamma = 1.25"
	splot x
	pause -1 "Hit return to continue"
	
	
	set palette gamma 1.5
	set title "gamma = 1.5"
	splot x
	pause -1 "Hit return to continue"
	
	
	set palette gamma 1.75
	set title "gamma = 1.75"
	splot x
	pause -1 "Hit return to continue"
	
	
	set palette gamma 2.0
	set title "gamma = 2.0"
	splot x
	pause -1 "Hit return to continue"
	
	reset
	

snippet pm3d_lighting.dem
	#
	# pm3d lighting model with specular highlighting
	#
	unset key
	unset border
	unset colorbox
	set view 33, 291, 1.2, 1.3
	set bmargin 0
	set tmargin 0
	set samples 200, 200
	set isosamples 200, 200
	set xyplane -0.05
	set format ""
	set grid x y lt black
	unset ztics
	set urange [ -20. : 20. ]
	set vrange [ -20. : 20. ]
	set zrange [ -0.25 : 0.5 ]
	
	set style fill solid 1.00 noborder
	set pm3d implicit at s hidden3d depth noborder
	
	slice(x,y) = (x**2+y**2 < 10.0) ? 1.0 : (x**2+y**2 > 300.0) ? NaN : sin(abs(atan2(x,y)))
	sinc2(x,y) = sin(sqrt(x**2+y**2))/sqrt(x**2+y**2)
	flatten(x,y) = sqrt(x**2+y**2)/5.
	F(x,y) =  sinc2(x,y) * slice(x,y) * flatten(x,y)
	
	set title "pm3d lighting model with specular highlighting"
	set pm3d lighting primary 0.5 specular 0.2
	splot '++' using 1:2:(F($1,$2)):(0xAAEEEE) nosurface lc rgb variable
	
	pause -1 "Hit <return> to continue"
	reset
	
	set title "PM3D surfaces with specular highlighting"
	
	unset border
	unset key
	set object 1 rect from screen 0, 0, 0 to screen 1, 1, 0 behind 
	set object 1 rect fc  rgb "gray"  fillstyle solid 1.0  border -1
	set view 236, 339, 1.245, 1.0
	set isosamples 75, 75
	unset xtics
	unset ytics
	unset ztics
	set parametric
	set dummy u,v
	set urange [ -pi : pi ]
	set vrange [ -pi : pi ]
	
	set palette rgbformulae 8, 9, 7
	set style fill solid 1.0 noborder
	set pm3d depthorder noborder
	set pm3d lighting specular 0.6
	
	splot cos(u)+.5*cos(u)*cos(v),sin(u)+.5*sin(u)*cos(v),.5*sin(v) with pm3d, \
	    1+cos(u)+.5*cos(u)*cos(v),.5*sin(v),sin(u)+.5*sin(u)*cos(v) with pm3d
	
	pause -1 "Hit return to continue"
	reset

snippet pointsize.dem
	#
	# Read individual point sizes from extra column of input file
	# Ethan A Merritt - October 2004
	#
	unset key
	unset border
	unset yzeroaxis
	unset xtics
	unset ytics
	unset ztics
	
	#
	# plot a 2D map with locations marked by variable sized points
	#
	set title "plot with variable size points"
	plot 'world.dat' with lines lt 3, \
	     'world.cor' using 1:2:(5.*rand(0)) with points lt 1 pt 6 ps variable
	pause -1 "Hit return to continue"
	
	#
	set title "splot with variable size points\nit is possible to specify size and color separately"
	set view map
	unset hidden3d
	splot 'world.dat' using 1:2:(0) with lines lt 3, \
	      'world.cor' using 1:2:(0.5-rand(0)):(5.*rand(0)) with points pt 5 ps var lt palette
	pause -1 "Hit return to continue"
	
	#
	# plot a '3D version using spherical coordinate system' of the world.
	set angles degrees
	set title "3D version using spherical coordinate system"
	set ticslevel 0
	set view 70,40,0.8,1.2
	set mapping spherical
	set parametric
	set samples 32
	set isosamples 9
	set urange [-90:90]
	set vrange [0:360]
	splot cos(u)*cos(v),cos(u)*sin(v),sin(u) with lines lt 5,\
	      'world.dat' with lines lt 3, \
	      'world.cor' using 1:2:(1):(5.*rand(0)) with points lt 1 pt 6 ps variable
	pause -1 "Hit return to continue"
	
	#
	# hidden3d still not working fully
	# pointsize is now handled, but axes are never obscured
	# 
	set title "3D solid version through hiddenlining"
	set hidden3d
	set arrow from 0,0,-1.2 to 0,0,1.2 lt 5 lw 2
	set arrow from -1.2, 0, 0 to 1.2, 0, 0 nohead lt 5 lw 1 
	set arrow from 0, -1.2, 0 to 0, 1.2, 0 nohead lt 5 lw 1
	splot cos(u)*cos(v),-cos(u)*sin(v),sin(u) with lines lt 5,\
	      'world.dat' u 1:2:(1.001) with lines lt 3, \
	      'world.cor' using 1:2:(1):(5.*rand(0))  with points lt 1 pt 6 ps var
	pause -1 "Hit return to continue"
	
	reset
	

snippet polar.dem
	#
	# $Id: polar.dem,v 1.9 2017/02/15 04:21:38 sfeam Exp $
	#
	# Various ways of plotting functions in polar coordinates
	#
	unset border
	set clip
	set polar
	set xtics axis nomirror
	set ytics axis nomirror
	unset rtics
	set samples 160
	set zeroaxis
	set trange [0:2*pi]
	set title "Three circles (with aspect ratio distortion)"
	plot .5,1,1.5
	pause -1 "Hit return to continue"
	set title ""
	set key box
	
	plot cos(2*t)
	pause -1 "Hit return to continue"
	
	plot 2*sqrt(cos(t)),-2*sqrt(cos(t))
	pause -1 "Hit return to continue"
	
	plot sin(4*t),cos(4*t)
	set offset 0,0,0,0
	pause -1 "Hit return to continue"
	
	set xrange [-5:5]
	set yrange [-5:5]
	plot t/cos(3*t)
	pause -1 "Hit return to continue"
	set autoscale
	
	plot 1-sin(t)
	pause -1 "Hit return to continue"
	
	set trange [0:12*pi]
	plot 2*t
	pause -1 "Hit return to continue"
	
	butterfly(x)=exp(cos(x))-2*cos(4*x)+sin(x/12)**5
	set samples 800
	set title "Butterfly"
	unset key
	plot butterfly(t)
	pause -1 "Hit return to continue"
	reset
	
	set polar
	set grid polar 
	unset xtics
	unset ytics
	set border 0
	set style fill solid 0.5
	set rrange [0.1 : 4]
	set size square
	set key title "bounding radius 2.5"
	set key outside top right samplen 0.7
	
	plot 3.+sin(t)*cos(5*t) with filledcurve above r=2.5 notitle,\
	     3.+sin(t)*cos(5*t) with line
	
	pause -1 "Hit return to continue"
	# undo what we've done above
	reset

snippet poldat.dem
	#
	# $Id: poldat.dem,v 1.11 2014/07/23 01:46:09 sfeam Exp $
	#
	#
	set size square
	set time
	set clip
	set xtics axis nomirror
	set ytics axis nomirror
	unset grid
	unset polar
	set title "Primitive Smith Chart"
	unset key
	set xlabel "Impedance or Admittance Coordinates"
	set para
	set rrange [-0 : 10]
	set trange [-pi : pi]
	set xrange [-1:1]
	set yrange [-1:1]
	tv(t,r) = sin(t)/(1+r)
	tu(t,r) = (cos(t) +r)/(1+r)
	cu(t,x) = 1 + cos(t)/x
	cv(t,x) = (1+ sin(t))/x
	plot cu(t,.1),cv(t,.1),cu(t,.1),-cv(t,.1),\
	cu(t,1),cv(t,1),cu(t,1),-cv(t,1),\
	cu(t,10),cv(t,10),cu(t,10),-cv(t,10),\
	tu(t,.1),tv(t,.1),\
	tu(t,.5),tv(t,.5),\
	tu(t,1),tv(t,1),\
	tu(t,5),tv(t,5),\
	tu(t,10),tv(t,10),\
	cu(t,.5),cv(t,.5),cu(t,.5),-cv(t,.5),\
	tu(t,0),tv(t,0)
	#cu(t,5),cv(t,5),cu(t,5),-cv(t,5)
	pause -1 "Hit return to continue"
	
	set angles degrees
	set polar
	set grid polar 15.
	unset border
	unset param
	set title "Antenna Pattern"
	set xlabel "Azimuth"
	set ylabel "dBSM"
	set style data line
	set rrange [-200:60]
	set trange [*:*]
	set xrange [-220:220]
	set yrange [-220:220]
	unset xtics
	plot "-" using 1:5,"-"
	# Input data ncmain.d4 generated by genpat2.f:Tx antenna pattern
	# x,y,z of center point B in coord 3
	#     -5.12	0	-1.5
	# euler matrix
	#      0	-1	0  
	#      0.7071	0	0.7071
	#     -0.7071	0	0.7071
	#  theta(deg)    phi(deg)  E-theta(dB)  phase     E-phi(dB)    phase
		0	0	-200	0	18.992	0
		2	0	-200	0	18.898	0
		4	0	-200	0	18.619	0
		6	0	-200	0	18.152	0
		8	0	-200	0	17.497	0
		10	0	-200	0	16.651	0
		12	0	-200	0	15.614	0
		14	0	-200	0	14.382	0
		16	0	-200	0	12.952	0
		18	0	-200	0	11.32	0
		20	0	-200	0	9.483	0
		22	0	-200	0	7.434	0
		24	0	-200	0	5.169	0
		26	0	-200	0	2.68	0
		28	0	-200	0	-0.039	0
		30	0	-200	0	-2.998	0
		32	0	-200	0	-6.204	0
		34	0	-200	0	-9.67	0
		36	0	-200	0	-13.407	0
		38	0	-200	0	-17.429	0
		40	0	-200	0	-21.751	0
		42	0	-200	0	-26.391	0
		44	0	-200	0	-31.368	0
		46	0	-200	0	-36.705	0
		48	0	-200	0	-42.429	0
		50	0	-200	0	-48.569	0
		52	0	-200	0	-55.16	0
		54	0	-200	0	-62.243	0
		56	0	-200	0	-69.867	0
		58	0	-200	0	-78.087	0
		60	0	-200	0	-86.971	0
		62	0	-200	0	-96.602	0
		64	0	-200	0	-107.08	0
		66	0	-200	0	-118.53	0
		68	0	-200	0	-131.11	0
		70	0	-200	0	-145.022	0
		72	0	-200	0	-160.534	0
		74	0	-200	0	-177.982	0
		76	0	-200	0	-195.848	0
		78	0	-200	0	-199.967	0
		80	0	-200	0	-200	0
		82	0	-200	0	-200	0
		84	0	-200	0	-200	0
		86	0	-200	0	-200	0
		88	0	-200	0	-200	0
		90	0	-200	0	-200	0
		92	0	-200	0	-200	0
		94	0	-200	0	-200	0
		96	0	-200	0	-200	0
		98	0	-200	0	-200	0
		100	0	-200	0	-200	0
		102	0	-200	0	-200	0
		104	0	-200	0	-200	0
		106	0	-200	0	-200	0
		108	0	-200	0	-200	0
		110	0	-200	0	-200	0
		112	0	-200	0	-200	0
		114	0	-200	0	-200	0
		116	0	-200	0	-200	0
		118	0	-200	0	-200	0
		120	0	-200	0	-200	0
		122	0	-200	0	-200	0
		124	0	-200	0	-200	0
		126	0	-200	0	-200	0
		128	0	-200	0	-200	0
		130	0	-200	0	-200	0
		132	0	-200	0	-200	0
		134	0	-200	0	-200	0
		136	0	-200	0	-200	0
		138	0	-200	0	-200	0
		140	0	-200	0	-200	0
		142	0	-200	0	-200	0
		144	0	-200	0	-200	0
		146	0	-200	0	-200	0
		148	0	-200	0	-200	0
		150	0	-200	0	-200	0
		152	0	-200	0	-200	0
		154	0	-200	0	-200	0
		156	0	-200	0	-200	0
		158	0	-200	0	-200	0
		160	0	-200	0	-200	0
		162	0	-200	0	-200	0
		164	0	-200	0	-200	0
		166	0	-200	0	-200	0
		168	0	-200	0	-200	0
		170	0	-200	0	-200	0
		172	0	-200	0	-200	0
		174	0	-200	0	-200	0
		176	0	-200	0	-200	0
		178	0	-200	0	-200	0
		180	0	-200	0	-200	0
	e
		0	9.0007	-35.926	-71.297	9.0783
		2	8.1667	0.13355	-32.735	8.9007
		4	6.0696	4.0782	-28.04	8.3501
		6	3.3053	4.8742	-26.062	7.3795
		8	0.131	4.2554	-25.105	5.9101
		10	-3.4589	2.6769	-24.794	3.8084
		12	-7.6407	0.15917	-25.211	0.82257
		14	-12.849	-3.6543	-26.702	-3.6253
		16	-20.405	-10.185	-30.112	-11.564
		18	-41.802	-37.181	-39.114	-24.83
		20	-25.979	-11.868	-38.677	-10.86
		22	-22.06	-7.1064	-29.743	-7.4312
		24	-21.258	-5.2193	-26.432	-6.2183
		26	-22.103	-4.8803	-25.429	-5.9416
		28	-24.356	-5.858	-26.299	-6.0712
		30	-28.216	-8.3757	-29.576	-6.4985
		32	-33.989	-13.34	-38.93	-7.5039
		34	-37.632	-21.69	-37.458	-9.6424
		36	-35.691	-16.286	-29.566	-13.578
		38	-35.22	-12.594	-27.052	-15.787
		40	-36.549	-12.197	-27.142	-11.075
		42	-38.418	-14.735	-29.897	-7.6375
		44	-39.287	-23.397	-38.646	-6.0943
		46	-39.287	-23.397	-38.646	-6.0943
		48	-38.418	-14.735	-29.897	-7.6375
		50	-36.549	-12.197	-27.142	-11.075
		52	-35.22	-12.594	-27.052	-15.787
		54	-35.691	-16.286	-29.566	-13.578
		56	-37.632	-21.69	-37.458	-9.6424
		58	-33.989	-13.34	-38.93	-7.5039
		60	-28.216	-8.3757	-29.576	-6.4985
		62	-24.356	-5.858	-26.299	-6.0712
		64	-22.103	-4.8803	-25.429	-5.9416
		66	-21.258	-5.2193	-26.432	-6.2183
		68	-22.06	-7.1064	-29.743	-7.4312
		70	-25.979	-11.868	-38.677	-10.86
		72	-41.802	-37.18	-39.114	-24.83
		74	-20.405	-10.185	-30.112	-11.564
		76	-12.849	-3.6543	-26.702	-3.6253
		78	-7.6407	0.15917	-25.211	0.82257
		80	-3.4589	2.6769	-24.794	3.8084
		82	0.131	4.2554	-25.105	5.9101
		84	3.3053	4.8742	-26.062	7.3795
		86	6.0696	4.0782	-28.04	8.3501
		88	8.1667	0.13355	-32.735	8.9007
		90	9.0007	-35.926	-71.297	9.0783
		92	8.1667	0.13355	-32.735	8.9007
		94	6.0696	4.0782	-28.04	8.3501
		96	3.3053	4.8742	-26.062	7.3795
		98	0.131	4.2554	-25.105	5.9101
		100	-3.4589	2.6769	-24.794	3.8084
		102	-7.6407	0.15917	-25.211	0.82257
		104	-12.849	-3.6543	-26.702	-3.6253
		106	-20.405	-10.185	-30.112	-11.564
		108	-41.802	-37.181	-39.114	-24.83
		110	-25.979	-11.868	-38.677	-10.86
		112	-22.06	-7.1064	-29.743	-7.4312
		114	-21.258	-5.2193	-26.432	-6.2183
		116	-22.103	-4.8803	-25.429	-5.9416
		118	-24.356	-5.858	-26.299	-6.0712
		120	-28.216	-8.3757	-29.576	-6.4985
		122	-33.989	-13.34	-38.93	-7.5039
		124	-37.632	-21.69	-37.458	-9.6424
		126	-35.691	-16.286	-29.566	-13.578
		128	-35.22	-12.594	-27.052	-15.787
		130	-36.549	-12.197	-27.142	-11.075
		132	-38.418	-14.735	-29.897	-7.6375
		134	-39.287	-23.397	-38.646	-6.0943
		136	-39.287	-23.397	-38.646	-6.0943
		138	-38.418	-14.735	-29.897	-7.6375
		140	-36.549	-12.197	-27.142	-11.075
		142	-35.22	-12.594	-27.052	-15.787
		144	-35.691	-16.286	-29.566	-13.578
		146	-37.632	-21.69	-37.458	-9.6424
		148	-33.989	-13.34	-38.93	-7.5039
		150	-28.216	-8.3757	-29.576	-6.4985
		152	-24.356	-5.858	-26.299	-6.0712
		154	-22.103	-4.8803	-25.429	-5.9416
		156	-21.258	-5.2193	-26.432	-6.2183
		158	-22.06	-7.1064	-29.743	-7.4312
		160	-25.979	-11.868	-38.677	-10.86
		162	-41.802	-37.181	-39.114	-24.83
		164	-20.405	-10.185	-30.112	-11.564
		166	-12.849	-3.6543	-26.702	-3.6253
		168	-7.6407	0.15917	-25.211	0.82257
		170	-3.4589	2.6769	-24.794	3.8084
		172	0.131	4.2554	-25.105	5.9101
		174	3.3053	4.8742	-26.062	7.3795
		176	6.0696	4.0782	-28.04	8.3501
		178	8.1667	0.13355	-32.735	8.9007
		180	9.0007	-35.926	-71.297	9.0783
		182	8.1667	0.13355	-32.735	8.9007
		184	6.0696	4.0782	-28.04	8.3501
		186	3.3053	4.8742	-26.062	7.3795
		188	0.131	4.2554	-25.105	5.9101
		190	-3.4589	2.6769	-24.794	3.8084
		192	-7.6407	0.15917	-25.211	0.82257
		194	-12.849	-3.6543	-26.702	-3.6253
		196	-20.405	-10.185	-30.112	-11.564
		198	-41.802	-37.181	-39.114	-24.83
		200	-25.979	-11.868	-38.677	-10.86
		202	-22.06	-7.1064	-29.743	-7.4312
		204	-21.258	-5.2193	-26.432	-6.2183
		206	-22.103	-4.8803	-25.429	-5.9416
		208	-24.356	-5.858	-26.299	-6.0712
		210	-28.216	-8.3757	-29.576	-6.4985
		212	-33.989	-13.34	-38.93	-7.5039
		214	-37.632	-21.69	-37.458	-9.6424
		216	-35.691	-16.286	-29.566	-13.578
		218	-35.22	-12.594	-27.052	-15.787
		220	-36.549	-12.197	-27.142	-11.075
		222	-38.418	-14.735	-29.897	-7.6375
		224	-39.287	-23.397	-38.646	-6.0943
		226	-39.287	-23.397	-38.646	-6.0943
		228	-38.418	-14.735	-29.897	-7.6375
		230	-36.549	-12.197	-27.142	-11.075
		232	-35.22	-12.594	-27.052	-15.787
		234	-35.691	-16.286	-29.566	-13.578
		236	-37.632	-21.69	-37.458	-9.6424
		238	-33.989	-13.34	-38.93	-7.5039
		240	-28.216	-8.3757	-29.576	-6.4985
		242	-24.356	-5.858	-26.299	-6.0712
		244	-22.103	-4.8803	-25.429	-5.9416
		246	-21.258	-5.2193	-26.432	-6.2183
		248	-22.06	-7.1064	-29.743	-7.4312
		250	-25.979	-11.868	-38.677	-10.86
		252	-41.802	-37.18	-39.114	-24.83
		254	-20.405	-10.185	-30.112	-11.564
		256	-12.849	-3.6543	-26.702	-3.6253
		258	-7.6407	0.15917	-25.211	0.82257
		260	-3.4589	2.6769	-24.794	3.8084
		262	0.131	4.2554	-25.105	5.9101
		264	3.3053	4.8742	-26.062	7.3795
		266	6.0696	4.0782	-28.04	8.3501
		268	8.1667	0.13355	-32.735	8.9007
		270	9.0007	-35.926	-71.297	9.0783
		272	8.1667	0.13355	-32.735	8.9007
		274	6.0696	4.0782	-28.04	8.3501
		276	3.3053	4.8742	-26.062	7.3795
		278	0.13099	4.2554	-25.105	5.9101
		280	-3.4589	2.6769	-24.794	3.8084
		282	-7.6407	0.15917	-25.211	0.82257
		284	-12.849	-3.6543	-26.702	-3.6253
		286	-20.405	-10.185	-30.112	-11.564
		288	-41.802	-37.181	-39.114	-24.83
		290	-25.979	-11.868	-38.677	-10.86
		292	-22.06	-7.1064	-29.743	-7.4312
		294	-21.258	-5.2193	-26.432	-6.2183
		296	-22.103	-4.8803	-25.429	-5.9416
		298	-24.356	-5.858	-26.299	-6.0712
		300	-28.216	-8.3757	-29.576	-6.4985
		302	-33.989	-13.34	-38.93	-7.5039
		304	-37.632	-21.69	-37.458	-9.6424
		306	-35.691	-16.286	-29.566	-13.578
		308	-35.22	-12.594	-27.052	-15.787
		310	-36.549	-12.197	-27.142	-11.075
		312	-38.418	-14.735	-29.897	-7.6375
		314	-39.287	-23.397	-38.646	-6.0943
		316	-39.287	-23.397	-38.646	-6.0943
		318	-38.418	-14.735	-29.897	-7.6375
		320	-36.549	-12.197	-27.142	-11.075
		322	-35.22	-12.594	-27.052	-15.787
		324	-35.691	-16.286	-29.566	-13.578
		326	-37.632	-21.69	-37.458	-9.6424
		328	-33.989	-13.34	-38.93	-7.5039
		330	-28.216	-8.3757	-29.576	-6.4985
		332	-24.356	-5.858	-26.299	-6.0712
		334	-22.103	-4.8803	-25.429	-5.9416
		336	-21.258	-5.2193	-26.432	-6.2183
		338	-22.06	-7.1064	-29.743	-7.4312
		340	-25.979	-11.868	-38.677	-10.86
		342	-41.802	-37.181	-39.114	-24.83
		344	-20.405	-10.185	-30.112	-11.564
		346	-12.849	-3.6543	-26.702	-3.6253
		348	-7.6407	0.15917	-25.211	0.82257
		350	-3.4589	2.6769	-24.794	3.8084
		352	0.131	4.2554	-25.105	5.9101
		354	3.3053	4.8742	-26.062	7.3795
		356	6.0696	4.0782	-28.04	8.3501
		358	8.1667	0.13355	-32.735	8.9007
		360	9.0007	-35.926	-71.297	9.0783
		2	8.1667	0.13355	-32.735	8.9007
		4	6.0696	4.0782	-28.04	8.3501
		6	3.3053	4.8742	-26.062	7.3795
		8	0.131	4.2554	-25.105	5.9101
		10	-3.4589	2.6769	-24.794	3.8084
		12	-7.6407	0.15917	-25.211	0.82257
		14	-12.849	-3.6543	-26.702	-3.6253
		16	-20.405	-10.185	-30.112	-11.564
		18	-41.802	-37.181	-39.114	-24.83
		20	-25.979	-11.868	-38.677	-10.86
		22	-22.06	-7.1064	-29.743	-7.4312
		24	-21.258	-5.2193	-26.432	-6.2183
		26	-22.103	-4.8803	-25.429	-5.9416
		28	-24.356	-5.858	-26.299	-6.0712
		30	-28.216	-8.3757	-29.576	-6.4985
		32	-33.989	-13.34	-38.93	-7.5039
		34	-37.632	-21.69	-37.458	-9.6424
		36	-35.691	-16.286	-29.566	-13.578
		38	-35.22	-12.594	-27.052	-15.787
		40	-36.549	-12.197	-27.142	-11.075
		42	-38.418	-14.735	-29.897	-7.6375
		44	-39.287	-23.397	-38.646	-6.0943
		46	-39.287	-23.397	-38.646	-6.0943
		48	-38.418	-14.735	-29.897	-7.6375
		50	-36.549	-12.197	-27.142	-11.075
		52	-35.22	-12.594	-27.052	-15.787
		54	-35.691	-16.286	-29.566	-13.578
		56	-37.632	-21.69	-37.458	-9.6424
		58	-33.989	-13.34	-38.93	-7.5039
		60	-28.216	-8.3757	-29.576	-6.4985
		62	-24.356	-5.858	-26.299	-6.0712
		64	-22.103	-4.8803	-25.429	-5.9416
		66	-21.258	-5.2193	-26.432	-6.2183
		68	-22.06	-7.1064	-29.743	-7.4312
		70	-25.979	-11.868	-38.677	-10.86
		72	-41.802	-37.18	-39.114	-24.83
		74	-20.405	-10.185	-30.112	-11.564
		76	-12.849	-3.6543	-26.702	-3.6253
		78	-7.6407	0.15917	-25.211	0.82257
		80	-3.4589	2.6769	-24.794	3.8084
		82	0.131	4.2554	-25.105	5.9101
		84	3.3053	4.8742	-26.062	7.3795
		86	6.0696	4.0782	-28.04	8.3501
		88	8.1667	0.13355	-32.735	8.9007
		90	9.0007	-35.926	-71.297	9.0783
		92	8.1667	0.13355	-32.735	8.9007
		94	6.0696	4.0782	-28.04	8.3501
		96	3.3053	4.8742	-26.062	7.3795
		98	0.131	4.2554	-25.105	5.9101
		100	-3.4589	2.6769	-24.794	3.8084
		102	-7.6407	0.15917	-25.211	0.82257
		104	-12.849	-3.6543	-26.702	-3.6253
		106	-20.405	-10.185	-30.112	-11.564
		108	-41.802	-37.181	-39.114	-24.83
		110	-25.979	-11.868	-38.677	-10.86
		112	-22.06	-7.1064	-29.743	-7.4312
		114	-21.258	-5.2193	-26.432	-6.2183
		116	-22.103	-4.8803	-25.429	-5.9416
		118	-24.356	-5.858	-26.299	-6.0712
		120	-28.216	-8.3757	-29.576	-6.4985
		122	-33.989	-13.34	-38.93	-7.5039
		124	-37.632	-21.69	-37.458	-9.6424
		126	-35.691	-16.286	-29.566	-13.578
		128	-35.22	-12.594	-27.052	-15.787
		130	-36.549	-12.197	-27.142	-11.075
		132	-38.418	-14.735	-29.897	-7.6375
		134	-39.287	-23.397	-38.646	-6.0943
		136	-39.287	-23.397	-38.646	-6.0943
		138	-38.418	-14.735	-29.897	-7.6375
		140	-36.549	-12.197	-27.142	-11.075
		142	-35.22	-12.594	-27.052	-15.787
		144	-35.691	-16.286	-29.566	-13.578
		146	-37.632	-21.69	-37.458	-9.6424
		148	-33.989	-13.34	-38.93	-7.5039
		150	-28.216	-8.3757	-29.576	-6.4985
		152	-24.356	-5.858	-26.299	-6.0712
		154	-22.103	-4.8803	-25.429	-5.9416
		156	-21.258	-5.2193	-26.432	-6.2183
		158	-22.06	-7.1064	-29.743	-7.4312
		160	-25.979	-11.868	-38.677	-10.86
		162	-41.802	-37.181	-39.114	-24.83
		164	-20.405	-10.185	-30.112	-11.564
		166	-12.849	-3.6543	-26.702	-3.6253
		168	-7.6407	0.15917	-25.211	0.82257
		170	-3.4589	2.6769	-24.794	3.8084
		172	0.131	4.2554	-25.105	5.9101
		174	3.3053	4.8742	-26.062	7.3795
		176	6.0696	4.0782	-28.04	8.3501
		178	8.1667	0.13355	-32.735	8.9007
		180	9.0007	-35.926	-71.297	9.0783
		182	8.1667	0.13355	-32.735	8.9007
		184	6.0696	4.0782	-28.04	8.3501
		186	3.3053	4.8742	-26.062	7.3795
		188	0.131	4.2554	-25.105	5.9101
		190	-3.4589	2.6769	-24.794	3.8084
		192	-7.6407	0.15917	-25.211	0.82257
		194	-12.849	-3.6543	-26.702	-3.6253
		196	-20.405	-10.185	-30.112	-11.564
		198	-41.802	-37.181	-39.114	-24.83
		200	-25.979	-11.868	-38.677	-10.86
		202	-22.06	-7.1064	-29.743	-7.4312
		204	-21.258	-5.2193	-26.432	-6.2183
		206	-22.103	-4.8803	-25.429	-5.9416
		208	-24.356	-5.858	-26.299	-6.0712
		210	-28.216	-8.3757	-29.576	-6.4985
		212	-33.989	-13.34	-38.93	-7.5039
		214	-37.632	-21.69	-37.458	-9.6424
		216	-35.691	-16.286	-29.566	-13.578
		218	-35.22	-12.594	-27.052	-15.787
		220	-36.549	-12.197	-27.142	-11.075
		222	-38.418	-14.735	-29.897	-7.6375
		224	-39.287	-23.397	-38.646	-6.0943
		226	-39.287	-23.397	-38.646	-6.0943
		228	-38.418	-14.735	-29.897	-7.6375
		230	-36.549	-12.197	-27.142	-11.075
		232	-35.22	-12.594	-27.052	-15.787
		234	-35.691	-16.286	-29.566	-13.578
		236	-37.632	-21.69	-37.458	-9.6424
		238	-33.989	-13.34	-38.93	-7.5039
		240	-28.216	-8.3757	-29.576	-6.4985
		242	-24.356	-5.858	-26.299	-6.0712
		244	-22.103	-4.8803	-25.429	-5.9416
		246	-21.258	-5.2193	-26.432	-6.2183
		248	-22.06	-7.1064	-29.743	-7.4312
		250	-25.979	-11.868	-38.677	-10.86
		252	-41.802	-37.18	-39.114	-24.83
		254	-20.405	-10.185	-30.112	-11.564
		256	-12.849	-3.6543	-26.702	-3.6253
		258	-7.6407	0.15917	-25.211	0.82257
		260	-3.4589	2.6769	-24.794	3.8084
		262	0.131	4.2554	-25.105	5.9101
		264	3.3053	4.8742	-26.062	7.3795
		266	6.0696	4.0782	-28.04	8.3501
		268	8.1667	0.13355	-32.735	8.9007
		270	9.0007	-35.926	-71.297	9.0783
		272	8.1667	0.13355	-32.735	8.9007
		274	6.0696	4.0782	-28.04	8.3501
		276	3.3053	4.8742	-26.062	7.3795
		278	0.13099	4.2554	-25.105	5.9101
		280	-3.4589	2.6769	-24.794	3.8084
		282	-7.6407	0.15917	-25.211	0.82257
		284	-12.849	-3.6543	-26.702	-3.6253
		286	-20.405	-10.185	-30.112	-11.564
		288	-41.802	-37.181	-39.114	-24.83
		290	-25.979	-11.868	-38.677	-10.86
		292	-22.06	-7.1064	-29.743	-7.4312
		294	-21.258	-5.2193	-26.432	-6.2183
		296	-22.103	-4.8803	-25.429	-5.9416
		298	-24.356	-5.858	-26.299	-6.0712
		300	-28.216	-8.3757	-29.576	-6.4985
		302	-33.989	-13.34	-38.93	-7.5039
		304	-37.632	-21.69	-37.458	-9.6424
		306	-35.691	-16.286	-29.566	-13.578
		308	-35.22	-12.594	-27.052	-15.787
		310	-36.549	-12.197	-27.142	-11.075
		312	-38.418	-14.735	-29.897	-7.6375
		314	-39.287	-23.397	-38.646	-6.0943
		316	-39.287	-23.397	-38.646	-6.0943
		318	-38.418	-14.735	-29.897	-7.6375
		320	-36.549	-12.197	-27.142	-11.075
		322	-35.22	-12.594	-27.052	-15.787
		324	-35.691	-16.286	-29.566	-13.578
		326	-37.632	-21.69	-37.458	-9.6424
		328	-33.989	-13.34	-38.93	-7.5039
		330	-28.216	-8.3757	-29.576	-6.4985
		332	-24.356	-5.858	-26.299	-6.0712
		334	-22.103	-4.8803	-25.429	-5.9416
		336	-21.258	-5.2193	-26.432	-6.2183
		338	-22.06	-7.1064	-29.743	-7.4312
		340	-25.979	-11.868	-38.677	-10.86
		342	-41.802	-37.181	-39.114	-24.83
		344	-20.405	-10.185	-30.112	-11.564
		346	-12.849	-3.6543	-26.702	-3.6253
		348	-7.6407	0.15917	-25.211	0.82257
		350	-3.4589	2.6769	-24.794	3.8084
		352	0.131	4.2554	-25.105	5.9101
		354	3.3053	4.8742	-26.062	7.3795
		356	6.0696	4.0782	-28.04	8.3501
		358	8.1667	0.13355	-32.735	8.9007
		360	9.0007	-35.926	-71.297	9.0783
	e
	pause -1 "Hit return to continue"
	
	reset
	
	set polar
	set grid polar
	set border 0
	set logscale r 10
	set view equal xy
	set size square
	set noxtics
	set noytics
	set rrange [.5 : 19]
	set trange [pi/2 : 2*pi]
	set rtics axis scale 0.5,0 nomirror rotate by -270  offset 0, 0.7
	set rtics (2,5,10,15)
	set key opaque box samplen 0.75
	set title "log scale polar axis" 
	#
	set style data impulse
	plot 'silver.dat' using ($1/100.):3:0 lc var lw 2 title 'silver.dat'
	
	pause -1 "Hit return to continue"
	
	reset

snippet prob2.dem
	#
	# $Id: prob2.dem,v 1.10 2009/05/31 05:54:41 sfeam Exp $
	#
	# Demo Statistical Approximations version 1.1
	#
	# Copyright (c) 1991, Jos van der Woude, jvdwoude@hut.nl
	
	# History:
	#    -- --- 1991 Jos van der Woude:  1st version
	#    06 Jun 2006 Dan Sebald:  Added plot methods for better visual effect.
	
	print ""
	print ""
	print ""
	print ""
	print ""
	print ""
	print "                        Statistical Approximations, version 1.1"
	print ""
	print "        Copyright (c) 1991, 1992, Jos van de Woude, jvdwoude@hut.nl"
	print ""
	print ""
	print ""
	print ""
	print ""
	print ""
	print ""
	print ""
	print ""
	print ""
	print ""
	print "     NOTE: contains 10 plots and consequently takes some time to run"
	print "                      Press Ctrl-C to exit right now"
	print ""
	pause   -1 "                      Press Return to start demo ..."
	
	load "stat.inc"
	rnd(x) = floor(x+0.5)
	r_xmin = -1
	r_sigma = 4.0
	
	# Binomial PDF using normal approximation
	n = 25; p = 0.15
	mu = n * p
	sigma = sqrt(n * p * (1.0 - p))
	xmin = floor(mu - r_sigma * sigma)
	xmin = xmin < r_xmin ? r_xmin : xmin
	xmax = ceil(mu + r_sigma * sigma)
	ymax = 1.1 * binom(floor((n+1)*p), n, p) #mode of binomial PDF used
	set key box
	unset zeroaxis
	set xrange [xmin - 1 : xmax + 1]
	set yrange [0 : ymax]
	set xlabel "k, x ->"
	set ylabel "probability density ->"
	set ytics 0, ymax / 10.0, ymax
	set format x "%2.0f"
	set format y "%3.2f"
	set sample 200
	set title "binomial PDF using normal approximation"
	set arrow from mu, 0 to mu, normal(mu, mu, sigma) nohead
	set arrow from mu, normal(mu + sigma, mu, sigma) \
	          to mu + sigma, normal(mu + sigma, mu, sigma) nohead
	set label "mu" at mu + 0.5, ymax / 10
	set label "sigma" at mu + 0.5 + sigma, normal(mu + sigma, mu, sigma)
	plot binom(rnd(x), n, p) with histeps, normal(x, mu, sigma)
	pause -1 "Hit return to continue"
	unset arrow
	unset label
	
	# Binomial PDF using poisson approximation
	n = 50; p = 0.1
	mu = n * p
	sigma = sqrt(mu)
	xmin = floor(mu - r_sigma * sigma)
	xmin = xmin < r_xmin ? r_xmin : xmin
	xmax = ceil(mu + r_sigma * sigma)
	ymax = 1.1 * binom(floor((n+1)*p), n, p) #mode of binomial PDF used
	set key box
	unset zeroaxis
	set xrange [xmin - 1 : xmax + 1]
	set yrange [0 : ymax]
	set xlabel "k ->"
	set ylabel "probability density ->"
	set ytics 0, ymax / 10.0, ymax
	set format x "%2.0f"
	set format y "%3.2f"
	set sample (xmax - xmin + 3)
	set title "binomial PDF using poisson approximation"
	set arrow from mu, 0 to mu, normal(mu, mu, sigma) nohead
	set arrow from mu, normal(mu + sigma, mu, sigma) \
	          to mu + sigma, normal(mu + sigma, mu, sigma) nohead
	set label "mu" at mu + 0.5, ymax / 10
	set label "sigma" at mu + 0.5 + sigma, normal(mu + sigma, mu, sigma)
	plot binom(x, n, p) with histeps, poisson(x, mu) with histeps
	pause -1 "Hit return to continue"
	unset arrow
	unset label
	
	# Geometric PDF using gamma approximation
	p = 0.3
	mu = (1.0 - p) / p
	sigma = sqrt(mu / p)
	lambda = p
	rho = 1.0 - p
	xmin = floor(mu - r_sigma * sigma)
	xmin = xmin < r_xmin ? r_xmin : xmin
	xmax = ceil(mu + r_sigma * sigma)
	ymax = 1.1 * p
	set key box
	unset zeroaxis
	set xrange [xmin - 1 : xmax + 1]
	set yrange [0 : ymax]
	set xlabel "k, x ->"
	set ylabel "probability density ->"
	set ytics 0, ymax / 10.0, ymax
	set format x "%2.0f"
	set format y "%3.2f"
	set sample 200
	set title "geometric PDF using gamma approximation"
	set arrow from mu, 0 to mu, gmm(mu, rho, lambda) nohead
	set arrow from mu, gmm(mu + sigma, rho, lambda) \
	          to mu + sigma, gmm(mu + sigma, rho, lambda) nohead
	set label "mu" at mu + 0.5, ymax / 10
	set label "sigma" at mu + 0.5 + sigma, gmm(mu + sigma, rho, lambda)
	plot geometric(rnd(x),p) with histeps, gmm(x, rho, lambda)
	pause -1 "Hit return to continue"
	unset arrow
	unset label
	
	# Geometric PDF using normal approximation
	p = 0.3
	mu = (1.0 - p) / p
	sigma = sqrt(mu / p)
	xmin = floor(mu - r_sigma * sigma)
	xmin = xmin < r_xmin ? r_xmin : xmin
	xmax = ceil(mu + r_sigma * sigma)
	ymax = 1.1 * p
	set key box
	unset zeroaxis
	set xrange [xmin - 1 : xmax + 1]
	set yrange [0 : ymax]
	set xlabel "k, x ->"
	set ylabel "probability density ->"
	set ytics 0, ymax / 10.0, ymax
	set format x "%2.0f"
	set format y "%3.2f"
	set sample 200
	set title "geometric PDF using normal approximation"
	set arrow from mu, 0 to mu, normal(mu, mu, sigma) nohead
	set arrow from mu, normal(mu + sigma, mu, sigma) \
	          to mu + sigma, normal(mu + sigma, mu, sigma) nohead
	set label "mu" at mu + 0.5, ymax / 10
	set label "sigma" at mu + 0.5 + sigma, normal(mu + sigma, mu, sigma)
	plot geometric(rnd(x),p) with histeps, normal(x, mu, sigma)
	pause -1 "Hit return to continue"
	unset arrow
	unset label
	
	# Hypergeometric PDF using binomial approximation
	nn = 75; mm = 25; n = 10
	p = real(mm) / nn
	mu = n * p
	sigma = sqrt(real(nn - n) / (nn - 1.0) * n * p * (1.0 - p))
	xmin = floor(mu - r_sigma * sigma)
	xmin = xmin < r_xmin ? r_xmin : xmin
	xmax = ceil(mu + r_sigma * sigma)
	ymax = 1.1 * hypgeo(floor(mu), nn, mm, n) #mode of binom PDF used
	set key box
	unset zeroaxis
	set xrange [xmin - 1 : xmax + 1]
	set yrange [0 : ymax]
	set xlabel "k ->"
	set ylabel "probability density ->"
	set ytics 0, ymax / 10.0, ymax
	set format x "%2.0f"
	set format y "%3.2f"
	set sample (xmax - xmin + 3)
	set title "hypergeometric PDF using binomial approximation"
	set arrow from mu, 0 to mu, binom(floor(mu), n, p) nohead
	set arrow from mu, binom(floor(mu + sigma), n, p) \
	          to mu + sigma, binom(floor(mu + sigma), n, p) nohead
	set label "mu" at mu + 0.5, ymax / 10
	set label "sigma" at mu + 0.5 + sigma, binom(floor(mu + sigma), n, p)
	plot hypgeo(x, nn, mm, n) with histeps, binom(x, n, p) with histeps
	pause -1 "Hit return to continue"
	unset arrow
	unset label
	
	# Hypergeometric PDF using normal approximation
	nn = 75; mm = 25; n = 10
	p = real(mm) / nn
	mu = n * p
	sigma = sqrt(real(nn - n) / (nn - 1.0) * n * p * (1.0 - p))
	xmin = floor(mu - r_sigma * sigma)
	xmin = xmin < r_xmin ? r_xmin : xmin
	xmax = ceil(mu + r_sigma * sigma)
	ymax = 1.1 * hypgeo(floor(mu), nn, mm, n) #mode of binom PDF used
	set key box
	unset zeroaxis
	set xrange [xmin - 1 : xmax + 1]
	set yrange [0 : ymax]
	set xlabel "k, x ->"
	set ylabel "probability density ->"
	set ytics 0, ymax / 10.0, ymax
	set format x "%2.0f"
	set format y "%3.2f"
	set sample 200
	set title "hypergeometric PDF using normal approximation"
	set arrow from mu, 0 to mu, normal(mu, mu, sigma) nohead
	set arrow from mu, normal(mu + sigma, mu, sigma) \
	          to mu + sigma, normal(mu + sigma, mu, sigma) nohead
	set label "mu" at mu + 0.5, ymax / 10
	set label "sigma" at mu + 0.5 + sigma, normal(mu + sigma, mu, sigma)
	plot hypgeo(rnd(x), nn, mm, n) with histeps, normal(x, mu, sigma)
	pause -1 "Hit return to continue"
	unset arrow
	unset label
	
	# Negative binomial PDF using gamma approximation
	r = 8; p = 0.6
	mu = r * (1.0 - p) / p
	sigma = sqrt(mu / p)
	lambda = p
	rho = r * (1.0 - p)
	xmin = floor(mu - r_sigma * sigma)
	xmin = xmin < r_xmin ? r_xmin : xmin
	xmax = ceil(mu + r_sigma * sigma)
	ymax = 1.1 * gmm((rho - 1) / lambda, rho, lambda) #mode of gamma PDF used
	set key box
	unset zeroaxis
	set xrange [xmin - 1 : xmax + 1]
	set yrange [0 : ymax]
	set xlabel "k, x ->"
	set ylabel "probability density ->"
	set ytics 0, ymax / 10.0, ymax
	set format x "%2.0f"
	set format y "%3.2f"
	set sample 200
	set title "negative binomial PDF using gamma approximation"
	set arrow from mu, 0 to mu, gmm(mu, rho, lambda) nohead
	set arrow from mu, gmm(mu + sigma, rho, lambda) \
	          to mu + sigma, gmm(mu + sigma, rho, lambda) nohead
	set label "mu" at mu + 0.5, ymax / 10
	set label "sigma" at mu + 0.5 + sigma, gmm(mu + sigma, rho, lambda)
	plot negbin(rnd(x), r, p) with histeps, gmm(x, rho, lambda)
	pause -1 "Hit return to continue"
	unset arrow
	unset label
	
	# Negative binomial PDF using normal approximation
	r = 8; p = 0.4
	mu = r * (1.0 - p) / p
	sigma = sqrt(mu / p)
	xmin = floor(mu - r_sigma * sigma)
	xmin = xmin < r_xmin ? r_xmin : xmin
	xmax = ceil(mu + r_sigma * sigma)
	ymax = 1.1 * negbin(floor((r-1)*(1-p)/p), r, p) #mode of gamma PDF used
	set key box
	unset zeroaxis
	set xrange [xmin - 1 : xmax + 1]
	set yrange [0 : ymax]
	set xlabel "k, x ->"
	set ylabel "probability density ->"
	set ytics 0, ymax / 10.0, ymax
	set format x "%2.0f"
	set format y "%3.2f"
	set sample 200
	set title "negative binomial PDF using normal approximation"
	set arrow from mu, 0 to mu, normal(mu, mu, sigma) nohead
	set arrow from mu, normal(mu + sigma, mu, sigma) \
	          to mu + sigma, normal(mu + sigma, mu, sigma) nohead
	set label "mu" at mu + 0.5, ymax / 10
	set label "sigma" at mu + 0.5 + sigma, normal(mu + sigma, mu, sigma)
	plot negbin(rnd(x), r, p) with histeps, normal(x, mu, sigma)
	pause -1 "Hit return to continue"
	unset arrow
	unset label
	
	# Normal PDF using logistic approximation
	mu = 1.0; sigma = 1.5
	a = mu
	lambda = pi / (sqrt(3.0) * sigma)
	xmin = mu - r_sigma * sigma
	xmax = mu + r_sigma * sigma
	ymax = 1.1 * logistic(mu, a, lambda) #mode of logistic PDF used
	set key box
	unset zeroaxis
	set xrange [xmin: xmax]
	set yrange [0 : ymax]
	set xlabel "x ->"
	set ylabel "probability density ->"
	set ytics 0, ymax / 10.0, ymax
	set format x "%.1f"
	set format y "%.2f"
	set sample 200
	set title "normal PDF using logistic approximation"
	set arrow from mu,0 to mu, normal(mu, mu, sigma) nohead
	set arrow from mu, normal(mu + sigma, mu, sigma) \
	          to mu + sigma, normal(mu + sigma, mu, sigma) nohead
	set label "mu" at mu + 0.5, ymax / 10
	set label "sigma" at mu + 0.5 + sigma, normal(mu + sigma, mu, sigma)
	plot logistic(x, a, lambda), normal(x, mu, sigma)
	pause -1 "Hit return to continue"
	unset arrow
	unset label
	
	# Poisson PDF using normal approximation
	mu = 5.0
	sigma = sqrt(mu)
	xmin = floor(mu - r_sigma * sigma)
	xmin = xmin < r_xmin ? r_xmin : xmin
	xmax = ceil(mu + r_sigma * sigma)
	ymax = 1.1 * poisson(mu, mu) #mode of poisson PDF used
	set key box
	unset zeroaxis
	set xrange [xmin - 1 : xmax + 1]
	set yrange [0 : ymax]
	set xlabel "k, x ->"
	set ylabel "probability density ->"
	set ytics 0, ymax / 10.0, ymax
	set format x "%2.0f"
	set format y "%3.2f"
	set sample 200
	set title "poisson PDF using normal approximation"
	set arrow from mu, 0 to mu, normal(mu, mu, sigma) nohead
	set arrow from mu, normal(mu + sigma, mu, sigma) \
	          to mu + sigma, normal(mu + sigma, mu, sigma) nohead
	set label "mu" at mu + 0.5, ymax / 10
	set label "sigma" at mu + 0.5 + sigma, normal(mu + sigma, mu, sigma)
	plot poisson(rnd(x), mu) with histeps, normal(x, mu, sigma)
	pause -1 "Hit return to continue"
	reset

snippet probably_tux.dem
	#
	# Distortion of an image by plotting against probability scaled axes.
	# Each pixel is re-scaled based on distance from the center, where
	# the scale factor treats that distance as a Z score.
	#
	#set title "Probability axes:  Scale image pixels by distance from center treated as a Z-score"
	set label 1 "Tux (probably)" font "Times:Bold" 
	set label 1 at graph 0.5, 0.4 center front 
	
	set xrange [-6:134]
	set yrange [-6:134]
	unset key
	
	if (!exists("sigma")) sigma = 20.
	center = 64.	# because the image is 128x128
	
	if (!exists("ticinterval")) ticinterval = 1
	
	f(x) = norm((x-center)/sigma)
	g(x) = sigma*invnorm(x)+center
	tic(x) = center + sigma * x 
	
	set xtics ("" 64)
	do for [i=-3:3:ticinterval] {
	    set xtics add (sprintf("%dσ",i) tic(real(i)), "" tic(real(i)+.5) 1)
	}
	set ytics ("" 64)
	do for [i=-3:3:ticinterval] {
	    set ytics add (sprintf("%dσ",i) tic(real(i)), "" tic(real(i)+.5) 1)
	}
	set tics out nomirror
	
	set nonlinear x via f(x) inverse g(x)
	set nonlinear y via f(y) inverse g(y)
	
	plot 'blutux.rgb' binary array=(128,128) flipy format='%uchar' with rgbimage pixels
	

snippet prob.dem
	#
	# $Id: prob.dem,v 1.10 2009/05/31 05:54:41 sfeam Exp $
	#
	# Demo Statistical Functions version 2.3
	#
	# Copyright (c) 1991, 1992 Jos van der Woude, jvdwoude@hut.nl
	
	# History:
	#    -- --- 1992 Jos van der Woude:  1st version
	#    06 Jun 2006 Dan Sebald:  Added some variety and plotting techniques for
	#                             better visual effect.  More tutorial in nature.
	
	print "                   Statistical Library Demo, version 2.3"
	print "\n          Copyright (c) 1991, 1992, Jos van de Woude, jvdwoude@hut.nl"
	print "\n\n\n\n\n\n\n"
	print "NOTE: contains 54 plots and consequently takes a lot of time to run"
	print "                      Press Ctrl-C to exit right now"
	pause   -1 "                      Press Return to start demo ..."
	
	load "stat.inc"
	
	eps = 1.0e-10  # Supposed to be float resolution (nice if were defined internally)
	
	## Gamma function
	xmin = -5.5
	xmax = 5
	ymin = -10
	ymax = 10
	unset key
	set xzeroaxis
	gsampfunc(t,n) = t<0?0.5*1/(-t+1.0)**n:1.0-0.5*1/(t+1.0)**n
	set parametric
	set trange [-1:1]
	set sample 200
	set xrange [xmin : xmax]
	set yrange [ymin : ymax]
	set xlabel "x"
	set ylabel "gamma(x)"
	set arrow 1 from  0,ymin to  0,ymax nohead lt 0
	set arrow 2 from -1,ymin to -1,ymax nohead lt 0
	set arrow 3 from -2,ymin to -2,ymax nohead lt 0
	set arrow 4 from -3,ymin to -3,ymax nohead lt 0
	set arrow 5 from -4,ymin to -4,ymax nohead lt 0
	set arrow 6 from -5,ymin to -5,ymax nohead lt 0
	set title "gamma function, very useful function for probability"
	plot gsampfunc(5*t,5)-6, gamma(gsampfunc(5*t,5)-6) lt 1, \
	     gsampfunc(5*t,5)-5, gamma(gsampfunc(5*t,5)-5) lt 1, \
	     gsampfunc(5*t,4)-4, gamma(gsampfunc(5*t,4)-4) lt 1, \
	     gsampfunc(5*t,3)-3, gamma(gsampfunc(5*t,3)-3) lt 1, \
	     gsampfunc(5*t,2)-2, gamma(gsampfunc(5*t,2)-2) lt 1, \
	     gsampfunc(5*t,1)-1, gamma(gsampfunc(5*t,1)-1) lt 1, \
	     5*gsampfunc(5*t,2), gamma(5*gsampfunc(5*t,2)) lt 1
	pause -1 "Hit return to continue"
	ymin = ymin/2
	ymax = ymax/2
	set yrange [ymin:ymax]
	set ylabel "lgamma(x)"
	set arrow 1 from  0,ymin to  0,ymax nohead lt 0
	set arrow 2 from -1,ymin to -1,ymax nohead lt 0
	set arrow 3 from -2,ymin to -2,ymax nohead lt 0
	set arrow 4 from -3,ymin to -3,ymax nohead lt 0
	set arrow 5 from -4,ymin to -4,ymax nohead lt 0
	set arrow 6 from -5,ymin to -5,ymax nohead lt 0
	set title "log gamma function, similarly very useful function"
	plot gsampfunc(5*t,5)-6, lgamma(gsampfunc(5*t,5)-6) lt 1, \
	     gsampfunc(5*t,5)-5, lgamma(gsampfunc(5*t,5)-5) lt 1, \
	     gsampfunc(5*t,4)-4, lgamma(gsampfunc(5*t,4)-4) lt 1, \
	     gsampfunc(5*t,3)-3, lgamma(gsampfunc(5*t,3)-3) lt 1, \
	     gsampfunc(5*t,3)-2, lgamma(gsampfunc(5*t,3)-2) lt 1, \
	     gsampfunc(5*t,3)-1, lgamma(gsampfunc(5*t,3)-1) lt 1, \
	     5*gsampfunc(5*t,3), lgamma(5*gsampfunc(5*t,3)) lt 1
	pause -1 "Hit return to continue"
	reset
	
	# Arcsinus PDF and CDF
	r = 2.0
	mu = 0.0
	sigma = r / sqrt2
	xmin = -(r+1)
	xmax = r+1
	unset key
	set zeroaxis
	set xrange [xmin : xmax]
	set yrange [-0.2 : 1.2]
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.1f"
	set format y "%.1f"
	set sample 50*xmax+1
	set title "arcsin PDF with r = 2.0"
	plot arcsin(x, r)
	pause -1 "Hit return to continue"
	set title "arcsin CDF with r = 2.0"
	set yrange [-0.2 : 1.2]
	plot carcsin(x, r)
	pause -1 "Hit return to continue"
	
	# Beta PDF and CDF
	p = 5.0; q = 3.0
	mu = p / (p + q)
	sigma = sqrt(p**q) / ((p + q ) * sqrt(p + q + 1.0))
	xmin = 0.0
	xmax = 1.0
	#Mode of beta PDF used
	ymax = (p < 1.0 || q < 1.0) ? 2.0 : 1.4 * beta((p - 1.0)/(p + q - 2.0), p, q)
	set key right box
	set zeroaxis
	set xrange [xmin : xmax]
	set yrange [0 : ymax]
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.1f"
	set format y "%.1f"
	set sample 200
	set title "beta PDF"
	plot beta(x, 0.5, 0.7) title "p = 0.5, q = 0.7", \
	     beta(x, 5.0, 3.0) title "p = 5.0, q = 3.0", \
	     beta(x, 0.5, 2.5) title "p = 0.5, q = 2.5"
	pause -1 "Hit return to continue"
	set yrange [0:1.1]
	set title "incomplete beta CDF"
	set key left box
	plot cbeta(x, 0.5, 0.7) title "p = 0.5, q = 0.7", \
	     cbeta(x, 5.0, 3.0) title "p = 5.0, q = 3.0", \
	     cbeta(x, 0.5, 2.5) title "p = 0.5, q = 2.5"
	pause -1 "Hit return to continue"
	
	# Binomial PDF and CDF
	n = 25; p = 0.15
	mu = n * p
	sigma = sqrt(n * p * (1.0 - p))
	xmin = int(mu - 4.0 * sigma)
	xmin = xmin < -2 ? -2 : xmin
	xmax = int(mu + 4.0 * sigma)
	xmax = xmax < n+2 ? n+2 : xmax
	ymax = 1.1 * binom(int((n+1)*p), n, p) #Mode of binomial PDF used
	unset key
	unset zeroaxis
	set xrange [xmin : xmax]
	set yrange [0 : ymax]
	set xlabel "k ->"
	set ylabel "probability density ->"
	set ytics 0, ymax / 10, ymax
	set format x "%2.0f"
	set format y "%3.2f"
	set sample (xmax - xmin) + 1
	set title "binomial PDF with n = 25, p = 0.15"
	plot binom(x, n, p) with impulses
	pause -1 "Hit return to continue"
	set ytics autofreq
	set xzeroaxis
	set title "binomial CDF with n = 25, p = 0.15"
	set yrange [-0.1 : 1.1]
	set ytics 0, 0.1, 1.0
	plot cbinom(x, n, p) with steps
	pause -1 "Hit return to continue"
	
	# Cauchy PDF and CDF
	a = 0.0; b = 2.0
	#cauchy PDF has no moments
	xmin = a - 5.0 * b
	xmax = a + 5.0 * b
	ymax = 1.1 * cauchy(a, a, b) #Mode of cauchy PDF used
	set key left box
	set zeroaxis
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.1f"
	set format y "%.2f"
	set sample 100
	set title "cauchy PDF"
	a=0
	b=2
	plot [xmin:xmax] [0:ymax] cauchy(x, 0, 2) title "a = 0, b = 2", \
	                          cauchy(x, 0, 4) title "a = 0, b = 4"
	pause -1 "Hit return to continue"
	set title "cauchy CDF"
	plot [xmin:xmax] [0:1.0] ccauchy(x, 0, 2) title "a = 0, b = 2", \
	                         ccauchy(x, 0, 4) title "a = 0, b = 4"
	pause -1 "Hit return to continue"
	
	# Chi-square PDF and CDF
	k = 4.0
	mu = k
	sigma = sqrt(2.0 * k)
	xmin = mu - 4.0 * sigma
	xmin = xmin < 0 ? 0 : xmin
	xmax = int(mu + 4.0 * sigma)
	k = 2.0
	ymax = (k > 2.0 ? 1.1*chisq(k - 2.0, k) : 0.5) #Mode of chi PDF used
	set key right box
	set zeroaxis
	set xrange [xmin+eps : xmax] #Discontinuity at zero for k < 2
	set yrange [0:ymax]
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.1f"
	set format y "%.2f"
	set sample 100
	set title "chi-square PDF"
	set key right box
	set samples 15*20+1
	keystr(k) = sprintf("k = %d", k)
	plot k = 1, x==0?1/0:chisq(x, k) title keystr(k), \
	     k = 2, x==0?1/0:chisq(x, k) title keystr(k), \
	     k = 3, chisq(x, k) title keystr(k), \
	     k = 4, chisq(x, k) title keystr(k), \
	     k = 5, chisq(x, k) title keystr(k), \
	     k = 6, chisq(x, k) title keystr(k), \
	     k = 7, chisq(x, k) title keystr(k), \
	     k = 8, chisq(x, k) title keystr(k)
	pause -1 "Hit return to continue"
	set yrange [0:1.1]
	set key bottom right box
	set title "chi-square CDF"
	plot k = 1, cchisq(x, k) title keystr(k), \
	     k = 2, cchisq(x, k) title keystr(k), \
	     k = 3, cchisq(x, k) title keystr(k), \
	     k = 4, cchisq(x, k) title keystr(k), \
	     k = 5, cchisq(x, k) title keystr(k), \
	     k = 6, cchisq(x, k) title keystr(k), \
	     k = 7, cchisq(x, k) title keystr(k), \
	     k = 8, cchisq(x, k) title keystr(k)
	pause -1 "Hit return to continue"
	
	# Erlang PDF and CDF
	lambda = 1.0; n = 2.0
	mu = n / lambda
	sigma = sqrt(n) / lambda
	xmax = int(mu + 5.0 * sigma)
	n = 1.0
	ymax = n < 2.0 ? 1.0 : 1.1 * erlang((n - 1.0) / lambda, n, lambda) #Mode of erlang PDF used
	set zeroaxis
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.1f"
	set format y "%.1f"
	set sample 100
	set title "erlang PDF"
	set key top right box
	l1 = 1.0; l2 = 0.5
	set arrow 1 from 2,0.8 to 0.33,erlang(0.33,1,l1)
	set arrow 2 from 2,0.8 to 0.33,erlang(0.33,1,l2)
	set label 1 "n = 1, exponential r.v." at 2.1,0.8 left
	keystr(n,lambda) = sprintf("lambda = %0.1f, n = %d", lambda, n)
	plot [0:xmax] [0:ymax] n = 1, lambda = l1, erlang(x, n, lambda) title keystr(n,lambda), \
	                       n = 1, lambda = l2, erlang(x, n, lambda) title keystr(n,lambda), \
	                       n = 2, lambda = l1, erlang(x, n, lambda) title keystr(n,lambda), \
	                       n = 2, lambda = l2, erlang(x, n, lambda) title keystr(n,lambda)
	pause -1 "Hit return to continue"
	unset label 1
	unset arrow 1; unset arrow 2
	set title "erlang CDF"
	set key bottom right box
	plot [0:xmax] [0:1.1] n = 1, lambda = l1, cerlang(x, n, lambda) title keystr(n,lambda), \
	                      n = 1, lambda = l2, cerlang(x, n, lambda) title keystr(n,lambda), \
	                      n = 2, lambda = l1, cerlang(x, n, lambda) title keystr(n,lambda), \
	                      n = 2, lambda = l2, cerlang(x, n, lambda) title keystr(n,lambda)
	pause -1 "Hit return to continue"
	
	# Thanks to mrb2j@kelvin.seas.Virginia.EDU for telling us about this.
	# Extreme (Gumbel extreme value) PDF and CDF
	alpha = 1.0; u = 0.0
	mu = u + (0.577215665/alpha)   # Euler's constant
	sigma = pi/(sqrt(6.0)*alpha)
	xmin = mu - 6.0 * sigma
	xmax = mu + 6.0 * sigma
	ymax = 1.1 * extreme(u, u, alpha) #Mode of extreme PDF used
	ymax = int(10*ymax)/10.0
	set zeroaxis
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.1f"
	set format y "%.2f"
	set sample 100
	set title "extreme PDF"
	set key top left box
	plot [xmin:xmax] [0:ymax] extreme(x, 1.0, 0.5) title "alpha = 0.5, u = 1.0", \
	                          extreme(x, 0.0, 1.0) title "alpha = 1.0, u = 0.0"
	pause -1 "Hit return to continue"
	set title "extreme CDF"
	plot [xmin:xmax] [0:1.1] cextreme(x, 1.0, 0.5) title "alpha = 0.5, u = 1.0", \
	                         cextreme(x, 0.0, 1.0) title "alpha = 1.0, u = 0.0"
	pause -1 "Hit return to continue"
	
	# F PDF and CDF
	df1 = 5.0; df2 = 9.0
	mu = df2 < 2.0 ? 1.0 : df2 / (df2 - 2.0)
	sigma = df2 < 4.0 ? 1.0 : mu * sqrt(2.0 * (df1 + df2 - 2.0) / (df1 * (df2 - 4.0)))
	xmin = mu - 3.0 * sigma
	xmin = xmin < 0 ? 0 : xmin
	xmax = int(mu + 3.0 * sigma)
	#Mode of F PDF used
	ymax = df1 < 3.0 ? 1.0 : 1.1 * f((df1 / 2.0 - 1.0) / (df1 / 2.0 + df1 / df2), df1, df2)
	set zeroaxis
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.1f"
	set format y "%.2f"
	set sample 100
	set title "F PDF"
	set key right box
	plot [xmin:xmax] [0:ymax] f(x, 5.0, 9.0) title "df1 = 5, df2 = 9", \
	                          f(x, 7.0, 6.0) title "df1 = 7, df2 = 6"
	pause -1 "Hit return to continue"
	set title "F CDF"
	set key left box
	plot [xmin:xmax] [0:1.1] cf(x, 5.0, 9.0) title "df1 = 5, df2 = 9", \
	                         cf(x, 7.0, 6.0) title "df1 = 7, df2 = 6"
	pause -1 "Hit return to continue"
	
	# Gamma PDF and incomplete gamma CDF
	rho = 1.0; lambda = 1.3
	mu = rho / lambda
	sigma = sqrt(rho) / lambda
	xmin = mu - 4.0 * sigma
	xmin = xmin < 0 ? 0 : xmin
	xmax = mu + 4.0 * sigma
	ymax = rho < 1.0 ? 2.0 : 1.1 * gmm((rho - 1.0) / lambda, rho, lambda) #Mode of gamma pdf used
	set zeroaxis
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.1f"
	set format y "%.1f"
	set sample 100
	set title "gamma PDF"
	set key right
	r1 = 0.5; r2 = 1.0; r3 = 1.0; r4 = 1.3; r5 = 2.0; r6 = 4.0; r7 = 6.0
	l1 = 1.0; l2 = 1.0; l3 = 1.3; l4 = 1.3; l5 = 2.0; l6 = 2.0; l7 = 2.0
	set arrow 1 from 1,1.3 to 0.15,gmm(0.15,r1,l1)
	set label 1 "rho < 1, tends to infinity" at 1.1,1.3 left
	set arrow 2 from 1.15,1.1 to 0.35,gmm(0.35,r3,l3)
	set label 2 "rho = 1, finite, nonzero limit" at 1.25,1.1 left
	set arrow 3 from 1.5,0.9 to 1.0,gmm(1.0,r5,l5)
	set label 3 "rho > 1, tends to zero" at 1.6,0.9 left
	keystr(rho,lambda) = sprintf("rho = %0.1f, lambda = %0.1f", rho, lambda)
	plot [0:5] [0:1.5] rho = r1, lambda = l1, gmm(x, rho, lambda) title keystr(rho,lambda), \
	                   rho = r2, lambda = l2, gmm(x, rho, lambda) title keystr(rho,lambda), \
	                   rho = r3, lambda = l3, gmm(x, rho, lambda) title keystr(rho,lambda), \
	                   rho = r4, lambda = l4, gmm(x, rho, lambda) title keystr(rho,lambda), \
	                   rho = r5, lambda = l5, gmm(x, rho, lambda) title keystr(rho,lambda), \
	                   rho = r6, lambda = l6, gmm(x, rho, lambda) title keystr(rho,lambda), \
	                   rho = r7, lambda = l7, gmm(x, rho, lambda) title keystr(rho,lambda)
	pause -1 "Hit return to continue"
	unset label 1; unset label 2; unset label 3
	unset arrow 1; unset arrow 2; unset arrow 3
	set title "incomplete gamma CDF"
	set key right bottom
	plot [0:5] [0:1.1] rho = r1, lambda = l1, cgmm(x, rho, lambda) title keystr(rho,lambda), \
	                   rho = r2, lambda = l2, cgmm(x, rho, lambda) title keystr(rho,lambda), \
	                   rho = r3, lambda = l3, cgmm(x, rho, lambda) title keystr(rho,lambda), \
	                   rho = r4, lambda = l4, cgmm(x, rho, lambda) title keystr(rho,lambda), \
	                   rho = r5, lambda = l5, cgmm(x, rho, lambda) title keystr(rho,lambda), \
	                   rho = r6, lambda = l6, cgmm(x, rho, lambda) title keystr(rho,lambda), \
	                   rho = r7, lambda = l7, cgmm(x, rho, lambda) title keystr(rho,lambda)
	pause -1 "Hit return to continue"
	
	# Geometric PDF and CDF
	p = 0.4
	mu = (1.0 - p) / p
	sigma = sqrt(mu / p)
	xmin = int(mu - 4.0 * sigma)
	xmin = xmin < -1 ? -1 : xmin
	xmin = -1
	xmax = int(mu + 4.0 * sigma)
	ymax = 1.1 * geometric(0, p) #mode of geometric PDF used
	unset key
	unset zeroaxis
	set xrange [xmin : xmax]
	set yrange [0 : ymax]
	set xlabel "k ->"
	set ylabel "probability density ->"
	set ytics 0, ymax / 10, ymax
	set format x "%2.0f"
	set format y "%3.2f"
	set sample (xmax - xmin) + 1
	set title "geometric PDF with p = 0.4"
	plot geometric(x, p) with impulses
	pause -1 "Hit return to continue"
	set title "geometric CDF with p = 0.4"
	set yrange [0 : 1.1]
	set ytics 0, 0.1, 1.0
	plot cgeometric(x, p) with steps
	pause -1 "Hit return to continue"
	
	# Half normal PDF and CDF
	mu = sqrt2invpi
	sigma = 1.0
	s = sigma*sqrt(1.0 - 2.0/pi)
	xmin = -0.2
	xmax = mu + 4.0 * s
	ymax = 1.1 * halfnormal(0, sigma) #Mode of half normal PDF used
	unset key
	set zeroaxis
	set xrange [xmin: xmax]
	set yrange [-0.1: ymax]
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.1f"
	set format y "%.1f"
	set sample 100
	set parametric
	set trange [xmin:xmax]
	set title "half normal PDF, sigma = 1.0"
	set arrow 1 from 0.5,0.13 to 0.0,0.4
	set label 1 "Discontinuity achieved by plotting\ntwice with limited parametric ranges" at 0.2,0.1 left
	plot t<0?t:-eps, halfnormal(t<0?t:-eps, sigma) ls 1, t<0?0.0:t, halfnormal(t<0?0.0:t, sigma) ls 1
	pause -1 "Hit return to continue"
	set title "half normal CDF, sigma = 1.0"
	set yrange [-0.1:1.1]
	set arrow 1 from 0.45,0.1 to 0.05,0.01
	set label 1 "Cusp achieved by plotting twice\nwith limited parametric ranges" at 0.5,0.1 left
	plot t<0?t:-eps, chalfnormal(t<0?t:-eps, sigma) ls 1, t<0?0.0:t, chalfnormal(t<0?0.0:t, sigma) ls 1
	pause -1 "Hit return to continue"
	unset label 1
	unset arrow 1
	unset parametric
	
	# Hypergeometric PDF and CPF
	N = 75; C = 25; d = 10
	p = real(C) / N
	mu = d * p
	sigma = sqrt(real(N - d) / (N - 1.0) * d * p * (1.0 - p))
	xmin = int(mu - 4.0 * sigma)
	xmin = xmin < -1 ? -1 : xmin
	xmax = int(mu + 4.0 * sigma)
	xmax = xmax < d+1 ? d+1 : xmax
	ymax = 1.1 * hypgeo(int(mu),N,C,d) # approximate mode of hypergeometric PDF used
	unset key
	unset zeroaxis
	set xrange [xmin : xmax]
	set yrange [0 : ymax]
	set xlabel "k ->"
	set ylabel "probability density ->"
	set ytics 0, ymax / 10, ymax
	set format x "%2.0f"
	set format y "%3.2f"
	set sample (xmax - xmin) + 1
	set title "hypergeometric PDF with N = 75, C = 25, d = 10"
	plot hypgeo(x,N,C,d) with impulses
	pause -1 "Hit return to continue"
	set yrange [0 : 1.1]
	set ytics 0, 1.0 / 10.0, 1.1
	set title "hypergeometric CDF with N = 75, C = 25, d = 10"
	plot chypgeo(x,N,C,d) with steps
	pause -1 "Hit return to continue"
	
	# Laplace PDF
	mu = 0.0; b = 1.0
	sigma = sqrt(2.0) * b
	xmin = mu - 4.0 * sigma
	xmax = mu + 4.0 * sigma
	ymax = 1.1 * laplace(mu, mu, b) #Mode of laplace PDF used
	unset key
	set zeroaxis
	set xrange [xmin: xmax]
	set yrange [0: ymax]
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.1f"
	set format y "%.2f"
	set sample 100+1
	set title "laplace (or double exponential) PDF with mu = 0, b = 1"
	set arrow 1 from -0.95,0.5 to -0.1,0.5
	set label 1 "Cusp achieved by selecting point\nas part of function samples" at -1.0,0.5 right
	plot laplace(x, mu, b)
	pause -1 "Hit return to continue"
	unset label 1
	unset arrow 1
	set title "laplace (or double exponential) CDF with mu = 0, b = 1"
	set yrange [0: 1.1]
	plot claplace(x, mu, b)
	pause -1 "Hit return to continue"
	
	# Logistic PDF and CDF
	a = 0.0; lambda = 2.0
	mu = a
	sigma = pi / (sqrt(3.0) * lambda)
	xmin = mu - 4.0 * sigma
	xmax = mu + 4.0 * sigma
	ymax = 1.1 * logistic(mu, a, lambda) #Mode of logistic PDF used
	unset key
	set zeroaxis
	set xrange [xmin: xmax]
	set yrange [0: ymax]
	unset key
	set zeroaxis
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.1f"
	set format y "%.1f"
	set sample 100
	set title "logistic PDF with a = 0, lambda = 2"
	plot logistic(x, a, lambda)
	pause -1 "Hit return to continue"
	set title "logistic CDF with a = 0, lambda = 2"
	set yrange [0: 1.1]
	plot clogistic(x, a, lambda)
	pause -1 "Hit return to continue"
	
	# Lognormal PDF and CDF
	mu = 1.0; sigma = 0.5
	m = exp(mu + 0.5 * sigma**2)
	s = sqrt(exp(2.0 * mu + sigma**2) * (2.0 * exp(sigma) - 1.0))
	xmin = m - 4.0 * s
	xmin = xmin < 0 ? 0 : xmin
	xmax = m + 4.0 * s
	ymax = 1.1 * lognormal(exp(mu - sigma**2), mu, sigma) #Mode of lognormal PDF used
	unset key
	set zeroaxis
	set xrange [xmin: xmax]
	set yrange [0: ymax]
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.2f"
	set format y "%.2f"
	set sample 100
	set title "lognormal PDF with mu = 1.0, sigma = 0.5"
	plot lognormal(x, mu, sigma)
	pause -1 "Hit return to continue"
	set title "lognormal CDF with mu = 1.0, sigma = 0.5"
	set yrange [0: 1.1]
	plot clognormal(x, mu, sigma)
	pause -1 "Hit return to continue"
	
	# Maxwell PDF
	a = 0.5
	mu = 2.0 / sqrt(pi) / a
	sigma = sqrt(3.0 - 8.0/pi) / a
	xmin = int(mu - 3.0 * sigma)
	xmin = xmin < 0 ? 0 : xmin
	xmax = int(mu + 3.0 * sigma)
	a = 1.5
	ymax = 1.1 * maxwell(1.0 / a, a) + 0.5 #Mode of maxwell PDF used
	ymax = int(ymax + 0.5)
	set zeroaxis
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.1f"
	set format y "%.1f"
	set sample 100
	set title "maxwell PDF"
	set key right top box
	plot [xmin:xmax] [0:ymax] maxwell(x, 1.5) title "a = 1.5", \
	                   maxwell(x, 1.0) title "a = 1.0", \
	                   maxwell(x, 0.5) title "a = 0.5"
	pause -1 "Hit return to continue"
	set title "maxwell CDF"
	set key right bottom box
	plot [xmin:xmax] [0:1.1] cmaxwell(x, 1.5) title "a = 1.5", \
	                         cmaxwell(x, 1.0) title "a = 1.0", \
	                         cmaxwell(x, 0.5) title "a = 0.5"
	pause -1 "Hit return to continue"
	
	# Negative binomial PDF and CDF
	r = 8; p = 0.4
	mu = r * (1.0 - p) / p
	sigma = sqrt(mu / p)
	xmin = int(mu - 4.0 * sigma)
	xmin = xmin < 0 ? 0 : xmin
	xmax = int(mu + 4.0 * sigma)
	ymax = 1.1 * negbin(int(mu - (1.0-p)/p), r, p) #mode of gamma PDF used
	unset key
	unset zeroaxis
	set xrange [xmin-1 : xmax]
	set yrange [0 : ymax]
	set xlabel "k ->"
	set ylabel "probability density ->"
	set ytics 0, ymax / 10, ymax
	set format x "%2.0f"
	set format y "%3.2f"
	set sample (xmax - xmin+1) + 1
	set title "negative binomial (or pascal or polya) PDF with r = 8, p = 0.4"
	plot negbin(x, r, p) with impulses
	pause -1 "Hit return to continue"
	set yrange [0 : 1.1]
	set ytics 0, 0.1, 1.0
	set title "negative binomial (or pascal or polya) CDF with r = 8, p = 0.4"
	plot cnegbin(x, r, p) with steps
	pause -1 "Hit return to continue"
	
	# Negative exponential PDF and CDF
	lambda = 2.0
	mu = 1.0 / lambda
	sigma = 1.0 / lambda
	xmax =  mu + 4.0 * sigma
	ymax = lambda #No mode
	unset key
	set zeroaxis
	set xrange [0: xmax]
	set yrange [0: ymax]
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.2f"
	set format y "%.1f"
	set sample 100
	set title "negative exponential (or exponential) PDF with lambda = 2.0"
	plot nexp(x, lambda)
	pause -1 "Hit return to continue"
	set title "negative exponential (or exponential) CDF with lambda = 2.0"
	set yrange [0: 1.1]
	plot cnexp(x, lambda)
	pause -1 "Hit return to continue"
	
	# Normal PDF and CDF
	mu = 0.0; sigma = 1.0
	xmin = mu - 4.0 * sigma
	xmax = mu + 4.0 * sigma
	mu = 2.0; sigma = 0.5
	ymax = 1.1 * normal(mu, mu, sigma) #Mode of normal PDF used
	set zeroaxis
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.1f"
	set format y "%.1f"
	set sample 100
	set title "normal (also called gauss or bell-curved) PDF"
	set key left top box
	plot [xmin:xmax] [0:ymax] normal(x, 0, 1.0) title "mu = 0, sigma = 1.0", \
	                          normal(x, 2, 0.5) title "mu = 2, sigma = 0.5", \
	                          normal(x, 1, 2.0) title "mu = 1, sigma = 2.0"
	pause -1 "Hit return to continue"
	set title "normal (also called gauss or bell-curved) CDF"
	set key left top box
	plot [xmin:xmax] [0:1.1] mu = 0, sigma = 1.0, cnormal(x, mu, sigma) title "mu = 0, sigma = 1.0", \
	                         mu = 2, sigma = 0.5, cnormal(x, mu, sigma) title "mu = 2, sigma = 0.5", \
	                         mu = 1, sigma = 2.0, cnormal(x, mu, sigma) title "mu = 1, sigma = 2.0"
	pause -1 "Hit return to continue"
	
	# Pareto PDF and CDF
	a = 1.0; b = 3.0
	mu = a * b / (b - 1.0)
	sigma = a * sqrt(b) / (sqrt(b - 2.0) * (b - 1.0))
	xmin = mu - 4.0 * sigma
	xmin = xmin < 0 ? 0 : xmin
	xmax = int(mu + 4.0 * sigma)
	ymax = 1.1 * pareto(a, a, b) #mode of pareto PDF used
	ymin = -0.1 * pareto(a, a, b)
	unset key
	set zeroaxis
	set xrange [xmin: xmax]
	set yrange [ymin: ymax]
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.1f"
	set format y "%.1f"
	set sample 200+1
	set title "pareto PDF with a = 1, b = 3"
	# Discontinuity at a
	set parametric
	set trange [0:1-eps]
	x1(t) = -1 + 2*t
	x2(t) =  1 + 3*t
	set arrow 1 from 1.75,0.8 to 1.0,0.8
	set arrow 2 from 1.0,0.0 to 1.0,3.0 nohead lt 0
	set label 1 "Discontinuity achieved by plotting twice\nwith affine mapped parametric ranges" at 1.8,0.8 left
	plot x1(t), pareto(x1(t), a, b) ls 1, x2(t), pareto(x2(t), a, b) ls 1
	pause -1 "Hit return to continue"
	unset arrow 2
	set title "pareto CDF with a = 1, b = 3"
	unset parametric
	set yrange [-0.1: 1.1]
	set arrow 1 from 1.45,0.1 to 1.05,0.01
	set label 1 "Cusp achieved by selecting point\nas part of function samples" at 1.5,0.1 left
	plot cpareto(x, a, b)
	pause -1 "Hit return to continue"
	unset label 1
	unset arrow 1
	
	# Poisson PDF and CDF
	mu = 4.0
	sigma = sqrt(mu)
	xmin = int(mu - 4.0 * sigma)
	xmin = xmin < -1 ? -1 : xmin
	xmax = int(mu + 4.0 * sigma)
	ymax = 1.1 * poisson(mu, mu) #mode of poisson PDF used
	unset key
	set zeroaxis
	set xrange [xmin : xmax]
	set yrange [0 : ymax]
	set xlabel "k ->"
	set ylabel "probability density ->"
	set ytics 0, ymax / 10, ymax
	set format x "%2.0f"
	set format y "%3.2f"
	set sample (xmax - xmin) + 1
	set title "poisson PDF with mu = 4.0"
	plot poisson(x, mu) with impulses
	pause -1 "Hit return to continue"
	set yrange [-0.1 : 1.1]
	set ytics -0.1, 0.1, 1.1
	set title "poisson CDF with mu = 4.0"
	plot cpoisson(x, mu) with steps
	pause -1 "Hit return to continue"
	
	# Rayleigh PDF and CDF
	lambda = 2.0
	mu = 0.5 * sqrt(pi / lambda)
	sigma = sqrt((1.0 - pi / 4.0) / lambda)
	xmax = mu + 4.0 * sigma
	ymax = 1.1 * rayleigh(1.0 / sqrt(2.0 * lambda), lambda) #Mode of rayleigh PDF used
	unset key
	set zeroaxis
	set xrange [0: xmax]
	set yrange [0: ymax]
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.2f"
	set format y "%.1f"
	set sample 100
	set title "rayleigh PDF with lambda = 2.0"
	plot rayleigh(x, lambda)
	pause -1 "Hit return to continue"
	set title "rayleigh CDF with lambda = 2.0"
	set yrange [0: 1.1]
	plot crayleigh(x, lambda)
	pause -1 "Hit return to continue"
	
	# Sine PDF and CDF
	a = 3.2; f = 2.6
	mu = a / 2.0
	sigma = sqrt(a * a / 3.0 * (1.0 - 3.0 / (2.0 * n * n * pi * pi)) - mu * mu)
	xmin = 0.0
	xmax = a - eps
	a = 2; f = 1.0
	ymax = 1.1 * 2.0 / a #Mode of sine PDF used
	set zeroaxis
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.2f"
	set format y "%.1f"
	set sample 250
	set title "sine PDF"
	set key bottom outside
	keystr(a, f) = sprintf("a = %0.1f, f = %0.1f", a, f)
	a1 = 2.0; a2 = 3.25; a3 = 2.75
	f1 = 1.0; f2 = 3.0; f3 = 2.6; f4 = 0.0
	plot [xmin:xmax] [0:ymax] a = a1, f = f1, sine(x, f, a) title keystr(a, f), \
	                          a = a1, f = f2, sine(x, f, a) title keystr(a, f), \
	                          a = a2, f = f3, sine(x, f, a) title keystr(a, f), \
	                          a = a3, f = f4, sine(x, f, a) title keystr(a, f) with steps
	pause -1 "Hit return to continue"
	set title "sine CDF"
	set key top left
	plot [xmin:xmax] [0:1.1] a = a1, f = f1, csine(x, f, a) title keystr(a, f), \
	                         a = a1, f = f2, csine(x, f, a) title keystr(a, f), \
	                         a = a2, f = f3, csine(x, f, a) title keystr(a, f), \
	                         a = a3, f = f4, csine(x, f, a) title keystr(a, f) with steps
	pause -1 "Hit return to continue"
	
	# t PDF and CDF
	nu = 20
	mu = 0
	sigma = nu > 2 ? sqrt(nu / (nu - 2.0)) : 1.0
	xmin = mu - 4.0 * sigma
	xmax = mu + 4.0 * sigma
	ymax = 1.1 * t(mu, nu) #Mode of t PDF used
	set key inside center left title "degrees of freedom"
	set zeroaxis
	set xrange [xmin: xmax]
	set yrange [0: ymax]
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.1f"
	set format y "%.2f"
	set sample 100
	set title "t PDF (and Gaussian limit)"
	ks(nu) = sprintf("nu = %d", nu)
	plot t(x, 1) ti ks(1), t(x, 2) ti ks(2), t(x, 4) ti ks(4), t(x, 10) ti ks(10), \
	     t(x, 20) ti ks(20), normal(x, 0, 1) ti "normal"
	pause -1 "Hit return to continue"
	set title "t CDF (and Gaussian limit)"
	set yrange [0: 1.1]
	plot ct(x, 1) ti ks(1), ct(x, 2) ti ks(2), ct(x, 4) ti ks(4), ct(x, 10) ti ks(10), \
	     ct(x, 20) ti ks(20), cnormal(x, 0, 1) ti "normal"
	pause -1 "Hit return to continue"
	
	# Thanks to efrank@upenn5.hep.upenn.edu for telling us about this
	# triangular PDF and CDF
	m = 3.0
	g = 2.0
	mu = m
	sigma = g/sqrt(6.0)
	xmin = m - 1.1*g
	xmax = m + 1.1*g
	ymax = 1.1 * triangular(m, m, g) #Mode of triangular PDF used
	ymin = -ymax/11.0;
	unset key
	set zeroaxis
	set xrange [xmin: xmax]
	set yrange [ymin: ymax]
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.1f"
	set format y "%.2f"
	set sample 50*1.1*g+1
	set title "triangular PDF with m = 3.0, g = 2.0"
	plot triangular(x, m, g)
	pause -1 "Hit return to continue"
	set title "triangular CDF with m = 3.0, g = 2.0"
	set yrange [-0.1: 1.1]
	plot ctriangular(x, m, g)
	pause -1 "Hit return to continue"
	
	# Uniform PDF and CDF
	a = -2.0; b= 2.0
	mu = (a + b) / 2.0
	sigma = (b - a) / sqrt(12.0)
	xmin = a - 0.1*(b - a)
	xmax = b + 0.1*(b - a)
	ymax = 1.1 * uniform(mu, a, b) #No mode
	ymin = -0.1 * uniform(mu, a, b)
	unset key
	set zeroaxis
	set xrange [xmin: xmax]
	set yrange [ymin: ymax]
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%.2f"
	set format y "%.2f"
	set sample 120+1
	set title "uniform PDF with a = -2.0, b = 2.0"
	plot uniform(x, a, b) with steps
	pause -1 "Hit return to continue"
	set title "uniform CDF with a = -2.0, b = 2.0"
	set yrange [-0.1 : 1.1]
	plot cuniform(x, a, b)
	pause -1 "Hit return to continue"
	
	# Weibull PDF and CDF
	lambda = 1.0/5; a = 1.0
	mu = 1.0 / lambda * gamma(1.0 / a) / a
	sigma = sqrt(lambda**(-2.0) * (2.0 * gamma(2.0 / a) / a - (gamma(1.0 / a) / a)**2))
	xmin = mu - 4.0 * sigma
	xmin = xmin < 0 ? 0 : xmin
	#Mode of weibull PDF used
	ymax = 1.8 * (a >= 1.0 ? weibull(((a - 1.0) / a)**(1.0 / a) / lambda, a, lambda) : 2.0)
	lambda = 1.0/15; a = 10.0
	mu = 1.0 / lambda * gamma(1.0 / a) / a
	sigma = sqrt(lambda**(-2.0) * (2.0 * gamma(2.0 / a) / a - (gamma(1.0 / a) / a)**2))
	xmax = int(mu + 4.0 * sigma)
	set key on title "" inside top right
	set zeroaxis
	set grid
	set xrange [xmin : xmax]
	set xlabel "x ->"
	set ylabel "probability density ->"
	set xtics autofreq
	set ytics autofreq
	set format x "%g"
	set format y "%g"
	set sample 100
	set title "weibull PDF"
	ks(a,lambda) = sprintf("lambda = 1/%g, a = %0.1f", 1.0/lambda, a)
	a1 = 0.5; a2 = 1.0; a3 = 2.0; a4 = 10.0
	lambda1 = 1.0/5; lambda2 = 1.0/15
	set arrow 1 from 3.8,0.27 to 0.5,weibull(0.5,a1,lambda1)
	set label 1 "a < 1, rate descreasing over time" at 4,0.27 left
	set arrow 2 from 8,0.19 to 6.4,weibull(6.4,a3,lambda1)
	set arrow 3 from 10.5,0.19 to 13,weibull(13,a4,lambda2)
	set label 2 "a > 1, rate increasing over time" at 9,0.2 center
	plot [] [0:ymax] lambda = lambda1, a = a1, weibull(x, a, lambda) ti ks(a, lambda), \
	                 lambda = lambda1, a = a2, weibull(x, a, lambda) ti ks(a, lambda), \
	                 lambda = lambda1, a = a3, weibull(x, a, lambda) ti ks(a, lambda), \
	                 lambda = lambda2, a = a4, weibull(x, a, lambda) ti ks(a, lambda)
	pause -1 "Hit return to continue"
	unset label 1; unset label 2
	unset arrow 1; unset arrow 2; unset arrow 3
	set key at 9,0.4 center
	set title "weibull CDF"
	plot [] [0:1.1] lambda = lambda1, a = a1, cweibull(x, a, lambda) ti ks(a, lambda), \
	                lambda = lambda1, a = a2, cweibull(x, a, lambda) ti ks(a, lambda), \
	                lambda = lambda1, a = a3, cweibull(x, a, lambda) ti ks(a, lambda), \
	                lambda = lambda2, a = a4, cweibull(x, a, lambda) ti ks(a, lambda)
	pause -1 "Hit return to continue"
	reset

snippet rainbow.dem
	#
	# Demonstrate use of pm3d palette colors for 2D plots
	# terminal-independent color choices
	#
	reset
	#
	set title "Terminal-independent RGB colors in 2D"
	set xlabel "Implemented using built-in rgb color names\n(only works for terminals that can do full rgb color)"
	#
	set xrange [-0.5:3.5]
	set yrange [-1:1.4]
	set bmargin 7
	unset ytics
	unset xtics
	#
	# define line styles using explicit rgbcolor names
	#
	set style line 1 lt rgb "red" lw 3
	set style line 2 lt rgb "orange" lw 2
	set style line 3 lt rgb "yellow" lw 3
	set style line 4 lt rgb "green" lw 2
	set style line 5 lt rgb "cyan" lw 3
	set style line 6 lt rgb "blue" lw 2
	set style line 7 lt rgb "violet" lw 3
	#
	set label 1 'set style line 1 lt rgb "red" lw 3'    at -0.4, -0.25 tc rgb "red"
	set label 2 'set style line 2 lt rgb "orange" lw 2' at -0.4, -0.35 tc rgb "orange"
	set label 3 'set style line 3 lt rgb "yellow" lw 3' at -0.4, -0.45 tc rgb "yellow"
	set label 4 'set style line 4 lt rgb "green" lw 2'  at -0.4, -0.55 tc rgb "green"
	set label 5 'set style line 5 lt rgb "cyan" lw 3'   at -0.4, -0.65 tc rgb "cyan"
	set label 6 'set style line 6 lt rgb "blue" lw 2'   at -0.4, -0.75 tc rgb "blue"
	set label 7 'set style line 7 lt rgb "violet" lw 3' at -0.4, -0.85 tc rgb "violet"
	#
	print ''
	print '# These are the input commands'
	print ''
	print '	set style line 1 lt rgb "red" lw 3'
	print '	set style line 2 lt rgb "orange" lw 2'
	print '	set style line 3 lt rgb "yellow" lw 3'
	print '	set style line 4 lt rgb "green" lw 2'
	print '	set style line 5 lt rgb "cyan" lw 3'
	print '	set style line 6 lt rgb "blue" lw 2'
	print '	set style line 7 lt rgb "violet" lw 3'
	print ''
	print '# And this is the result'
	#
	show style line
	#
	# draw some plots
	#
	plot cos(x)     ls 1 title 'red',   \
	     cos(x-.2)  ls 2 title 'orange',\
	     cos(x-.4)  ls 3 title 'yellow',\
	     cos(x-.6)  ls 4 title 'green', \
	     cos(x-.8)  ls 5 title 'cyan',  \
	     cos(x-1.)  ls 6 title 'blue',  \
	     cos(x-1.2) ls 7 title 'violet'
	#
	pause -1 "Hit return to continue"
	
	
	set title "Terminal-independent palette colors in 2D\nImplemented using command line macros referring to a fixed HSV palette"
	set colorbox horizontal user origin .1,.08 size .8,.05
	set xlabel 'HSV color wheel'
	#
	unset label 1
	unset label 2
	unset label 3
	unset label 4
	unset label 5
	unset label 6
	unset label 7
	#
	# Load terminal-independent colorwheel (HSV full saturation)
	#
	load 'colorwheel.dem'
	#
	# define line styles using macros and a fixed palette
	#
	set style line 1 @red lw 3
	set style line 2 @orange lw 2
	set style line 3 @yellow lw 3
	set style line 4 @green lw 2
	set style line 5 @cyan lw 3
	set style line 6 @blue lw 2
	set style line 7 @violet lw 3
	
	replot
	
	pause -1 "Hit return to continue"
	
	reset

snippet random.dem
	#
	# $Id: random.dem,v 1.18 2017/03/16 18:16:12 sfeam Exp $
	#
	# random.dem
	#
	# Lattice test for random numbers;
	# If you can see any patterns in this plot, the random number generator
	# is not very good.
	#
	# Copyright (c) 1991, Jos van der Woude, jvdwoude@hut.nl
	
	# History:
	#	-  6. 6. 2006 ds: added univariate and multivariate normal example
	#	- 10. 5. 2006 ds: added univariate and multivariate normal example
	#	-  ?. ?  1991 jvdw: 1st version
	
	unset key
	set xrange [0: 1]
	set yrange [0: 1]
	set zrange [0: 1]
	set title "Lattice test for random numbers"
	set xlabel "rand(n) ->"      rotate parallel
	set ylabel "rand(n + 1) ->"  rotate parallel offset 0,-1
	set zlabel "rand(n + 2) ->"  rotate parallel offset 0,-1
	set format x "%3.2f"
	set format y "%3.2f"
	set format z "%3.2f"
	set tics
	set sample 1000
	set style function dots
	set parametric
	plot rand(0), rand(0)
	pause -1 "Hit return to continue"
	print "3D plot ahead, one moment please ..."
	set sample 50
	splot rand(0), rand(0), rand(0)
	pause -1 "Hit return to continue"
	
	print ""
	print "Multivariate normal distribution"
	print ""
	print "The surface plot shows a two variable multivariate probability"
	print "density function.  On the x-y plane are some samples of the random"
	print "vector and a contour plot illustrating the correlation, which in"
	print "this case is zero, i.e. a circle.  (Easier to view in map mode.)"
	print ""
	nsamp = 50
	# Generate N random data points.
	set print $random
	do for [i=1:nsamp] {
	    print sprintf("%8.5g %8.5g", invnorm(rand(0)), invnorm(rand(0)))
	}
	unset print
	#
	unset xlabel
	unset ylabel
	unset zlabel
	set parametric
	tstring(n) = sprintf("%d random samples from a 2D Gaussian PDF with\nunit variance, zero mean and no dependence", n)
	set title tstring(nsamp)
	unset key
	set hidden3d
	set contour
	set view 68, 28, 1, 1
	set cntrparam levels discrete 0.1
	unset clabel
	set xrange [-3:3]
	set yrange [-3:3]
	set zrange [-0.2:0.2]
	set ztics 0,0.05
	set urange [-3:3]
	set vrange [-3:3]
	set ticslevel 0
	set isosamples 30
	splot u,v,( 1/(2*pi) * exp(-0.5 * (u**2 + v**2)) ) with line lc rgb "black", \
	   $random using 1:2:(-0.2) with points pointtype 7 lc rgb "black"
	pause -1 "Hit return to continue"
	unset contour
	unset parametric
	
	load "stat.inc"
	
	print ""
	print "Simple Monte Carlo simulation"
	print ""
	print "The first curve is a histogram where the binned frequency of occurrence"
	print "of a pseudo random variable distributed according to the normal"
	print "(Gaussian) law is scaled such that the histogram converges to the"
	print "normal probability density function with increasing number of samples"
	print "used in the Monte Carlo simulation.  The second curve is the normal"
	print "probability density function with unit variance and zero mean."
	print ""
	nsamp = 5000
	binwidth = 20
	xlow = -3.0
	xhigh = 3.0
	scale = (binwidth/(xhigh-xlow))
	# Generate N random data points.
	set print $random
	do for [i=1:nsamp] {
	    print sprintf("%8.5g %8.5g", invnorm(rand(0)), (1.0*scale/nsamp))
	}
	unset print
	#
	set samples 200
	tstring(n) = sprintf("Histogram of %d random samples from a univariate\nGaussian PDF with unit variance and zero mean", n)
	set title tstring(nsamp)
	set key
	set grid
	set xrange [-3:3]
	set yrange [0:0.45]
	bin(x) = (1.0/scale)*floor(x*scale)
	plot $random using (bin($1)):2 smooth frequency with steps \
	                title "scaled bin frequency", \
	                normal(x,0,1) with lines title "Gaussian p.d.f."
	pause -1 "Hit return to continue"
	
	print ""
	print "Another Monte Carlo simulation"
	print ""
	print "This is similar to the previous simulation but uses multivariate"
	print "zero mean, unit variance normal data by computing the distance "
	print "each point is from the origin.  That distribution is known to fit"
	print "the Maxwell probability law, as shown."
	print ""
	reset
	undefine $random
	load "gen-random.inc"
	#
	oneplot = 1
	#
	if (oneplot) set multiplot layout 1,2
	#
	unset key
	rlow = -4.0
	rhigh = 4.0
	set parametric
	set xrange [rlow:rhigh]; set yrange [rlow:rhigh]; set zrange [rlow:rhigh]
	set xtics axis nomirror; set ytics axis nomirror; set ztics axis nomirror;
	set border 0
	set xyplane at 0
	set xzeroaxis lt -1
	set yzeroaxis lt -1
	set zzeroaxis lt -1
	set view 68, 28, 1.4, 0.9
	tstring(n) = sprintf("Gaussian 3D cloud of %d random samples\n", n)
	set title tstring(nsamp) offset graph 0.15, graph -0.33
	splot $random every :::::0 with dots
	if (!oneplot) pause -1 "Hit return to continue"
	
	unset parametric
	unset xzeroaxis; unset yzeroaxis;
	set border
	set grid
	set samples 200
	set size 0.47,0.72
	set origin 0.44,0.18
	tstring(n) = sprintf("Histogram of distance from origin of\n%d multivariate unit variance samples", n)
	set title tstring(nsamp) offset graph 0, graph 0.15
	set key bmargin right vertical
	xlow = 0.0
	xhigh = 4.5
	binwidth = 20
	scale = (binwidth/(xhigh-xlow))
	set xrange [0:xhigh]
	set yrange [0:0.65]
	bin(x) = (1.0/scale)*floor(x*scale)
	plot $random using (bin(sqrt($1**2+$2**2+$3**2))):(1.0*scale/nsamp) every :::::0 smooth frequency with steps \
	        title "scaled bin frequency", \
	        maxwell(x, 1/sqrt(2)) with lines title "Maxwell p.d.f.", \
	        $random using (sqrt($1**2+$2**2+$3**2)):(scale/nsamp) bins=25 binrange [xlow:xhigh] with impulse lw 5 title "assign samples to 25 bins"
	#
	if (oneplot) unset multiplot
	#
	pause -1 "Hit return to continue"
	
	reset
	

snippet rectangle.dem
	# demo for the use of "set object rectangle"
	# Updated for version 5
	#	show front/back/behind, opaque key box
	
	set object 1 rect from 0,0 to 1,4 fc lt 2 front
	set object 2 rect from -1,1 to 0,5 fc rgb "gold"
	set object 5 rect from 0, -3 to 2, -2 fc rgb "cyan" fs pattern 1 bo -1
	
	set style fill pattern 2 bo 1
	
	#
	# Test clipping and placement in the presence of an inverted axis range
	#
	set obj 4 rect at -4,0 size 3,1
	set label 4 at -2.6,0 "There should be a\nclipped rectangle here" left offset 0,.5
	
	#
	# The key box has its own option "opaque" to generate a filled rectangle
	#
	set key box opaque height 2
	
	#
	# Illustrate using character widths to put a box around a label
	#
	LABEL = "Label in a box"
	set obj 10 rect at -3,-4 size char strlen(LABEL), char 1 
	set obj 10 fillstyle empty border -1 front
	set label 10 at -3,-4 LABEL front center
	
	set obj 9 rect from -4, -4 to -4, -3 fc lt -1
	
	set obj 20 rect from graph 0, graph 0 to graph 1, graph 1 fs solid 0.15 fc rgb "#FFD700" behind
	
	set xrange [5:-5]
	
	plot x, -3+sin(x*5)/x lt 3 lw 3
	
	pause -1 "Hit return to continue"
	
	reset
	
	#
	# Requires data file "using.dat" from this directory,
	# so change current working directory to this directory before running.
	#
	
	set xrange [1:8]
	set title "Convex     November 1-7 1989"
	set key below
	set label "(Weekend)" at 5,25 center
	
	set style rect fc lt -1 fs solid 0.15 noborder
	
	set obj rect from 1, graph 0 to 2, graph 1
	set obj rect from 3, graph 0 to 4, graph 1
	set obj rect from 5, graph 0 to 6, graph 1
	set obj rect from 7, graph 0 to 8, graph 1
	
	plot 'using.dat' using 3:4 title "Logged in" with impulses,\
	     'using.dat' using 3:5 t "Load average" with points,\
	     'using.dat' using 3:6 t "%CPU used" with lines
	
	pause -1 "Hit return to continue"
	
	reset

snippet rgba_lines.dem
	#
	# Demonstrate use of RGBA (Alpha channel + RGB) colors for lines and filled areas.
	# Color is represented by 32 bits AARRGGBB  (i.e. Alpha is in the high bits)
	# Alpha = 0 is opaque;  Alpha = 255 is transparent
	# This is BACKWARDS from the convention used by 'set style fill TRANSPARENT SOLID'
	# It is also BACKWARDS from the convention used by 'with rgbalpha'
	#
	set angle degrees
	set style arrow 1 size screen 0.03,15,90 filled
	
	if (GPVAL_TERM eq "png") LW = 1; else LW = 3
	
	do for [i = 5 : 360 : 5] {
	    set arrow i from 0,0 length 1.0 angle i filled
	    color = (int(255.*i/360.) << 24) + 0xFF0000
	#   print sprintf("%x",color)
	    set arrow i as 1 lw LW lc rgb color
	}
	set obj  1 rect from -.5,-.5 to -.3,0   fs solid fc rgb "#00bbbbbb" front
	set obj  2 rect from -.3,-.3 to -.1,0.2 fs solid fc rgb "#55bbbbbb" front
	set obj  3 rect from -.1,-.1 to  .3,0.3 fs solid fc rgb "#aabbbbbb" front
	set obj  4 rect from  .3,-.4 to  .4,0.4 fs solid fc rgb "#22bbbbbb" back
	set obj 10 circle at 0.15, -0.15 radius 0.5 fs solid fc  rgb "cyan" behind
	
	
	unset border
	unset xtics
	unset ytics
	set xrange [-1:1]
	set yrange [-1:1]
	set size square
	
	plot 0 notitle
	
	pause -1 "Hit return to continue"
	reset

snippet rgbalpha.dem
	set border linecolor rgb "gold"
	set key title "Plot style rgbalpha" textcolor rgb "gold" 
	set key bottom left at screen .75, screen .75
	set key Left width -7 sample 1
	
	set object  1 rect from screen 0, 0 to screen 1, 1 behind \
	              fc rgb "gray10"  fillstyle  solid 1.00 noborder
	set samples 128, 128
	set size ratio 0.95
	set bmargin at screen .1
	set tmargin at screen .9
	set lmargin at screen .1
	
	
	set xrange [ 0. : 128. ] 
	set yrange [ 0. : 128. ]
	
	# Alpha =  linear gradient on x
	
	plot 100.*(.4+sin(x/5.)/(x/5.)) lw 5 title 'solid line', \
	     'lena.rgb' binary array=(128,128) format="%uchar" flipy using 1:2:3:(2.*column(0)) \
	                with rgbalpha title "Lena with linear\nalpha gradient"
	
	pause -1 "Hit return to continue"
	
	# Alpha = circular mask
	
	focus(x,y) = ((column(0)-x)**2 + (column(-1)-(127-y))**2) > 400 ? 0 : 255
	
	plot 100.*(.4+sin(x/5.)/(x/5.)) lw 5 title 'solid line', \
	     'lena.rgb' binary array=(128,128) format="%uchar" flipy using 1:2:3:(focus(70,50)) \
	                with rgbalpha title "Lena with circular mask"
	
	pause -1 "Hit return to continue"
	reset

snippet rgb_variable.dem
	#
	# Demo of reading color information from the data file itself
	#
	
	#
	rgb(r,g,b) = int(r)*65536 + int(g)*256 + int(b)
	
	#
	set border 0
	unset xtics; unset ytics; unset ztics
	set rmargin 5; set lmargin 5; set bmargin 2
	
	set angle degrees
	xrgb(r,g,b) = (g-b)/255. * cos(30.)  
	yrgb(r,g,b) = r/255. - (g+b)/255. * sin(30.)
	set arrow 1 from 0,0 to 0,1 nohead lw 3 lc rgb "red" back
	set arrow 2 from 0,0 to cos(-30), sin(-30) nohead lw 3 lc rgb "green" back
	set arrow 3 from 0,0 to cos(210), sin(210) nohead lw 3 lc rgb "blue" back
	
	set title "RGB color information read from data file"
	plot 'rgb_variable.dat' using (xrgb($1,$2,$3)):(yrgb($1,$2,$3)):(rgb($1,$2,$3)) \
	     with points pt 7 ps 4 lc rgb variable notitle
	pause -1 "Hit return to continue"
	
	#
	set title "Both RGB color information\n and point size controlled by input"
	plot 'rgb_variable.dat' using (xrgb($1,$2,$3)):(yrgb($1,$2,$3)):(1.+2.*rand(0)):(rgb($1,$2,$3)) \
	     with points pt 7 ps var lc rgb variable notitle
	pause -1 "Hit return to continue"
	
	#
	set border -1 front linetype -1 linewidth 1.000
	set ticslevel 0
	set xtics border
	set ytics border
	set ztics border
	#
	unset arrow 1
	unset arrow 2
	unset arrow 3
	#
	set xlabel "Red" tc rgb "red"
	set xrange [0:255]
	set ylabel "Green" tc rgb "green"
	set yrange [0:255]
	set zlabel "Blue" tc rgb "blue"
	set zrange [0:255]
	
	#
	splot 'rgb_variable.dat' using 1:2:3:(rgb($1,$2,$3)) with points pt 7 ps 4 lc rgb variable, \
	      '' using 1:2:3:(sprintf("0x%x",rgb($1,$2,$3))) with labels left offset 1 notitle
	pause -1 "Hit return to continue"
	#
	#
	# Unfortunately, not all platforms allow us to read hexadecimal constants
	# from a data file. Warn the user if that is the case.
	#
	if (0 == int('0x01')) \
	   set label 99 at screen .05, screen .15 "If you see only black dots,\nthis means your platform does not \nsupport reading hexadecimal constants\nfrom a data file. Get a newer libc."
	
	splot 'rgb_variable.dat' using 1:2:3:(5*rand(0)):4 with points pt 7 ps variable lc rgb variable \
	      title "variable pointsize and rgb color read as hexidecimal"
	
	pause -1 "Hit return to continue"
	set label 99 ""
	#
	set border 0
	set xtics axis nomirror
	set ytics axis nomirror
	set ztics axis nomirror
	set xzeroaxis lt -1 lc rgb "red" lw 2
	set yzeroaxis lt -1 lc rgb "green" lw 2
	set zzeroaxis lt -1 lc rgb "blue" lw 2
	set xyplane at 0.0
	
	splot 'rgb_variable.dat' using 1:2:3:(5*rand(0)):(rgb($1,$2,$3)) with points pt 7 ps variable lc rgb variable \
	      title "variable pointsize and rgb color computed from coords"
	pause -1 "Hit return to continue"
	
	set title "Demo of hidden3d with points only (no surface)"
	set hidden3d
	replot
	pause -1 "Hit return to continue"
	reset
	#
	
	RGB(R,G,B) =  int(255.*R) * 2**16 + int(255.*G) * 2**8  + int(255.*B)
	
	set xr [0.01:1]
	set yr [0.01:1]
	set ur [0.01:1]
	set vr [0.01:1]
	set zr [0:1.065]
	set xtics ("R=1" 1)
	set ytics ("G=1" 1)
	set ztics ("0" 0, "B=1" 1)
	unset colorbox
	set isosamples 40,40
	set xyplane at 0.0
	set view 63,58,1.,1.4
	
	set title "RGB coloring of pm3d surface"
	unset key
	
	# Just some function that is well-defined in this range
	# (except at the origin)
	f(x,y) = 0.4 + sin(sqrt(100.*x**2+100.*y**2)) \
	       / (1.5*sqrt(100.*x**2+100.*y**2))
	
	splot '++' using 1:2:(f($1,$2)):(RGB($1,$2,f($1,$2))) \
	       with pm3d lc rgb variable
	
	pause -1 "Hit return to continue"
	
	set title "HSV coloring of pm3d surface\n(V=1)"
	unset xtics
	unset ytics
	set ztics ("0" 0)
	set xlabel "H"
	set ylabel "S"
	
	splot '++' using 1:2:(f($1,$2)):(hsv2rgb($1,$2,1.0)) \
	       with pm3d lc rgb variable
	
	pause -1 "Hit return to continue"
	
	set title "Explicit borders for pm3d tiling"
	set pm3d border lt black hidden3d
	set sample 10
	set isosample 10
	replot
	
	pause -1 "Hit return to continue"
	
	reset

snippet rotate_labels.dem
	#
	# Additional data columns can be used to hold text rotation or color
	# for plot style "with labels"
	#
	$Data <<EOD
	1 one -30
	2 two -60
	3 three -90
	4 four -120
	5 five -150
	6 six -180
	7 seven -210
	8 eight -240
	9 nine -270
	10 ten -300
	11 eleven -330
	12 twelve -360
	EOD
	
	set angle degrees
	unset key
	set title "variable color and orientation in plotstyle 'with labels'" offset 0,-2
	
	set xrange [0:13]
	set yrange [0:13]
	set xtics 1,1,12 nomirror
	set ytics 1,1,12 nomirror
	set border 3
	
	plot $Data using 1:1:2:3:0 with labels rotate variable tc variable font ",20"
	
	pause -1 "<cr> to continue"
	reset

snippet rugplot.dem
	#
	# Demo of a Rug Plot
	# The X and Y coordinates of each point are marked by a mark
	# on the corresponding axis.
	#
	load "gen-random.inc"
	
	unset key
	set bmargin at screen .2; 
	set rmargin at screen .8; set lmargin at screen .2;
	set offset .08, .08, .08, .08
	set border lw 0.5
	
	set title "Rug Plot" font ",24" offset 0,.5
	
	set xtics out scale 3
	set ytics out scale 3
	plot $random every 15 using 1:2:xtic(""):ytic("") with points ps .5
	
	pause -1 "Hit <cr> to continue"
	reset

snippet running_avg.dem
	#
	# This script demonstrates the use of assignment operators and
	# sequential expression evaluation to track data points as they
	# are read in.
	#
	# We use the '=' and ',' operators to track the running total
	# and previous 5 values of a stream of input data points.
	#
	# Ethan A Merritt - August 2007
	#
	# Define a function to calculate average over previous 5 points
	#
	set title \
	    "Demonstrate use of assignment and serial evaluation operators\n" \
	    . "to accumulate statistics as successive data lines are read in\n"
	set key invert box center right reverse Left
	set xtics nomirror
	set ytics nomirror
	set border 3
	
	samples(x) = $0 > 4 ? 5 : ($0+1)
	avg5(x) = (shift5(x), (back1+back2+back3+back4+back5)/samples($0))
	shift5(x) = (back5 = back4, back4 = back3, back3 = back2, back2 = back1, back1 = x)
	
	#
	# Initialize a running sum
	#
	init(x) = (back1 = back2 = back3 = back4 = back5 = sum = 0)
	
	#
	# Plot data, running average and cumulative average
	#
	
	datafile = 'silver.dat'
	set xrange [0:57]
	
	set style data linespoints
	
	plot sum = init(0), \
	     datafile using 0:2 title 'data' lw 2 lc rgb 'forest-green', \
	     '' using 0:(avg5($2)) title "running mean over previous 5 points" pt 7 ps 0.5 lw 1 lc rgb "blue", \
	     '' using 0:(sum = sum + $2, sum/($0+1)) title "cumulative mean" pt 1 lw 1 lc rgb "dark-red"
	
	pause -1 "Hit return to continue"
	
	reset

snippet sampling.dem
	# 18 Aug 2017
	# Each of these plot commands failed on some version between 5.0 and 5.3.
	# I collect them here to use as unit tests for continued success
	# of the code added to repair breakage.
	#
	# Usage:  call 'sampling.dem' <testno>
	#
	
	set urange [1:100]
	set vrange [1:100]
	set xrange [1:100]
	set key opaque box
	
	if (ARGC < 1) ARG1 = 0
	
	set trange [20:50]
	
	if (ARG1 <= 1) {
	    print "test 1: explicit trange distinct from xrange"
	    plot '+' using 1:(10. + sin($1)) title "trange [20:50]"
	    pause -1 "Hit <cr> to continue"
	}
	
	if (ARG1 <= 2) {
	    print "test 2: range set by 'sample' keyword, linear x axis"
	    plot sample [t=20:50] '+' using (t):(10. + sin(t))
	    pause -1 "Hit <cr> to continue"
	}
	
	
	set log x
	
	if (ARG1 <= 3) {
	    print "test 3: range set by 'sample' keyword, logscale x axis"
	    plot sample [t=20:50] '+' using (t):(10. + sin(t))
	    pause -1 "Hit <cr> to continue"
	}
	
	unset log
	
	if (ARG1 <= 4) {
	    print "test 4: splot '++' with autoscaled y (linear xy)"
	    splot '++' using 1:2:($1*$2)
	    pause -1 "Hit <cr> to continue"
	}
	
	set log xy
	
	if (ARG1 <= 5) {
	    print "test 5: splot '++' with autoscaled y (logscale xy)"
	    splot '++' using 1:2:($1*$2)
	    pause -1 "Hit <cr> to continue"
	}
	
	unset log
	
	if (ARG1 <= 6) {
	    print "test 6: plot '++' with image (linear xy)"
	    plot '++' using 1:2:($1*$2) with image
	    pause -1 "Hit <cr> to continue"
	}
	
	
	set log xy
	
	if (ARG1 <= 7) {
	    print "test 7: plot '++' with image (logscale xy)"
	    plot '++' using 1:2:($1*$2) with image
	    pause -1 "Hit <cr> to continue"
	}
	
	reset 
	#
	#  piecewise functions along nonlinear x axis
	#
	unset border
	set key center top reverse Left
	set xzeroaxis
	set yzeroaxis
	set xtics axis out scale 1,8
	set xtics add  (1.00000 1, 6.28319 1)
	set ytics axis
	set xrange [ .1 : 12 ] noreverse nowriteback
	
	
	
	if (ARG1 <= 8) {
	    print "test 8: multiple sampling ranges in one 2D plot command"
	    set multiplot layout 2,1
	    set title "Piecewise function sampling along linear x" font ",13"
	    plot sample [*:1] x, [1:2.*pi] cos(x), [2.*pi:10] (x-8)**2
	    set nonlinear x via x inv x
	    set title "nonlinear (identity mapped) x" font ",13"
	    plot sample [*:1] x, [1:2.*pi] cos(x), [2.*pi:10] (x-8)**2
	    unset multiplot
	    pause -1 "Hit <cr> to continue"
	}
	
	reset
	
	set xrange [1:100]
	set yrange [1:100]
	set urange [0:100]
	set vrange [70:90]
	set style data lines
	set xyplane at 0
	
	set hidden3d
	set title "3D sampling range distinct from plot x/y range"
	print "test 9: 3D sampling range distinct from plot x/y range"
	
	splot '++' using 1:2:($1*25.*sin($2/10)), \
	      [u=30:70][v=0:50] '++' using 1:2:(u*v), \
	      [u=40:80][v=30:60] '++' using (u):(v):(u*sqrt(v)) lt 4, \
	      [u=1:100][v=500:1000] '++' using (90):(u):(v) lt 6
	
	pause -1 "Hit <cr> to continue"
	
	reset
	
	unset key
	set view map
	set xrange [ 20:100 ]
	set yrange [ 0:80 ]
	set size ratio 1.0
	radius(x,y) = 10 * sqrt((x-50)**2 + (y-50)**2)
	alpha(x,y) = radius(x,y) > 255 ? 0 : 255-radius(x,y)
	set title "3D custom sampling on u and v using pseudofile '++'"
	print "test 10: splot '++' with explicit sampling intervals"
	
	splot sample [u=25:75:1][v=25:75:4] '++' \
	      using 1:2:0:(u*4-50):(v*4-50):(255*sin(u*v/2500.)):(alpha(u,v)) with rgbalpha, \
	      [u=25:75:4][v=25:75:1] '++' \
	      using (u+20):(v-20):0:(u*4-50):(v*4-50):(255*sin(u*v/2500.)):(alpha(u,v)) with rgbalpha
	
	pause -1 "Hit <cr> to continue"
	
	set title "2D custom sampling on u and v using pseudofile '++'"
	print "test 10: plot '++' with explicit sampling intervals"
	
	plot sample [u=25:75:1][v=25:75:4] '++' \
	      using 1:2:(u*4-50):(v*4-50):(255*sin(u*v/2500.)):(alpha(u,v)) with rgbalpha, \
	      [u=25:75:4][v=25:75:1] '++' \
	      using (u+20):(v-20):(u*4-50):(v*4-50):(255*sin(u*v/2500.)):(alpha(u,v)) with rgbalpha
	
	pause -1 "Hit <cr> to continue"
	
	reset
	

snippet scatter.dem
	#
	# $Id: scatter.dem,v 1.7 2003/10/28 05:35:54 sfeam Exp $
	#
	# Simple demo of scatter data conversion to grid data.
	#
	
	set title "Simple demo of scatter data conversion to grid data"
	unset hidden3d
	set ticslevel 0.5
	set view 60,30
	set autoscale
	set parametric
	set style data points
	set xlabel "data style point - no dgrid"
	set key box
	splot "hemisphr.dat"
	pause -1 "Hit return to continue (1)"
	
	set dgrid3d 10,10,1
	set xlabel " data style lines, dgrid3d 10,10,1"
	set style data lines
	splot "hemisphr.dat"
	pause -1 "Hit return to continue (2)"
	
	set dgrid3d ,,4
	set xlabel " data style lines, dgrid3d ,,4 "
	set style data lines
	splot "hemisphr.dat"
	pause -1 "Hit return to continue (3)"
	
	set dgrid3d ,,16
	set xlabel " data style lines, dgrid3d ,,16"
	set style data lines
	splot "hemisphr.dat"
	pause -1 "Hit return to continue (4)"
	
	set contour
	set xlabel "data style lines, dgrid3d ,,16, contour"
	splot "hemisphr.dat"
	pause -1 "Hit return to continue (5)"
	
	unset dgrid3d
	set style data points
	set xlabel "data style points, nodgrid3d"
	splot "scatter2.dat"
	pause -1 "Hit return to continue (6)"
	
	set key nobox
	set dgrid3d ,,1
	set xlabel "data style lines, dgrid3d ,,1"
	set style data lines
	splot "scatter2.dat"
	pause -1 "Hit return to continue (7)"
	
	set dgrid3d ,,4
	set xlabel "data style lines, dgrid3d ,,4"
	set style data lines
	splot "scatter2.dat"
	pause -1 "Hit return to continue (8)"
	reset

snippet simple.dem
	#
	# $Id: simple.dem,v 1.7 2012/11/18 23:12:22 sfeam Exp $
	#
	# Requires data files "[123].dat" from this directory,
	# so change current working directory to this directory before running.
	# gnuplot> set term <term-type>
	# gnuplot> load 'simple.dem'
	#
	set title "Simple Plots" font ",20"
	set key left box
	set samples 50
	set style data points
	
	plot [-10:10] sin(x),atan(x),cos(atan(x))
	pause -1 "Hit return to continue"
	
	set key right nobox
	set samples 100
	plot [-pi/2:pi] cos(x),-(sin(x) > sin(x+1) ? sin(x) : sin(x+1))
	pause -1 "Hit return to continue"
	
	set key left box
	set samples 200
	plot [-3:5] asin(x),acos(x)
	pause -1 "Hit return to continue"
	
	plot [-30:20] besj0(x)*0.12e1 with impulses, (x**besj0(x))-2.5 with points
	pause -1 "Hit return to continue"
	
	set samples 400
	plot [-10:10] real(sin(x)**besj0(x))
	pause -1 "Hit return to continue"
	
	set key bmargin center horizontal
	plot [-5*pi:5*pi] [-5:5] real(tan(x)/atan(x)), 1/x
	pause -1 "Hit return to continue"
	
	set key left box
	set autoscale
	set samples 800
	plot [-30:20] sin(x*20)*atan(x)
	pause -1 "Hit return to continue"
	
	plot [-19:19] '1.dat'with impulses ,'2.dat' ,'3.dat' with lines
	pause -1 "Hit return to continue"
	
	reset
	

snippet singulr.dem
	# $Id: singulr.dem,v 1.6 2003/10/28 05:35:54 sfeam Exp $
	#
	# Demo that plots some surfaces with singularities.
	# Author: Carsten Steger
	#
	# (x,y,x^2-y^2,2xy) is the graph of w=z^2 in 4-space.
	# Therefore (x^2-y^2,2xy,x,y) is the graph of w=sqrt(z) in 4-space.
	# Coordinates 1, 2, and 3 give the real part of either function,
	# whereas coordinates 1, 2, and 4 give the imaginary part.
	# The same holds for the cube function w=z^3. The graphs are given by
	# (x,y,x^3-3xy^2,3x^2y-y^3) and (x^3-3xy^2,3x^2y-y^3,x,y).
	# And so on...
	
	
	set parametric
	set hidden3d
	set isosamples 21
	set autoscale
	
	
	set view 60,30
	set urange [-3:3]
	set vrange [-3:3]
	set title "Real part of complex square root function"
	splot u**2-v**2,2*u*v,u
	pause -1 "Hit return to continue (1)"
	
	
	set view 60,210
	set title "Real part of complex square root function (different view)"
	replot
	pause -1 "Hit return to continue (2)"
	
	
	set view 60,120
	set urange [-3:3]
	set vrange [-3:3]
	set title "Imaginary part of complex square root function"
	splot u**2-v**2,2*u*v,v
	pause -1 "Hit return to continue (3)"
	
	
	set view 60,300
	set title "Imaginary part of complex square root function (different view)"
	replot
	pause -1 "Hit return to continue (4)"
	
	
	set view 60,30
	set urange [-3:3]
	set vrange [-3:3]
	set title "Real part of complex cube root function"
	splot u**3-3*u*v**2,3*u**2*v-v**3,u
	pause -1 "Hit return to continue (5)"
	
	
	set view 60,210
	set title "Real part of complex cube root function (different view)"
	replot
	pause -1 "Hit return to continue (6)"
	
	
	set view 60,30
	set urange [-3:3]
	set vrange [-3:3]
	set title "Imaginary part of complex cube root function"
	splot u**3-3*u*v**2,3*u**2*v-v**3,v
	pause -1 "Hit return to continue (7)"
	
	
	set view 60,210
	set title "Imaginary part of complex cube root function (different view)"
	replot
	pause -1 "Hit return to continue (8)" 
	
	
	set view 60,30
	set isosamples 31
	set urange [-1:1]
	set vrange [-1:1]
	set title "Real part of complex 4th root function"
	splot u**4-6*u**2*v**2+v**4,4*u**3*v-4*u*v**3,u
	pause -1 "Hit return to continue (9)"
	
	
	set view 60,210
	set title "Real part of complex 4th root function (different view)"
	replot
	pause -1 "Hit return to continue (10)"
	
	
	set view 60,120
	set urange [-1:1]
	set vrange [-1:1]
	set title "Imaginary part of complex 4th root function"
	splot u**4-6*u**2*v**2+v**4,4*u**3*v-4*u*v**3,v
	pause -1 "Hit return to continue (11)"
	
	
	set view 60,300
	set title "Imaginary part of complex 4th root function (different view)"
	replot
	pause -1 "Hit return to continue (12)"
	
	
	set isosamples 21
	set view 60,20
	set urange [-3:3]
	set vrange [-3:3]
	set title "Enneper's surface"
	splot u-u**3/3+u*v**2,v-v**3/3+v*u**2,u**2-v**2
	pause -1 "Hit return to continue (13)"
	
	
	set view 60,110
	set title "Enneper's surface (different view)"
	replot
	pause -1 "Hit return to continue (14)"
	
	
	set isosamples 31,11
	set view 60,30
	set title "Moebius strip"
	set urange [0:2*pi]
	set vrange [-0.25:0.25]
	splot (2-v*sin(u/2))*sin(u),(2-v*sin(u/2))*cos(u),v*cos(u/2)
	pause -1 "Hit return to continue (15)"
	
	
	set view 60,210
	set title "Moebius strip (view from opposite side)"
	replot
	pause -1 "Hit return to continue (16)"
	
	unset key
	set xrange [-10:10]
	set yrange [-10:10]
	set zrange [-3:3]
	set urange [0:2*pi]
	set vrange [0:2*pi]
	set isosamples 39,60
	set view 60,120
	set title "Klein bottle"
	splot  (2*sin(u)*cos(v/2)-sin(2*u)*sin(v/2)+8)*cos(v), \
	       (2*sin(u)*cos(v/2)-sin(2*u)*sin(v/2)+8)*sin(v), \
	        2*sin(u)*sin(v/2)+sin(2*u)*cos(v/2)
	pause -1 "Hit return to continue (17)"
	
	
	set urange [0:2*pi]
	set vrange [0:4*pi/3]
	set isosamples 39,40
	set view 60,20
	set title "Klein bottle with look at the 'inside'"
	replot
	pause -1 "Hit return to continue (18)"
	
	set style data lines
	set xrange [-12:12]
	set yrange [-12:12]
	set zrange [*:*]
	unset hidden3d
	set ticslevel 0.
	set view 50,15	#HBB: ,1,1.7
	set title "Klein bottle, glassblowers' version (look-through)"
	splot "klein.dat"
	pause -1 "Hit return to continue (19)"
	
	
	set hidden3d
	set view 70,305
	set title "Klein bottle, glassblowers' version (solid)"
	splot "klein.dat"
	pause -1 "Hit return to continue (20)"
	
	reset

snippet smooth.dem
	#
	# Find number of points
	#
	stats 'random-points' nooutput
	N = STATS_records
	
	bin(x, s) = s*int(x/s)
	
	set zeroaxis
	set style data points
	set key box top left
	
	# Uniform
	set title "Uniform Distribution"
	set boxwidth 0.05
	set xrange [-0.1 : 1.1] noextend
	set yrange [-0.4 : 1.6]
	plot "random-points" u 1:(0.25*rand(0)-.35) t '', \
	     "" u (bin($1,0.05)):(20./N) smooth freq t 'smooth frequency' w boxes, \
	     "" u 1:(1.) smooth cnorm t 'smooth cnorm'
	pause -1 "Hit enter to continue"
	
	
	# Normal
	set title "Normal Distribution"
	set boxwidth 0.05
	set xrange [* : *] noextend
	set yrange [-0.4 : 1.1]
	set ytics 0,.2
	
	plot "random-points" u 2:(0.25*rand(0)-.35) t '', \
	     "" u (bin($2,0.05)):(20./N) smooth freq t 'smooth frequency' w boxes, \
	     "" u 2:(1.) smooth cnorm t 'smooth cnorm'
	pause -1 "Hit enter to continue"
	
	
	# Lognormal
	set title "Lognormal Distribution"
	set boxwidth 0.1
	set xrange [0 : *] noextend
	plot "random-points" u 3:(0.25*rand(0)-.35) t '', \
	    "" u (bin($3,0.1)):(10./N) smooth freq t 'smooth frequency' w boxes, \
	    "" u 3:(1.) smooth cnorm t 'smooth cnorm'
	pause -1 "Hit enter to continue"
	
	
	# Mixed
	set title "Mixed Distribution (Lognormal with shifted Gaussian)"
	set boxwidth 0.1
	set xrange [* : *] noextend
	set link y2 via y*N inverse y/N
	set ytics nomirror  0,.2
	set y2tics nomirror tc "red" 0,50
	
	plot "random-points" u 4:(0.25*rand(0)-.35) t '', \
	     "" u (bin($4,0.1)):(10./N) smooth freq t 'smooth frequency' w boxes, \
	     "" u 4:(1.) smooth cnorm t 'smooth cnorm' lc "#88eeff" lw 5, \
	     "" u 4:(1.) axes x1y2 smooth cumulative t 'smooth cumulative' lc "red"
	pause -1 "Hit enter to continue"
	
	
	reset

snippet smooth_splines.dem
	#
	# $Id: smooth_splines.dem,v 1.1 2017/01/04 01:00:24 sfeam Exp $
	#
	# First part of old mgr.dem
	# show same data with various spline smoothing choices
	
	print "various splines for smoothing"
	
	set title "cubic spline fit to data (no weights)"
	set samples 300
	set xlabel "Time (sec)"
	set ylabel "Rate"
	plot "silver.dat" t "experimental" w errorb, \
	               ""  smooth csplines t "cubic smooth" lw 2
	
	pause -1 "Now apply a smoothing spline, weighted by 1/rel error (-> return)"
	set title "acsplines weighted by relative error"
	# error is column 3; weight larger errors less
	# start with rel error = 1/($3/$2)
	S=1
	plot "silver.dat" t "experimental" w errorb,\
	               "" u 1:2:(S*$2/$3) smooth acsplines t "acspline Y/Z" lw 2
	
	pause -1 "Make it smoother by changing the smoothing weights (-> return)"
	set title "acsplines with increasing weight from error estimate"
	plot "silver.dat" t "rate" w errorb,\
	               "" u 1:2:($2/($3*1.e1)) sm acs t "acspline Y/(Z*1.e1)" lw 2,\
	               "" u 1:2:($2/($3*1.e3)) sm acs t "         Y/(Z*1.e3)" lw 2,\
	               "" u 1:2:($2/($3*1.e5)) sm acs t "         Y/(Z*1.e5)" lw 2
	
	pause -1 "Accentuate the relative changes with a log-scale (-> return)"
	set title "same plot (various weighting) in log scale"
	set logscale y
	set grid x y mx my
	replot
	
	pause -1 "Now approximate the data with a bezier curve between the endpoints (-> return)"
	set title "Bezier curve rather than cubic spline"
	unset logscale y
	plot "silver.dat" t "experimental" w errorb,\
	               "" smooth sbezier t "bezier" lw 2
	
	pause -1 "You would rather use log-scales ? (-> return)"
	set title "Bezier curve with log scale"
	set logscale y
	replot
	
	pause -1 "Hit return to continue"
	reset

snippet solar_params.dem
	#
	# Calculate solar position in equitorial coordinates (ascension, declination)
	# then convert to horizontal coordinates (altitude, azimuth).
	# Parameters L, g, lambda, and eps are appoximated using values chosen
	# for accuracy in the era on either side of Jan 2000.
	#
	# Input:
	#	Date - string with format "dd-mm-YYYY HH:MM"
	#	Latitude - in degrees
	#	Longitude - in degrees
	#
	# Output:
	#	Function definitions
	#		Azimuth(t) = Solar azimuth at time t
	#		Altitude(t) = Solar altitude at time t
	#	where t is time in seconds relative to local solar noon on Date.
	#		sunrise, sunset (string containing local standard time)
	#		sunlight (string containing time between sunrise, sunset)
	#
	# Local values:
		Minute = 60.
		Hour = 3600.
		Day = 86400.
		TimeFormat = "%d-%m-%Y %H:%M"
		Phi = Latitude
		set angle degrees
	#
	#     n	= local time in days since noon on 1 Jan 2000
	#     L	= mean solar longitude in ecliptic coordinates
	#  	= 280.460° + 0.9856474° * n
	#     g	= orbital anomaly
	#  	= 357.528° + 0.9856003° * n
	# lamba	= solar longitude
	#      	= L + 1.915° * sin(g) + 0.020° * sin(2g)
	#   eps	= obliquity of the ecliptic
	#	= 23.439° - 0.0000004° * n
	
	n = (strptime(TimeFormat, Date) - strptime(TimeFormat, "01-01-2000 12:00")) / Day
	
	L = n * 0.9856474 + 280.460
	L = L - 360 * int(L / 360.)
	if (L < 0) { L += 360. }
	
	g = n * 0.9856003 + 357.528
	g = g - 360 * int(g / 360.)
	if (g < 0) { g += 360. }
	
	lambda = L + 1.915 * sin(g) + 0.020 * sin(2*g)
	
	eps = 23.439 - n * 0.0000004
	
	# Equatorial coordinates
	#     RAsc  = right ascension
	#     Dec = declination
	
	RAsc = atan2( cos(eps) * sin(lambda), cos(lambda) )
	Dec = asin( sin(eps) * sin(lambda) )
	
	# Length of day
	daylength = 2.0 * acos( -sin(Dec)*sin(Phi) / cos(Phi) )
	# Correction for displacement of this location from center of timezone
	corr = Longitude - floor(Longitude/15.0) * 15.0 - 7.5
	rise = (daylength/2.0 - corr) * Day/360.
	set  = (daylength/2.0 + corr) * Day/360.
	
	sunlight = strftime("%tH h %tM m", daylength * Day/360.)
	sunrise = strftime("%tH:%tM", (Day/2)-rise)
	sunset  = strftime("%tH:%tM", (Day/2)+set)
	
	# Horizontal coordinates
	
	h(t) = 360. * t / Day
	Altitude(t) = asin( sin(Dec) * sin(Phi) + cos(Dec) * cos(Phi) * cos(h(t)) )
	
	cosAzi(t)  = ( sin(Dec) * sin(Phi) - cos(Dec) * cos(h(t)) * sin(Phi) ) \
	           / cos(Altitude(t))
	sinAzi(t)  = ( -cos(Dec) * sin(h(t)) ) \
	           / cos(Altitude(t))
	Azimuth(t) = atan2( sinAzi(t), cosAzi(t) )
	

snippet solar_path.dem
	#
	# Calculate solar path in horizontal coordinates (altitude, azimuth)
	# Use gnuplot's polar mode with inverted R range so that the horizon
	# (altitude 0°) is at the perimeter and altitude 90° is at the center.
	#
	
	save_encoding = GPVAL_ENCODING
	set encoding utf8
	
	set label 1 at screen 0.02, 0.95 font ":Bold,10"
	set label 1 "Solar path at\nLatitude 47.67 N"
	set timestamp "Seattle - %d %b %Y" offset 1,1
	
	# Set up equations for Azimuth(t) and Altitude(t) as a function of
	# t = seconds  before or after solar noon on the day of interest
	# at a particular latitude.
	# Here we calculate for Seattle (47.67 N  -122.28 W)
	
	  Latitude = 47.67
	  Longitude = -122.28
	  Date = "21-12-2016"
	  call 'solar_params.dem'
	
	  array dummy[150]
	  array winter_alt[150]; array winter_azi[150]
	  time = -12 * Hour
	  do for [t = 1 : 150] {
		winter_alt[t] = Altitude(time)
		winter_azi[t] = Azimuth(time)
		time = time + 10 * Minute
	  }
	  print Date, "    sunrise ", sunrise, "   sunset ", sunset, "    sunlight ", sunlight
	
	  Date = "22-06-2017"
	  call 'solar_params.dem'
	
	  array summer_alt[150]; array summer_azi[150]
	  time = -12 * Hour
	  do for [t = 1 : 150] {
		summer_alt[t] = Altitude(time)
		summer_azi[t] = Azimuth(time)
		time = time + 10 * Minute
	  }
	  print Date, "    sunrise ", sunrise, "   sunset ", sunset, "    sunlight ", sunlight
	
	  Date = strftime("%d-%m-%Y",time(0) + Day*Longitude/360.)
	  call 'solar_params.dem'
	
	  array today_alt[150]; array today_azi[150]
	  time = -12 * Hour
	  do for [t = 1 : 150] {
		today_alt[t] = Altitude(time)
		today_azi[t] = Azimuth(time)
		dummy[t] = time
		time = time + 10 * Minute
	  }
	  print Date, "    sunrise ", sunrise, "   sunset ", sunset, "    sunlight ", sunlight
	  today = "Today ☉  \n".sunlight."\nof sunlight"
	
	#
	set angle degrees
	
	set polar
	set grid polar
	unset xtics 
	unset ytics
	unset border
	unset key
	set size square
	set rrange [90:0]
	
	set ttics add ("N" 0, "E" 90, "S" 180, "W" -90) font ":Bold"
	set rtics format "%.0f°"
	set rtics 30
	set rlabel "Altitude" offset -2 font ":Bold"
	set border polar
	set style data lines
	
	# By convention North (azimuth 0°) is at the top with angles increasing clockwise
	set theta top clockwise
	
	plot dummy using (winter_azi[$1]):(winter_alt[$1]) lw 2 title "   Winter Solstice" at beg left, \
	     dummy using (summer_azi[$1]):(summer_alt[$1]) lw 2 title "   Summer Solstice" at beg left, \
	     dummy using (today_azi[$1]):(today_alt[$1]) lw 2 title today at end right
	
	pause -1 "<cr> to continue"
	
	set encoding save_encoding
	reset
snippet special_chars.dem
	set title "set minussign; set micro"
	unset minus
	unset micro
	set label 1 gprintf("no special characters:  %.2s %cUnits", -1.23e-6)
	set minus
	set micro
	set label 2 gprintf("   special characters:  %.2s %cUnits", -1.23e-6)
	set label 3 sprintf("current encoding %s", GPVAL_ENCODING)
	
	set label 1 at 1,4 font ",20"
	set label 2 at 1,3 font ",20"
	set label 3 at 1,1 font ",15" noenhanced
	
	unset border
	unset xtics
	unset ytics
	unset key
	
	plot [0:10][0:10] -10
	
	pause -1 "Hit return to continue"
	reset

snippet spline.dem
	#
	# $Id: spline.dem,v 1.6 2004/09/28 06:06:10 sfeam Exp $
	#
	# Some curve plotting using common cubic polynomial basis function for cagd.
	#
	#				Gershon Elber, Aug. 1992
	#
	set xrang [0:1]
	set grid
	set key box
	
	set yrange[-0.2:1.4]
	m0(x) = 1
	m1(x) = x
	m2(x) = x**2
	m3(x) = x**3
	set title "The cubic Monomial basis functions"
	plot m0(x), m1(x), m2(x), m3(x)
	pause -1 "Press return to continue"
	
	h00(x) = x**2 * ( 2 * x - 3) + 1
	h01(x) = -x**2 * (2 * x - 3)
	h10(x) = x * (x - 1)**2
	h11(x) = x**2 * (x - 1)
	
	set title "The cubic Hermite basis functions"
	plot h00(x), h01(x), h10(x), h11(x)
	pause -1 "Press return to continue"
	
	bez0(x) = (1 - x)**3
	bez1(x) = 3 * (1 - x)**2 * x
	bez2(x) = 3 * (1 - x) * x**2
	bez3(x) = x**3
	set title "The cubic Bezier basis functions"
	plot bez0(x), bez1(x), bez2(x), bez3(x)
	pause -1 "Press return to continue"
	
	bsp0(x) = ( 1 - 3 * x + 3 * x**2 - x**3 ) / 6;
	bsp1(x) = ( 4 - 6 * x**2 + 3 * x**3 ) / 6;
	bsp2(x) = ( 1 + 3 * x + 3 * x**2 - 3 * x**3 ) / 6
	bsp3(x) = x**3 / 6
	set title "The cubic uniform Bspline basis functions"
	plot bsp0(x), bsp1(x), bsp2(x), bsp3(x)
	pause -1 "Press return to continue"
	
	y0 = 1
	y1 = 0.2
	y2 = 0.8
	y3 = 0
	
	x0 = 0
	x1 = 0.33
	x2 = 0.66
	x3 = 1
	
	xv0 = -0.3
	yv0 = 0.5
	xv1 = -0.4
	yv1 = 0.2
	
	set arrow from x0,y0 to x1,y1 nohead
	set arrow from x1,y1 to x2,y2 nohead
	set arrow from x2,y2 to x3,y3 nohead
	
	cub_bezier_x(t) = bez0(t) * x0 + bez1(t) * x1 + bez2(t) * x2 + bez3(t) * x3
	cub_bezier_y(t) = bez0(t) * y0 + bez1(t) * y1 + bez2(t) * y2 + bez3(t) * y3
	cub_bsplin_x(t) = bsp0(t) * x0 + bsp1(t) * x1 + bsp2(t) * x2 + bsp3(t) * x3
	cub_bsplin_y(t) = bsp0(t) * y0 + bsp1(t) * y1 + bsp2(t) * y2 + bsp3(t) * y3
	
	set parametric
	set trange [0:1]
	set title "The cubic Bezier/Bspline basis functions in use"
	plot cub_bezier_x(t), cub_bezier_y(t) with lines lt 2,\
	     cub_bsplin_x(t), cub_bsplin_y(t) with lines lt 3
	pause -1 "Press return to continue"
	
	unset arrow
	#
	# Note the arrows here, scaled by 1/3 so they will fit into plotting area
	#
	set arrow from x1,y1 to x1+xv0/3,y1+yv0/3
	set arrow from x2,y2 to x2+xv1/3,y2+yv1/3
	set arrow from x1,y1 to x1+xv0,y1+yv0
	set arrow from x2,y2 to x2+xv1,y2+yv1
	
	cub_hermit_x1(t) = h00(t) * x1 + h01(t) * x2 + h10(t) * xv0 + h11(t) * xv1
	cub_hermit_y1(t) = h00(t) * y1 + h01(t) * y2 + h10(t) * yv0 + h11(t) * yv1
	cub_hermit_x2(t) = h00(t) * x1 + h01(t) * x2 + h10(t) * xv0*3 + h11(t) * xv1*3
	cub_hermit_y2(t) = h00(t) * y1 + h01(t) * y2 + h10(t) * yv0*3 + h11(t) * yv1*3
	set title "The cubic Hermite basis functions in use"
	plot cub_hermit_x1(t), cub_hermit_y1(t) with lines lt 2,\
	     cub_hermit_x2(t), cub_hermit_y2(t) with lines lt 3
	pause -1 "Press return to continue"
	reset

snippet stats.dem
	#
	# Exercise the "stats" command.
	# Skip this demo cleanly if the stats option was not configured.
	#
	if (!strstrt(GPVAL_COMPILE_OPTIONS,"+STATS")) {
	    print "No support for stats command"
	} else {
	
	set title "Use of stats command to find min/max/mean before plotting\nOne data column"
	
	set style data line
	set offset 0,0,.5,.5
	set autoscale fix
	set key left Left
	
	stats 'orbital_elements.dat' index 1 using 2 prefix "A"
	
	set arrow 1 from A_index_min, graph 0.1 to A_index_min, A_min fill
	set arrow 2 from A_index_max, graph 0.9 to A_index_max, A_max fill
	set label 1 at A_index_min, graph 0.1 "min" center offset 0,-1
	set label 2 at A_index_max, graph 0.9 "max" center offset 0,1
	
	plot 'orbital_elements.dat' index 1 using 0:2 title "  Data" lw 2, \
	     A_mean title "  Mean"
	
	pause -1 "Hit return to continue"
	
	set title "Use of stats command to find min/max/mean before plotting\nTwo data columns"
	
	f(x) = log(1+x)
	
	stats 'orbital_elements.dat' index 1 using (f($0)):2 prefix "B"
	
	set arrow 1 from B_pos_min_y, graph 0.1 to B_pos_min_y, B_min_y fill
	set arrow 2 from B_pos_max_y, graph 0.9 to B_pos_max_y, B_max_y fill
	set label 1 at B_pos_min_y, graph 0.1 "min" center offset 0,-1
	set label 2 at B_pos_max_y, graph 0.9 "max" center offset 0,1
	
	plot 'orbital_elements.dat' index 1 using (f($0)):2 title "  Data" lw 2, \
	     B_mean_y title "  Mean", \
	     B_slope * x + B_intercept title "Linear fit"
	
	pause -1 "Hit return to continue"
	
	reset
	}

snippet steps.dem
	#
	# $Id: steps.dem,v 1.5 2003/10/17 15:02:21 mikulik Exp $
	#
	# This file will serve as the datafile used in demonstrating the 
	# "plot with steps" option.   Here is a gnuplot input file
	# which uses "plot with steps", inverse error function, normal
	# distribution function, and the inverse normal distribution
	# function.
	
	set title "Compare steps, fsteps and histeps"
	plot [0:12][0:13] "steps.dat" notitle with points,  \
	 "steps.dat" title 'steps' with steps, \
	 'steps.dat' title 'fsteps' with fsteps, \
	 'steps.dat' title 'histeps' with histeps
	
	pause -1 "Hit return for demonstration of automatic histogram creation"
	set title "Histogram built from unsorted data by 'smooth frequency'"
	set ylabel 'counts per bin'
	set xlabel 'bins'
	plot 'hemisphr.dat' u (floor($1*20)):(1) smooth frequency with histeps
	unset xlabel
	unset ylabel
	
	pause -1 "Hit return for normal distribution function."
	set title "Normal Distribution Function"
	plot [-3:3][0:1] norm(x)
	
	pause -1 "Hit return for inverse error function."
	set title "Inverse Error Function"
	plot [-1:1] inverf(x)
	
	pause -1 "Hit return for inverse normal distribution function."
	set title "Inverse Normal Distribution Function"
	plot [0:1] invnorm(x)
	
	pause -1 "Press return to continue"
	reset

snippet stringvar.dem
	#
	# Miscellaneous neat things you can do using the string variables code
	#
	iswindows = substr(GPVAL_SYSNAME, 1, 7) eq "Windows"
	unset border
	
	set print $EXAMPLE
	print ""
	print "Exercise substring handling"
	print ""
	beg = 2
	end = 4
	print "beg = ",beg," end = ",end
	foo = "ABCDEF"
	print "foo           = ",foo
	print "foo[3:5]      = ",foo[3:5]
	print "foo[1:1]      = ",foo[1:1]
	print "foo[5:3]      = ",foo[5:3]
	print "foo[beg:end]  = ",foo[beg:end]
	print "foo[end:beg]  = ",foo[end:beg]
	print "foo[5:]       = ",foo[5:]
	print "foo[5:*]      = ",foo[5:*]
	print "foo[:]        = ",foo[:]
	print "foo[*:*]      = ",foo[*:*]
	print "foo.foo[2:2]  = ",foo.foo[2:2]
	print "(foo.foo)[2:2]= ",(foo.foo)[2:2]
	print ""
	print "foo[1:1] eq 'A' && foo[2:2] ne 'X' = ", \
	      (foo[1:1] eq 'A' && foo[2:2] ne 'X') ? "true" : "false"
	
	unset print
	
	do for [i=1:|$EXAMPLE|] {
	    set label i $EXAMPLE[i] at graph 0.1, graph (1.0 - i*0.05)
	}
	
	unset xtics
	unset ytics
	set yrange [0:1]
	plot 0 lc bgnd notitle
	
	pause -1 "Hit return to continue"
	
	
	set print $EXAMPLE
	print "Exercise string handling functions"
	print ""
	print "foo          = ",foo
	print "strlen(foo)  = ",strlen(foo)
	print "substr(foo,3,4) = ",substr(foo,3,4)
	print ""
	if (!iswindows) {
	  haystack = system("date")
	} else {
	  haystack = system("date /T") . " " . system("time /T")
	}
	needle = ":"
	S = strstrt(haystack,needle)
	print "haystack     = \`date\`"
	print "haystack     = ",haystack
	print "needle       = ",needle
	print "S = strstrt(haystack,needle) = ",S
	print "haystack[S-2:S+2] = ",haystack[S-2:S+2]
	print "It is now " . haystack[S-2:S+2]
	#
	print ""
	print "words(haystack)   = ",words(haystack)
	print "word(haystack,5)  = ",word(haystack,5)
	#
	print ""
	#
	foo = sprintf("%40d %40d %40d %40d %40d %40d",1,2,3,4,5,6)
	if (strlen(foo) == 245) print "sprintf output of long strings works OK"
	if (strlen(foo) != 245) print "sprintf output of long strings BROKEN"
	print ""
	
	unset print
	
	unset label
	do for [i=1:|$EXAMPLE|] {
	    set label i $EXAMPLE[i] at graph 0.1, graph (1.0 - i*0.05)
	}
	
	unset xtics
	unset ytics
	set yrange [0:1]
	plot 0 lc bgnd notitle
	
	pause -1 "Hit return to continue"
	set print $EXAMPLE
	print "Exercise word and words functions"
	print ""
	foo = "word and words can handle 'quoted string'"
	print "foo = ",foo
	print "words(foo) = ",words(foo)
	print "word(foo, 6) = ",word(foo,6)
	print ""
	foo = "\"double quotes\" or 'single quotes'"
	print "foo = ",foo
	print "words(foo) = ",words(foo)
	print ""
	foo = "Apostrophes inside words don't matter"
	print "foo = ",foo
	print "word(foo, 4) = ",word(foo, 4)
	
	unset print
	
	unset label
	do for [i=1:|$EXAMPLE|] {
	    set label i $EXAMPLE[i] at graph 0.1, graph (1.0 - i*0.05)
	}
	
	unset xtics
	unset ytics
	set yrange [0:1]
	plot 0 lc bgnd notitle
	
	pause -1 "Hit return to continue"
	reset
	#
	set xrange [300:400]
	set title "String-valued expression in using spec"
	plot 'silver.dat' using 1:2 with linespoints notitle, \
	     '' using 1:2:(sprintf("[%.0f,%.0f]",$1,$2)) with labels
	#
	pause -1 "Hit return to continue"
	#
	set xrange [0:1]
	set yrange [0:1]
	set title "Constant string expressions as plot symbols"
	set xrange [250:500]
	set auto y
	set style data lines
	plot 'silver.dat' u 1:2:($3+$1/50.) w filledcurves above title 'Above', \
	     '' u 1:2:($3+$1/50.) w filledcurves below title 'Below', \
	     '' u 1:2 lt -1 lw 0.5 notitle, \
	     '' u 1:($3+$1/50.) lt 3 lw 0.5 notitle, \
	     '' using 1:2:( ($2>($3+$1/50.)) ? "Up" : "Dn" ) with labels \
	        title 'plot <foo> using 1:2:( ($3>$2) ? "Up" : "Dn" ) with labels'
	#
	pause -1 "Hit return to continue"
	#
	reset
	set title "String-valued functions to generate datafile names"
	set key title 'file(i) = sprintf("%1d.dat",i); N=2; M=3'
	set key left width 25 Left reverse
	N = 2
	M = 3
	file(i) = sprintf("%1d.dat",i)
	plot 5*sin(x)/x, file(N), file(M)
	#
	pause -1 "Hit return to continue"
	#
	reset
	fmt = '%Y-%m-%d %H:%M:%S'
	time_str = '2005-05-09 19:44:12'
	seconds = strptime(fmt, time_str)
	time_str2 = strftime(fmt, seconds+10.)
	print ''
	print 'time_str          = "', time_str, '"'
	print '-> seconds        = ', seconds
	print '   seconds + 10.  = ', seconds+10.
	print '-> time_str2      = "', time_str2, '"'
	#
	print ""
	#
	set xdata time
	set key inside left
	#set format x '%Y-%m-%d'
	fmt = "%d/%m/%y\t%H%M"
	print "read_time(fmt, c) =" \
	      . " strptime(fmt, stringcolumn(c).' '.stringcolumn(c+1))"
	read_time(fmt, c) = strptime(fmt, stringcolumn(c).' '.stringcolumn(c+1))
	plot 'timedat.dat' skip 1 using (read_time(fmt,1)):3 with histeps, \
	     'timedat.dat' skip 1 using (read_time(fmt,1)):($3-0.01):2 with labels title ''
	#
	print ""
	pause -1 "Hit return to continue"
	#
	reset

snippet surface1.dem
	#
	# $Id: surface1.dem,v 1.15 2017/03/01 18:42:49 sfeam Exp $
	#
	set title "3D surface from a grid (matrix) of Z values"
	set xrange [-0.5:4.5]
	set yrange [-0.5:4.5]
	
	set grid
	set hidden3d
	$grid << EOD
	5 4 3 1 0
	2 2 0 0 1
	0 0 0 1 0
	0 0 0 2 3
	0 1 2 4 3
	EOD
	splot '$grid' matrix with lines notitle
	
	pause -1 "Hit return to continue"
	unset hidden3d
	undefine $*
	
	set samples 21
	set isosample 11
	set xlabel "X axis" offset -3,-2
	set ylabel "Y axis" offset 3,-2
	set zlabel "Z axis" offset -5
	set title "3D surface from a function"
	set label 1 "This is the surface boundary" at -10,-5,150 center
	set arrow 1 from -10,-5,120 to -10,0,0 nohead
	set arrow 2 from -10,-5,120 to 10,0,0 nohead
	set arrow 3 from -10,-5,120 to 0,10,0 nohead
	set arrow 4 from -10,-5,120 to 0,-10,0 nohead
	set xrange [-10:10]
	set yrange [-10:10]
	splot x*y
	pause -1 "Hit return to continue (1)"
	unset arrow
	unset label
	set grid
	splot x**2+y**2, x**2-y**2
	pause -1 "Hit return to continue (2)"
	splot x**2+y**2, x**2-y**2, x*y
	pause -1 "Hit return to continue (3)"
	splot x**2+y**2, x**2-y**2, x*y, (x**3+y**3)/10
	pause -1 "Hit return to continue (4)"
	set ticslevel 0.0
	set title "3D gnuplot demo ( ticslevel = 0.0 )"
	rep
	pause -1 "Hit return to continue (5)"
	set ticslevel 2.0
	set title "3D gnuplot demo ( ticslevel = 2.0 )"
	rep
	pause -1 "Hit return to continue (6)"
	set ticslevel 0.5
	set title "3D gnuplot demo ( ticslevel = 0.5 )"
	rep
	pause -1 "Hit return to continue (7)"
	set title "3D gnuplot demo"
	unset grid
	splot x*y with points
	pause -1 "Hit return to continue (8)"
	unset xtics
	unset ytics
	set xrange [-1:1]
	set yrange [-1:1]
	set title "Surfaces with no grid or tics"
	splot x*y with lines, x**2*y**3 with dots, x**3*y*2 with points
	pause -1 "Hit return to continue (9)"
	set xtics ("low" -3, "mid" 0, "high" 3)
	set ytics -2,0.5,2
	set xrange [-3:3]
	set yrange [-3:3]
	set log z
	set title "Surfaces with z log scale"
	splot x**2*y**2 + 2, x**2*y**4 + 2, x**4*y**2 + 2
	pause -1 "Hit return to continue (10)"
	unset log z
	set xtics autofreq
	set ytics autofreq
	set xrange [-1:1]
	set yrange [-1:1]
	set samples 51
	set isosample 21
	set dummy u,v
	set title "3D gnuplot demo"
	splot u*v / (u**2 + v**2 + 0.1)
	pause -1 "Hit return to continue (11)"
	splot [x=-3:3] [y=-3:3] sin(x) * cos(y)
	pause -1 "Hit return to continue (12)"
	set zrange [-1.0:1.0]
	replot
	pause -1 "Hit return to continue (13)"
	set title "Sinc function"
	set zrange [-1:1]
	set label 1 "This is equal to 1" at 0,3.2,1 left
	set arrow 1 from 0,3,1 to 0,0,1
	sinc(u,v) = sin(sqrt(u**2+v**2)) / sqrt(u**2+v**2)
	splot [-5:5.01] [-5:5.01] sinc(u,v)
	pause -1 "Hit return to continue (14)"
	set view 70,20,1
	set zrange [-0.5:1.0]
	set ztics -1,0.25,1
	set label 1 "This is equal to 1" at -5,-2,1.5 centre
	set arrow 1 from -5,-2.1,1.4 to 0,0,1
	splot [-12:12.01] [-12:12.01] sinc(u,v)
	pause -1 "Hit return to continue (15)"
	set title "fence plot constructed with separate parametric surfaces"
	# A method suggested by Hans-Bernhard Broeker
	# <broeker@physik.rwth-aachen.de>: display a separate parametric
	# surface for each fence.
	set zrange [-1:1]
	unset label
	unset arrow
	set xrange [-5:5]; set yrange [-5:5]
	set arrow from 5,-5,-1.2 to 5,5,-1.2 lt -1
	set label 1 "increasing v" at 6,0,-1
	set arrow from 5,6,-1 to 5,5,-1 lt -1
	set label 2 "u=0" at 5,6.5,-1
	set arrow from 5,6,sinc(5,5) to 5,5,sinc(5,5) lt -1
	set label 3 "u=1" at 5,6.5,sinc(5,5)
	set parametric
	set hidden3d offset 0	# front/back coloring makes no sense for fenceplot #
	set isosamples 2,33
	xx=-5; dx=(4.99-(-4.99))/9
	x0=xx; xx=xx+dx
	x1=xx; xx=xx+dx
	x2=xx; xx=xx+dx
	x3=xx; xx=xx+dx
	x4=xx; xx=xx+dx
	x5=xx; xx=xx+dx
	x6=xx; xx=xx+dx
	x7=xx; xx=xx+dx
	x8=xx; xx=xx+dx
	x9=xx; xx=xx+dx
	splot [u=0:1][v=-4.99:4.99] \
		x0, v, (u<0.5) ? -1 : sinc(x0,v) notitle, \
		x1, v, (u<0.5) ? -1 : sinc(x1,v) notitle, \
		x2, v, (u<0.5) ? -1 : sinc(x2,v) notitle, \
		x3, v, (u<0.5) ? -1 : sinc(x3,v) notitle, \
		x4, v, (u<0.5) ? -1 : sinc(x4,v) notitle, \
		x5, v, (u<0.5) ? -1 : sinc(x5,v) notitle, \
		x6, v, (u<0.5) ? -1 : sinc(x6,v) notitle, \
		x7, v, (u<0.5) ? -1 : sinc(x7,v) notitle, \
		x8, v, (u<0.5) ? -1 : sinc(x8,v) notitle, \
		x9, v, (u<0.5) ? -1 : sinc(x9,v) notitle
	pause -1 "Hit return to continue (16)"
	set title "\"fence plot\" using single parametric surface with undefined points"
	# Another method suggested by Hans-Bernhard Broeker
	# <broeker@physik.rwth-aachen.de>: Use parametric variable u to control
	# x, y, and z.  u=3*fencenumber+remainder, where the remainder selects
	# one of three types of points:
	# 	  remainder=0: x=xmin+fencenumber*dx, y=v, z=zbase
	# 	  remainder=1: x=xmin+fencenumber*dx, y=v, z=f(x,y)
	# 	  remainder=2: x=xmin+fencenumber*dx, y=v, z=1/0 (undefined)
	set parametric
	unset label
	unset arrow
	xmin=-4.99; xmax=5; n=10; dx=(xmax-xmin)/(n-1.)
	set isosamples 3*n,33
	zbase=-1
	set arrow from 5,-5,-1.2 to 5,5,-1.2 lt -1
	set label 1 "increasing v" at 6,0,-1
	set arrow from -5,-5,-1.2 to 5,-5,-1.2 lt -1
	set label 2 "increasing u" at 0,-5,-1.5
	set arrow from 5,6,-1 to 5,5,-1 lt -1
	set label 3 "floor(u)%3=0" at 5,6.5,-1
	set arrow from 5,6,sinc(5,5) to 5,5,sinc(5,5) lt -1
	set label 4 "floor(u)%3=1" at 5,6.5,sinc(5,5)
	splot [u=.5:3*n-.5][v=-4.99:4.99] \
		 xmin+floor(u/3)*dx, v, ((floor(u)%3)==0) ? zbase : \
				 (((floor(u)%3)==1) ? sinc(xmin+u/3.*dx,v) : 1/0) lt 3 notitle
	pause -1 "Hit return to continue (17)"
	unset hidden3d; unset parametric; set isosamples 20,20
	set xrange [-1:1]; set yrange [-1:1]
	set ztics autofreq
	unset arrow
	unset label
	set log
	set auto
	set title "This has logarithmic scale"
	splot [x=1:15] [y=1:15] x**2+y**2
	pause -1 "Hit return to continue (18)"
	unset log
	set xrange [0:15]
	set yrange [0:15]
	set auto
	#set zrange [-0.6:0.7]
	set ticslevel 0
	set style data lines
	set title "Data grid plotting"
	set parametric
	splot "glass.dat"
	pause -1 "Hit return to continue (19)"
	splot "glass.dat" using 3:2:1
	pause -1 "Hit return to continue (20)"
	set zrange [-1.2:1.2]
	set ticslevel 0.5
	unset parametric
	set xlabel "line index within group"
	set ylabel "group index"
	splot "glass.dat" using 1, "glass.dat" using 2, "glass.dat" using 3
	pause -1 "Hit return to continue (21)"
	set title "splot of part of a data file"
	set xlabel "X axis" offset -3,-2
	set ylabel "Y axis" offset 3,-2
	set hidden3d
	splot 'glass.dat' every 2::0::12 with lines
	pause -1 "Hit return to continue (22)"
	set title "splot with \"set pm3d\" (implemented with some terminals)"
	set pm3d
	set hidden3d
	splot 'glass.dat' every 2::0::12 with lines
	pause -1 "Hit return to continue (23)"
	unset hidden3d
	unset pm3d
	set param
	set title "Test of spherical coordinates"
	set mapping spherical
	splot "glass.dat"
	pause -1 "Hit return to continue (24)"
	
	# mandelbrot demo
	set title "Mandelbrot function"
	unset parametric
	set mapping cartesian
	set view 60,30,1,1
	set auto
	set isosamples 60
	set hidden3d
	compl(a,b)=a*{1,0}+b*{0,1}
	mand(z,a,n) = n<=0 || abs(z)>100 ? 1:mand(z*z+a,a,n-1)+1
	splot [-2:1][-1.5:1.5] mand({0,0},compl(x,y),30)     
	pause -1 "Hit return to continue (25)"
	reset

snippet surface2.dem
	#
	# $Id: surface2.dem,v 1.4 1999/10/17 19:17:01 lhecking Exp $
	#
	set parametric
	set isosamples 50,10
	set hidden
	set key below
	
	set title "Parametric Sphere"
	set urange [-pi/2:pi/2]
	set vrange [0:2*pi]
	set ztics nomirror -1.0,0.25,1.0
	set view 45,50
	splot cos(u)*cos(v),cos(u)*sin(v),sin(u)
	pause -1 "Hit return to continue (1)"
	
	set view ,,,0.7
	set title "Parametric Sphere, crunched z axis"
	replot
	pause -1 "Hit return to continue (2)"
	
	set view ,,,1.4
	set title "Parametric Sphere, enlarged z axis"
	replot
	pause -1 "Hit return to continue (3)"
	
	set view ,,,1.0
	
	set title "Parametric Torus"
	set urange [0:2*pi]
	set vrange [0:2*pi]
	set zrange [-1:1]        # imitate old 'set view' hack
	splot (1-0.2*cos(v))*cos(u),(1-0.2*cos(v))*sin(u),0.2*sin(v)
	pause -1 "Hit return to continue (4)"
	
	
	set title "Parametric Hexagon"
	set urange [-1.3:1.3]
	set vrange [0:2*pi]
	set autoscale z
	set ticslevel 0.	# reserve more space z direction.
	set view ,,0.7,1.4	# crunch xyz, and re-extend z back to full size
	set ztics autofreq
	splot cos(v)**3*cos(u)**3,sin(v)**3*cos(u)**3,sin(u)**3
	pause -1 "Hit return to continue (5)"
	
	set view ,,1.,1.
	
	set title "Parametric Helix"
	set isosamples 100,20
	set urange [0:10*pi]
	set vrange [0:2*pi]
	set autoscale z
	splot (1-0.1*cos(v))*cos(u),(1-0.1*cos(v))*sin(u),0.1*(sin(v)+u/1.7-10)
	pause -1 "Hit return to continue (6)"
	
	
	set title "Parametric Shell (clipped to limited z range)"
	set isosamples 40,20
	set view 50,30,1.0
	set urange [0:2*pi]
	set vrange [0:2*pi]
	set zrange [-3:1.5]
	splot cos(u)*u*(1+cos(v)/2),sin(v)*u/2,sin(u)*u*(1+cos(v)/2)
	pause -1 "Hit return to continue (7)"
	
	set autoscale z
	set title "Parametric Shell (automatic z range)"
	replot
	pause -1 "Hit return to continue (8)"
	
	set title "Interlocking Tori"
	
	set urange [-pi:pi]
	set vrange [-pi:pi]
	set isosamples 50,20
	splot cos(u)+.5*cos(u)*cos(v),sin(u)+.5*sin(u)*cos(v),.5*sin(v) with lines, \
	      1+cos(u)+.5*cos(u)*cos(v),.5*sin(v),sin(u)+.5*sin(u)*cos(v) with lines
	pause -1 "Hit return to continue (9)"
	reset

snippet textbox.dem
	set grid
	unset xtics 
	set xrange [ -0.50 : 12.00 ]
	set yrange [ 30.00 : 110.0 ]
	unset ylabel
	set bmargin  3
	unset key
	
	set multiplot layout 1,2
	
	set style textbox opaque border lc "blue"
	set title "textboxes with blue border"
	plot 'table.dat' using 0:2 title column with lines lw 3,  \
	     ''          using 0:2:2 with labels center boxed notitle column
	
	set style textbox opaque margins 6,1.5 fc "grey75" border lc black
	set title "larger textboxes with grey fill"
	replot
	
	unset multiplot
	
	pause -1 "<cr> to continue"
	reset

snippet textcolor.dem
	#
	# $Id: textcolor.dem,v 1.2 2003/10/17 15:02:21 mikulik Exp $
	#
	# Textcolor in 2D
	#
	reset
	#
	set title "Textcolor options in 2D plot (notice this title in color)" tc lt 1
	set xrange [-2:2]
	set yrange [-2:2]
	#
	set label 11 "label with textcolor lt 1"  at -1.5, 1.8  front nopoint tc lt 1
	set label 10 "label with tc default" at -1.5, 1.6  front nopoint tc def
	set label 12 "label with tc lt 2"    at -1.5, 1.4  front nopoint tc lt 2
	set label 13 "label with tc lt 3"    at -1.5, 1.2  front nopoint tc lt 3
	#
	set xlabel 'color of xlabel should be lt 4' tc lt 4
	set ylabel 'color of ylabel should still be black'
	#
	plot sin(x)
	#
	pause -1 "Hit return to continue"
	#
	#  Textcolor in 3D (req. pm3d)
	#
	set title "Textcolor options in splot (notice this title in color)" tc lt 1
	set samples 20; set isosamples 20 
	set autoscale
	set key box
	set pm3d at s
	set colorbox horiz user origin .1,.12 size .8,.015
	set view 58, 64, 0.83
	set xrange [-10:10]
	set yrange [-10:10]
	set zrange [-10:10]
	#
	# Test labels
	#
	set label 1 "textcolor palette z" at 12,-10,-10  nopoint  tc pal z
	set label 3 "tc pal z" at 12, -6, -6  nopoint  tc pal z
	set label 4 "tc pal z" at 12, -3, -3  nopoint  tc pal z
	set label 5 "tc pal z" at 12, -0,  0  nopoint  tc pal z
	set label 6 "tc pal z" at 12,  3,  3  nopoint  tc pal z
	set label 7 "tc pal z" at 12,  6,  6  nopoint  tc pal z
	set label 8 "tc pal z" at 12,  9,  9  nopoint  tc pal z
	#
	set xlabel 'xlabel should be lt 4' tc lt 4
	set cblabel 'color cblabel' textcolor lt 3
	#
	set label 10 "textcolor lt 1" at -10,-8,24  front nopoint tc lt 1
	set label 11 "tc lt 2"    at -10,-8,21  front nopoint tc lt 2
	set label 12 "tc lt 3"    at -10,-8,18  front nopoint tc lt 3
	set label 13 "textcolor default" at -10,-8,15  front nopoint tc def
	set label 14 "textcolor cb 5"   at -10,-8,12  front nopoint tc pal cb 5
	set label 15 "tc cb 0"    at -10,-8,9  front nopoint tc pal cb 0
	set label 16 "tc cb -5"    at -10,-8,6  front nopoint tc pal cb -5
	set label 17 "textcolor frac .75" at -10,-8,3  nopoint tc pal frac .75
	set label 18 "tc frac .25" at -10,-8,0  nopoint tc pal frac .25
	#
	set ylabel 'ylabel should still be black'
	#set zlabel 'zlabel should have yet another color' tc lt 2
	#
	splot y
	#
	pause -1 "Hit return to continue"
	#
	
	reset
	

snippet textrotate.dem
	#
	# $Id: textrotate.dem,v 1.2 2003/10/17 15:02:21 mikulik Exp $
	#
	# Demonstrate rotation of label text
	#
	reset
	#
	set title "Rotation of label text"
	#
	set xrange [-5:5]
	set yrange [-5:5]
	set xlabel "xlabel"
	set ylabel "ylabel"
	#
	set label 1 "Default" at -4,4
	#
	set label 2 "rotate left"   at 0,0 rotate left
	set label 3 "rotate right"  at 0.5,0 rotate right
	set label 4 "rotate center" at 1.0,0 rotate center
	#
	set label 12 "rotate by -90 left"   at 2.0,0 rotate by -90 left
	set label 13 "rotate by -90 right"  at 2.5,0 rotate by -90 right
	set label 14 "rotate by -90 center" at 3.0,0 rotate by -90 center
	#
	set label 21 "  rotate by 45" at -3.0,0.0 rotate by 45 point ps 2
	set label 22 "  rotate by 90" at -3.0,0.0 rotate by 90 point ps 2
	set label 23 "  rotate by -30" at -3.0,0.0 rotate by -30 point ps 2
	set label 24 "  rotate by -60" at -3.0,0.0 rotate by -60 point ps 2
	set label 25 "  rotate by -90" at -3.0,0.0 rotate by -90 point ps 2
	#
	plot 0
	#
	pause -1 "Hit return to continue"
	
	reset
	

snippet tics.dem
	#
	# $Id: tics.dem,v 1.1 2005/08/05 15:53:23 mikulik Exp $
	#
	# demo for tics settings
	
	set xlabel "x"
	set ylabel "y"
	set mxtics
	
	set title "Default tics settings"
	set xrange [-15:15]
	set yrange [-0.25:1]
	plot sin(sqrt(x**2))/sqrt(x**2) notitle
	pause -1 "Hit return to continue"
	
	set title "Different modification of tics settings"
	set tics scale 3,2 rotate by 45
	set xtics out offset 0,-1.0
	replot
	pause -1 "Hit return to continue"
	
	set xtics textcolor rgb "red" norotate
	set ytics rotate by 90 offset 2,0
	replot
	pause -1 "Hit return to continue"
	
	set title "Modification of tics settings (pm3d map with colorbar)"
	set pm3d map
	set border 4095
	set samples 25
	set isosamples 20
	set palette color positive
	set samples 50; set isosamples 50
	set tics norotate nooffset
	set cbtics in scale 4
	set xrange [-15:15]
	set yrange [-15:15]
	set zrange [-0.25:1]
	splot sin(sqrt(x**2+y**2))/sqrt(x**2+y**2) notitle
	pause -1 "Hit return to continue"
	
	reset
	
	print "End of tics demo."

snippet timedat.dem
	#
	# $Id: timedat.dem,v 1.13.2.1 2017/09/03 23:32:22 sfeam Exp $
	#
	
	set title "Fsteps plot\nwith date and time as x-values"
	set style data fsteps
	set xlabel "Date\nTime"
	set timefmt "%d/%m/%y\t%H%M"
	set yrange [ 0 : ]
	set xdata time
	set xrange [ "1/6/93":"1/11/93" ]
	set ylabel "Concentration\nmg/l"
	set format x "%d/%m\n%H:%M"
	set grid
	set key left
	plot 'timedat.dat' using 1:3 t '', \
	     'timedat.dat' using 1:3 t 'Total P' with points, \
	     'timedat.dat' using 1:4 t '', \
	     'timedat.dat' using 1:4 t 'PO4' with points 
	pause -1 "Hit return to continue"
	
	reset
	
	set title "Time data on Y, millisecond precision" font ",14"
	set ydata time
	set timefmt "%s"
	set offset 0.5,1.5,.2,.2
	unset key
	
	fulltime(col) = strftime("%d %b %Y\n%H:%M:%.3S",column(col))
	parttime(col) = strftime("%H:%M:%.3S",column(col))
	
	plot '-' using 0:1:(fulltime(1)):xticlabels(2):yticlabels(parttime(1)) \
	         with labels point pt 7 left offset 1,1 font ",7"
	1390852607.1	A
	1390852607.2	B
	1390852607.4	C
	1390852607.8	D
	1390852608.4	E
	1390852610.001	F
	e
	
	pause -1 "Hit return to continue"
	
	reset
	set title "Date format (top) vs Time format (bottom)" font ":Bold,14"
	set xrange [-7000:3000]
	set link x2
	set tics font ",8"
	set x2tics format "%D\n%R" time
	set xtics format "%tH:%tM:%tS" time
	
	set yzeroaxis
	unset ytics
	set bmargin at screen .3
	set tmargin at screen .6
	
	plot x notitle
	
	pause -1 "Hit return to continue"
	
	t = -3672.5
	print 't = ',t
	print 'print strftime("%.2tM == %.2tS", t)' , "	", strftime("%.2tM min == %.2tS sec", t)
	print 'print strftime("%tM:%.2tS", t)  ' , "	", strftime("%tM:%.2tS", t)
	print 'print strftime("%tH:%tM:%.2tS", t)' , "	", strftime("%tH:%tM:%.2tS", t)
	
	t = 3672.5
	print 't = ',t
	print 'print strftime("%.2tM == %.2tS", t)' , "	", strftime("%.2tM min == %.2tS sec", t)
	print 'print strftime("%tM:%.2tS", t)  ' , "	", strftime("%tM:%.2tS", t)
	print 'print strftime("%tH:%tM:%.2tS", t)' , "	", strftime("%tH:%tM:%.2tS", t)
	
	reset

snippet transparent.dem
	#
	# Example of transparent fill areas
	# Ethan A Merritt - Aug 2006
	# NB:
	#    Not all terminal types support transparency
	#    Assumes UTF-8 support for plot titles
	#
	save_encoding = GPVAL_ENCODING
	set encoding utf8
	set style fill solid 1.0 noborder
	set style function filledcurves y1=0
	set clip two
	
	Gauss(x,mu,sigma) = 1./(sigma*sqrt(2*pi)) * exp( -(x-mu)**2 / (2*sigma**2) )
	d1(x) = Gauss(x, 0.5, 0.5)
	d2(x) = Gauss(x,  2.,  1.)
	d3(x) = Gauss(x, -1.,  2.)
	
	set xrange [-5:5]
	set yrange [0:1]
	
	unset colorbox
	
	set key title "Gaussian Distribution"
	set key top left Left reverse samplen 1
	
	#set obj 1 rect from graph 0,0 to graph 1,1
	#set obj 1 rect back fs solid 0.25 fc lt 4
	
	set title "Solid filled curves"
	plot d1(x) fs solid 1.0 lc rgb "forest-green" title "μ =  0.5 σ = 0.5", \
	     d2(x) lc rgb "gold" title "μ =  2.0 σ = 1.0", \
	     d3(x) lc rgb "dark-violet" title "μ = -1.0 σ = 2.0"
	
	pause -1 "Hit return to continue"
	
	set style fill transparent solid 0.5 noborder
	set title "Transparent filled curves"
	replot
	
	pause -1 "Hit return to continue"
	
	set style fill pattern 4 bo
	set title "Pattern-filled curves"
	replot
	
	pause -1 "Hit return to continue"
	
	set style fill transparent pattern 4 bo
	set title "Transparent pattern-filled curves"
	replot
	
	pause -1 "Hit return to continue"
	set encoding save_encoding
	reset

snippet transparent_solids.dem
	set title "Interlocking Tori - PM3D surface with depth sorting and transparency"
	
	unset border
	unset key
	set object 1 rect from screen 0, 0, 0 to screen 1, 1, 0 behind 
	set object 1 rect fc  rgb "gray"  fillstyle solid 1.0  border -1
	set view 64, 345, 1.24375, 0.995902
	set isosamples 50, 20
	unset xtics
	unset ytics
	unset ztics
	set parametric
	set dummy u,v
	set urange [ -pi : pi ]
	set vrange [ -pi : pi ]
	
	set palette rgbformulae 8, 9, 7
	set style fill  transparent solid 0.30 border
	set pm3d depthorder border linecolor rgb "#a0a0f0"  linewidth 0.5
	
	splot cos(u)+.5*cos(u)*cos(v),sin(u)+.5*sin(u)*cos(v),.5*sin(v) with pm3d, \
	    1+cos(u)+.5*cos(u)*cos(v),.5*sin(v),sin(u)+.5*sin(u)*cos(v) with pm3d
	
	pause -1 "Hit return to continue"
	
	unset title
	set label 1 "Kuen's Surface" at screen 0.57, 0.9
	set label 1 font "frscript,20"
	set pm3d depthorder border linetype -1 linewidth 0.5
	set style fill  transparent solid 0.65 border
	set palette
	set hidden3d
	
	set ticslevel 0
	unset xtics ; unset ytics ; unset ztics
	unset border ; unset colorbox ; unset key
	set lmargin at screen 0.1
	set bmargin at screen 0.1
	set rmargin at screen 0.9
	set tmargin at screen 0.9
	
	set parametric
	set dummy u,v
	set urange [-4.5:4.5]
	set vrange [0.05:pi-0.05]
	set isosamples 51,51
	set view 122, 357, 1.35, 1.08
	
	a = 1.0
	
	x(u,v) = 2.*a*(cos(u)+u*sin(u))*sin(v) / (1+u**2.*(sin(v))**2)
	y(u,v) = 2.*a*(sin(u)-u*cos(u))*sin(v) / (1+u**2.*(sin(v))**2)
	z(u,v) = a*log(tan(v/2.))+2.*cos(v)/(1+u**2.*(sin(v))**2)
	
	splot x(u,v), y(u,v), z(u,v) with pm3d
	
	pause -1 "Hit return to continue"
	reset

snippet ttics.dem
	#
	# Use of "set ttics" to place angular labels around polar grid
	#
	set title "Angle labels (ttics) for polar plots" offset 0,1
	set polar
	set ttics 0,30 format "%g".GPVAL_DEGREE_SIGN font ":Italic"
	set mttics 3
	set grid r polar 60
	unset xtics
	unset ytics
	set border 0
	set size square
	unset key
	
	set rrange [0:6.1]
	if (GPVAL_ENCODING eq "utf8") {
	    set ttics add ("π" 180, "π/2" 90, "3π/2" 270)
	} else {
	    set ttics add ("pi" 180, "pi/2" 90, "3pi/2" 270)
	}
	
	plot t lt 3 lw 2, -t lt 4 lw 2
	#
	pause -1 "<cr> to continue"
	
	set title "Polar plot with border and rotated labels for ttics"
	set ttics rotate
	set rrange [0:6.5]
	set border polar
	replot
	
	pause -1 "<cr> to continue"
	
	set title "Theta origin at top, increasing clockwise"
	set ttics norotate offset 1
	set theta clockwise top
	replot
	
	pause -1 "<cr> to continue"
	
	reset

snippet using.dem
	#
	# $Id: using.dem,v 1.6 2003/10/28 05:35:54 sfeam Exp $
	#
	# Requires data file "using.dat" from this directory,
	# so change current working directory to this directory before running.
	#
	
	set title "Convex     November 1-7 1989    Circadian"
	set key left box
	set xrange[-1:24]
	plot 'using.dat' using 2:4 title "Logged in" with impulses,\
	     'using.dat' using 2:4 title "Logged in" with points
	pause -1 "Hit return to continue"
	
	set xrange [1:8]
	#set xdtic
	set title "Convex     November 1-7 1989"
	set key below
	set label "(Weekend)" at 5,25 center
	plot 'using.dat' using 3:4 title "Logged in" with impulses,\
	     'using.dat' using 3:5 t "Load average" with points,\
	     'using.dat' using 3:6 t "%CPU used" with lines
	unset label
	pause -1 "Hit return to continue"
	reset

snippet utf8.dem
	# Ethan A Merritt - April 2006
	#
	# Test/Demonstration of UTF-8 support by gnuplot terminals.
	#
	# First we dump all 7-bit characters by octal code value, then display a
	# selection of 8-bit unicode (UTF-8) characters from various code points.
	#
	# This will only produce reasonable output if the current gnuplot
	# terminal supports multi-byte ("wide") characters and the current
	# font contains the corresponding UTF-8 encoded characters maps.
	#
	# Some possible terminal + font selections are:
	#
	#    set term x11 font "mbfont:sazanami mincho,vera,20"
	#    set term png font "/usr/share/fonts/ttf/arialuni.ttf" 13
	#    set term wxt
	#
	#
	save_encoding = GPVAL_ENCODING
	set encoding utf8
	#
	set label 100 at screen 0.5, 0.95 center 
	set label 100 "Dump all 7-bit characters and a selection of UTF-8 unicode characters\nCharacters that are missing from your current font will not appear in the output below\nOnly a portion of the characters on a unicode page are dumped"
	#
	set border 0
	unset xtics
	unset ytics
	#
	set label 101 "page 0 (Latin1)" at screen 0.05, 0.80
	set label 102 "0020-0040:" at screen 0.05, 0.75
	set label 103 "0041-0060:" at screen 0.05, 0.70
	set label 104 "0061-007E:" at screen 0.05, 0.65
	set label 105 ""        at screen 0.05, 0.60
	set label 106 "page 1:" at screen 0.05, 0.50
	set label 107 "page 3:" at screen 0.05, 0.45
	set label 108 "page 4:" at screen 0.05, 0.40
	set label 109 "pages 33,34:" at screen 0.05, 0.35
	set label 110 "pages 37,38:" at screen 0.05, 0.30
	set label 111 "Japanese:" at screen 0.05, 0.25
	set label 112 "Hebrew:" at screen 0.05, 0.20
	#
	set label 1  at screen 0.2, 0.80
	set label 2  at screen 0.2, 0.75
	set label 3  at screen 0.2, 0.70
	set label 4  at screen 0.2, 0.65
	set label 5  at screen 0.2, 0.60
	set label 6  at screen 0.2, 0.50
	set label 7  at screen 0.2, 0.45
	set label 8  at screen 0.2, 0.40
	set label 9  at screen 0.2, 0.35
	set label 10 at screen 0.2, 0.30
	set label 11 at screen 0.2, 0.25
	set label 12 at screen 0.2, 0.20
	#
	#set label 1 "\001\002\003\004\005\006\007\010\011 \013\014\015\016\017\020\021\022\023\024\025\026\027\030\031\032\033\034\035\036\037\040"
	set label 2 "\041\042\043\044\045\046\047\050\051\052\053\054\055\056\057\060\061\062\063\064\065\066\067\070\071\072\073\074\075\076\077\100"
	set label 3 "\101\102\103\104\105\106\107\110\111\112\113\114\115\116\117\120\121\122\123\124\125\126\127\130\131\132\133\134\135\136\137\140"
	set label 4 "\141\142\143\144\145\146\147\150\151\152\153\154\155\156\157\160\161\162\163\164\165\166\167\170\171\172\173\174\175\176\177"
	
	set label 5  "¡¢£¤¥¦§¨©ª«¬­®¯°±²³´µ¶·¸¹º»¼½¾¿" . "ÀÁÂÃÄÅÆÇÈÉÊËÌÍÎÏÐÑñòóôõö÷øùúûüýþÿ"
	set label 6  "ĀāĂăĄąĆćĈĉĊċČčŚśŜŝŞşŠšŴŵŶŷŸŹźŻżŽž"
	set label 7  "αβγδεζηθικλμνξοπρςστυφχψωϑϖ"
	set label 8  "абвгдежзийклмнопрстуфхцчшщъыьэюяѐёђѓєѕіїјљњћќѝўџ"
	set label 9  "ℎℏℋℓℛÅ∀∂∃∇∈∋√∞∧∨∩∪∫⊂⊃⊆⊇⊥⊗⊙"
	set label 10 "◎▲△▼▽☉☿♀♁♂♃♄♅♆♇♈♉♊♋♌♍♎♏♐♑"
	set label 11 "日本語フォント  かな　漢字"
	set label 12 "אבגדהוזחטיךכלםמןנסעףפץצקרשת"
	#
	set xrange [-1:1]
	set yrange [-1:1]
	plot -10 notitle
	
	pause -1 "Hit return to continue"
	set encoding save_encoding
	reset

snippet varcolor.dem
	#
	# Demo/test of variable color in many different plot styles
	#
	set boxwidth 0.2 abs
	set bars front
	by3(x) = (((int(x)%3)+1)/6.)
	by4(x) = (((int(x)%4)+1)/7.)
	rgbfudge(x) = x*51*32768 + (11-x)*51*128 + int(abs(5.5-x)*510/9.)
	
	set yrange [-4:10]
	set xrange [0:11]
	unset key #below 
	
	set title "variable color points, circles, candlesticks, boxes, and boxxyerror"
	plot 'candlesticks.dat' using 1:(1):1 with points pt 11 lc variable, \
	     '' using 1:2:(.1):1 with circles lc variable, \
	     '' using 1:3:2:6:5:1 with candlesticks lc variable, \
	     '' using ($1+.3):3:2:6:5:1 with financebars lc variable, \
	     '' using 1:(8):(by3($0)):(by4($0)):1 with boxxy lc var fs solid, \
	     '' using 1:(-$2/2):1 with boxes lc var
	
	pause -1 'Hit <cr> to continue'
	
	set title "variable color boxerror, xyerrorbars, impulses, vectors, and labels"
	unset colorbox
	plot 'candlesticks.dat' \
	        using 1:5:2:6:(.2):1 with boxerror lc var fs solid 0.5 border -1 , \
	     '' using 1:(1):1 with points pt 11 lc variable, \
	     '' using 1:(8):(by3($0)):(by4($0)):1 with xyerrorbars lc var, \
	     '' using ($1+.5):($2/2):1 with impulses lc var lw 3,\
	     '' using 1:(-3):(0.5):(1):1 with vectors lc var,\
	     '' using 1:(-1):1:1 with labels tc variable
	
	pause -1 'Hit <cr> to continue'
	
	set title "variable color using 'lc palette z'"
	set colorbox
	set errorbars lw 1.0 lc black dt '-'
	plot 'candlesticks.dat' using 1:(1):1 with points pt 11 lc pal z, \
	     '' using 1:2:(.1):1 with circles lc pal z, \
	     '' using 1:3:2:6:5:1 with candlesticks lc pal z, \
	     '' using ($1+.3):3:2:6:5:1 with financebars lc pal z, \
	     '' using 1:(8):(by3($0)):(by4($0)):1 with boxxy lc pal z fs solid, \
	     '' using 1:(-$2/2):1 with boxes lc pal z
	
	pause -1 'Hit <cr> to continue'
	
	set errorbars
	plot 'candlesticks.dat' \
	        using 1:5:2:6:(.2):1 with boxerror lc pal z fs solid 0.5, \
	     '' using 1:(1):1 with points pt 11 lc pal z, \
	     '' using 1:(8):(by3($0)):(by4($0)):1 with xyerrorbars lc pal z, \
	     '' using ($1+.5):($2/2):1 with impulses lc pal z lw 3,\
	     '' using 1:(-3):(0.5):(1):1 with vectors lc pal z,\
	     '' using 1:(-1):1:1 with labels tc pal z
	
	
	pause -1 'Hit <cr> to continue'
	
	set title "variable color using 'lc rgb variable'"
	plot 'candlesticks.dat' using 1:(1):(rgbfudge($1)) with points pt 11 lc rgb var, \
	     '' using 1:2:(.1):(rgbfudge($1)) with circles lc rgb var, \
	     '' using 1:3:2:6:5:(rgbfudge($1)) with candlesticks lc rgb var, \
	     '' using ($1+.3):3:2:6:5:(rgbfudge($1)) with financebars lc rgb var, \
	     '' using 1:(8):(by3($0)):(by4($0)):(rgbfudge($1)) with boxxy lc rgb var fs solid, \
	     '' using 1:(-$2/2):(rgbfudge($1)) with boxes lc rgb var
	
	pause -1 'Hit <cr> to continue'
	
	plot 'candlesticks.dat' \
	        using 1:5:2:6:(.2):(rgbfudge($1)) with boxerror lc rgb var fs solid 0.5 noborder, \
	     '' using 1:(1):(rgbfudge($1)) with points pt 11 lc rgb var, \
	     '' using 1:(8):(by3($0)):(by4($0)):(rgbfudge($1)) with xyerrorbars lc rgb var, \
	     '' using ($1+.5):($2/2):(rgbfudge($1)) with impulses lc rgb var lw 3,\
	     '' using 1:(-3):(0.5):(1):(rgbfudge($1)) with vectors lc rgb var,\
	     '' using 1:(-1):1:(rgbfudge($1)) with labels tc rgb var point
	
	pause -1 'Hit <cr> to continue'
	reset

snippet vector.dem
	#
	# $Id: vector.dem,v 1.13 2014/04/05 06:17:08 markisch Exp $
	#
	# This file demonstrates
	# -1- saving contour lines as a gnuplottable datablock
	# -2- plotting a vector field on the same graph
	# -3- manipulating columns using the '$1,$2' syntax.
	# the example is taken here from Physics is the display of equipotential
	# lines and electrostatic field for a dipole (+q,-q)
	
	print "\n This file demonstrates"
	print " -1- saving contour lines as a gnuplottable datablock"
	print " -2- plotting a vector field on the same graph"
	print " -3- manipulating columns using the '$1,$2' syntax."
	print " the example is taken here from Physics is the display of equipotential"
	print " lines and electrostatic field for a dipole (+q,-q)"
	#
	      r(x,y)=sqrt(x*x+y*y)
	      v1(x,y)=  q1/(r((x-x0),y))
	      v2(x,y)=  q2/(r((x+x0),y))
	#
	      vtot(x,y)=v1(x,y)+v2(x,y)
	#
	      e1x(x,y)= q1*(x-x0)/r(x-x0,y)**3
	      e1y(x,y)= q1*(y)/r(x-x0,y)**3
	      e2x(x,y)= q2*(x+x0)/r(x+x0,y)**3
	      e2y(x,y)= q2*(y)/r(x+x0,y)**3
	      etotx(x,y)=e1x(x,y)+e2x(x,y)
	      etoty(x,y)=e1y(x,y)+e2y(x,y)
	      enorm(x,y)=sqrt(etotx(x,y)*etotx(x,y)+etoty(x,y)*etoty(x,y))
	      dx1(x,y)=coef*etotx(x,y)/enorm(x,y)
	      dy1(x,y)=coef*etoty(x,y)/enorm(x,y)
	      dx2(x,y)=coef*etotx(x,y)
	      dy2(x,y)=coef*etoty(x,y)
	#
	      coef=.7
	      x0=1.
	      q1=1
	      q2=-1
	      xmin=-10.
	      xmax=10.
	      ymin=-10.
	      ymax=10.
	#
	reset
	unset autoscale
	set xr [xmin:xmax]
	set yr [ymin:ymax]
	set isosam 31,31
	#set view 0, 0, 1, 1
	set view map
	unset surface
	set contour base
	set cntrparam order 4
	set cntrparam linear
	set cntrparam levels discrete -3,-2 ,-1 ,-0.5 ,-0.2 ,-0.1 ,-0.05 ,-0.02 ,0 ,0.02 ,0.05 ,0.1 ,0.2 ,0.5 ,1 ,2 ,3 
	set cntrparam points 5
	#
	set label "-q" at -1,0 center
	set label "+q" at  1,0 center
	splot vtot(x,y) w l
	print "Now create a in-memory datablock with equipotential lines"
	pause -1 "Hit return to continue"
	
	set table $equipo2
	replot
	unset table
	reset
	pause 0
	
	plot $equipo2 w l
	print "Now create a x/y datablock for plotting with vectors "
	print "and display vectors parallel to the electrostatic field"
	pause -1 "Hit return to continue"
	set isosam 31,31
	
	set table $field2xy
	splot vtot(x,y) w l
	unset table
	pause 0
	
	unset autoscale
	set xr [xmin:xmax]
	set yr [ymin:ymax]
	set isosam 31,31
	set key under Left reverse
	plot $field2xy u 1:2:(coef*dx1($1,$2)):(coef*dy1($1,$2)) w vec, \
	     $equipo2 w l
	pause -1 "Hit return to continue"
	
	reset
	

snippet violinplot.dem
	#
	# Various ways of displaying distribution of y values in a data file
	# 1) Violin plots (bee swarm with large number of points)
	# 2) Gaussian jitter
	# 3) Random jitter
	# 4) kernel density
	#
	
	# Generate a reusable set of data points from a mixture of Gaussians
	nsamp = 3000 
	set print $viol1
	do for [i=1:nsamp] {
	    y = (i%4 == 0) ? 300. +  70.*invnorm(rand(0)) \
	      : (i%4 == 1) ? 400. +  10.*invnorm(rand(0)) \
	      :              120. +  40.*invnorm(rand(0))
	    print sprintf(" 35.0 %8.5g", y)
	}
	unset print
	
	set print $viol2
	do for [i=1:nsamp] {
	    y = (i%4 == 0) ? 300. +  70.*invnorm(rand(0)) \
	      : (i%4 == 1) ? 250. +  10.*invnorm(rand(0)) \
	      :               70. +  20.*invnorm(rand(0))
	    print sprintf(" 34.0 %8.5g", y)
	}
	unset print
	#
	
	set border 2
	set xrange [33:36]
	set xtics ("A" 34, "B" 35)
	set xtics nomirror scale 0
	set ytics nomirror rangelimited
	unset key
	
	set jitter overlap first 2
	set title font ",15"
	set title "swarm jitter with a large number of points\n approximates a violin plot"
	set style data points
	
	set linetype  9 lc "#80bbaa44" ps 0.5 pt 5
	set linetype 10 lc "#8033bbbb" ps 0.5 pt 5
	
	plot $viol1 lt 9, $viol2 lt 10
	
	pause -1 'Hit <cr> to continue'
	
	set title "Gaussian random jitter"
	unset jitter
	
	J = 0.1
	plot $viol1 using ($1 + J*invnorm(rand(0))):2 lt 9, \
	     $viol2 using ($1 + J*invnorm(rand(0))):2 lt 10 
	
	pause -1 "Hit <cr> to continue"
	
	set title "Same data - kernel density"
	set style data filledcurves below
	set auto x
	set xtics 0,50,500
	unset ytics
	set border 3
	set margins screen .15, screen .85, screen .15, screen .85
	set key
	
	plot $viol1 using 2:(1) smooth kdensity bandwidth 10. with filledcurves above y lt 9 title 'B', \
	     $viol2 using 2:(1) smooth kdensity bandwidth 10. with filledcurves above y lt 10 title 'A'
	
	pause -1 "Hit <cr> to continue"
	
	#
	# Save each kernel density plot to a data block 
	# Then replot, mirrored, along the vertical axis
	#
	
	set title "kdensity mirrored sideways to give a violin plot"
	
	set table $kdensity1
	plot $viol1 using 2:(1) smooth kdensity bandwidth 10. with filledcurves above y lt 9 title 'B'
	set table $kdensity2
	plot $viol2 using 2:(1) smooth kdensity bandwidth 10. with filledcurves above y lt 10 title 'A'
	unset table
	unset key
	
	set border 2
	unset margins
	unset xtics
	set ytics nomirror rangelimited
	
	set xrange [-1:5]
	plot $kdensity2 using (1 + $2/20.):1 with filledcurve x=1 lt 10, \
	     '' using (1 - $2/20.):1 with filledcurve x=1 lt 10, \
	     $kdensity1 using (3 + $2/20.):1 with filledcurve x=3 lt 9, \
	     '' using (3 - $2/20.):1 with filledcurve x=3 lt 9
	
	pause -1 "Hit <cr> to continue"
	
	set title 'Superimposed violin plot and box plot'
	
	set style fill solid bo -1
	set boxwidth 0.075
	set errorbars lt black lw 1
	
	replot $viol2 using (1):2 with boxplot fc "white" lw 2, \
	       $viol1 using (3):2 with boxplot fc "white" lw 2
	
	pause -1 "Hit <cr> to continue"
	
	reset

snippet volatile.dem
	#
	# Unit test for "volatile" "skip" "refresh"
	#
	set title "Nothing interesting here, just a unit test for volatile, skip, and refresh"
	set style data lines
	splot "whale.dat" skip 5 volatile
	refresh
	reset

snippet world2.dem
	#
	# Plot of location on globe, 
	# this time with labels color-coded by explicit 4th input column
	# Requires EAM_DATASTRINGS
	#
	set dummy u,v
	set angles degrees
	set parametric
	set view 60, 136, 1.22, 1.26
	set samples 64,64
	set isosamples 13,13
	set mapping spherical
	unset xtics
	unset ytics
	unset ztics
	set border 0
	set title "Labels colored by GeV plotted in spherical coordinate system"
	set urange [ -90.0000 : 90.0000 ] noreverse nowriteback
	set vrange [ 0.00000 : 360.000 ] noreverse nowriteback
	set cblabel "GeV"
	set cbrange [0:8]
	set colorb vert user size 0.02, 0.75
	unset hidden
	splot cos(u)*cos(v),cos(u)*sin(v),sin(u) notitle with lines lt 5, \
	      'world.dat' notitle with lines lt 2, \
	      'srl.dat' using 3:2:(1):1:4 with labels notitle point pt 6 lw .1 left offset 1,0 font "Helvetica,7" tc pal
	pause -1 "Same plot with hidden line removal"
	set title "Labels with hidden line removal"
	set hidden nooffset
	replot
	pause -1 "Hit return to continue"
	reset
snippet world.dem
	#
	# $Id: world.dem,v 1.12 2016/09/10 05:46:22 sfeam Exp $
	#
	#
	set title "Gnuplot Correspondences\ngeographic coordinate system"
	unset key
	set xrange [-180:180]
	set yrange [-90:90]
	set yzeroaxis
	set xtics geographic
	set ytics geographic
	set format x "%D %E"
	set format y "%D %N"
	#
	# plot world map and correspondent locations as a +
	plot 'world.dat' with lines lc rgb "blue" , 'world.cor' with points lt 1 pt 2
	pause -1 "Hit return to continue"
	#
	# plot a '3D version using spherical coordinate system' of the world.
	reset
	unset key
	set border
	set yzeroaxis
	set xtics
	set ytics
	set angles degrees
	set title "3D version using spherical coordinate system"
	set ticslevel 0
	set view 70,40,0.8,1.2
	set view equal xyz
	set mapping spherical
	set parametric
	set samples 32
	set isosamples 9
	set urange [-90:90]
	set vrange [0:360]
	splot cos(u)*cos(v),cos(u)*sin(v),sin(u) with lines lc rgb "cyan" ,\
	'world.dat' with lines lc rgb "blue" , 'world.cor' with points lt 1 pt 2
	pause -1 "Hit return to continue"
	
	# HBB 20000715: new demo:
	# same plot, but with hidden3d active, plus axes through the
	# poles:
	set title "3D solid version with hidden line removal"
	set hidden3d offset 0
	set arrow from 0,0,-1.2 to 0,0,1.2 lc rgb "cyan" lw 2
	set arrow from -1.2, 0, 0 to 1.2, 0, 0 nohead lc rgb "cyan" lw 1 
	set arrow from 0, -1.2, 0 to 0, 1.2, 0 nohead lc rgb "cyan" lw 1
	splot cos(u)*cos(v),-cos(u)*sin(v),sin(u) with lines lc rgb "cyan" ,\
	'world.dat' u 1:2:(1.001) with lines lc rgb "blue" , 'world.cor' with points lt 1 pt 2
	pause -1 "Hit return to continue"
	
	unset arrow
	
	#
	# plot a '3D version using cylindrical coordinate system' of the world.
	set title "3D version using cylindrical coordinate system"
	set ticslevel 0.0
	set view 70,40,0.8,1.2
	set view equal xy
	set mapping cylindrical
	set parametric
	set samples 32
	set isosamples 13
	set urange [-180:180]
	set vrange [-90:90]
	splot cos(u),sin(u),v with lines lc rgb "cyan" ,\
	'world.dat' with lines lc rgb "blue" lw 2, 'world.cor' with points lt 1 pt 2
	pause -1 "Hit return to continue"
	reset

snippet zerror.dem
	#
	# zerrorfill with and without depth-sorting
	#
	set yrange [0.5:5.5]
	set zrange [1:*]
	set xyplane at 1
	set log z
	set key opaque box height 1 at screen .9, .8
	set border 127
	set grid z
	
	set title "splot with zerrorfill (no depth sorting)"
	splot for [k=1:5] 'silver.dat' using 1:(k):2:3 with zerror lt black fc lt k title "k = ".k
	
	pause -1 "Hit return to continue" 
	
	set title "splot with zerrorfill (set pm3d depthorder)"
	set pm3d depthorder
	set style fill transparent solid 0.5
	splot for [k=1:5] 'silver.dat' using 1:(k):2:3 with zerror lt black fc lt k title "k = ".k
	
	pause -1 "Hit return to continue"
	reset
	#
	# Reproduce surface1.dem plot #16
	# using zerrorfill instead of hidden3d parametric surfaces
	#
	set format z "%.1f" 
	unset key
	set view 66, 200, 1, 1
	unset xtics
	unset ytics
	set title "fence plot constructed with zerrorfill" 
	set xlabel "X axis"  rotate parallel 
	set ylabel "Y axis" rotate parallel
	set pm3d depthorder
	sinc(u,v) = sin(sqrt(u**2+v**2)) / sqrt(u**2+v**2)
	
	set style fill  transparent solid 0.75 noborder
	splot for [i=-5:4][y=-50:50:5] '+' using (i):($1/100.):(-1):(-1):(sinc($1/10., 1.+i)) with zerrorfill
	
	pause -1 "Hit return to continue"
	reset

