function! Redir(cmd)
	for win in range(1, winnr('$'))
		if getwinvar(win, 'scratch')
			execute win . 'windo close'
		endif
	endfor
	if a:cmd =~ '^!'
		let output = system(matchstr(a:cmd, '^!\zs.*'))
	else
		redir => output
		execute a:cmd
		redir END
	endif
	vnew
	let w:scratch = 1
	setlocal buftype=nofile bufhidden=wipe nobuflisted noswapfile
	call setline(1, split(output, "\n"))
endfunction

command! -nargs=1 -complete=command Redir silent call Redir(<q-args>)

" Usage:
" 	:Redir hi ............. show the full output of command ':hi' in a scratch window
" 	:Redir !ls -al ........ show the full output of command ':!ls -al' in a scratch window

" :hi CustomPink ctermbg=205 guibg=hotpink guifg=black ctermfg=black
" :au FileType haskell :call matchadd('CustomPink' '\<undefined\>')
" :au BufEnter * :call matchadd('CustomPink' '\<undefined\>')
" :au BufEnter * :call matchadd('Keyword' '\<undefined\>')
" :call matchadd('Comment', '\<undefined\>')
